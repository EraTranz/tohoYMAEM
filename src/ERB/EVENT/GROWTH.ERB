;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;キャラの特定ステータス成長処理関連
;
;キャラのBASE、MAXBASEが増減する処理をここで行う
;大部分は時間経過処理(EVENT.ERB)より呼び出される
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;=============================================================================
;体力・気力の最大値上昇処理
;=============================================================================
;調教終了後それぞれ2/5の確率で体力・気力の最大値が上昇。異常経験の値で上昇する数値が増加します
;ただし増加する値はそれぞれ＋40(難易度によっては＋80)が限度かつ、性別(及び処女性)によって
;体力、気力それぞれにある値を超えるとそれ以上は成長しない限界値というものが設定されています。
;また、体力・気力を減らしすぎた場合は上昇しないよう条件を変えました。
;LOCAL:0 最大体力と現在体力の差
;LOCAL:1 最大気力と現在気力の差
;LOCAL:2 体力と気力のいずれを上げるのか？
;LOCAL:3 上昇幅を求めるための基礎値
;LOCAL:4 実際の体力上昇値
;LOCAL:5 実際の気力上昇値
;LOCAL:6 1回の成長値
;LOCAL:7 体力成長限界
;LOCAL:8 気力成長限界

@EVENT_GROWTH
VARSET LOCAL,0
LOCAL = MAXBASE:0 - BASE:体力
LOCAL:1 = MAXBASE:1 - BASE:気力
LOCAL:2 = RAND:5
LOCAL:3 = 1
LOCAL:6 = EXP:異常経験 * 2
;PHANTASM/ULTRA/OVERDRIVEは1回の成長の最大値が大きい
IF FLAG:3 >= 5
	LOCAL:6 = MIN(LOCAL:6, 100)
;EASYとHARDは1回の成長の最大値が少し大きい
ELSEIF FLAG:3 == 1 || FLAG:3 == 3
	LOCAL:6 = MIN(LOCAL:6, 60)
ELSE
	LOCAL:6 = MIN(LOCAL:6, 20)
ENDIF

;調教対象の性別、処女性によって成長限界が変化(オトコは限界値がかなり高く設定されている)
IF TALENT:オトコ
	LOCAL:7 = 5000
	LOCAL:8 = 3000
ELSE
	LOCAL:7 = 3000
	IF TALENT:処女 == 1 && EXP:Ｖ経験 == 0 && ABL:Ｖ感覚 == 0 && JUEL:快Ｖ == 0
		LOCAL:8 = 3000
	ELSEIF TALENT:処女 == 1 && EXP:Ｖ経験 == 0 && ABL:Ｖ感覚 == 0
		LOCAL:8 = 2500
	ELSE
		LOCAL:8 = 2000
	ENDIF
ENDIF
;ベリージュエルを装着していると成長限界と速度がダウン
IF CFLAG:42 & 4
	TIMES LOCAL:6 , 0.75
	TIMES LOCAL:7 , 0.75
	TIMES LOCAL:8 , 0.75
ENDIF
;性転換をしていると成長限界と速度がダウン
IF CFLAG:4995
	TIMES LOCAL:6 , 0.50
	TIMES LOCAL:7 , 0.50
	TIMES LOCAL:8 , 0.50
ENDIF

;体力が大きい場合は気力が、気力が大きい場合は体力が成長する確率が上昇
IF LOCAL:2 == 1 && MAXBASE:0 > MAXBASE:1 && RAND:3 == 0
	LOCAL:2 = 2
ELSEIF LOCAL:2 == 2 && MAXBASE:1 > MAXBASE:0 && RAND:3 == 0
	LOCAL:2 = 1
ENDIF

IF LOCAL:2 <= 1 && LOCAL != 0 && BASE:体力 >= 300 && CFLAG:体力上昇累積分 < LOCAL:7
	SIF LOCAL > 0 && LOCAL <= 500
		LOCAL:3 = 250
	SIF LOCAL > 500 && LOCAL <= 1000
		LOCAL:3 = 300
	SIF LOCAL > 1000 && LOCAL <= 1500
		LOCAL:3 = 350
	SIF LOCAL > 1500 && LOCAL <= 2000
		LOCAL:3 = 400
	SIF LOCAL > 2000 && LOCAL <= 2500
		LOCAL:3 = 450
	SIF LOCAL > 2500
		LOCAL:3 = 500
	SIF BASE:体力 < 500
		LOCAL:3 = 750
	LOCAL:4 = MIN(LOCAL / LOCAL:3, 20)
	SIF TALENT:恋慕 == 0 && TALENT:服従 == 0
		LOCAL:4 /= 2
	LOCAL:4 += LOCAL:6
	IF LOCAL:4 > 0
		CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 120, LOCAL:4
		PRINTFORMW %CALLNAME:TARGET%の体力の最大値が{LOCAL:4}上がりました。
		MAXBASE:0 += LOCAL:4
		CFLAG:体力上昇累積分 += LOCAL:4
	ENDIF
ELSEIF LOCAL:2 <= 1 && LOCAL != 0 && BASE:体力 >= 300 && CFLAG:体力上昇累積分 >= LOCAL:7
	LOCAL:2 = 0
ENDIF

IF (LOCAL:2 == 0 || LOCAL:2 == 2) && LOCAL:1 != 0 && BASE:気力 >= 100 && CFLAG:気力上昇累積分 < LOCAL:8
	SIF LOCAL:1 > 0 && LOCAL:1 <= 300
		LOCAL:3 = 200
	SIF LOCAL:1 > 300 && LOCAL:1 <= 500
		LOCAL:3 = 250
	SIF LOCAL:1 > 500 && LOCAL:1 <= 1000
		LOCAL:3 = 300
	SIF LOCAL:1 > 1000 && LOCAL:1 <= 1500
		LOCAL:3 = 350
	SIF LOCAL:1 > 1500 && LOCAL:1 <= 2000
		LOCAL:3 = 400
	SIF LOCAL:1 > 2000 && LOCAL:1 <= 2500
		LOCAL:3 = 450
	SIF LOCAL:1 > 2500
		LOCAL:3 = 500
	SIF BASE:気力 < 300
		LOCAL:3 = 750
	LOCAL:5 = MIN(LOCAL:1 / LOCAL:3, 20)
	SIF TALENT:恋慕 == 0 && TALENT:服従 == 0
		LOCAL:5 /= 2
	LOCAL:5 += LOCAL:6
	IF LOCAL:5 > 0
		CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 121, LOCAL:5
		PRINTFORMW %CALLNAME:TARGET%の気力の最大値が{LOCAL:5}上がりました。
		MAXBASE:1 += LOCAL:5
		CFLAG:気力上昇累積分 += LOCAL:5
	ENDIF
ENDIF

;=============================================================================
;陰毛成長関連
;=============================================================================
;陰毛の成長具合(BASE:陰毛)の増減処理や、状態判定をここで行う。細かくやると面倒なため、処理は大雑把に
;剃毛プレイ等を実行(つまり初期状態のときではない場合)または脱毛状態でないときは毛が生えてくるように
;大まかな目安として、0～50：ツルツル、51～250：生えかけ、251～1000：生えそろった(剃毛可能)……となっている
;--------------------------------------------------
;時間経過における陰毛成長処理
;--------------------------------------------------
@CHECK_DAY_PUBIC_HAIR
LOCAL = TARGET
LOCAL:1 = TFLAG:900
TARGET = TFLAG:900

;日光浴、月光浴、吸血鬼
IF TIME == 0
	LOCAL:5 = 5
	SIF TALENT:日光浴
		TIMES LOCAL:5 , 1.20
ELSE
	LOCAL:5 = 10
	SIF TALENT:月光浴 || TALENT:吸血鬼
		TIMES LOCAL:5 , 1.20
ENDIF

;回復早い、回復遅い、吸血鬼、蓬莱人
IF TALENT:回復早い || TALENT:吸血鬼
	TIMES LOCAL:5 , 1.30
ELSEIF TALENT:回復遅い
	TIMES LOCAL:5 , 0.70
ELSEIF TALENT:蓬莱人
	TIMES LOCAL:5 , 1.50
ENDIF

;小柄体型
SIF TALENT:小柄体型
	LOCAL:5 /= 2

BASE:陰毛 = MIN(BASE:陰毛 + MAX(LOCAL:5, 1), 1000)
IF TALENT:パイパン && BASE:陰毛 > 250
	PRINTFORML %CALLNAME:TARGET%の下の毛が生えそろいました
	PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:65%]ではなくなった
	TALENT:パイパン = 0
ENDIF

TARGET = LOCAL
TFLAG:900 = LOCAL:1

;=============================================================================
;陰核成長関連
;=============================================================================
;陰核の成長具合(BASE:陰核)の増減処理や、状態判定をここで行う
;大まかな目安として、0：成長しない、1～50：ノーマル、51～100：小指サイズ、
;101～200：男性器サイズ(150以上になると挿入可能な[肥大陰核]が付く)、201～250(最大値)：異様なサイズ(初めての時は異常経験が付く)
;……となっている
;--------------------------------------------------
;時間経過における陰核成長処理
;--------------------------------------------------
@CHECK_DAY_CLITORIS
LOCAL = TARGET
LOCAL:1 = TFLAG:900
TARGET = TFLAG:900

BASE:陰核 += (TIME == 1) ? RAND:10 # RAND:5

CALL CHECK_CLITORIS_GROWTH

TARGET = LOCAL
TFLAG:900 = LOCAL:1

;--------------------------------------------------
;陰核成長判定処理
;--------------------------------------------------
@CHECK_CLITORIS_GROWTH
SIF BASE:陰核 >= 150 && TALENT:肥大陰核 == 0
	CALL HUGE_CLITORIS, TARGET
SIF BASE:陰核 > 200 && (CFLAG:37 & 4) == 0
	CALL GIGATORIS, TARGET
BASE:陰核 = MIN(BASE:陰核, 250)

;--------------------------------------------------
;陰核の成長具合150以上による[肥大陰核]付加処理
;--------------------------------------------------
@HUGE_CLITORIS, ARG
PRINTFORMW %CALLNAME:ARG%の陰核は十分な大きさにまで成長した。

;肥大陰核取得による口上呼び出し
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, ARG, 125

TALENT:ARG:肥大陰核 = 1
IF (CFLAG:ARG:37 & 2) == 0
	CFLAG:ARG:37 |= 2
	;異常経験
	CALL COMMON_UP_EXP, ARG, 50, 1, 0
ENDIF

;--------------------------------------------------
;陰核の成長具合200以上による異常経験付加処理
;--------------------------------------------------
@GIGATORIS, ARG
PRINTFORMW %CALLNAME:ARG%の陰核は異様な大きさにまで成長した。

;陰核肥大化完了による口上呼び出し
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, ARG, 126

CFLAG:ARG:37 |= 4
;異常経験
CALL COMMON_UP_EXP, ARG, 50, 1, 0
