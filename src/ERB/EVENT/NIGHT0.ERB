;=============================================================================
;夜イベント拡張パッチ処理
;=============================================================================
;夜イベント拡張パッチ （これはランダム夜這いパッチに他人が勝手に改修を加えたものです）
;元パッチの作者様は深い感謝と尊敬の意を。(by やぁふぁの人 :2009年4月中旬）
;※よりによってプログラム囓った程度の人間が手を加えましたので、汚いですご容赦ください……

;コンセプト;朝イベントの対を目指して作ったもの。
;ランダムで奴隷が主人の下へやってくるという形。夜這い
;たくさん奴隷がいると、シチュ的に楽しめると思う

;6/18 貞操帯装備中の処理を勝手に追加させていただきました
;6月下旬 処理の追加させていただきましたm(_ _)m　(やぁふぁ)

;2009/10/04 襲撃SMプレイ追加　By HEROHERO
;
;LOCAL:1 = 0:ノーマル？,1:乱交,2:フラン*4
;LOCAL:2 = 0~6:乱数？,7:レイプ,8:恋慕キャラ輪姦
;
;
;
;
;
;// 新たに使用したもの(このファイル、およびCALL先で使用するものも記す)
;// PL_VAR_X:2 = 乱交時にキャラが分身するので、応急処置として使用
;// 			重複なしでキャラを抽出できた回数を一時保存するための変数
;// 本来ならば"NOSAMES"が仕事をすれば"PL_VAR_X:2"なんて必要なかった
;// 
;
;
;
;-----------------------------------------------
;夜イベント判定
;-----------------------------------------------
@NIGHT_ATTACK
;必要フラグのクリア
CALLF CLEAR_AGG
CALLF CLEAR_CHARA_STOCK
CALLF CLEAR_NIGHT_KOJO
VARSET LOCAL, 0

;TARGETとASSIを退避
TFLAG:210 = TARGET
TFLAG:211 = ASSI
;所持キャラ全員に発動チェック
LOCAL:20 = 0
;基礎値0から判定開始
LOCAL:21 = 0
;主人による判定値ボーナス計算
;魅惑
SIF TALENT:MASTER:魅惑
	LOCAL:21 += 2
;謎の魅力
SIF TALENT:MASTER:謎の魅力
	LOCAL:21 += 2
;魅力
SIF TALENT:MASTER:魅力
	LOCAL:21 += 2
;人気
SIF TALENT:MASTER:人気
	LOCAL:21 += 1

;奴隷のランダム判定有りなら上の処理、無しなら下の処理。無しだと少し判定が厳しくなる
IF FLAG:12 & 65536
	LOCAL:20 = NIGHT_CHECK2(LOCAL:21)
	LOCAL:21 = RAND:100
ELSE
	SIF TARGET == -1
		RETURN 0
	CALL NIGHT_CHECK
	LOCAL:20 = 0
	IF RESULT > RAND:60
		IF RESULT >= 18
			LOCAL:20 = 1
			TFLAG:400 = TARGET
			LOCAL:21 = RAND:110
		ENDIF
	ENDIF
ENDIF

;8%の確率で強制的に誰も来ない。ランダム要素が無いともう少しだけペナルティ＝約3/4の確率となる
SIF LOCAL:21 >= 92
	RETURN 0
;全員チェックして実行者0人なら終了
SIF LOCAL:20 == 0
	RETURN 0

;使う変数を0にしておく
;乱交シチュ用
LOCAL:1 = 0
;乱交確率用
LOCAL:2 = 0
;重複なしで選択できた回数一時保存用変数の初期化
PL_VAR_X:2 = 0

;実行者が２人以上だったら、その中から４人抽出
IF LOCAL:20 >= 2
	;一番下の変数、つまりGETCHARA_G(0)がメインのターゲットとなる
	;NOSAMESが仕事しないんで、ここで重複排除
	$SET_LOOP
		TARGET = TFLAG:(RAND:(LOCAL:20)+400)
		;重複の検出
		IF CFLAG:TARGET:75
			GOTO SET_LOOP
		ELSE
			CFLAG:TARGET:75 = 1
			LOCAL:(13 - PL_VAR_X:2) = TARGET
			CALLF SET_CHARA_G(3 - PL_VAR_X:2, TARGET)
			PL_VAR_X:2++
		ENDIF
		IF PL_VAR_X:2 >= 4
			;フラグのお掃除してｶｴﾚ
			CALL RESET_CFLAG75
		;実行者が4人未満でもう1,2,3人選んだ
		ELSEIF LOCAL:20 == PL_VAR_X:2
			;フラグのお掃除してｶｴﾚ
			CALL RESET_CFLAG75
		ELSE
			;選ぶ余地があるのでもう少し頑張る
			GOTO SET_LOOP
		ENDIF
;	;NOSAMESが仕事するならば下記のコードでも十分
;	;一番下の変数、つまりGETCHARA_G(0)がメインのターゲットとなる
;	REPEAT 4
;		TARGET = TFLAG:(RAND:(LOCAL:20)+400)
;		LOCAL:(13 - COUNT) = TARGET
;		CALLF SET_CHARA_G(3 - COUNT, TARGET)
;		;フランドールが２人以上いてもフラグとなる。だがGETCHARA_G(0)がフランじゃないとダメ
;		SIF NO:(LOCAL:(13 - COUNT)) == 1011
;			LOCAL:1 += 1
;		;PRINTFORML %CALLNAME:TARGET%
;	REND
;		;フランドールが２人以上いてもフラグとなる。だがGETCHARA_G(0)がフランじゃないとダメ
;		SIF NO:(LOCAL:(13 - COUNT)) == 1011
;			LOCAL:1 += 1
;		SIF TALENT:MASTER:998
;			PRINTFORML %CALLNAME:TARGET%
		;GET_CHARA_G(0)がフラン、つまり、TARGETがフランならば、フラン*4人乱交のチャンスあり
		
	IF (TALENT:(LOCAL:10):120 || TALENT:(LOCAL:10):121) && MARK:(LOCAL:10):9 == 3 && TALENT:MASTER:オトコ == 0
		
	ELSEIF TALENT:(LOCAL:10):150 == 0 && TALENT:(LOCAL:10):170 == 0
		LOCAL:1 = 0
	ENDIF
	;一人しか含まれてなかったら消去
	SIF LOCAL:1 == 1
		LOCAL:1 = 0
	SIF LOCAL:1 >= 2
		LOCAL:2 = ((TALENT:(LOCAL:10):120 || TALENT:(LOCAL:10):121) && MARK:(LOCAL:10):9 == 3 && TALENT:MASTER:オトコ == 0) ? 7 # RAND:3
	;全員恋慕か淫乱で乱交フラグ
	IF (TALENT:(LOCAL:10):150 || TALENT:(LOCAL:10):170) && (TALENT:(LOCAL:11):150 || TALENT:(LOCAL:11):170) && (TALENT:(LOCAL:12):150 || TALENT:(LOCAL:12):170) && (TALENT:(LOCAL:13):150 || TALENT:(LOCAL:13):170)
		;全員違ったら普通の乱交判定。フランドール乱交との分岐を行う
		;上記の文は、NOSAMESがちゃんと働くときの文
		;本当ならば、このNOSAMESで重複を排除するはず、でも、NOSAMES仕事しねぇ、どういうことだ！キバヤシー！
		;IF NOSAMES(LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13)
			;LOCAL:2 = 乱交の確率判定、奴隷が11人以上ならば、6分の1で、10人以下ならば、3分の1の確率で、発生
			;主人を含む(?)全員が貞操帯装備していたらはじく
			IF (CFLAG:MASTER:42 & 2) == 0 || (CFLAG:(LOCAL:10):42 & 2) == 0 || (CFLAG:(LOCAL:11):42 & 2) == 0 || (CFLAG:(LOCAL:12):42 & 2) == 0 || (CFLAG:(LOCAL:13):42 & 2) == 0
				LOCAL:1 = 1
				LOCAL:2 = 1
				SIF LOCAL:20 >= 11
					LOCAL:2 = RAND:10
			ENDIF
		;ENDIF
	;全員反発かつ主犯がLv3
	ELSEIF MARK:(LOCAL:10):9 == 3 && MARK:(LOCAL:11):9 && MARK:(LOCAL:12):9 && MARK:(LOCAL:13):9
;		;全員違ったらその時点で襲撃確定
;		IF NOSAMES(LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13)
			LOCAL:1 = 1
			LOCAL:2 = 7
;		ENDIF
	;もし主犯が反発Lv3(もちろん、恋慕なし)で、3人の中に1人だけ、反発なし処女か恋慕がいて、それ以外は、最低でも反発を持っていた場合、
	;必ずその反発なしが輪姦される。処理上、LOCAL:11が被害を受ける
	ELSEIF MARK:(LOCAL:10):9 == 3
;		;全員違わないとダメ
;		IF NOSAMES(LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13)
			;LOCAL:11が恋慕
			IF ((TALENT:(LOCAL:11):0 || TALENT:(LOCAL:11):150) && MARK:(LOCAL:11):9 == 0) && MARK:(LOCAL:12):9 && MARK:(LOCAL:13):9
				LOCAL:1 = 1
				LOCAL:2 = 8
			;LOCAL:12が恋慕
			ELSEIF ((TALENT:(LOCAL:12):0 || TALENT:(LOCAL:12):150) && MARK:(LOCAL:12):9 == 0) && MARK:(LOCAL:13):9 && MARK:(LOCAL:11):9
				LOCAL:1 = 1
				LOCAL:2 = 8
				SWAP LOCAL:12, LOCAL:11
				CALLF SET_CHARA_G(1, LOCAL:11)
				CALLF SET_CHARA_G(2, LOCAL:12)
				TARGET = LOCAL:10
			;LOCAL:13が恋慕
			ELSEIF ((TALENT:(LOCAL:13):0 || TALENT:(LOCAL:13):150) && MARK:(LOCAL:13):9 == 0) && MARK:(LOCAL:11):9 && MARK:(LOCAL:12):9
				LOCAL:1 = 1
				LOCAL:2 = 8
				SWAP LOCAL:13, LOCAL:11
				CALLF SET_CHARA_G(1, LOCAL:11)
				CALLF SET_CHARA_G(3, LOCAL:13)
				TARGET = LOCAL:10
			ENDIF
;		ENDIF
	ENDIF
	;主犯がフランなら、1/4でフラン*4人プレイ可能
	IF NO:(TARGET) == 1011 && RAND:4 == 0
		LOCAL:1 = 2
		;主犯のフランが恋慕奴隷を輪姦する処理は今の時点では無理なので主人輪姦ということで
		SIF LOCAL:2 == 8
			LOCAL:2 = 7
	ENDIF
;実行者1人
ELSE
	TARGET = TFLAG:400
	LOCAL:10 = TARGET
	CALLF SET_CHARA_G(0, TARGET)
	;フランドール乱交分岐
	IF NO:(LOCAL:10) == 1011 && RAND:2 == 0
		LOCAL:1 = 2
		LOCAL:11 = LOCAL:10
		LOCAL:12 = LOCAL:10
		LOCAL:13 = LOCAL:10
		IF TALENT:(LOCAL:10):150 || TALENT:(LOCAL:10):170
			LOCAL:2 = RAND:5
			;とりあえず[debug]は確率を弄る
			SIF TALENT:MASTER:998
				LOCAL:2 = 1
		ELSEIF (TALENT:(LOCAL:10):120 || TALENT:(LOCAL:10):121) && MARK:(LOCAL:10):9 == 3 && TALENT:MASTER:オトコ == 0
			LOCAL:2 = 7
		ENDIF
	ENDIF
ENDIF

;RETURN 0対策としてここで夜這いの奴隷を記録してしまう。ランダム機能が有りの場合のみ
;同フォルダ内の「朝に起こるイベント」処理の前に、ここに記録した奴隷をTARGETに戻す
SIF FLAG:12 & 32768
	TFLAG:212 = TARGET

;もし実効値が0になってしまっている場合は強制終了
SIF GET_AGG(TARGET) == 0
	RETURN 0

;この数値でメインターゲットの積極値がわかる
SIF TALENT:MASTER:998
	PRINTFORML %CALLNAME:TARGET%の積極値：{GET_AGG(TARGET)}

;幼稚か幼児、幼児退行＋恋慕なら、(100-欲望LV*15)%でただの添い寝
LOCAL:25 = ((TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行) && TALENT:恋慕 && (100-(15*ABL:欲望) > RAND:100)) ? 2 # 0

;LOCAL:22の初期化、相性検索のときにカウンタとして使用
LOCAL:22 = 0

;TARGETとの相性検索。相性100以上のキャラと３Ｐ
;ぱるプレの骨組みを流用させていただきました。多謝！
;検索
REPEAT CHARANUM
	;対象が主人ならスキップ
	SIF COUNT == MASTER
		CONTINUE
	;対象がその場にいないならスキップ
	SIF CFLAG:COUNT:4
		CONTINUE
;	;対象の部屋に施錠されているならスキップ（巡回施錠追加）
;	SIF CFLAG:COUNT:3050
;		CONTINUE
	;対象が眠っているならスキップ
	SIF EQUIP:COUNT:睡眠薬
		CONTINUE
	;調教した事がないもしくは好感度が0のキャラはスキップ
	SIF CFLAG:COUNT:1610 == 0 || CFLAG:COUNT:好感度 == 0
		CONTINUE
	;抱卵中は夜這いどころではない！
	SIF TALENT:COUNT:抱卵中
		CONTINUE
	;酔っ払い、気力切れ、体力少ない、ストレス過多なのはスキップ
	SIF BASE:COUNT:酔い > MAXBASE:COUNT:8 || BASE:COUNT:体力 < 500 || BASE:COUNT:気力 < 1 || CFLAG:COUNT:ストレス >= PALAMLV:3
		CONTINUE
	;反発刻印持ち排除システム発動
	SIF MARK:COUNT:反発刻印 && FLAG:12 & 131072 == 0 && FLAG:3 < 4
		CONTINUE

	;相性101以上のキャラを保存 ※もちろん、TARGETとCOUNTが違うこと
	IF RELATION:TARGET:(NO:COUNT) > 100 && TARGET != COUNT
		TFLAG:(400+LOCAL:22) = COUNT
		LOCAL:22++
	ENDIF
REND

IF LOCAL:22 > 0
	CALLF SET_CHARA_M(TFLAG:(400+RAND:(LOCAL:22)))
	LOCAL:15 = GET_CHARA_M()
ENDIF
;欲情系各種参考数値
LOCAL:5 = MAX(RAND:(MAX(ABL:欲望, 1)), 2) + (TALENT:淫乱 > 0)*2
;情愛系各種参考値
LOCAL:6 = MAX(RAND:(MAX(ABL:従順, 1)), 2) + (TALENT:恋慕 > 0)*2

DRAWLINE

RESULT = 0

;都合のいいときだけこき使われてストレス貯める助手さんが余りにも不憫なので何かイベントを足そうかと。
;条件。助手が居る+助手が主人でない(謎) 助手が反発3ならスルー、なお、必ず呼び止めるのもうざいので確率50%
SIF ASSI >= 0 && ASSI != MASTER && MARK:ASSI:反発刻印 < 3 && RAND:2 == 0
	CALL NIGHT_ASSI1, TARGET

;助手を呼び止めたら、最優先で助手と過す。(輪姦されなければ……)
IF RESULT == 1
	;主人集団襲撃予定だったが、助手も追加
	IF LOCAL:2 == 7
		REPEAT 4
			TARGET = LOCAL:(13 - COUNT)
			SIF (TALENT:TARGET:オトコ || TALENT:TARGET:ふたなり) && !(CFLAG:TARGET:42 & 2)
				LOCAL:2  += 1
		REND
	ENDIF
	;全員棒持ちだったので助手も集団レイプ
	IF LOCAL:2 == 11
		CALL NIGHT_ASSI_SEX_LUNA, LOCAL:10, LOCAL:11, LOCAL:12, LOCAL:13
	;反発奴隷をスルーして助手とアレコレ
	ELSE
		CALL NIGHT_ASSI2, TARGET
	ENDIF
;恨みを持った棒付き奴隷が女調教主をレイプしにくる
;反発LV3と禁断と調合知識があったら、穴を作ってでも犯しに来る
;更に棒が付いてなくとも触手操作か禁断の知識持ち反発3奴隷が、女調教主を(ry
;両方持ってるキャラが少ないので、調合か禁断のどちらかがあれば性転換
;主人公の性別は関係無くなりました。条件に当てはまるものが無いときは平和な夜が過ぎるだけ
;貞操帯処理を統合して、もはや性別など関係無くなった
;各文章量増加に伴い飛ばす箇所を変更する
;ここでは相性判定相手は既に用無しなのでこれを被害者の判定に用いる

;主犯が反発Lv3
ELSEIF MARK:TARGET:反発刻印 == 3
	;輪姦判定(LOCAL:2 = 8)がある、または、その他が恋慕なしで1/3であり、主人を犯る気満々でない(LOCAL:2 != 7)とき
	;LOCAL:11が輪姦されます、合掌、ホントはロシアンルーレット的なのにしたかった
	;つか、主人犯る気満々なのに仲間割れって残念すぎる
	IF LOCAL:2 == 8 || (LOCAL:2 != 7 && RAND:3 == 0 && TALENT:(LOCAL:12):150 == 0 && TALENT:(LOCAL:13):150 == 0)
		CALLF SET_CHARA_M(LOCAL:11)
		LOCAL:15 = LOCAL:11
	;それ以外なら対象は主人
	ELSE
		CALLF SET_CHARA_M(MASTER)
		LOCAL:15 = MASTER
	ENDIF
	CALL NIGHT_HANTEI_EX, LOCAL:15, TARGET, LOCAL:1, LOCAL:2, LOCAL:5
;可能な限り男女の区別なく夜這いが発生するようにしようかと。誰得だが。
ELSE
	LOCAL:16 = RAND:10
	SIF LOCAL:20 > 10
		LOCAL:2 = LOCAL:16
	;結構増えたし、わざわざ睡眠確率増やさなくてもいいよね？
	;幼稚キャラによる甘えん坊添い寝（恋慕必須）
	IF LOCAL:25 == 2
		CALL NIGHT_EVENT_2, LOCAL:6
	;嫁(TARGET)との一夜（相愛持ち専用）
	ELSEIF TALENT:TARGET:相愛 && LOCAL:16 > 4
		CALL NIGHT_EVENT_3, LOCAL:6, LOCAL:5
	;乱交（夜這い数10人以上で確率が減る。もちろん、ランダム機能ONですよね！）
	;誰かが貞操帯ある場合はJ条件で弾いた。フラン乱交の場合は此処で弾く
	ELSEIF LOCAL:2 == 1 && (LOCAL:1 == 1 || (LOCAL:1 == 2 && (CFLAG:TARGET:42 & 2) == 0 && (CFLAG:MASTER:42 & 2) == 0))
		CALL NIGHT_HANTEI_M, LOCAL:5, LOCAL:6, LOCAL:1, LOCAL:2
	;３Ｐ女女（誰かを連れてきた場合３Ｐ。TARGETが{恋慕or淫乱}且つ{レズっ気1以上or両刀必須}）
	ELSEIF LOCAL:15 > 0 && LOCAL:22 > 0 && RAND:4 == 0 && (TALENT:恋慕 || TALENT:淫乱) && (ABL:レズっ気 >= 1 || TALENT:両刀 == 1) && TALENT:オトコ == 0 && TALENT:(LOCAL:15):120 == 0 && (CFLAG:MASTER:42 & 2) == 0 && (CFLAG:42 & 2) == 0 && (CFLAG:(LOCAL:15):42 & 2) == 0
	;上の条件を下で書き直せられる、もし、正しく動くなら下の条件を採用、NOSAMESが仕事しないんでALLSAMESも仕事しないくさい
	;ELSEIF ALLSAMES(RAND:4,TALENT:TARGET:オトコ,TALENT:(LOCAL:15):120,(CFLAG:MASTER:42 & 2),(CFLAG:TARGET:42 & 2),(CFLAG:(LOCAL:15):42 & 2)) && LOCAL:15 > 0 && TALENT:TARGET:オトコ == 0
		CALL NIGHT_HANTEI_M, LOCAL:5, LOCAL:6, LOCAL:1, LOCAL:2
	;３Ｐ男男
	ELSEIF LOCAL:15 > 0 && LOCAL:22 > 0 && RAND:4 == 0 && (TALENT:恋慕 || TALENT:淫乱) && (ABL:ＢＬっ気 >= 1 || TALENT:両刀 == 1) && TALENT:オトコ && TALENT:(LOCAL:15):120 && (CFLAG:MASTER:42 & 2) == 0 && (CFLAG:42 & 2) == 0 && (CFLAG:(LOCAL:15):42 & 2) == 0
		CALL NIGHT_HANTEI_M, LOCAL:5, LOCAL:6, LOCAL:1, LOCAL:2
	;３Ｐ異性
	ELSEIF LOCAL:15 > 0 && LOCAL:22 > 0 && RAND:4 == 0 && (TALENT:恋慕 || TALENT:淫乱) && ((TALENT:オトコ && TALENT:(LOCAL:15):120 == 0) || (TALENT:オトコ == 0 && TALENT:(LOCAL:15):120)) && (CFLAG:MASTER:42 & 2) == 0 && (CFLAG:42 & 2) == 0 && (CFLAG:(LOCAL:15):42 & 2) == 0
		CALL NIGHT_HANTEI_M, LOCAL:5, LOCAL:6, LOCAL:1, LOCAL:2
	;奉仕精神Lv4以上、触手中毒Lv1以上の禁断or触手操作術持ちの奴隷が触手で一緒に楽しみに来る。
	;但し、紳士的触手を解放しており、主人が貞操帯をしていない場合のみ。
	ELSEIF RAND:5 == 0 && ABL:奉仕精神 > 3 && ABL:触手中毒 > 0 && (TALENT:禁断の知識 || TALENT:触手使役術) && (CFLAG:MASTER:42 & 2) == 0 && FLAG:15 & 4096
		CALL NIGHT_HANTEI_SHOKUSHU
	;禁断、触手操作を持っていなくても、仲の良いMが禁断持ちの場合、低確率で教えて貰ってくる。
	ELSEIF LOCAL:22 > 0 && TALENT:(LOCAL:15):56 && RAND:20 == 0 && ABL:奉仕精神 > 3 && ABL:触手中毒 > 0 && (TALENT:禁断の知識 == 0 || TALENT:触手使役術 == 0) && (CFLAG:MASTER:42 & 2) == 0 && FLAG:15 & 4096
		CALL NIGHT_HANTEI_SHOKUSHU
	;SM系に必要なABLを3→2に。
	;マゾな奴隷が主人に叩かれにくる。もしくは仲の良い奴隷に叩かれに行く
	ELSEIF RAND:5 == 0 && (ABL:マゾっ気 >= 2 || TALENT:マゾ || TALENT:ドＭ)
		CALL NIGHT_HANTEI_SM, LOCAL:22, 1
		;主人が関係しており、ビデオカメラがあると盗撮される(サドのときにこれをしないのはイベントの都合で）
		SIF RAND:4 == 0 && GET_CHARA_M() == MASTER && GET_CHARA_G(2) != TARGET && LOCAL:20 >= 2 && ((ITEM:ビデオカメラ && ITEM:ビデオテープ) || NOITEM)
			CALL NIGHT_TOUSATSU, TARGET, GET_CHARA_G(2)
	;サドな奴隷が、仲の良い奴隷かMな主人を叩きに行く
	ELSEIF RAND:4 == 0 && (ABL:サドっ気 >= 2 || TALENT:ドＳ || TALENT:サド) && (ITEM:鞭 || NOITEM) && ((ABL:MASTER:マゾっ気 >= 2 || TALENT:MASTER:マゾ || TALENT:MASTER:ドＭ) || LOCAL:22 > 0)
		CALL NIGHT_HANTEI_SM, LOCAL:22, 2
	ELSEIF (CFLAG:42 & 2) == 0 && LOCAL:16 >= 8
		;主人がハブられる、仲の良い奴隷(LOCAL:15)と一緒に過す(旧襲撃系を含む
		IF LOCAL:22 > 0 && RAND:3 == 0 && (CFLAG:(LOCAL:15):42 & 2) == 0
			SIF NIGHT_HANTEI_HABU1(LOCAL:15, TARGET)
				CALL NIGHT_HANTEI_HABU2, LOCAL:15, TARGET, LOCAL:5, LOCAL:6
		;通常の夜這い処理
		ELSE
			CALL NIGHT_EVENT_4, LOCAL:5, LOCAL:6
			;ビデオカメラがあると盗撮される
			IF RAND:4 == 0 && LOCAL:20 >= 2 && GET_CHARA_G(2) != TARGET && ((ITEM:ビデオカメラ && ITEM:ビデオテープ) || NOITEM)
				CALL NIGHT_TOUSATSU, TARGET, GET_CHARA_G(2)
			;2分の一、複数の奴隷だと覗き見オナニー
			ELSEIF RAND:2 == 0 && LOCAL:20 > 2 && GET_CHARA_G(2) != TARGET && EXP:(GET_CHARA_G(2)):10 >= 1
				CALL NIGHT_EVENT_10, TARGET, GET_CHARA_G(2), GET_CHARA_G(3), LOCAL:5, LOCAL:6, LOCAL:20
			ENDIF
		ENDIF
	;寝てしまった主人
	ELSEIF LOCAL:16 < 4
		CALL NIGHT_EVENT_1, LOCAL:5, LOCAL:6, LOCAL:25, LOCAL:20
	
	ENDIF
ENDIF

;一度奴隷の存在を元の値に戻す。
TARGET = TFLAG:210
WAIT
DRAWLINE
;ここで夜這いは終了
RETURN 0

;=====================================================
;
;奴隷による善意の触手姦
;
;=====================================================
;-----------------------------------------------
;行動判定
;----------------------------------------------
@NIGHT_HANTEI_SHOKUSHU
;序文(地の文カット)
IF CFLAG:99 == 0
	PRINTFORML その夜、突然%CALLNAME:TARGET%が%CALLNAME:MASTER%の部屋へやってきた。
	IF TALENT:TARGET:禁断の知識 == 0 && TALENT:TARGET:触手使役術 == 0
		TALENT:TARGET:禁断の知識 = 1
		PRINTFORML %CALLNAME:TARGET%が言うには、%CALLNAME:MASTER%に気持ち良くなって貰うために色々勉強してきたらしい。
		PRINTFORML その甲斐があって、%CALLNAME:TARGET%は[%TALENTNAME:56%]を習得した。
	ELSE
		PRINTFORML %CALLNAME:TARGET%が言うには、今日は%CALLNAME:MASTER%に気持ち良くなって貰うために色々奉仕してくれるらしい。
	ENDIF
ENDIF

;触手暴走判定LOCAL
;基本値は技巧LV＋触手使役LV(奴隷の触手使役LVは通常は0)
LOCAL = ABL:技巧 + ABL:触手使役
;触手操作術を持つ場合+3
SIF TALENT:触手使役術
	LOCAL += 3

;50％の確率でRAND:10以下なら暴走。
LOCAL = (RAND:2 == 0 && RAND:10 > LOCAL) ? 0 # 1

;選択による分岐を行う。
PRINTL さて、どうしようか。
;CONFIGでランダム性を排除していたら選択できる。
IF FLAG:14 & 8
	PRINTFORML [0] \@(LOCAL) ? %CALLNAME:TARGET%に気持ち良くして貰おう # 嫌な予感がするけれど、折角そう言ってくれている事だし…\@
	PRINTL [1] 今日は気乗りしないので帰って貰おう
	$INPUT_LOOP
	INPUT
ELSE
	RESULT = (((RAND:10 < (3-TALENT:MASTER:禁断の知識)) && LOCAL == 0) || (RAND:10 < 6 && LOCAL == 1)) ? 0 # 1
ENDIF
IF RESULT == 0
	CALL NIGHT_SHOKUSHU_SOFT, LOCAL
ELSEIF RESULT == 1
	PRINTL 
	PRINTFORML %CALLNAME:TARGET%はどこか不満げに帰って行った…。
ELSE
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

;=====================================================
;
;調教主がはぶられた場合の夜行動
;
;=====================================================
;-----------------------------------------------
;行動判定
;----------------------------------------------
@NIGHT_HANTEI_HABU1(ARG, ARG:1)
#FUNCTION
;最初にただ一緒に過すか、エロイ事するか。
REPEAT 2
	TARGET = (COUNT == 0) ? ARG # ARG:1
	LOCAL = 9
	;恋慕持ちは性行為しにくい。
	SIF TALENT:恋慕
		LOCAL -= 1
	;淫乱持ちは性行為しやすい。
	SIF TALENT:淫乱
		LOCAL += 1
	;同性の場合
	IF TALENT:(ARG:1):120 && TALENT:ARG:オトコ
		SIF (ABL:ＢＬっ気 >= 3 || TALENT:両刀 == 1)
			LOCAL += 1
	ELSEIF TALENT:(ARG:1):120 == 0 && TALENT:ARG:オトコ == 0
		SIF (ABL:レズっ気 >= 3 || TALENT:両刀 == 1)
			LOCAL += 1
	;異性の場合
	ELSE
		;相性良いなら性行為しやすい
		SIF RELATION:(ARG:1):(NO:ARG) >= 110 && RELATION:ARG:(NO:(ARG:1)) >= 110
			LOCAL += 1
		;貞操観念持ちは性行為しにくい
		SIF TALENT:貞操観念
			LOCAL -= 1
		;貞操無頓着なら性行為しやすい
		SIF TALENT:貞操無頓着
			LOCAL += 1
	ENDIF
	;親愛持ちは性行為しにくい。
	SIF TALENT:親愛
		LOCAL -= 1
	SIF COUNT == 0
		LOCAL:1 = LOCAL
REND

RETURNF (LOCAL > 9 && LOCAL:1 > 9)

@NIGHT_HANTEI_HABU2, ARG, ARG:1, ARG:2, ARG:3
IF TALENT:(ARG:1):120 && TALENT:ARG:オトコ
	CALL NIGHT_EVENT_12, ARG, ARG:2, ARG:3
ELSEIF TALENT:(ARG:1):120 == 0 && TALENT:ARG:オトコ == 0
	CALL NIGHT_EVENT_8, ARG, ARG:1, ARG:2, ARG:3
ELSE
	CALL NIGHT_SEX_9, ARG, ARG:1
ENDIF

;=====================================================
;
;SM行為による夜行動
;
;=====================================================
;-----------------------------------------------
;行動判定
;----------------------------------------------
@NIGHT_HANTEI_SM, ARG, ARG:1
;叩かれたい奴隷の場合
IF ARG:1 == 1
	;仲の良い奴隷が居る場合
	IF ARG > 0
		;サド具合を比べる。より気持ち良くしてくれる方へ行こうとする。
		LOCAL:1 = 0
		REPEAT 2
			LOCAL:3 = (COUNT == 0) ? MASTER # GET_CHARA_M()
			LOCAL = 0
			;サドッ気加算
			LOCAL += ABL:(LOCAL:3):20
			;サド持ちなら+3
			SIF TALENT:(LOCAL:3):81
				LOCAL += 3
			;どS持ちなら+5
			SIF TALENT:(LOCAL:3):177
				LOCAL += 5
			;技巧による補正
			LOCAL *= ABL:(LOCAL:3):12
			;一回目ならLOCAL:1に保存
			SIF COUNT == 0
				LOCAL:1 = LOCAL
		REND
		IF LOCAL:1 > LOCAL
			CALLF SET_CHARA_M(MASTER)
		ELSEIF RAND:2 == 0 && LOCAL:1 == LOCAL
			CALLF SET_CHARA_M(MASTER)
		ENDIF
	ELSE
		CALLF SET_CHARA_M(MASTER)
	ENDIF
	CALL NIGHT_EVENT_SM_M, GET_CHARA_M()
;叩きたい奴隷の場合
ELSE
	;叩ければなんでもいい。
	;仲の良い奴隷が居て主がマゾ
	IF ARG > 0 && (ABL:MASTER:マゾっ気 >= 2 || TALENT:MASTER:マゾ || TALENT:MASTER:ドＭ)
		SIF RAND:2 == 0
			CALLF SET_CHARA_M(MASTER)
	;仲の良い奴隷が居らず主がマゾ
	ELSEIF ABL:MASTER:マゾっ気 >= 2 || TALENT:MASTER:マゾ || TALENT:MASTER:ドＭ
		CALLF SET_CHARA_M(MASTER)
	ENDIF
	CALL NIGHT_EVENT_SM_S, GET_CHARA_M()
ENDIF

;=====================================================
;
;多人数で行われる性行為について(乱交、宴会)
;
;=====================================================
;-----------------------------------------------
;行動判定
;----------------------------------------------
;ARG：旧E　ARG:1：旧F:7　ARG:2：旧R:1、ARG:3：旧R:2
@NIGHT_HANTEI_M, ARG, ARG:1, ARG:2, ARG:3
;とりあえずフラン乱交はそっちへ飛ばす
IF ARG:2 == 2
	CALL NIGHT_RANKOU_FLAN, ARG, ARG:2
	RETURN 0
ENDIF

;序文。
;乱交と３Ｐから切り取ってきた
;地の文カット
IF CFLAG:99 == 0
	IF ARG:3 == 1
		
		PRINTFORMW その夜、突然%CALLNAME:(GET_CHARA_G(0))%と%CALLNAME:(GET_CHARA_G(1))%と%CALLNAME:(GET_CHARA_G(2))%と%CALLNAME:(GET_CHARA_G(3))%が部屋へとやってきた
		
	ELSE
		
		PRINTFORM 夜遅くに扉がノックされると、そこには
		PRINTDATAW
			DATAFORM %CALLNAME:(GET_CHARA_M())%と%CALLNAME:TARGET%がいた
			DATAFORM %CALLNAME:TARGET%と%CALLNAME:(GET_CHARA_M())%がいた
		ENDDATA
	ENDIF
	PRINTL 
ENDIF

;選択による宴会と乱交への分岐を行う。
PRINTL さて、どうしようか。折角来てくれた事だし……
;CONFIGでランダム性を排除していたら選択できる。
IF FLAG:14 & 8
	PRINTFORML [0] \@(ARG:3 == 1) ? 乱交する # ベットへ誘う\@
	;鬼殺しを除いて、酒が10本以上ないと宴会できない。
	SIF ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 9
		PRINTL [1] 宴会する
	$INPUT_LOOP
	INPUT
ELSE
	RESULT = ((RAND:10 < (2+TALENT:鬼+TALENT:黄昏の怪物)) && ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 9) ? 1 # 0
ENDIF

IF RESULT == 1 && (ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 9)
	CALL NIGHT_ENKAI, ARG:3
ELSEIF RESULT == 0
	IF ARG:3 == 1
		CALL NIGHT_RANKOU, ARG:2
	ELSE
		CALL NIGHT_3P, ARG, ARG:1, GET_CHARA_M(), TARGET
	ENDIF
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF
PRINTL 

;-----------------------------------------------
;４人の関係
;GET_CHARA_G()の中身は変えない
;-----------------------------------------------
@NIGHT_4_RELATIONS, ARG:0, ARG:1, ARG:2, ARG:3
PC_VAR2 = 0
FOR PC_VAR1, 0, 4
	PL_VAR_X:PC_VAR1 = ARG:PC_VAR1
	SIF CHECK_MASTERS_CHILD(ARG:PC_VAR1) > 0
		PC_VAR2++
NEXT

SIF PC_VAR2 == 4
	RETURN 4





;=====================================================
;
;助手との夜イベント
;
;=====================================================
;-----------------------------------------------
;序章？
;----------------------------------------------
@NIGHT_ASSI1, ARG
;地の文カット
IF CFLAG:99 == 0
	PRINT その日の調教を終え、
	;料理技能判定。技能が高い方が食事を作る
	PRINTFORM \@(ABL:MASTER:料理技能 > ABL:ASSI:料理技能 || (RAND:2 == 0 && ABL:MASTER:料理技能 == ABL:ASSI:料理技能)) ? %CALLNAME:MASTER% # %CALLNAME:ASSI%\@
	PRINTL の作った夕餉を食べ終えて一息つくと
ENDIF

TARGET = ASSI
;助手の積極性チェック
CALL NIGHT_CHECK
;ここではどんなに確率高くても70%に押さえておく
LOCAL = MIN(RESULT, 70)
;助手が部屋を辞するときのイベント口上呼び出し
TRYCALL SELF_KOJO, TARGET, 460, LOCAL
TARGET = ARG
;地の文カット
SIF CFLAG:99 == 0
	PRINTFORML %CALLNAME:ASSI%は挨拶をして、%CALLNAME:MASTER%の部屋を去ろうとした。

;CONFIGでランダム性を排除していたら選択できる。
IF FLAG:14 & 8
	PRINTL 
	PRINTL [0] 呼び止める
	PRINTL [1] 見送る
	$INPUT_LOOP
	INPUT
ELSE
	LOCAL:1 = RAND:100
	RESULT = (LOCAL:1 < LOCAL) ? 0 # 1
ENDIF

IF RESULT == 0
	PRINTFORML %CALLNAME:MASTER%が、退出しようとする%CALLNAME:ASSI%を呼び止めると
	IF LOCAL > 90
		PRINTFORML %CALLNAME:ASSI%は何故か頬を上気させながら%CALLNAME:MASTER%の元へ駆け寄ってきた
	ELSEIF LOCAL < 50
		PRINTFORML %CALLNAME:ASSI%は何やら警戒するような様子で%CALLNAME:MASTER%の表情を伺った
	ELSE
		PRINTFORML %CALLNAME:ASSI%はどこか期待に満ちた瞳で見つめてきた
	ENDIF
ELSEIF RESULT == 1
	PRINTFORML %CALLNAME:MASTER%が、退出しようとする%CALLNAME:ASSI%に労いの言葉をかけ見送ると
	IF LOCAL > 90
		PRINTFORML %CALLNAME:ASSI%は名残惜しそうに部屋を出て行った…
	ELSEIF LOCAL < 50
		PRINTFORML %CALLNAME:ASSI%は一礼して部屋を出て行った…
	ELSE
		PRINTFORML %CALLNAME:ASSI%はどこか残念そうに部屋を出て行った…
	ENDIF
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF
PRINTFORML 

RETURN (1 - RESULT)

;-----------------------------------------------
;行動選択
;----------------------------------------------
@NIGHT_ASSI2, ARG
;助手の性向により選択肢の有無が増える
;マゾ→鞭打ち、サド→主人が鞭打ちされる、淫乱→夜の里へ繰り出す
CALLF SET_CHARA_M(MASTER)
TARGET = ASSI

PRINTL さてどうしようか。
;CONFIGでランダム性を排除していたら選択できる。
IF FLAG:14 & 8
	IF TALENT:ASSI:恋慕
		PRINTL [0] 情熱的な夜を過す
	ELSEIF GET_AGG(TARGET) > 90
		PRINTL [0] 奉仕してもらう
	ELSEIF GET_AGG(TARGET) < 50
		PRINTL [0] 欲情をぶつける
	ELSE
		PRINTL [0] 一夜を過す
	ENDIF
	SIF ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 1
		PRINTL [1] 酒を飲む
	SIF (ABL:サドっ気 > 1 || TALENT:サド || TALENT:ドＳ) && ITEM:鞭 || NOITEM
		PRINTL [2] 鞭打ちさせる
	SIF ABL:マゾっ気 > 1 || TALENT:マゾ || TALENT:ドＭ
		PRINTFORML [3] \@(ITEM:鞭 || NOITEM) ? 鞭打ちする # スパンキング\@
	SIF TALENT:淫乱 && (TALENT:オトコ || (TALENT:処女 == 0 && EXP:Ｖ経験 > 0)) && (TALENT:MASTER:オトコ || (TALENT:MASTER:処女 == 0 && EXP:MASTER:Ｖ経験 > 0))
		PRINTL [9] 里へ悦しみに行く
	$INPUT_LOOP
	INPUT
ELSE
	IF RAND:10 < 2 &&((ABL:サドっ気 > 1 || TALENT:サド || TALENT:ドＳ) && ITEM:鞭 || NOITEM)
		RESULT = 2
	ELSEIF RAND:10 < 2 && (ABL:マゾっ気 > 1 || TALENT:マゾ || TALENT:ドＭ)
		RESULT = 3
	ELSEIF (RAND:10 < (2+TALENT:鬼+TALENT:黄昏の怪物)) && (ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 1) 
		RESULT = 1
	ELSEIF RAND:10 < 2 && TALENT:淫乱 && (TALENT:オトコ || (TALENT:処女 == 0 && EXP:Ｖ経験 > 0)) && (TALENT:MASTER:オトコ || (TALENT:MASTER:処女 == 0 && EXP:MASTER:Ｖ経験 > 0))
		RESULT = 9
	ELSE
		RESULT = 0
	ENDIF
ENDIF
IF RESULT == 0
	CALL NIGHT_ASSI_SEX, ARG
ELSEIF RESULT == 1 && (ITEM:カルーア+ITEM:ビール+ITEM:ワイン+ITEM:大吟醸酒+ITEM:スピリタス > 1)
	CALL NIGHT_ASSI_SAKE
ELSEIF RESULT == 2 && ((ABL:サドっ気 > 1 || TALENT:サド || TALENT:ドＳ) && ITEM:鞭 || NOITEM)
	CALL NIGHT_ASSI_SM_S
ELSEIF RESULT == 3 && (ABL:マゾっ気 > 1 || TALENT:マゾ || TALENT:ドＭ)
	CALL NIGHT_ASSI_SM_M
ELSEIF RESULT == 9 && (TALENT:淫乱 && (TALENT:オトコ || (TALENT:処女 == 0 && EXP:Ｖ経験 > 0)) && (TALENT:MASTER:オトコ || (TALENT:MASTER:処女 == 0 && EXP:MASTER:Ｖ経験 > 0)))
	CALL NIGHT_ASSI_SEX_PH
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

;事後処理
TARGET = ASSI

;相愛or親愛の場合ストレスが0になる。直後に日を跨ぐから又増えるけど。
;相愛とか親愛付いてるのは、もう育てる必要なかったりエンディング後だったり。なのでバランスには影響ないはず。
IF RESULT == 0 && (TALENT:ASSI:親愛 || TALENT:ASSI:相愛)
	CFLAG:ASSI:ストレス = 0
;恋慕ならストレスが半分になる。
ELSEIF RESULT == 0 && TALENT:ASSI:恋慕
	CFLAG:ASSI:ストレス = CFLAG:ASSI:ストレス/2
;乗り気じゃなかった場合。ストレスが倍になる。
;既に助手可の状態で、このケースがあるのかは不明
ELSEIF RESULT == 0 && GET_AGG(TARGET) < 50
	CFLAG:ASSI:ストレス = 2*CFLAG:ASSI:ストレス
;どれでもなければストレスそのまま。
ENDIF

;=====================================================
;
;ここから反発奴隷のターン
;
;=====================================================
;-----------------------------------------------
;反発奴隷による、行動判定
;----------------------------------------------
;ARG:0：襲撃されるキャラ
;ARG:1：襲撃するキャラ
;ARG:2：輪姦の制御フラグ
;ARG:3：暴行が単独か複数犯かの分岐
;ARG:4：輪姦の珠処理用
@NIGHT_HANTEI_EX, ARG, ARG:1, ARG:2, ARG:3, ARG:4
;分岐判定はLOCAL
LOCAL = 0

;寝取られ判定。
;被害者が奴隷かつ{ARG:1との相性が良い場合or(ARG:1の技巧が主人よりも高くて
;ARGから見た好感がARG:1>=主人)}
SIF ARG != MASTER && (RELATION:ARG:(NO:(ARG:1)) >= 150 || (ABL:(ARG:1):12 > ABL:MASTER:技巧)) && RELATION:ARG:(NO:(ARG:1)) >= RELATION:ARG:(NO:MASTER)
	LOCAL += 8
;どちらも貞操帯を付けていない
SIF (CFLAG:ARG:42 & 2) == 0 && (CFLAG:(ARG:1):42 & 2) == 0
	LOCAL += 4
;ARGは、棒があり、ARG:1に穴があり非処女
SIF (TALENT:ARG:オトコ || TALENT:ARG:ふたなり) && (TALENT:(ARG:1):120 == 0 && TALENT:(ARG:1):0 == 0)
	LOCAL += 2
;ARG:1は、棒がある、または、{禁断or調合持ちで、ARGには穴があり非処女}
SIF ((TALENT:(ARG:1):120 || TALENT:(ARG:1):121) || (TALENT:(ARG:1):55 || TALENT:(ARG:1):56)) && (TALENT:ARG:オトコ == 0 && TALENT:ARG:処女 == 0)
	LOCAL += 1

;NTRはまんま寝取られ。Mは棒付きを寝取る。Fなら棒なしを寝取る
IF LOCAL > 13
	CALL NIGHT_NTR_M, ARG, ARG:1
ELSEIF LOCAL > 12
	CALL NIGHT_NTR_F, ARG, ARG:1
ENDIF
;あれ？NTRれたのにここで抜けないの？


LOCAL = 0
;ARGがMASTERならば、ARG:3は基本的に0か7
;ARGがその他ならば、ARG:3は基本的に0か8
;集団輪姦or暴行。ARG:3はこの時点では7か8か0しかない
;ARG:3 = 7:主人襲撃,8:恋慕キャラ輪姦
IF ARG:3 > 6
	;ARG:3 = 8なら、ARGはその他
	;ARGは女性か
	SIF TALENT:ARG:オトコ == 0
		LOCAL += 1
	;GET_CHARA_G(1)以外の襲撃者は棒つきか
	SIF (TALENT:(ARG:1):120 || TALENT:(ARG:1):121) && (TALENT:(GET_CHARA_G(2)):120 || TALENT:(GET_CHARA_G(2)):121) && (TALENT:(GET_CHARA_G(3)):120 || TALENT:(GET_CHARA_G(3)):121)
		LOCAL += 1
	;GET_CHARA_G(1)は襲撃者(ARG:7 == 7)なら棒ないとダメ
	SIF ARG:3 < 8 && (TALENT:(GET_CHARA_G(1)):120 || TALENT:(GET_CHARA_G(1)):121)
		LOCAL--
	;関係者は全員貞操帯をつけていないか？
	SIF (CFLAG:ARG:42 & 2) == 0 && (CFLAG:(ARG:1):42 & 2) == 0 && (CFLAG:(GET_CHARA_G(1)):42 & 2) == 0 && (CFLAG:(GET_CHARA_G(2)):42 & 2) == 0 && (CFLAG:(GET_CHARA_G(3)):42 & 2) == 0
		LOCAL += 1
	
	;LOCAL=3で輪姦、それ以外で暴行
	IF LOCAL == 3
		CALL NIGHT_RINKAN, ARG, ARG:1, GET_CHARA_G(1), GET_CHARA_G(2), GET_CHARA_G(3), ARG:4, ARG:2
	ELSE
		CALL NIGHT_BOUKOU, ARG, ARG:1, GET_CHARA_G(1), GET_CHARA_G(2), GET_CHARA_G(3), ARG:3
	ENDIF
;主犯が襲う
ELSE
	;禁断or触手持ちで、相手が貞操帯を付けていない女なら触手陵辱(紳士的な触手がOFFならだめ）
	IF RAND:4 == 0 && (TALENT:禁断の知識 || TALENT:触手使役術) && (TALENT:ARG:オトコ == 0 && (CFLAG:ARG:42 & 2) == 0) && FLAG:15 & 4096
		CALL NIGHT_SHOKUSHU, ARG, ARG:1
	;それ以外の場合
	ELSE
		;襲撃者が貞操帯をつけてない
		SIF (CFLAG:(ARG:1):42 & 2) == 0
			LOCAL += 3
		;対象が女
		SIF TALENT:ARG:オトコ == 0
			LOCAL += 1
		;襲撃者が棒を持っている
		SIF TALENT:(ARG:1):120 || TALENT:(ARG:1):121
			LOCAL += 1
		;対象が主人で、襲撃者が調合or禁断持ちだ
		SIF ARG == MASTER && (TALENT:(ARG:1):55 || TALENT:(ARG:1):56)
			LOCAL += 2
		;対象が男奴隷である。もしくは男主人だが、禁断も調合も無い
		SIF TALENT:ARG:オトコ && (ARG != MASTER || (TALENT:(ARG:1):55 == 0 && TALENT:(ARG:1):56 == 0))
			LOCAL = 0
		;対象が男奴隷で、襲撃者に穴があり処女ではなく貞操帯を装備していない
		SIF (TALENT:ARG:オトコ && ARG != MASTER) && (TALENT:(ARG:1):0 == 0 && TALENT:(ARG:1):120 == 0 && (CFLAG:(ARG:1):42 & 2) == 0)
			LOCAL = -1
		;対象が貞操帯をつけている
		SIF (CFLAG:ARG:42 & 2)
			LOCAL = 0
		
		;Nはnormal、Sはsleep
		;決して男が女に比べ、節操無しというわけではないが。
		;でも画面の前の皆なら、例え『あなた』への報復の為でも、美人が腰振ってくれたら堕ちちゃうよね？
		IF RAND:3 == 0 && LOCAL > 4
			CALL NIGHT_GOUKAN_S, ARG, ARG:1, ARG:4
		ELSEIF RAND:2 == 0 && LOCAL > 0
			CALL NIGHT_GOUKAN_N, ARG, ARG:1
		ELSEIF RAND:2 == 0 && LOCAL < 0
			CALL NIGHT_NTR_M, ARG, ARG:1
		ELSE
			CALL NIGHT_BOUKOU, ARG, ARG:1, GET_CHARA_G(1), GET_CHARA_G(2), GET_CHARA_G(3), ARG:3
		ENDIF
	ENDIF
ENDIF

;-----------------------------------------------
;積極性のチェック関数。ランダム検索しない場合は下ではなくこちらの関数を使います
@NIGHT_CHECK
LOCAL = 20
;恋慕
SIF TALENT:恋慕
	LOCAL += 10
;淫乱
SIF TALENT:淫乱
	LOCAL += 10
;従順
	LOCAL += (ABL:従順*ABL:従順)
;従順3以上でボーナス
SIF ABL:従順 >= 3
	LOCAL += 15
;従順4以上でボーナス
SIF ABL:従順 >= 4
	LOCAL += 5
;従順5以上でボーナス
SIF ABL:従順 >= 5
	LOCAL += 10
;欲望
	LOCAL += (ABL:欲望*ABL:欲望)
;欲望3以上でボーナス
SIF ABL:欲望 >= 3
	LOCAL += 15
;欲望4以上でボーナス
SIF ABL:欲望 >= 4
	LOCAL += 10
;欲望5以上でボーナス
SIF ABL:欲望 >= 5
	LOCAL += 15
;理性
SIF !TALENT:理性
	TIMES LOCAL, 1.10
;発情中
SIF TALENT:常時発情 || (TALENT:発情可 && (TALENT:危険日 || TALENT:排卵))
	TIMES LOCAL, 1.20
;調教度
IF CFLAG:3668 <= -700
	TIMES LOCAL , 0.80
ELSEIF CFLAG:3668 <= -350
	TIMES LOCAL , 0.90
ENDIF
;臆病
SIF TALENT:臆病
	TIMES LOCAL , 0.85
;反抗的
SIF TALENT:反抗的
	TIMES LOCAL , 0.85
;素直
SIF TALENT:素直
	TIMES LOCAL , 1.20
;大人しい
SIF TALENT:大人しい
	TIMES LOCAL , 0.85
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL , 0.70
;好奇心
SIF TALENT:好奇心
	TIMES LOCAL , 1.30
;保守的
SIF TALENT:保守的
	TIMES LOCAL , 0.85
;楽観的
SIF TALENT:楽観的
	TIMES LOCAL , 1.35
;一線超えない
SIF TALENT:一線越えない
	TIMES LOCAL , 0.85
;抑圧
SIF TALENT:抑圧
	TIMES LOCAL , 0.75
;解放
SIF TALENT:解放
	TIMES LOCAL , 1.35
;恥じらい
IF TALENT:恥じらい
	TIMES LOCAL , 0.90
;恥薄い
ELSEIF TALENT:恥薄い
	TIMES LOCAL , 1.25
ENDIF
;快感に素直
IF TALENT:快感に素直
	TIMES LOCAL , 1.25
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES LOCAL , 0.60
ENDIF
;恋慕
SIF TALENT:恋慕
	TIMES LOCAL , 1.30
;淫乱
SIF TALENT:淫乱
	TIMES LOCAL , 2.00
;男嫌い(主人がオトコの場合のみ)
IF TALENT:男嫌い && TALENT:MASTER:オトコ
	TIMES LOCAL , 0.75
;女嫌い(主人がオトコじゃないの場合のみ)
ELSEIF TALENT:女嫌い && TALENT:MASTER:オトコ == 0
	TIMES LOCAL , 0.75
ENDIF
;反発刻印2以上
SIF MARK:反発刻印 >= 2
	TIMES LOCAL , 0.50
;恐怖刻印3以上
SIF MARK:恐怖刻印 >= 3
	TIMES LOCAL , 0.65
;相性
SIF RELATION:(NO:MASTER) != 0
	LOCAL = (LOCAL*RELATION:(NO:MASTER))/100

;対象が欲望LV3未満、従順LV0だとダメ
SIF ABL:欲望 < 3 || ABL:従順 < 1
	LOCAL = 0
;反発刻印3で奴隷に棒有り、主人に穴有りで襲いに来る(誰得
;棒が無くても触手操作or禁断の知識持ちの反発3で女主人公なら襲いに来る
;反発3有れば徘徊する
SIF MARK:反発刻印 == 3
	LOCAL = 99
;調教した事がないもしくは好感度が0のキャラはダメ(反発あっても許さない)
SIF CFLAG:1610 == 0 || CFLAG:好感度 == 0
	LOCAL = 0
;その場にいないor眠っていてもダメ(反発あっても許さない)
SIF CFLAG:4 || EQUIP:睡眠薬
	LOCAL = 0
;抱卵中は夜這いどころではない！(妊娠や懐卵などとは意味合いが違う！)
SIF TALENT:抱卵中
	LOCAL = 0
;体力が切れ掛かっているとき、気力切れのとき、酔っ払っているとき、ストレスが大きいときは夜這いせずに休む
SIF BASE:酔い > MAXBASE:8 || BASE:体力 < 500 || BASE:気力 < 1 || CFLAG:ストレス >= PALAMLV:3
	LOCAL = 0

;反発刻印持ち排除システム発動
SIF MARK:反発刻印 && FLAG:12 & 131072 == 0 && FLAG:3 < 4
	LOCAL = 0

CALLF SET_AGG(TARGET, LOCAL)

RETURN LOCAL

;こちらは全奴隷を見て、その奴隷がどのくらい夜這いに来やすいか見る関数です
@NIGHT_CHECK2, ARG
#FUNCTION
LOCAL:10 = 0
REPEAT CHARANUM
	;実行者が250人に達したらREPEAT脱出
	SIF LOCAL:10 >= 250
		BREAK
	;対象が主人ならスキップ
	SIF COUNT == MASTER
		CONTINUE
	;対象がその場にいないならスキップ
	SIF CFLAG:COUNT:4
		CONTINUE
	;調教した事がないもしくは好感度が0のキャラはスキップ
	SIF CFLAG:COUNT:1610 == 0 || CFLAG:COUNT:好感度 <= 0
		CONTINUE
	;対象が眠っているならスキップ
	SIF EQUIP:COUNT:睡眠薬
		CONTINUE
	;対象が抱卵中ならスキップ
	SIF TALENT:COUNT:抱卵中
		CONTINUE
	;対象の体力が切れ掛かっているとき、気力切れのとき、酔っ払っているとき、ストレスが大きいときはスキップ
	SIF BASE:COUNT:酔い > MAXBASE:COUNT:8 || BASE:COUNT:体力 < 500 || BASE:COUNT:気力 < 1 || CFLAG:COUNT:ストレス >= PALAMLV:3
		CONTINUE
	;対象が欲望LV3未満、従順LV0だとダメ
	;反発刻印3で奴隷に棒有り犯しに来る(誰得
	;棒が無くても触手操作or禁断の知識持ちの反発3で女主人公なら襲いに来る
	;遂に反発3だけでも不穏な事を考えて徘徊するようになった
	;IF MARK:COUNT:反発刻印 == 3
		
	SIF ABL:COUNT:欲望 < 3 || ABL:COUNT:従順 < 1 
		CONTINUE

	;発動判定値計算
	LOCAL = ARG
	;従順
	LOCAL += (ABL:COUNT:従順*ABL:COUNT:従順)
	;欲望
	LOCAL += (ABL:COUNT:欲望*ABL:COUNT:欲望)
;	;従順3以上でボーナス
;	SIF ABL:COUNT:従順 >= 3
;		LOCAL += 10
;	;欲望3以上でボーナス
;	SIF ABL:COUNT:欲望 >= 3
;		LOCAL += 10
;	;欲望5LVで更にボーナス
;	SIF ABL:COUNT:欲望 >= 5
;		LOCAL += 15
	;反発刻印1以上の時
	SIF MARK:COUNT:反発刻印 >= 1
		TIMES LOCAL , 0.50
	;恐怖刻印1以上の時
	SIF MARK:COUNT:恐怖刻印 >= 1
		TIMES LOCAL , 0.80
	;恐怖刻印2以上の時
	SIF MARK:COUNT:恐怖刻印 >= 2
		TIMES LOCAL , 0.80
	;理性
	SIF !TALENT:COUNT:理性
		TIMES LOCAL, 1.10
	;発情中
	SIF TALENT:COUNT:常時発情 || (TALENT:COUNT:発情可 && (TALENT:COUNT:危険日 || TALENT:COUNT:排卵))
		TIMES LOCAL, 1.20
	;調教度
	IF CFLAG:COUNT:3668 <= -700
		TIMES LOCAL , 0.80
	ELSEIF CFLAG:COUNT:3668 <= -350
		TIMES LOCAL , 0.90
	ENDIF
	;臆病
	SIF TALENT:COUNT:臆病
		TIMES LOCAL , 0.80
	;反抗的
	SIF TALENT:COUNT:反抗的
		TIMES LOCAL , 0.80
	;素直
	SIF TALENT:COUNT:素直
		TIMES LOCAL , 1.05
	;大人しい
	SIF TALENT:COUNT:大人しい
		TIMES LOCAL , 0.85
	;感情乏しい
	SIF TALENT:COUNT:感情乏しい
		TIMES LOCAL , 0.50
	;好奇心
	SIF TALENT:COUNT:好奇心
		TIMES LOCAL , 1.05
	;保守的
	SIF TALENT:COUNT:保守的
		TIMES LOCAL , 0.85
	;一線超えない
	SIF TALENT:COUNT:一線越えない
		TIMES LOCAL , 0.85
	;抑圧
	SIF TALENT:COUNT:抑圧
		TIMES LOCAL , 0.70
	;解放
	SIF TALENT:COUNT:解放
		TIMES LOCAL , 1.05
	;抵抗
	SIF TALENT:COUNT:抵抗
		TIMES LOCAL , 0.80
	;恥じらい
	IF TALENT:COUNT:恥じらい
		TIMES LOCAL , 0.85
	;恥薄い
	ELSEIF TALENT:COUNT:恥薄い
		TIMES LOCAL , 1.05
	ENDIF
	;快感に素直
	IF TALENT:COUNT:快感に素直
		TIMES LOCAL , 1.20
	;快感の否定
	ELSEIF TALENT:COUNT:快感の否定
		TIMES LOCAL , 0.60
	ENDIF
;	;恋慕
;	SIF TALENT:COUNT:恋慕
;		TIMES LOCAL , 1.15
	;淫乱
	SIF TALENT:COUNT:淫乱
		TIMES LOCAL , 1.20
	;男嫌い(主人がオトコの場合のみ)
	IF TALENT:COUNT:男嫌い && TALENT:MASTER:オトコ
		TIMES LOCAL , 0.70
	;女嫌い(主人がオトコじゃない場合のみ)
	ELSEIF TALENT:COUNT:女嫌い && TALENT:MASTER:オトコ == 0
		TIMES LOCAL , 0.70
	ENDIF
	;相性
	SIF RELATION:COUNT:(NO:MASTER) != 0
		LOCAL = LOCAL + (LOCAL * RELATION:COUNT:(NO:MASTER) )/1000
	;反発刻印3で奴隷に棒有り、主人に穴有りでやるき満々(誰得
	;遂に主人が男でも関係なくなった。
	;棒が無くても触手操作or禁断の知識持ちの反発3で女主人公なら襲いに来る
	;遂に反発だけで良くなった。
	SIF MARK:COUNT:反発刻印 == 3
		LOCAL = 90

	;どれだけ高くなっても、その人が行動を起こす可能性は90%
	LOCAL = MIN(LOCAL, 90)
	
	;反発刻印持ち排除システム発動
	SIF MARK:COUNT:反発刻印 && FLAG:12 & 131072 == 0 && FLAG:3 < 4
		LOCAL = 0

	SIF TALENT:MASTER:998
		PRINTFORML %CALLNAME:COUNT%が行動を起こす確率：{LOCAL}％

	CALLF SET_AGG(COUNT, LOCAL)

	;実行判定
	IF LOCAL > RAND:100
		TFLAG:(400+LOCAL:10) = COUNT
		LOCAL:10 += 1
	ENDIF
REND

RETURNF LOCAL:10

;-------------------------------------------------
;女性化
;-------------------------------------------------
@SHIFT_TO_F, ARG
;既に女性なら帰れ
SIF !TALENT:ARG:オトコ
	RETURN 0
;虹の飴玉より流用
IF CFLAG:ARG:4995 == 0
	CFLAG:ARG:4995 = 1
	;男が初めてTSしたら処女に決まってんだろJK
	CFLAG:ARG:4996 = 1
	;元が男の場合は胸のサイズをランダムで決定
	;ただし、小柄・小人体型はそこから1サイズ小さく、巨躯は1サイズ大きくする
	LOCAL:1 = RAND:100
	IF LOCAL:1 < 5
		CFLAG:ARG:4997 = 0
	ELSEIF LOCAL:1 < 25
		CFLAG:ARG:4997 = 1
	ELSEIF LOCAL:1 > 95 - (TALENT:ARG:巨躯 * 20 + (TALENT:ARG:小人体型  + TALENT:ARG:小柄体型) * 5)
		CFLAG:ARG:4997 = 4
	ELSEIF LOCAL:1 > 75
		CFLAG:ARG:4997 = 3
	ELSE
		CFLAG:ARG:4997 = 2
	ENDIF
	CFLAG:ARG:4997 += (TALENT:ARG:巨躯 - TALENT:ARG:小柄体型 - TALENT:ARG:小人体型)
	IF CFLAG:ARG:4997 < 0
		CFLAG:ARG:4997 = 0
	ELSEIF CFLAG:ARG:4997 > 4
		CFLAG:ARG:4997 = 4
	ENDIF
ENDIF
TALENT:ARG:処女 = CFLAG:ARG:4996
TALENT:ARG:オトコ = 0
TALENT:ARG:ふたなり = CFLAG:ARG:4998
;[早漏]、[遅漏]関係の保存
CALL CHANGE_SORO_COMMON, ARG
IF TALENT:ARG:ふたなり
	MAXBASE:ARG:2 = CALC_GAUGE2_COMMON(ARG)
ELSE
	MAXBASE:ARG:2 = 0
ENDIF
TALENT:ARG:母乳体質 = CFLAG:ARG:4999
SIF TALENT:ARG:母乳体質
	CALL CALC_GAUGE3_SLAVE, ARG, 1
IF CFLAG:ARG:4997 == 0
	TALENT:ARG:絶壁 = 1
ELSEIF CFLAG:ARG:4997 == 1
	TALENT:ARG:貧乳 = 1
ELSEIF CFLAG:ARG:4997 == 3
	TALENT:ARG:巨乳 = 1
ELSEIF CFLAG:ARG:4997 == 4
	TALENT:ARG:爆乳 = 1
ENDIF
;40薬物経験
CALL COMMON_UP_EXP, ARG, 40, 1, 2
IF (CFLAG:ARG:20 & 512) == 0
	;50異常経験
	CALL COMMON_UP_EXP, ARG, 50, 1, 2
	CFLAG:ARG:20 |= 512
ENDIF

;積極性保管用汎用セッター・ゲッター
@GET_AGG(ARG)
#FUNCTION
RETURNF AGGRESSIVE("GET", ARG)

@SET_AGG(ARG, ARG:1)
#FUNCTION
RETURNF AGGRESSIVE("SET", ARG, ARG:1)

@CLEAR_AGG
#FUNCTION
RETURNF AGGRESSIVE("CLEAR")

@AGGRESSIVE(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF LOCAL:ARG
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF 0
ENDIF
RETURNF 0

;夜イベント対象キャラ保存用セッターゲッター
@GET_CHARA_M
#FUNCTION
RETURNF CHARA_STOCK("GET", "M")

@GET_CHARA_G(ARG)
#FUNCTION
RETURNF CHARA_STOCK("GET", "G", ARG)

@SET_CHARA_M(ARG)
#FUNCTION
RETURNF CHARA_STOCK("SET", "M", ARG)

@SET_CHARA_G(ARG, ARG:1)
#FUNCTION
RETURNF CHARA_STOCK("SET", "G", ARG, ARG:1)

@CLEAR_CHARA_STOCK
#FUNCTION
RETURNF CHARA_STOCK("CLEAR")

@CHARA_STOCK(ARGS, ARGS:1, ARG, ARG:1)
#FUNCTION
IF ARGS == "GET"
	RETURNF (ARGS:1 == "M") ? LOCAL:10 # LOCAL:ARG
ELSEIF ARGS == "SET"
	IF ARGS:1 == "M"
		LOCAL:10 = ARG
		RETURNF ARG
	ELSE
		LOCAL:ARG = ARG:1
		RETURNF ARG:1
	ENDIF
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF 0
ENDIF
RETURNF 0

;口上用フラグ保存
@GET_NIGHT_KOJO(ARG)
#FUNCTION
RETURNF NIGHT_KOJO("GET", ARG)

@SET_NIGHT_KOJO(ARG, ARG:1)
#FUNCTION
RETURNF NIGHT_KOJO("SET", ARG, ARG:1)

@CLEAR_NIGHT_KOJO()
#FUNCTION
RETURNF NIGHT_KOJO("CLEAR")

@NIGHT_KOJO(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF ARG:1
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF 0
ENDIF
RETURNF 0
