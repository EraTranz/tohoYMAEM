;=============================================================================
;eratoho系で必要な東方関係固有のイベント
;=============================================================================
;-------------------------------------------------
;[半白澤]で時間が夜の時、1/3の確率でハクタクに変身
;-------------------------------------------------
@CHANGE_HAKUTAKU
;調教対象
SIF TARGET >= 0 && TALENT:半白澤 == 1 && RAND:3 == 0
	CALL HAKUTAKU_ON, TARGET
;主人
SIF MASTER >= 0 && TALENT:MASTER:半白澤 == 1 && RAND:3 == 0
	CALL HAKUTAKU_ON, MASTER
;助手
SIF ASSI >= 0 && TALENT:ASSI:半白澤 == 1 && RAND:3 == 0
	CALL HAKUTAKU_ON, ASSI

;-------------------------------------------------
;[半白澤]でハクタク化している時、元に戻る
;-------------------------------------------------
@EVENT_NEXTDAY_TOHO
;調教対象
SIF TARGET >= 0 && TALENT:半白澤 == 2
	CALL HENSHIN_OFF, TARGET
;主人
SIF MASTER >= 0 && TALENT:MASTER:半白澤 == 2
	CALL HENSHIN_OFF, MASTER
;助手
SIF ASSI >= 0 && TALENT:ASSI:半白澤 == 2
	CALL HENSHIN_OFF, ASSI
IF FLAG:408 >= 50 && CHECK_GAMEMODE_ENDING_EXIST() == 1 && (FLAG:70 & 256) == 0
	PRINTFORML %CALLNAME:MASTER%はハクタクの詳しい生態を理解した…
	PRINTFORMW ※強くてニューゲーム開始時に%CALLNAME:MASTER%の種族を[半白澤]に選択することができます
	FLAG:70 |= 256
ENDIF

;=============================================================================
;ハクタク変身・解除処理
;=============================================================================
;TALENT:半白澤の値が1の時は人間、2の時はハクタク化
;-------------------------------------------------
;変身処理(ハクタク化)
;-------------------------------------------------
@HAKUTAKU_ON, ARG
PRINTFORMW ＜%CALLNAME:ARG%はハクタクの姿になった＞
TALENT:ARG:半白澤 = 2

;-------------------------------------------------
;変身解除処理(人間化)
;-------------------------------------------------
@HENSHIN_OFF, ARG
PRINTFORMW ＜%CALLNAME:ARG%は元の姿にもどった＞
TALENT:ARG:半白澤 = 1
;主人以外の場合、変身回数をカウント
SIF ARG != MASTER
	FLAG:408 += 1

;=============================================================================
;人狼発毛処理
;=============================================================================
;-------------------------------------------------
;[人狼]で時間が夜の時、1/3の確率で[パイパン]を失う
;-------------------------------------------------
@HATUMOU_OOKAMI
;調教対象
SIF TARGET >= 0 && TALENT:パイパン == 1 && TALENT:人狼 == 1 && RAND:3 == 0
	CALL HATUMOU_ON, TARGET
;主人
SIF MASTER >= 0 && TALENT:パイパン == 1 && TALENT:人狼 == 1 && RAND:3 == 0
	CALL HATUMOU_ON, MASTER
;助手
SIF ASSI >= 0 && TALENT:パイパン == 1 && TALENT:人狼 == 1 && RAND:3 == 0
	CALL HATUMOU_ON, ASSI
;-------------------------------------------------
;発毛処理(オオカミ化)
;-------------------------------------------------
@HATUMOU_ON, ARG
PRINTFORML ＜%CALLNAME:ARG%は毛深くなった＞
PRINTFORMW %CALLNAME:ARG%は[パイパン]を失った
TALENT:ARG:パイパン = 0
BASE:ARG:陰毛 = 1000
