;=============================================================================
;キャラ入れ替え・ソート処理
;=============================================================================
;ここで使用する変数
; LOCAL:0  : 20限定表記に使用 現在ページ
; LOCAL:1  : 入れ替えに使用   一人めの奴隷番号
; LOCAL:2  : 入れ替えに使用   二人めの奴隷番号
; LOCAL:4  : 20限定表記に使用 表記できる奴隷の人数
; LOCAL:5  : 20限定表記に使用 ↑を20で割ったもの・最大ページの算出に使う
; LOCAL:99 : 20限定表記に使用 現在ページの保存・再現
; MARK:98  : 1 = 存在 2 = 調教対象 3 = 助手
; TFLAG:0  : オートソート時に使用
; TFLAG:1  : オートソート時に使用
; TFLAG:2  : オートソート時に使用
;--------------------------------------------------
;キャラ入れ替え・ソート
;--------------------------------------------------
@CHARA_SORT
;前回抜けたとき表示していたページを再現
VARSET LOCAL, 0
CALLF CLEAR_LIST
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	;主人公は除外
	SIF COUNT == 0
		CONTINUE
	;失踪中も除外
	SIF CFLAG:COUNT:4
		CONTINUE
	;調教対象と助手を保存する
	IF COUNT == TARGET
		MARK:COUNT:98 = 2
	ELSEIF COUNT == ASSI
		MARK:COUNT:98 = 3
	ELSE
		MARK:COUNT:98 = 1
	ENDIF
	;20限定表記の基礎処理
	CALLF SET_LIST, LOCAL:4, COUNT
	LOCAL:4 += 1
REND
IF LOCAL:4 < 1
	PRINTW 並び替えのできる奴隷がいません
	RETURN 0
ELSE
	PRINTFORML 順番を並べ変えます
ENDIF
LOCAL:5 = (LOCAL:4 - 1) / 20

;一人目選択
$CHARA_SORT_FIRST
TFLAG:0 = 0
PRINTFORML 誰を入れ替えますか？ ＜page.{LOCAL + 1}＞
DRAWLINE
CALL SORT_LIST, LOCAL, -1
DRAWLINE
PRINTL 下の項目でオートソートする
PRINTL [1102]キャラ番号　　[1103]能力　　　[1104]経験
PRINTL [1105]刻印　　　　　[1106]珠　　　　[1107]記念日
PRINTFORML [1108]体力気力理性\@(FLAG:16 & 16) ? および体格 #\@
PRINTL [1109]好感度稼ぎストレスなど
DRAWLINE
PRINTFORMLC \@(LOCAL == 0) ? %" " * 13% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:5) ? %" " * 13% # [1009]後のページ\@
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	CVARSET MARK, 98, 0
	RETURN 0
ELSEIF RESULT == 1102
	TSTR:99 = キャラ番号
ELSEIF RESULT == 1103
	TSTR:99 = 能力
ELSEIF RESULT == 1104
	TSTR:99 = 経験
ELSEIF RESULT == 1105
	TSTR:99 = 刻印
ELSEIF RESULT == 1106
	TSTR:99 = 珠
ELSEIF RESULT == 1107
	TSTR:99 = 記念日
ELSEIF RESULT == 1108
	TSTR:99 = 体力気力理性
	
	
ELSEIF RESULT == 1109
	TSTR:99 = 好感度稼ぎストレスなど
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO CHARA_SORT_FIRST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:5
	LOCAL += 1
	GOTO CHARA_SORT_FIRST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE 無効な値です
	GOTO INPUT_LOOP_1
ENDIF
IF RESULT >= 1102 && RESULT <= 1109
	TFLAG:0 = RESULT - 1102
	CALL CHARA_SORT_INPUT
	RESTART
ENDIF
LOCAL:1 = RESULT
DRAWLINE
DRAWLINE
;二人目選択
$CHARA_SORT_SECOND
PRINTFORML 誰を入れ替えますか？(現在 {LOCAL:1}.%CALLNAME:(LOCAL:1)% を選択中) ＜page.{LOCAL + 1}＞
DRAWLINE
CALL SORT_LIST, LOCAL, LOCAL:1
DRAWLINE
PRINTL 　　　☆:調教中 ★:助手 ○:助手可 ●:元助手を表しています
DRAWLINE
PRINTFORMLC \@(LOCAL == 0) ? %" " * 13% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:5) ? %" " * 13% # [1009]後のページ\@
PRINTL 
$INPUT_LOOP_2
INPUT
IF RESULT == 1000
	GOTO CHARA_SORT_FIRST
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO CHARA_SORT_SECOND
ELSEIF RESULT == 1009 && LOCAL < LOCAL:5
	LOCAL += 1
	GOTO CHARA_SORT_SECOND
ELSEIF RESULT < 0 || RESULT >= CHARANUM || RESULT == LOCAL:1 || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE 無効な値です
	GOTO INPUT_LOOP_2
ENDIF
LOCAL:2 = RESULT

PRINTFORMW %CALLNAME:(LOCAL:1)%と%CALLNAME:(LOCAL:2)%を入れ替えました
SWAPCHARA LOCAL:1, LOCAL:2
;調教対象と助手を保存していたものにする
REPEAT CHARANUM
	SIF MARK:COUNT:98 == 2
		TARGET = COUNT
	SIF MARK:COUNT:98 == 3
		ASSI = COUNT
REND
RESTART

;--------------------------------------------------
;キャラオートソート対象選択
;--------------------------------------------------
@CHARA_SORT_INPUT
SIF TFLAG:0 == 0
	GOTO UP_DOWN
VARSET LOCALS,""
SPLIT "誕生日,購入された日,連れてこられた日,結婚記念日,契約記念日,陥落した日",",",LOCALS
TFLAG:2 = 0
LOCAL:1 = 0
$PRINT_LIST
LOCAL:2 = 0
CALLF CLEAR_LIST
DRAWLINE
PRINTFORML ソート基準の%TSTR:99%を選択してください。 ＜page.{LOCAL:1}＞
IF TFLAG:0 == 5
	REPEAT 6
		TSTR:0 = %LOCALS:COUNT%
		SIF LOCAL:2 / 30 >= LOCAL:1 && LOCAL:2 / 30 < 1 + LOCAL:1
			PRINTFORMLC [{COUNT, 3}] %TSTR:0, 12, LEFT%
		LOCAL:2 += 1
		CALLF SET_LIST, COUNT, 1
	REND
ELSE
	REPEAT 250
		IF TFLAG:0 == 1
			SIF COUNT >= 100
				CONTINUE
			SIF STRLENS (ABLNAME:COUNT) < 1
				CONTINUE
			TSTR:0 = %ABLNAME:COUNT%
		ELSEIF TFLAG:0 == 2
			SIF COUNT >= 100
				CONTINUE
			SIF STRLENS(EXPNAME:COUNT) < 1
				CONTINUE
			TSTR:0 = %EXPNAME:COUNT%
		ELSEIF TFLAG:0 == 3
			SIF COUNT >= 12
				CONTINUE
			SIF STRLENS(MARKNAME:COUNT) < 1
				CONTINUE
			TSTR:0 = %MARKNAME:COUNT%
		ELSEIF TFLAG:0 == 4
			SIF (COUNT >= 20 && COUNT < 30) || COUNT > 198
				CONTINUE
			SIF STRLENS(PALAMNAME:COUNT) < 1
				CONTINUE
			TSTR:0 = %PALAMNAME:COUNT%の珠
		ELSEIF TFLAG:0 == 6
			SIF COUNT >= 2 && COUNT < 9 || COUNT > 99
				CONTINUE
			SIF (FLAG:16 & 16) == 0 && COUNT >= 10
				CONTINUE
			SIF STRLENS(BASENAME:COUNT) < 1
				CONTINUE
			TSTR:0 = %BASENAME:COUNT%
		ELSEIF TFLAG:0 == 7
			SIF STRLENS(CFLAGNAME:COUNT) < 1
				CONTINUE
			SIF (COUNT == 31 || (COUNT >= 60 && COUNT <= 64)) && TALENT:MASTER:超常目算 == 0 && TALENT:MASTER:998 == 0
				CONTINUE
			SIF (COUNT == 80 || COUNT == 81) && FLAG:13 & 1024 == 0
				CONTINUE
			TSTR:0 = %CFLAGNAME:COUNT%
		ENDIF
		SIF LOCAL:2 / 30 >= LOCAL:1 && LOCAL:2 / 30 < 1 + LOCAL:1
			PRINTFORMLC [{COUNT, 3}] %TSTR:0, 12, LEFT%
		LOCAL:2 += 1
		CALLF SET_LIST, COUNT, 1
	REND
ENDIF
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL:1 <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL:1 >= LOCAL:2 / 30) ? %" " * 16% # [1009]次のページ\@
PRINTL 
$INPUT_LOOP_0
INPUT
IF RESULT == 1000
	RETURN 1
ELSEIF RESULT == 1001
	IF LOCAL:1 > 0
		LOCAL:1 -= 1
		GOTO PRINT_LIST
	ELSE
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT == 1009
	IF LOCAL:1 < LOCAL:2 / 30
		LOCAL:1 += 1
		GOTO PRINT_LIST
	ELSE
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT < 0 || RESULT > 250
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ELSEIF GET_LIST(RESULT) == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ELSE
	TFLAG:2 = RESULT
ENDIF

IF TFLAG:0 == 1
	TSTR:0 = %ABLNAME:RESULT%
ELSEIF TFLAG:0 == 2
	TSTR:0 = %EXPNAME:RESULT%
ELSEIF TFLAG:0 == 3
	TSTR:0 = %MARKNAME:RESULT%
ELSEIF TFLAG:0 == 4
	TSTR:0 = %PALAMNAME:RESULT%の珠
ELSEIF TFLAG:0 == 5
	TFLAG:2 += 3110
	TSTR:0 = %LOCALS:RESULT%
ELSEIF TFLAG:0 == 6
	TSTR:0 = %BASENAME:RESULT%
ELSEIF TFLAG:0 == 7
	IF (RESULT == 80 || RESULT == 81) && FLAG:13 & 1024 == 0
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP_0
	ENDIF
	TSTR:0 = %CFLAGNAME:RESULT%
ENDIF
IF TFLAG:0 == 6 && TFLAG:2 > 9 && FLAG:16 & 16
	REPEAT CHARANUM
		CALL MAKE_SIZE, 1, COUNT
	REND
ENDIF
TFLAG:1 = 0
$UP_DOWN
PRINTFORML どう並べますか？
DRAWLINE
PRINTL [1]昇順（小さいものから大きいものへ）
PRINTL [2]降順（大きいものから小さいものへ）
DRAWLINE
PRINTL [100]戻る
INPUT
IF RESULT == 100
	RETURN 1
ELSEIF RESULT < 1 || RESULT > 2
	CLEARLINE 1
	REUSELASTLINE 無効な値です
	GOTO UP_DOWN
ENDIF
TFLAG:1 = RESULT

;--------------------------------------------------
;キャラオートソート処理
;--------------------------------------------------
SELECTCASE TFLAG:0
	CASE 0
		IF TFLAG:1 == 1
			SORTCHARA
			PRINTFORMW ＜キャラ番号昇順にソートしました＞
		ELSE
			SORTCHARA NO, BACK
			PRINTFORMW ＜キャラ番号降順にソートしました＞
		ENDIF
	CASE 1
		IF TFLAG:1 == 1
			SORTCHARA ABL:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA ABL:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
	CASE 2
		IF TFLAG:1 == 1
			SORTCHARA EXP:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA EXP:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
	CASE 3
		IF TFLAG:1 == 1
			SORTCHARA MARK:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA MARK:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
	CASE 4
		IF TFLAG:1 == 1
			SORTCHARA JUEL:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA JUEL:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
	CASE 5 , 7
		IF TFLAG:1 == 1
			SORTCHARA CFLAG:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA CFLAG:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
	CASE 6
		IF TFLAG:1 == 1
			SORTCHARA BASE:(TFLAG:2)
			PRINTFORMW ＜%TSTR:0%の低い順にソートしました＞
		ELSE
			SORTCHARA BASE:(TFLAG:2), BACK
			PRINTFORMW ＜%TSTR:0%の高い順にソートしました＞
		ENDIF
ENDSELECT

;--------------------------------------------------
;キャラ入れ替え・ソート用リスト
;--------------------------------------------------
@SORT_LIST, ARG, ARG:1
REPEAT 20
	LOCAL = GET_LIST(COUNT + ARG * 20)
	;主人公と失踪キャラは排除
	SIF LOCAL == 0
		CONTINUE
	SIF CFLAG:LOCAL:4
		CONTINUE
	;一度目に選んだキャラは排除
	SIF LOCAL == ARG:1
		CONTINUE
	IF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF ISASSI:LOCAL == 1
		PRINT ●
	ELSEIF CFLAG:LOCAL:1 == 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	CALL ARRANGE_CHARALIST, LOCAL
	CALL ARRANGE_CHARALIFE, LOCAL
	CALL ARRANGE_CHARATALENT, LOCAL
	PRINTL 
REND
