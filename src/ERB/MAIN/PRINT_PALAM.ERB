;ここで調教中のパラメータの表示(芋形式・Ｊ形式)の処理を行う
;=============================================================================
;パラメータ表示(弐型表示)
;=============================================================================
;表示順
; 0,快Ｃ　　 3,快Ｂ　　13,欲情
; 1,快Ｖ　　 2,快Ａ　　10,潤滑
;12,恭順　　14,屈服　　11,習得
;15,恥情　　17,恐怖　　16,苦痛
;30,薬毒　　31,侵蝕　　32,先導
;20,反感　　21,不快　　22,抑鬱
@PRINT_PALAM_2
VARSET LOCALS,""
SPLIT "0,3,13,1,2,10,12,14,11,15,17,16,30,31,32,20,21,22",",",LOCALS

REPEAT 18
	LOCAL = TOINT(LOCALS:COUNT)

	PRINTFORM 　%PALAMNAME:LOCAL% 
	IF LOCAL == 10 && PALAM:潤滑 >= 20000
		PRINTFORM 　　濡れ->
	ELSEIF PALAM:LOCAL >= 99999999
		PRINTFORM 　　超越->
	ELSEIF PALAM:LOCAL > 9999999
		PRINTFORM 　　限界->
	ELSE
		PRINTFORM  {PALAM:LOCAL, 7}->
	ENDIF
	IF LOCAL == 10
		;潤滑のみ現在のPALAMLVを表示
		GETPALAMLV PALAM:LOCAL, (TALENT:恋慕 || TALENT:服従 || TALENT:淫乱) ? 14 # 13
		PRINTFORM  PLv{RESULT, 2}
	ELSE
		;取得予定の珠数を表示
		PRINTFORM {CALC_JUEL(LOCAL), 6}J
	ENDIF
	SIF COUNT%3 == 2
		PRINTL 
REND

;=============================================================================
;パラメータ表示(暮型表示)
;=============================================================================
;表示順
; 0,快Ｃ　　 3,快Ｂ　　13,欲情
; 1,快Ｖ　　 2,快Ａ　　10,潤滑
;12,恭順　　14,屈服　　11,習得
;15,恥情　　17,恐怖　　16,苦痛
;30,薬毒　　31,侵蝕　　32,先導
;20,反感　　21,不快　　22,抑鬱
@NEW_PRINT_PALAM
#LOCALSIZE 21
#DIM CNT, 1
#DIM PALAM_NO, 1
#DIM PALAM_SCALE, 1
VARSET LOCALS,""
SPLIT "0,1,2,3,10,11,12,13,14,15,16,17,30,31,32,20,21,22",",",LOCALS
;パラメータを表示する順番を並べ替える
FOR CNT, 0, 18
	PALAM_NO = TOINT( LOCALS:CNT )

	;現在のパラメータレベル取得
	GETPALAMLV PALAM:PALAM_NO, ( TALENT:恋慕 || TALENT:服従 || TALENT:淫乱 ) ? 14 # 13

	;パラメータレベルが9以上なら表示を固定
	IF RESULT >= 9
		PALAM_SCALE = 8
	;パラメータレベルごとの閾値の8分の1を1メモリとする
	ELSE
		PALAM_SCALE = ( PALAM:PALAM_NO * 8 ) / ( ( RESULT >= 13 ) ? PALAMLV:RESULT # PALAMLV:( RESULT + 1 ) )
	ENDIF

	;潤滑だけ特殊表示
	IF PALAMNAME:PALAM_NO == "潤滑" && PALAM:PALAM_NO >= 20000
		LOCALS:20 = 濡れ
	;1億でカンストする様にしているのでメガ以降は削除
	ELSEIF PALAM:PALAM_NO > 999999
		LOCALS:20 = { PALAM:PALAM_NO / 1000 }k
	;6桁までノーマル表示
	ELSE
		LOCALS:20 = { PALAM:PALAM_NO }
	ENDIF

	;パラメータの情報をここで表示
	PRINTFORM  % PALAMNAME:PALAM_NO %Lv{ RESULT, 2 }
	CALL PRINT_COLORBAR, PALAM_SCALE, 8, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
	PRINTFORM % LOCALS:20, 6 %

	;3回ごとに改行
	SIF CNT % PRINTCPERLINE() == ( PRINTCPERLINE() - 1 )
		PRINTL 
NEXT
SIF !LINEISEMPTY()
	PRINTL 