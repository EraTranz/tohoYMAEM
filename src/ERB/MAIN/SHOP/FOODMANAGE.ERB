;-------------------------------------------------
;食糧管理について
;CFLAG:食事量 … 食事量
;-------------------------------------------------
@FOODMANAGE
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
LOCAL:2 = 0
;CHARADATA_LISTのために待避
LOCAL:99 = GET_LIST(2)
CALLF CLEAR_LIST
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	;主人は除外
	SIF COUNT == MASTER
		CONTINUE
	;失踪中などこの場にいないなら除外
	SIF CFLAG:COUNT:4 == 1 || CFLAG:COUNT:4 == 8192
		CONTINUE

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
REND
LOCAL:1 = (LOCAL:2 - 1) / 20
MARK:MASTER:98 = 1
$PRINT_LIST
ライン数:0 = LINECOUNT
DRAWLINE
PRINTFORML 食べる量を変えるキャラを選んでください　＜page.{TFLAG:600+1}＞
DRAWLINE
;PRINTFORML 　[0] %CALLNAME:MASTER%(主人)
;DRAWLINE
CALL CHARADATA_LIST_FM, TFLAG:600, LOCAL:99
DRAWLINE
PRINTFORMLC \@(TFLAG:600 <= 0) ? 　 # [1111]前のページ\@
PRINTLC [5555]戻る
PRINTFORMLC \@(TFLAG:600 >= LOCAL:1) ? 　 # [9999]次のページ\@
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 5555
	TFLAG:600 = 0
	RETURN 0
ELSEIF RESULT == 1111 && TFLAG:600 > 0
	TFLAG:600 -= 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 9999 && TFLAG:600 < LOCAL:1
	TFLAG:600 += 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT < 1 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF
CALL FOODVOLOME, RESULT
IF RESULT == 1
	TFLAG:600 = 0
	RETURN 0
ENDIF
RESTART

;-------------------------------------------------
;キャラ一覧表示処理
;-------------------------------------------------
@CHARADATA_LIST_FM, ARG, ARG:2
LOCAL:3 = 0
REPEAT 20
	LOCAL = GET_LIST(COUNT + ARG * 20)
	SIF LOCAL == 0
		CONTINUE

	;主人公と助手対象と調教中キャラは排除
	SIF LOCAL == 0
		CONTINUE
	SIF CFLAG:LOCAL:4 == 1 || CFLAG:LOCAL:4 == 8192
		CONTINUE
	IF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF CFLAG:LOCAL:4 == 4
		PRINT ◆
		LOCAL:3 |= 1
	ELSEIF CFLAG:LOCAL:4 == 8
		PRINT ▲
		LOCAL:3 |= 2
	ELSEIF CFLAG:LOCAL:4 == 16
		PRINT ■
		LOCAL:3 |= 4
	ELSEIF CFLAG:LOCAL:4 == 1024
		CALL PRINTCOLOR(RGBCOLOR(220, 20, 60), "▼", 0)
		LOCAL:3 |= 16
	ELSEIF CFLAG:LOCAL:4 == 2048 || CFLAG:LOCAL:4 == 4096
		PRINT ▽
		LOCAL:3 |= 8
	ELSEIF ISASSI:LOCAL == 1
		PRINT ●
	ELSEIF CFLAG:LOCAL:1 == 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	CALL ARRANGE_CHARALIST, LOCAL
	CALL ARRANGE_CHARALIFE, LOCAL
	CALL ARRANGE_FOOD,LOCAL
	CALL ARRANGE_BODY,LOCAL
	CALL ARRANGE_CALORIE,LOCAL
	PRINTL 
REND
IF ARG:2 >= 1
	PRINT 　　　☆:調教中 ★:助手 ○:助手可 ●:元助手
	SIF FLAG:15 & 4096 && FLAG:75 & 32768 && LOCAL:3 & 1
		PRINT  ◆:触手幽閉中
	SIF FLAG:73 & 1 && LOCAL:3 & 4
		PRINT  ■:牢収監中
	SIF FLAG:15 & 4096 && LOCAL:3 & 2
		PRINT  ▲:檻幽閉中
	IF LOCAL:3 & 16
		PRINT  
		CALL PRINTCOLOR(RGBCOLOR(220, 20, 60), "▼", 0)
		PRINT :売春中
	ENDIF
	SIF FLAG:79 & 1 && LOCAL:3 & 8
		PRINT  ▽:ハーブ作業中
	PRINTL 　を表しています
ENDIF


@ARRANGE_FOOD,LOCAL
IF CFLAG:LOCAL:食事量 == 2
	PRINT マシマシ　
ELSEIF CFLAG:LOCAL:食事量 == 1
	PRINT 大盛　
ELSEIF CFLAG:LOCAL:食事量 == -1
	PRINT 小盛　
ELSEIF CFLAG:LOCAL:食事量 == -2
	PRINT 極小盛　
ELSE
	PRINT 並盛　
ENDIF

@ARRANGE_BODY,LOCAL
IF TALENT:LOCAL:ぽっちゃり == 1
	PRINT ぽっちゃり
ELSEIF TALENT:LOCAL:ムチムチ == 1
	PRINT ムチムチ
ELSEIF TALENT:LOCAL:スレンダー == 1
	PRINT スレンダー
ELSEIF TALENT:LOCAL:がりがり == 1
	PRINT がりがり
ELSE
	PRINT ふつう
ENDIF

@ARRANGE_CALORIE,ARG
TFLAG:1 = CFLAG:ARG:体型数値置き場
PRINTFORML 　{TFLAG:1}

@FOODVOLOME,ARG

PRINTFORML %CALLNAME:ARG%の食事の量を決めてください
PRINT 今現在:
CALL ARRANGE_FOOD,ARG
PRINTL
DRAWLINE

$INPUTLOOP_0
PRINTL [2] - マシマシ
PRINTL [1] - 大盛
PRINTL [0] - 並盛
PRINTL [-1] - 小盛
PRINTL [-2] - 極小盛
PRINTL [10] - 変えない

INPUT
IF RESULT == 2
	CFLAG:ARG:食事量 = 2
	PRINTL 飯をマシマシにします
ELSEIF RESULT == 1
	CFLAG:ARG:食事量 = 1
	PRINTL 飯を大盛りにします
ELSEIF RESULT == 0
	CFLAG:ARG:食事量 = 0
	PRINTL 飯の量を普通にします
ELSEIF RESULT == -1
	CFLAG:ARG:食事量 = -1
	PRINTL 飯を小盛にします
ELSEIF RESULT == -2
	CFLAG:ARG:食事量= -2
	PRINTL 飯を極小盛にします
ELSEIF RESULT == 10
	PRINTL 量を変えません
ELSE
	GOTO INPUTLOOP_0
ENDIF
RETURN 0
