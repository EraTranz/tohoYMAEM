;ご苦労だった…と言いたいところだが、君等には消えてもらう。
;実は今まで調教していた淫乱化名無し妖精の正体は
;消息不明とされていた、あなただったのだ。
;君等は何も知らずに主人を売り捌いてきたのだよ。
;しかしそれは、私の筋書き通りなのだ…
;後は君等をcaved!すれば私の計画は完成する。
;これから私の最強の特別触手部隊をもって君等をもがすので
;　よ　ろ　し　く
;=============================================================================
;触手開発・培養施設の処理
;=============================================================================
;文字通りここでは触手の開発や寄生触手、触手幽閉等、触手進化拡張パッチにおける処理の大半が行われる。
;基本的な流れとして、調教で触手寄生→SHOPから触手開発・培養施設に飛ぶ→寄生触手の摘出でエネルギーを溜めてあれこれやる、
;あるいは開発しつくしたなら奴隷に寄生している触手の栄養を放出させて身体的な強化(弱体化?)を行うことも可能。
;-----------------------------------------------
;触手開発・培養施設のメニュー
;-----------------------------------------------
@TENTACLE_LABORATORY
;キャラ固有の番号を確認
LOCAL = (TARGET < 0) ? -1 # CFLAG:3109
$PAGE_START
RESULT:1 = 0
DRAWLINE
PRINTFORML 現在の触手エネルギー({BASE:MASTER:触手}τ)
PRINTFORML 現在の主人の%PALAMNAME:198%の珠:{JUEL:MASTER:触手, 7}
DRAWLINE
PRINTL 何をしますか？
PRINTL  [0] 寄生触手
SIF SPECIALTENTACLE_CHECK() < 19 || CHARATENTACLE_CHECK() < 17
	PRINTL  [1] 触手開発
SIF BASE:MASTER:触手 && FLAG:75 & 2048
	PRINTL  [2] 触手錬金
SIF FLAG:75 & 4096 && CHARANUM > 1 && TIME == 1
	PRINTL  [3] 触手漬け
SIF FLAG:75 & 32768 && CHARANUM > 1
	PRINTL  [4] 触手幽閉
SIF CHARANUM > 1 && FLAG:75 & 131072
	PRINTL  [5] 触手養殖
SIF BASE:MASTER:触手 && FLAG:75 & 2048
	PRINTL [97] 錬金転化
IF ABL:MASTER:触手使役 < 5
	LOCALS:0 = ×
	LOCALS:1 = ○
	CALL ISABLUP198
	PRINTFORML [98] 触手使役 %LOCALS:RESULT%
ENDIF
SIF CHARANUM > 1
	PRINTL [99] 触手生贄
PRINTLC 　　　
PRINTLC [1000]戻る
$INPUT_LOOP
INPUT
IF RESULT == 0
	CALL TENTACLE_LABORATORY_PARASITIC
ELSEIF RESULT == 1 && (SPECIALTENTACLE_CHECK() < 19 || CHARATENTACLE_CHECK() < 17)
	CALL TENTACLE_LABORATORY_EVOLUTION
ELSEIF RESULT == 2 && FLAG:75 & 2048
	CALL TENTACLE_LABORATORY_ALCHEMY
ELSEIF RESULT == 3 && FLAG:75 & 4096 && CHARANUM > 1 && TIME == 1
	CALL TENTACLE_LABORATORY_SPECIALROOM
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 1
	ENDIF
ELSEIF RESULT == 4 && FLAG:75 & 32768 && CHARANUM > 1
	CALL TENTACLE_LABORATORY_PRIVATEROOM
ELSEIF RESULT == 5 && FLAG:75 & 131072 && CHARANUM > 1
	CALL TENTACLE_LABORATORY_BREED
ELSEIF RESULT == 97 && JUEL:MASTER:触手 >= 100
	CALL TENTACLE_LABORATORY_ALCHEMY_2
ELSEIF RESULT == 98 && ABL:MASTER:触手使役 < 5
	CALL ABLUP198
ELSEIF RESULT == 99 && CHARANUM > 1
	CALL TENTACLE_LABORATORY_SACRIFICE
ELSEIF RESULT == 1000
	LOCAL = FINDCHARA(CFLAG:3109, LOCAL, 0, CHARANUM)
	SIF LOCAL >= 0 && (CFLAG:LOCAL:4 || LOCAL == MASTER)
		LOCAL = -1
	TARGET = LOCAL
	RETURN 0
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF
SIF RESULT:1 == 1
	RETURN 0
GOTO PAGE_START

;-----------------------------------------------
;寄生触手に関する扱い
;-----------------------------------------------
;体質変化のみ処理が別種なので関数@TENTACLE_LABORATORY_CONSTITUTIONに移動
;ここで行った体力・気力底上げ処理は調教による体力気力累積上昇分にカウントされない
;(普通の成長とは別次元という解釈のため)
@TENTACLE_LABORATORY_PARASITIC
VARSET LOCAL, 0
LOCALS:0 = 触手摘出
LOCALS:1 = 体力強化
LOCALS:2 = 気力強化
LOCALS:99 = 触手処分

DRAWLINE
PRINTFORML 現在の触手エネルギー({BASE:MASTER:触手}τ)
PRINTL 何をしますか？
DRAWLINE
PRINTL  [ 0]触手摘出
SIF FLAG:75 & 1024
	PRINTL  [ 1]体力強化
SIF FLAG:75 & 1024
	PRINTL  [ 2]気力強化
SIF FLAG:75 & 1024
	PRINTL  [ 3]体質変化
PRINTL  [99]触手を処分する
PRINTLC 　　　
PRINTLC [1000]戻る
$INPUT_LOOP_0
INPUT
IF RESULT == 0
	LOCAL = 0
ELSEIF RESULT == 1 && FLAG:75 & 1024
	LOCAL = 1
ELSEIF RESULT == 2 && FLAG:75 & 1024
	LOCAL = 2
ELSEIF RESULT == 3 && FLAG:75 & 1024
	LOCAL = 3
ELSEIF RESULT == 99
	LOCAL = 99
ELSEIF RESULT == 1000
	RETURN 0
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF

$SET_LOOP_1
ライン数:0 = LINECOUNT
;表示させるキャラを抽出（LOCAL:2に人数）
CALLF CLEAR_LIST
LOCAL:1 = 0
LOCAL:2 = 0
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	SIF COUNT == MASTER
		CONTINUE
	SIF TALENT:COUNT:死亡
		CONTINUE
	SIF CFLAG:COUNT:4
		CONTINUE
	SIF TALENT:COUNT:寄生 == 0
		CONTINUE
	SIF (LOCAL == 1 || LOCAL == 2 || LOCAL == 3) && BASE:COUNT:触手 == 0
		CONTINUE
	IF LOCAL == 1 && MAXBASE:COUNT:0 >= 5000
		CONTINUE
	ELSEIF LOCAL == 2 && MAXBASE:COUNT:1 >= 5000
		CONTINUE
	ELSEIF LOCAL == 3 && TALENT:COUNT:隷属
		CONTINUE
	ENDIF
	SIF NO:COUNT == 5001 && (FLAG:570 & 48) == 0
		CONTINUE

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
	SIF (LOCAL:2%20) == 1 && LOCAL:2 > 20
		LOCAL:5 += 1
REND

$PRINT_LIST
DRAWLINE
PRINTFORML %LOCALS:LOCAL%を実行します
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL:3 + 1}＞
DRAWLINE
CALL PARASITIC_LIST_CHARA, LOCAL:3
DRAWLINE
PRINTFORMLC \@(LOCAL:3 <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL:3 >= LOCAL:5) ? %" " * 16% # [1009]次のページ\@
PRINTL 
PRINTLC 　　　
PRINTLC [1111]メニューへ戻る
PRINTL 
$INPUT_LOOP_2
INPUT
IF RESULT == 1000
	RESTART
ELSEIF RESULT == 1111
	RESULT:1 = 1
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL:3 > 0
	LOCAL:3 -= 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL:3 < LOCAL:5
	LOCAL:3 += 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_2
ENDIF

LOCAL:1 = TARGET
TARGET = RESULT
PRINTFORM %CALLNAME:TARGET%に
IF LOCAL == 0
	PRINTL 寄生している触手を摘出し、触手を成長させるエネルギーに変換します
ELSEIF LOCAL == 1
	PRINTL 寄生している触手の栄養を全て放出し、体力の最大値を底上げします
ELSEIF LOCAL == 2
	PRINTL 寄生している触手の栄養を全て放出し、気力の最大値を底上げします
ELSEIF LOCAL == 3
	PRINTL 寄生している触手から特殊なホルモンを分泌し、体質を強制的に変化させます
	CALL TENTACLE_LABORATORY_CONSTITUTION
	TARGET = LOCAL:1
	RESTART
ELSEIF LOCAL == 99
	PRINTL 寄生している触手を処分します
ENDIF
PRINTL 実行しますか？
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
$INPUT_LOOP_3
INPUT
IF RESULT == 1
	TARGET = LOCAL:1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT != 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_3
ENDIF

PRINTFORM %CALLNAME:TARGET%に
IF LOCAL == 0
	LOCAL:2 = BASE:触手
	SIF BASE:触手 == MAXBASE:6
		TIMES LOCAL:2 , 1.50
	PRINTL 寄生している触手を摘出し、エネルギーに変換しました
	PRINTFORML 触手エネルギー＋{LOCAL:2}τ
	PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:146%]を失った
	BASE:MASTER:触手 += LOCAL:2
	BASE:触手 = 0
	TALENT:寄生 = 0
ELSEIF LOCAL == 1 || LOCAL == 2
	PRINTL 寄生していた触手は蓄えていた栄養を全て放出した
	LOCAL:2 = BASE:触手
	SELECTCASE LOCAL:2
		CASE IS < 5
			LOCAL:3 = 0
		CASE IS < 10
			LOCAL:3 = 1
		CASE IS < 25
			LOCAL:3 = 2
		CASE IS < 50
			LOCAL:3 = 4
		CASE IS < 100
			LOCAL:3 = 10
		CASE IS < 250
			LOCAL:3 = 20
		CASE IS < 500
			LOCAL:3 = 50
		CASE IS < 1000
			LOCAL:3 = 75
		CASEELSE
			LOCAL:3 = BASE:触手 / 10
	ENDSELECT
	;触手寄生LVが溜められる値の最大値まであった場合、成長値は増加
	SIF BASE:触手 == MAXBASE:6
		TIMES LOCAL:3 , 1.50
	;ベリージュエルを装着していると成長値は減少
	SIF CFLAG:42 & 4
		TIMES LOCAL:3 , 0.75
	IF LOCAL:3 > 0
		IF LOCAL == 1
			LOCAL:3 = MAX(LOCAL:3 / 2, 1)
			MAXBASE:0 += LOCAL:3
			BASE:体力 += LOCAL:3
			PRINTFORML 栄養が%CALLNAME:TARGET%の体内に行き渡り、身体をより丈夫に鍛えた
			PRINTFORMW %CALLNAME:TARGET%：体力最大値＋{LOCAL:3}
		ELSEIF LOCAL == 2
			LOCAL:3 = MAX(LOCAL:3 / 5, 1)
			MAXBASE:1 += LOCAL:3
			BASE:気力 += LOCAL:3
			PRINTFORML 栄養が%CALLNAME:TARGET%の体内に行き渡り、心をより丈夫に鍛えた
			PRINTFORMW %CALLNAME:TARGET%：気力最大値＋{LOCAL:3}
		ENDIF
	ELSE
		PRINTFORMW 残念ながら%CALLNAME:TARGET%を成長させるための栄養が足りなかったようだ……
	ENDIF
	BASE:触手 = 0
ELSEIF LOCAL == 99
	PRINTL 寄生している触手を処分しました
	PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:146%]を失った
	BASE:触手 = 0
	TALENT:寄生 = 0
ENDIF
DRAWLINE
TARGET = LOCAL:1
RESTART

;-----------------------------------------------
;寄生触手キャラ選択補助処理
;-----------------------------------------------
@PARASITIC_LIST_CHARA, ARG
REPEAT 20
	LOCAL = GET_LIST(COUNT + ARG * 20)
	SIF LOCAL == 0
		CONTINUE

	;主人公と助手対象と調教中キャラは排除
	SIF LOCAL == 0
		CONTINUE
	SIF CFLAG:LOCAL:4
		CONTINUE
	SIF MARK:LOCAL:98 == 0
		CONTINUE
	SIF TALENT:LOCAL:死亡
		CONTINUE
	IF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF ISASSI:LOCAL == 1
		PRINT ●
	ELSEIF CFLAG:LOCAL:1 == 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	CALL ARRANGE_CHARALIST, LOCAL
	CALL ARRANGE_CHARALIFE, LOCAL
	PRINTFORM  寄生触手成長LV({BASE:LOCAL:触手, 4}/{MAXBASE:LOCAL:6, 4})
	SIF TALENT:LOCAL:懐卵
		PRINT [懐卵]
	PRINTL 
REND

;-----------------------------------------------
;体質変化にかかわる処理
;-----------------------------------------------
;Dは体質変化に必要な寄生触手成長LV
;Eは同化刻印取得に必要な条件, Fはそのために必要な触手幼生
@TENTACLE_LABORATORY_CONSTITUTION
ライン数:0 = LINECOUNT
DRAWLINE
PRINTFORML %CALLNAME:TARGET%の体質をどのように変えますか？(寄生触手成長LV:{BASE:触手})
DRAWLINE
LOCAL = 0
SIF TALENT:爆乳 == 0
	LOCAL |= 1
SIF TALENT:絶壁 == 0
	LOCAL |= 2
LOCAL:1 = (ABL:MASTER:触手使役 >= 3) ? TENTACLE_SEAL_OF_IMMORTAL() # 0
IF TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0
	SIF TALENT:母乳体質 == 0
		PRINTFORML  [ 0]母乳体質にする　　(必要寄生触手成長LV:1200)
	SIF TALENT:母乳体質
		PRINTFORML  [ 1]母乳体質を改善　　(必要寄生触手成長LV:1500)
	SIF LOCAL & 1
		PRINTFORML  [ 2]胸のサイズを大きく(必要寄生触手成長LV: 200)
	SIF LOCAL & 2
		PRINTFORML  [ 3]胸のサイズを小さく(必要寄生触手成長LV: 200)
	SIF TALENT:濡れやすい == 0
		PRINTFORML  [ 4]愛液分泌量を増強　(必要寄生触手成長LV:1000)
	SIF TALENT:濡れにくい == 0
		PRINTFORML  [ 5]愛液分泌量を低下　(必要寄生触手成長LV:1000)
	SIF TALENT:ふたなり == 0
		PRINTFORML  [ 6]ペニスを生成する　(必要寄生触手成長LV: 500)
	SIF TALENT:ふたなり && TALENT:薬毒耐性 == 0
		PRINTFORML  [ 7]ペニスを消去する　(必要寄生触手成長LV: 100)
ENDIF
IF (TALENT:オトコ || TALENT:ふたなり) && TALENT:薬毒耐性 == 0
	SIF TALENT:早漏 == 0
		PRINTFORML  [ 8]早漏体質にする　　(必要寄生触手成長LV: 750)
	SIF TALENT:遅漏 == 0
		PRINTFORML  [ 9]遅漏体質にする　　(必要寄生触手成長LV: 750)
ENDIF
SIF TALENT:薬毒耐性 == 0 && TALENT:動物耳 == 0
	PRINTFORML  [10]動物耳をつける　　(必要寄生触手成長LV:2000)
SIF TALENT:薬毒耐性
	PRINTFORML  [11]薬毒耐性を除去　　(必要寄生触手成長LV:5000＋α)
SIF TALENT:薬毒耐性 == 0 && CFLAG:41
	PRINTFORML  [12]触手の種を除去　　(必要寄生触手成長LV: 500)
IF LOCAL:1 == 3
	PRINTFORML  [99]同化刻印LV3取得 　(必要寄生触手成長LV:7000＋α)
ELSEIF LOCAL:1 == 2
	PRINTFORML  [99]同化刻印LV2取得 　(必要寄生触手成長LV:5000＋α)
ELSEIF LOCAL:1 == 1
	PRINTFORML  [99]同化刻印LV1取得 　(必要寄生触手成長LV:3000＋α)
ENDIF
PRINTLC 　　　
PRINTLC [1000]戻る
$INPUT_LOOP_0
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 0 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:母乳体質 == 0
	PRINTFORML %CALLNAME:TARGET%を母乳体質にします
	LOCAL:2 = 1200
ELSEIF RESULT == 1 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:母乳体質
	PRINTFORML %CALLNAME:TARGET%の母乳体質を治します
	LOCAL:2 = 1500
ELSEIF RESULT == 2 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && LOCAL & 1
	PRINTFORML %CALLNAME:TARGET%の胸のサイズを1ランク大きくします
	LOCAL:2 = 200
ELSEIF RESULT == 3 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && LOCAL & 2
	PRINTFORML %CALLNAME:TARGET%の胸のサイズを1ランク小さくします
	LOCAL:2 = 200
ELSEIF RESULT == 4 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:濡れやすい == 0
	PRINTFORML %CALLNAME:TARGET%の愛液分泌量を増強させます
	LOCAL:2 = 1000
ELSEIF RESULT == 5 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:濡れにくい == 0
	PRINTFORML %CALLNAME:TARGET%の愛液分泌量を低下させます
	LOCAL:2 = 1000
ELSEIF RESULT == 6 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:ふたなり == 0 && TALENT:雌雄可変 == 0 && !(CFLAG:42 & 1)
	PRINTFORML %CALLNAME:TARGET%をふたなりにします
	LOCAL:2 = 500
ELSEIF RESULT == 7 && TALENT:オトコ == 0 && TALENT:薬毒耐性 == 0 && TALENT:ふたなり
	PRINTFORML %CALLNAME:TARGET%のふたなりを消去します
	LOCAL:2 = 100
ELSEIF RESULT == 8 && TALENT:薬毒耐性 == 0 && (TALENT:オトコ || TALENT:ふたなり) && TALENT:早漏 == 0
	PRINTFORML %CALLNAME:TARGET%を早漏体質にします
	LOCAL:2 = 750
ELSEIF RESULT == 9 && TALENT:薬毒耐性 == 0 && (TALENT:オトコ || TALENT:ふたなり) && TALENT:遅漏 == 0
	PRINTFORML %CALLNAME:TARGET%を遅漏体質にします
	LOCAL:2 = 750
ELSEIF RESULT == 10 && TALENT:薬毒耐性 == 0 && TALENT:動物耳 == 0
	PRINTFORML %CALLNAME:TARGET%に動物耳を生やします
	LOCAL:2 = 2000
ELSEIF RESULT == 11 && TALENT:薬毒耐性
	PRINTFORML %CALLNAME:TARGET%の薬毒に対する耐性を低下させます
	PRINTL 
	PRINTFORML ※副作用として%CALLNAME:TARGET%の[寄生]・触手中毒・触手経験を全て失い、回復能力も低下します
	PRINT それでも
	LOCAL:2 = 5000
ELSEIF RESULT == 12 && TALENT:薬毒耐性 == 0 && CFLAG:41
	PRINTFORML %CALLNAME:TARGET%に植え付けられた『種』を除去します
	PRINTL 
	PRINTFORML ※副作用として%CALLNAME:TARGET%の体力も低下します
	PRINT それでも
	LOCAL:2 = 500
ELSEIF RESULT == 99 && LOCAL:1 == 3
	PRINTFORML %CALLNAME:TARGET%に不滅の命を与え、%CALLNAME:MASTER%の玩具として仕えさせます
	PRINTL 
	PRINTFORML ※実行するにはこの他に%CALLNAME:TARGET%の子供(触手)10匹と100萬圓が別途必要です
	LOCAL:2 = 7000
ELSEIF RESULT == 99 && LOCAL:1 == 2
	PRINTFORML %CALLNAME:TARGET%に刻まれた印の力を強め、より死に難い体へと変化させます
	PRINTL 
	PRINTFORML ※実行するにはこの他に%CALLNAME:TARGET%の子供(触手)5匹と50萬圓が別途必要です
	LOCAL:2 = 5000
ELSEIF RESULT == 99 && LOCAL:1 == 1
	PRINTFORML %CALLNAME:TARGET%の体内に不死の刻印を刻み込み、触手によって死ぬことがない体に変えます
	PRINTL 
	PRINTFORML ※実行するにはこの他に%CALLNAME:TARGET%の子供(触手)1匹と10萬圓が別途必要です
	LOCAL:2 = 3000
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF
LOCAL:3 = RESULT
PRINTL 実行しますか？
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	RESTART
ELSEIF RESULT != 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF
IF BASE:触手 < LOCAL:2
	PRINTW 必要な寄生触手成長LVに達していません
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	RESTART
ELSEIF LOCAL:3 == 99 && LOCAL:1
	CALL TENTACLE_GAIN_IMMORTAL, LOCAL:1, LOCAL:2
ELSE
	BASE:触手 -= LOCAL:2
	PRINTFORML %CALLNAME:TARGET%の体内に寄生していた触手は
	PRINTW 溜めこんでいた栄養を使って特殊なホルモンを分泌し始めた
ENDIF
SELECTCASE LOCAL:3
	CASE 0
		PRINTFORMW %CALLNAME:TARGET%は母乳が出るようになった
		TALENT:母乳体質 = 1
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%から母乳が出なくなった
		TALENT:母乳体質 = 0
	CASE 2
		IF TALENT:絶壁
			PRINTFORMW %CALLNAME:TARGET%の胸がほんの少しだけ膨らんだ
			TALENT:絶壁 = 0
			TALENT:貧乳 = 1
		ELSEIF TALENT:貧乳
			PRINTFORMW %CALLNAME:TARGET%の胸が大きくなった
			TALENT:貧乳 = 0
		ELSEIF TALENT:巨乳
			PRINTFORMW %CALLNAME:TARGET%の胸が体に不釣合いなまでに大きくなった
			TALENT:巨乳 = 0
			TALENT:爆乳 = 1
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の胸がはち切れんばかりの大きさになった
			TALENT:巨乳 = 1
		ENDIF
		CFLAG:508 = 0
	CASE 3
		IF TALENT:爆乳
			PRINTFORMW %CALLNAME:TARGET%の豊かすぎる胸が一回り小さくなった
			PRINTFORMW %CALLNAME:TARGET%は少しだけ寂しそうな顔をしている
			TALENT:爆乳 = 0
			TALENT:巨乳 = 1
		ELSEIF TALENT:巨乳
			PRINTFORMW %CALLNAME:TARGET%の胸が人並みに小さくなった
			PRINTFORMW %CALLNAME:TARGET%は少々ショックを受けているようだ
			TALENT:巨乳 = 0
		ELSEIF TALENT:貧乳
			PRINTFORMW %CALLNAME:TARGET%の胸が極端に薄くなった
			PRINTFORMW %CALLNAME:TARGET%は愕然としている
			TALENT:貧乳 = 0
			TALENT:絶壁 = 1
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の胸が小さくなった
			PRINTFORMW %CALLNAME:TARGET%はショックを受けているようだ
			TALENT:貧乳 = 1
		ENDIF
		CFLAG:508 = 0
	CASE 4
		IF TALENT:濡れにくい
			PRINTFORMW %CALLNAME:TARGET%は性感帯への刺激に対する感度が普通になった
			TALENT:濡れにくい = 0
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は性感帯への刺激に対して敏感になった
			TALENT:濡れやすい = 1
		ENDIF
	CASE 5
		IF TALENT:濡れやすい
			PRINTFORMW %CALLNAME:TARGET%は性感帯への刺激に対する感度が普通になった
			TALENT:濡れやすい = 0
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は性感帯への刺激に対して鈍感になった
			TALENT:濡れにくい = 1
		ENDIF
	CASE 6
		PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:121%]になった
		TALENT:ふたなり = 1
		CALL CHANGE_FUTA_COMMON, TARGET
	CASE 7
		PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:121%]ではなくなった
		TALENT:ふたなり = 0
		CALL CHANGE_FUTA_COMMON, TARGET
	CASE 8
		IF TALENT:遅漏
			PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:133%]を失った
			TALENT:遅漏 = 0
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:132%]になった
			TALENT:早漏 = 1
		ENDIF
	CASE 9
		IF TALENT:早漏
			PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:132%]を失った
			TALENT:早漏 = 0
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:133%]になった
			TALENT:遅漏 = 1
		ENDIF
	CASE 10
		PRINTFORMW %CALLNAME:TARGET%に[%TALENTNAME:116%]が生えた
		TALENT:動物耳 = 1
	CASE 11
		PRINTFORML %CALLNAME:TARGET%は毒物・薬物に対する抵抗力が低下した…
		PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:44%]を失った
		PRINTFORML %CALLNAME:TARGET%の体は触手に対する適応能力が低下している…
		PRINTFORML %CALLNAME:TARGET%に寄生していた触手は力尽きて消滅した
		PRINTFORMW %CALLNAME:TARGET%の[%EXPNAME:53%]と[%ABLNAME:35%]は0に戻った
		IF TALENT:回復早い
			PRINTFORMW %CALLNAME:TARGET%の回復能力が低下した
			TALENT:回復早い = 0
		ELSEIF TALENT:回復遅い == 0
			PRINTFORMW %CALLNAME:TARGET%の回復能力がかなり低下した
			TALENT:回復遅い = 1
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は一気に疲労した
			BASE:体力 /= 10
		ENDIF
		TALENT:薬毒耐性 = 0
		EXP:触手経験 = 0
		ABL:触手中毒 = 0
		CFLAG:13 |= 4096
	CASE 12
		PRINTFORMW %CALLNAME:TARGET%に植え付けられた『種』は消滅した
		PRINTFORMW %CALLNAME:TARGET%は疲労した
		CFLAG:41 = 0
		BASE:体力 /= 2
ENDSELECT
PRINTL 
BASE:体力 = MAX(BASE:体力, 1)
DRAWLINE
RESTART

;-----------------------------------------------
;触手開発
;-----------------------------------------------
;追加された触手コマンドの大多数はここで能力を獲得してからでないと実行できない
;一文字変数の内、LOCAL:1は能力獲得に必要触手エネルギー、LOCAL:2は必要な金額、
;LOCAL:3は何の能力を獲得するのかを判定し、FLAG:75(ビット演算)に加算するための数値。
@TENTACLE_LABORATORY_EVOLUTION
VARSET LOCAL, 0
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL 現在の触手の能力:
CALL SHOW_TENTACLE_ABL
DRAWLINE
PRINTFORML 現在の触手エネルギー:{BASE:MASTER:触手}τ　　所持金:{MONEY}圓
PRINTL どの能力を開発しますか？
PRINTL (なお、主人の触手使役Lvが足りなくて開発できない触手能力は表示されていません)
DRAWLINE
TFLAG:0 = 0
SIF (FLAG:75 & 1) == 0
	PRINTL  [ 0] 鞭触手　　　　　　 (100τ/200圓)
SIF (FLAG:75 & 2) == 0
	PRINTL  [ 1] 針触手　　　　　　 (150τ/350圓)
SIF (FLAG:75 & 4) == 0
	PRINTL  [ 2] 放電触手　　　　　 (500τ/5000圓)
SIF (FLAG:75 & 8) == 0 && ABL:MASTER:触手使役 >= 4
	PRINTL  [ 3] 催眠触手　　　　　(2500τ/2萬5000圓)
SIF (FLAG:75 & 16) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [ 4] 極細触手　　　　　(1500τ/5萬圓)
SIF (FLAG:75 & 32) == 0 && ABL:MASTER:触手使役 >= 5
	PRINTL  [ 5] 養分注入触手　　　(3000τ/2萬圓)
SIF (FLAG:75 & 64) == 0 && ABL:MASTER:触手使役 >= 2
	PRINTL  [ 6] 抗菌触手　　　　　(1200τ/1萬圓)
SIF (FLAG:75 & 128) == 0 && ABL:MASTER:触手使役 >= 2
	PRINTL  [ 7] ゴーヤ型触手　　　(1800τ/2萬圓)
SIF (FLAG:75 & 256) == 0
	PRINTL  [ 8] 磯巾着型触手　　　(2000τ/2萬圓)
SIF (FLAG:75 & 512) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [ 9] 触手遠隔精密制御　(1200τ/1萬2000圓)
SIF (FLAG:75 & 1024) == 0
	PRINTL  [10] 特殊ホルモン分泌　(5000τ/5000圓)
SIF (FLAG:75 & 2048) == 0
	PRINTL  [11] 触手賢者の石　　　 (1萬τ/5萬圓)
SIF (FLAG:75 & 4096) == 0
	PRINTL  [12] 奴隷懲罰用触手部屋(25萬τ/10萬圓)
SIF (FLAG:75 & 8192) == 0
	PRINTL  [13] 体内適応力強化　　(4500τ/1300圓)
SIF (FLAG:75 & 16384) == 0
	PRINTL  [14] 精液放出量強化　　 (2萬τ/5000圓)
SIF (FLAG:75 & 32768) == 0
	PRINTL  [15] 精子と触手の部屋　 (5萬τ/30萬圓)
SIF (FLAG:75 & 65536) == 0 && ABL:MASTER:触手使役 >= 2
	PRINTL  [16] 吸血触手　　　　　(1200τ/7000圓)
SIF (FLAG:75 & 131072) == 0
	PRINTL  [17] 保育施設・分娩台 (75000τ/15萬圓)
SIF FLAG:97 & 1 && (FLAG:98 & 1) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [50] SATORI触手　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 2 && (FLAG:98 & 2) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [51] NITORI触手　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 4 && (FLAG:98 & 4) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [52] YUYUKO触手　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 8 && (FLAG:98 & 8) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [53] SUIKA触手 　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 16 && (FLAG:98 & 16) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [54] ALICE触手　　　　　(5萬τ/25萬圓)
SIF FLAG:97 & 32 && (FLAG:98 & 32) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [55] HINA触手　　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 64 && (FLAG:98 & 64) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [56] AYA触手　　　　　　(5萬τ/25萬圓)
SIF FLAG:97 & 128 && (FLAG:98 & 128) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [57] PARSEE触手　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 256 && (FLAG:98 & 256) == 0 && ABL:MASTER:触手使役 >= 4
	PRINTL  [58] MEDICINE触手　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 512 && (FLAG:98 & 512) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [59] PATCHOULI触手　　　(5萬τ/25萬圓)
SIF FLAG:97 & 1024 && (FLAG:98 & 1024) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [60] HIGH-FAIRY触手　　 (5萬τ/25萬圓)
SIF FLAG:97 & 2048 && (FLAG:98 & 2048) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [61] CIRNO触手　　　　　(5萬τ/25萬圓)
SIF FLAG:97 & 4096 && (FLAG:98 & 4096) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [62] NAZRIN触手　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 8192 && (FLAG:98 & 8192) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [63] EIKI触手　　　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 16384 && (FLAG:98 & 16384) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [64] SHIZUHA触手　　　　(5萬τ/25萬圓)
SIF FLAG:97 & 32768 && (FLAG:98 & 32768) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [65] MINORIKO触手　　　 (5萬τ/25萬圓)
SIF FLAG:97 & 65536 && (FLAG:98 & 65536) == 0 && ABL:MASTER:触手使役 >= 3
	PRINTL  [66] SUWAKO触手　　　　 (5萬τ/25萬圓)
IF DA:1000:15 == 0 && JUEL:MASTER:触手 > 100000 && FLAG:10003 && FLAG:10004 && ABL:MASTER:触手使役 >= 5
	TFLAG:0 = CHARATENTACLE_CHECK()
	SIF TFLAG:0 >= 10
		PRINTL  [99] 現し身触手　　　 (100萬τ/100萬圓)
ENDIF
PRINTLC 　　　
PRINTLC [1000]やめる
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 0 && (FLAG:75 & 1) == 0
	LOCAL:1 = 100
	LOCAL:2 = 200
	LOCAL:3 = 1
ELSEIF RESULT == 1 && (FLAG:75 & 2) == 0
	LOCAL:1 = 150
	LOCAL:2 = 350
	LOCAL:3 = 2
ELSEIF RESULT == 2 && (FLAG:75 & 4) == 0
	LOCAL:1 = 500
	LOCAL:2 = 5000
	LOCAL:3 = 4
ELSEIF RESULT == 3 && (FLAG:75 & 8) == 0 && ABL:MASTER:触手使役 >= 4
	LOCAL:1 = 2500
	LOCAL:2 = 25000
	LOCAL:3 = 8
ELSEIF RESULT == 4 && (FLAG:75 & 16) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 1500
	LOCAL:2 = 50000
	LOCAL:3 = 16
ELSEIF RESULT == 5 && (FLAG:75 & 32) == 0 && ABL:MASTER:触手使役 >= 5
	LOCAL:1 = 3000
	LOCAL:2 = 20000
	LOCAL:3 = 32
ELSEIF RESULT == 6 && (FLAG:75 & 64) == 0 && ABL:MASTER:触手使役 >= 2
	LOCAL:1 = 1200
	LOCAL:2 = 10000
	LOCAL:3 = 64
ELSEIF RESULT == 7 && (FLAG:75 & 128) == 0 && ABL:MASTER:触手使役 >= 2
	LOCAL:1 = 1800
	LOCAL:2 = 20000
	LOCAL:3 = 128
ELSEIF RESULT == 8 && (FLAG:75 & 256) == 0
	LOCAL:1 = 2000
	LOCAL:2 = 20000
	LOCAL:3 = 256
ELSEIF RESULT == 9 && (FLAG:75 & 512) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 1200
	LOCAL:2 = 12000
	LOCAL:3 = 512
ELSEIF RESULT == 10 && (FLAG:75 & 1024) == 0
	LOCAL:1 = 5000
	LOCAL:2 = 5000
	LOCAL:3 = 1024
ELSEIF RESULT == 11 && (FLAG:75 & 2048) == 0
	LOCAL:1 = 10000
	LOCAL:2 = 50000
	LOCAL:3 = 2048
ELSEIF RESULT == 12 && (FLAG:75 & 4096) == 0
	LOCAL:1 = 250000
	LOCAL:2 = 100000
	LOCAL:3 = 4096
ELSEIF RESULT == 13 && (FLAG:75 & 8192) == 0
	LOCAL:1 = 4500
	LOCAL:2 = 1300
	LOCAL:3 = 8192
ELSEIF RESULT == 14 && (FLAG:75 & 16384) == 0
	LOCAL:1 = 20000
	LOCAL:2 = 5000
	LOCAL:3 = 16384
ELSEIF RESULT == 15 && (FLAG:75 & 32768) == 0
	LOCAL:1 = 50000
	LOCAL:2 = 300000
	LOCAL:3 = 32768
ELSEIF RESULT == 16 && (FLAG:75 & 65536) == 0 && ABL:MASTER:触手使役 >= 2
	LOCAL:1 = 1200
	LOCAL:2 = 7000
	LOCAL:3 = 65536
ELSEIF RESULT == 17 && (FLAG:75 & 131072) == 0
	LOCAL:1 = 75000
	LOCAL:2 = 150000
	LOCAL:3 = 131072
ELSEIF RESULT == 50 && FLAG:97 & 1 && (FLAG:98 & 1) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 1
ELSEIF RESULT == 51 && FLAG:97 & 2 && (FLAG:98 & 2) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 2
ELSEIF RESULT == 52 && FLAG:97 & 4 && (FLAG:98 & 4) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 4
ELSEIF RESULT == 53 && FLAG:97 & 8 && (FLAG:98 & 8) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 8
ELSEIF RESULT == 54 && FLAG:97 & 16 && (FLAG:98 & 16) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 16
ELSEIF RESULT == 55 && FLAG:97 & 32 && (FLAG:98 & 32) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 32
ELSEIF RESULT == 56 && FLAG:97 & 64 && (FLAG:98 & 64) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 64
ELSEIF RESULT == 57 && FLAG:97 & 128 && (FLAG:98 & 128) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 128
ELSEIF RESULT == 58 && FLAG:97 & 256 && (FLAG:98 & 256) == 0 && ABL:MASTER:触手使役 >= 4
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 256
ELSEIF RESULT == 59 && FLAG:97 & 512 && (FLAG:98 & 512) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 512
ELSEIF RESULT == 60 && FLAG:97 & 1024 && (FLAG:98 & 1024) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 1024
ELSEIF RESULT == 61 && FLAG:97 & 2048 && (FLAG:98 & 2048) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 2048
ELSEIF RESULT == 62 && FLAG:97 & 4096 && (FLAG:98 & 4096) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 4096
ELSEIF RESULT == 63 && FLAG:97 & 8192 && (FLAG:98 & 8192) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 8192
ELSEIF RESULT == 64 && FLAG:97 & 16384 && (FLAG:98 & 16384) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 16384
ELSEIF RESULT == 65 && FLAG:97 & 32768 && (FLAG:98 & 32768) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 32768
ELSEIF RESULT == 66 && FLAG:97 & 65536 && (FLAG:98 & 65536) == 0 && ABL:MASTER:触手使役 >= 3
	LOCAL:1 = 50000
	LOCAL:2 = 250000
	LOCAL:3 = 65536
ELSEIF RESULT == 99 && DA:1000:15 == 0 && JUEL:MASTER:触手 > 100000 && FLAG:10003 && FLAG:10004 && TFLAG:0 >= 10 && ABL:MASTER:触手使役 >= 5
	LOCAL:1 = 1000000
	LOCAL:2 = 1000000
	LOCAL:3 = 999
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
SIF TALENT:MASTER:998
	LOCAL:1 = 0
IF BASE:MASTER:触手 < LOCAL:1 || MONEY < LOCAL:2
	IF BASE:MASTER:触手 < LOCAL:1 && MONEY < LOCAL:2
		PRINT 必要なエネルギーと資金
	ELSEIF BASE:MASTER:触手 < LOCAL:1
		PRINT 必要なエネルギー
	ELSEIF MONEY < LOCAL:2
		PRINT 必要な資金
	ENDIF
	PRINTW が不足しています。
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	RESTART
ENDIF
BASE:MASTER:触手 -= LOCAL:1
MONEY -= LOCAL:2
;現し身触手
IF LOCAL == 99
	;ゲームモードにかかわらず1000のあなたが所持するというもの
	DA:1000:15 = 1
;キャラ触手用
ELSEIF LOCAL >= 50
	FLAG:98 |= LOCAL:3
;通常の触手能力用
ELSE
	FLAG:75 |= LOCAL:3
ENDIF
SELECTCASE LOCAL
	CASE 0
		PRINTL 触手は[鞭触手]を得た
		PRINTW コマンド＜触手鞭打ち＞が使えるようになりました
	CASE 1
		PRINTL 触手は[針状触手]を得た
		PRINTW コマンド＜触手針＞が使えるようになりました
	CASE 2
		PRINTL 触手は[放電触手]を得た
		PRINTW コマンド＜触手放電＞が使えるようになりました
	CASE 3
		PRINTL 触手は[催眠触手]を得た
		PRINTW コマンド＜触手催眠＞が使えるようになりました
	CASE 4
		PRINTL 触手は[極細触手]を得た
		PRINTW コマンド＜触手乳腺挿入＞が使えるようになりました
	CASE 5
		PRINTL 触手は[養分注入触手]を得た
		PRINTW コマンド＜触手強制回復＞が使えるようになりました
	CASE 6
		PRINTL 触手は[抗菌触手]を得た
		PRINTW 尿道調教関連のコマンドが使えるようになりました
	CASE 7
		PRINTL 触手は[ゴーヤ型触手]を得た
		PRINTW 拡張関連のコマンドが使えるようになりました
	CASE 8
		PRINTL 触手は[磯巾着型触手]を得た
		PRINTW コマンド＜触手くすぐり＞が使えるようになりました
	CASE 9
		PRINTL 触手は[遠隔精密制御]を得た
		PRINTW コマンド＜触手貫通＞が使えるようになりました
	CASE 10
		PRINTL 触手は[特殊ホルモン分泌]を得た
		PRINTW 薬品関連のコマンドが使えるようになりました
	CASE 11
		PRINTL 触手は[触手賢者の石]を得た
		PRINTW ＜触手錬金＞が解禁されました
	CASE 12
		PRINTL 触手は[奴隷懲罰用触手部屋]を得た
		PRINTW ＜触手漬け＞が解禁されました
	CASE 13
		PRINTL 触手は[体内適応力強化]を得た
		PRINTW 触手の寄生能力が強化されました
	CASE 14
		PRINTL 触手は[精液放出量強化]を得た
		PRINTW 触手の射精能力が強化されました
	CASE 15
		PRINTL 触手は[精子と触手の部屋]を得た
		PRINTW ＜触手幽閉＞が解禁されました
	CASE 16
		PRINTL 触手は[吸血触手]を得た
		PRINTW コマンド＜触手吸血＞が使えるようになりました
	CASE 17
		PRINTL 触手は[触手保育施設]と[触手分娩台]を得た
		PRINTW ＜触手養殖＞が解禁されました
	CASE 50
		PRINTL 触手は[SATORI触手]を得た
		PRINTW コマンド＜触手読心責め＞が使えるようになりました
	CASE 51
		PRINTL 触手は[NITORI触手]を得た
		PRINTW コマンド＜触手潮干狩り＞が使えるようになりました
	CASE 52
		PRINTL 触手は[YUYUKO触手]を得た
		PRINTW コマンド＜触手幽霊膣鏡＞が使えるようになりました
	CASE 53
		PRINTL 触手は[SUIKA触手]を得た
		PRINTW コマンド＜触手強制飲酒＞が使えるようになりました
	CASE 54
		PRINTL 触手は[ALICE触手]を得た
		PRINTW コマンド＜触手精神制御＞が使えるようになりました
	CASE 55
		PRINTL 触手は[HINA触手]を得た
		PRINTW コマンド＜触手厄落とし＞が使えるようになりました
	CASE 56
		PRINTL 触手は[AYA触手]を得た
		PRINTW コマンド＜触手腸内撮影＞が使えるようになりました
	CASE 57
		PRINTL 触手は[PARSEE触手]を得た
		PRINTW コマンド＜触手言葉責め＞が使えるようになりました
	CASE 58
		PRINTL 触手は[MEDICINE触手]を得た
		PRINTW コマンド＜触手鈴蘭媚薬＞が使えるようになりました
	CASE 59
		PRINTL 触手は[PATCHOULI触手]を得た
		PRINTW コマンド＜触手官能図書＞が使えるようになりました
	CASE 60
		PRINTL 触手は[HIGH-FAIRY触手]を得た
		PRINTW コマンド＜触手妖精按摩＞が使えるようになりました
	CASE 61
		PRINTL 触手は[CIRNO触手]を得た
		PRINTW コマンド＜触手氷精手淫＞が使えるようになりました
	CASE 62
		PRINTL 触手は[NAZRIN触手]を得た
		PRINTW コマンド＜触手淫脈探査＞が使えるようになりました
	CASE 63
		PRINTL 触手は[EIKI触手]を得た
		PRINTW コマンド＜触手最終審判＞が使えるようになりました
	CASE 64
		PRINTL 触手は[SHIZUHA触手]を得た
		PRINTW コマンド＜触手百裂紅葉＞が使えるようになりました
	CASE 65
		PRINTL 触手は[MINORIKO触手]を得た
		PRINTW コマンド＜触手豊穣淫香＞が使えるようになりました
	CASE 66
		PRINTL 触手は[SUWAKO触手]を得た
		PRINTW コマンド＜触手○○○○＞が使えるようになりました
	CASE 99
		PRINTL 触手は[現し身触手]を得た
		PRINTL 触手は現し身の力を得ることで、生贄を用意することなく
		PRINTW 一度取得したキャラ触手を使用できるようになりました
ENDSELECT
DRAWLINE
ライン数:1 = LINECOUNT
CLEARLINE ライン数:1 - ライン数:0
RESTART

;-----------------------------------------------
;触手錬金
;-----------------------------------------------
@TENTACLE_LABORATORY_ALCHEMY
;元パッチの機能の微妙な移植になる可能性大。
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL 現在の触手の能力:
CALL SHOW_TENTACLE_ABL
DRAWLINE
IF ABL:MASTER:触手使役 == 0
	LOCAL = 5
ELSEIF ABL:MASTER:触手使役 == 1
	LOCAL = 10
ELSEIF ABL:MASTER:触手使役 == 2
	LOCAL = 15
ELSEIF ABL:MASTER:触手使役 == 3
	LOCAL = 25
ELSEIF ABL:MASTER:触手使役 == 4
	LOCAL = 35
ELSE
	LOCAL = 50
ENDIF
PRINTFORML 触手が貯めたエネルギーを消費して金貨を作ることが出来ます(換金効率:1τ分の金貨＝{LOCAL}圓)
PRINTFORML 現在の触手エネルギー:{BASE:MASTER:触手}τ　　所持金:{MONEY}圓
PRINTFORML いくらエネルギーを使いますか？ (1-{BASE:MASTER:触手}枚、無効な値で戻る)
DRAWLINE
PRINTLC [0] 戻る
PRINTLC [1] 1枚
SIF BASE:MASTER:触手 >= 100
	PRINTLC [100] 100枚
SIF BASE:MASTER:触手 >= 500
	PRINTLC [500] 500枚
SIF BASE:MASTER:触手 >= 1000
	PRINTLC [1000] 1000枚
SIF BASE:MASTER:触手 >= 5000
	PRINTLC [5000] 5000枚
SIF BASE:MASTER:触手 != 1 && BASE:MASTER:触手 != 5 && BASE:MASTER:触手 != 10
	PRINTFORMLC [{BASE:MASTER:触手}] 可能な限り
PRINTL 
INPUT
IF RESULT > BASE:MASTER:触手
	PRINTW エネルギーが足りません。
	RETURN 0
ELSEIF RESULT <= 0
	RETURN 0
ENDIF
LOCAL:1 = RESULT

DRAWLINE
PRINTFORML 錬成を実行した場合、合計{LOCAL:1 * LOCAL}圓分の収入が見込めます
PRINTL 錬成しますか？
PRINTL [0] はい
PRINTL [1] いいえ
$INPUT_LOOP
INPUT
IF RESULT == 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	RESTART
ELSEIF RESULT != 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

MONEY:1 = LOCAL:1 * LOCAL
BASE:MASTER:触手 -= LOCAL:1
MONEY += MONEY:1
PRINTFORML ＜%CALLNAME:MASTER%は触手錬金で作りだした金貨を売った＞
PRINTFORMW ＜資金が{MONEY:1}圓増えました＞
;--------------------------------------------------
;触手錬金で稼いだ最高金額判定処理
IF MONEY:1 > MONEY:975
	MONEY:975 = MONEY:1
	;ハイスコア更新処理
	SIF MONEY:1 > MONEY:998
		MONEY:998 = MONEY:1
ENDIF
;--------------------------------------------------

;-----------------------------------------------
;錬金転化
;-----------------------------------------------
@TENTACLE_LABORATORY_ALCHEMY_2
;元パッチの機能の微妙な移植になる可能性大。
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL 現在の触手の能力:
CALL SHOW_TENTACLE_ABL
DRAWLINE
IF ABL:MASTER:触手使役 == 0
	LOCAL = 1
ELSEIF ABL:MASTER:触手使役 == 1
	LOCAL = 2
ELSEIF ABL:MASTER:触手使役 == 2
	LOCAL = 3
ELSEIF ABL:MASTER:触手使役 == 3
	LOCAL = 4
ELSEIF ABL:MASTER:触手使役 == 4
	LOCAL = 5
ELSE
	LOCAL = 6
ENDIF
PRINTFORML 主人の%PALAMNAME:198%の珠を消費してエネルギーを作ることが出来ます(転化効率:10枚分のエネルギー＝{LOCAL}τ)
PRINTFORML 現在の主人の%PALAMNAME:198%の珠:{JUEL:MASTER:触手}枚　　現在の触手エネルギー:{BASE:MASTER:触手}τ
PRINTFORML いくら%PALAMNAME:198%の珠を使いますか？ (1-{JUEL:MASTER:触手 / 10}、無効な値で戻る)
DRAWLINE
PRINTLC [0] 戻る
SIF JUEL:MASTER:触手 >= 10
	PRINTLC [1] 10枚
SIF JUEL:MASTER:触手 >= 50
	PRINTLC [5] 50枚
SIF JUEL:MASTER:触手 >= 100
	PRINTLC [10] 100枚
SIF JUEL:MASTER:触手 >= 500
	PRINTLC [50] 500枚
SIF JUEL:MASTER:触手 >= 10
	PRINTFORMLC [{JUEL:MASTER:触手 / 10}] 可能な限り
PRINTL 
INPUT
IF RESULT > JUEL:MASTER:触手 / 10
	PRINTFORMW %PALAMNAME:198%の珠が足りません。
	RETURN 0
ELSEIF RESULT <= 0
	RETURN 0
ENDIF
LOCAL:1 = RESULT

DRAWLINE
PRINTFORML 転化を実行した場合、合計{LOCAL:1 * LOCAL}τ分のエネルギーが見込めます
PRINTL 転化しますか？
PRINTL [0] はい
PRINTL [1] いいえ
$INPUT_LOOP
INPUT
IF RESULT == 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	RESTART
ELSEIF RESULT != 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

LOCAL:2 = LOCAL:1 * LOCAL
JUEL:MASTER:触手 -= LOCAL:1 * 10
BASE:MASTER:触手 += LOCAL:2
PRINTFORML ＜%CALLNAME:MASTER%は錬金転化で作りだしたエネルギーを得った＞
PRINTFORMW ＜触手エネルギーが{LOCAL:2}τ増えました＞

;-----------------------------------------------
;触手漬け
;-----------------------------------------------
@TENTACLE_LABORATORY_SPECIALROOM
;躾が悪い奴隷はここに閉じ込めて一晩中延々と触手の相手をしてもらう……感じになる予定。
;バランスブレイカーになりかねないのでシチュ用に割り切るか、何らかのデメリットを付加して行こうと思う。
;地の文は期待しない方が吉
;表示させるキャラを抽出(LOCAL:2に人数)
VARSET LOCAL, 0
CALLF CLEAR_LIST
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	SIF COUNT == 0
		CONTINUE
;	SIF COUNT == TARGET
;		CONTINUE
	SIF CFLAG:COUNT:4
		CONTINUE
	;弱味、妄信、恋慕系、服従系は除外
	SIF TALENT:COUNT:弱味 || TALENT:COUNT:恋慕 || TALENT:COUNT:妄信 || TALENT:COUNT:親愛 || TALENT:COUNT:相愛 || TALENT:COUNT:服従 || TALENT:COUNT:烙印 || TALENT:COUNT:隷属 || TALENT:COUNT:使い魔
		CONTINUE
	;妊娠、育児中、抱卵中も除外
	SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中 || TALENT:COUNT:抱卵中 || TALENT:COUNT:死亡
		CONTINUE
	;反発刻印Lv0は除外
	SIF MARK:COUNT:反発刻印 == 0
		CONTINUE
	;従順か奉仕精神のどちらかがLv3以上なら除外
	;(反抗的、プライド高い、抵抗はどちらかが4以上なら除外)
	IF TALENT:COUNT:反抗的 || TALENT:COUNT:プライド高い || TALENT:COUNT:抵抗
		SIF ABL:COUNT:従順 > 3 || ABL:COUNT:奉仕精神 > 3
			CONTINUE
	ELSE
		SIF ABL:COUNT:従順 > 2 || ABL:COUNT:奉仕精神 > 2
			CONTINUE
	ENDIF
	;触手中毒Lv2以上なら除外
	SIF ABL:COUNT:触手中毒 > 1
		CONTINUE
	;[養分注入触手]が無い状態では体力500未満の奴隷を連れ込めない
	SIF BASE:COUNT:体力 < 500 && (FLAG:75 & 32) == 0
		CONTINUE
	;体力、気力の最大値のどちらかが500未満のの場合も奴隷を連れ込めない
	SIF MAXBASE:COUNT:0 < 500 || MAXBASE:COUNT:1 < 500
		CONTINUE
	;楽して快感の否定持ちを堕とそうなんて考えないように
	SIF TALENT:COUNT:快感の否定 && MARK:COUNT:反発刻印
		CONTINUE
	SIF NO:COUNT == 5001 && (FLAG:570 & 48) == 0
		CONTINUE

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
	SIF (LOCAL:2%20) == 1 && LOCAL:2 > 20
		LOCAL:5 += 1
REND

$PRINT_LIST
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL 反抗的な奴隷を地下室に閉じ込め、一晩かけて触手の魅力を教え込みます。
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL:3+1}＞
CALL TENTACLE_LIST_CHARA, LOCAL:3
DRAWLINE
PRINTFORMLC \@(LOCAL:3 <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL:3 >= LOCAL:5) ? %" " * 16% # [1009]次のページ\@
PRINTL 
PRINTLC 　　　
PRINTLC [1111]メニューへ戻る
PRINTL 

$INPUT_LOOP_0
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1111
	RESULT:1 = 1
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL:3 > 0
	LOCAL:3 -= 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL:3 < LOCAL:5
	LOCAL:3 += 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF

LOCAL:98 = TARGET
TARGET = RESULT
LOCAL:1 = 5000
;必要エネルギー減少系
;欲望
LOCAL:2 = ABL:欲望
LOCAL:1 -= LOCAL:2 * 250
;大人しい
SIF TALENT:大人しい
	LOCAL:1 -= 300
;プライド低い
SIF TALENT:プライド低い
	LOCAL:1 -= 1000
;貞操無頓着
SIF TALENT:貞操無頓着
	LOCAL:1 -= 500
;解放
SIF TALENT:解放
	LOCAL:1 -= 1000
;倒錯的
SIF TALENT:倒錯的
	LOCAL:1 -= 700
;必要エネルギー増加系
;従順
LOCAL:2 = ABL:従順
LOCAL:1 += (3 - LOCAL:2) * 600
;反抗的
SIF TALENT:反抗的
	LOCAL:1 += 1200
;気丈
SIF TALENT:気丈
	LOCAL:1 += 500
;生意気
SIF TALENT:生意気
	LOCAL:1 += 2500
;プライド高い
SIF TALENT:プライド高い
	LOCAL:1 += 5000
;保守的
SIF TALENT:保守的
	LOCAL:1 += 3000
;貞操観念
SIF TALENT:貞操観念
	LOCAL:1 += 1000
;抑圧
SIF TALENT:抑圧
	LOCAL:1 += 2500
;抵抗
SIF TALENT:抵抗
	LOCAL:1 += 5000
;屈服刻印
LOCAL:2 = MARK:屈服刻印
LOCAL:1 += (1 + LOCAL:2) * 750
;倍率変動
;淫乱
SIF TALENT:淫乱
	LOCAL:1 /= 2
;触手中毒
SIF ABL:触手中毒
	LOCAL:1 /= 2
;一線越えない＋反発刻印Lv3
IF TALENT:一線越えない && MARK:反発刻印 >= 3
	LOCAL:1 *= 35
ELSE
	;一線越えない
	SIF TALENT:一線越えない
		LOCAL:1 *= 3
	;反発刻印
	LOCAL:2 = 2 * MARK:反発刻印
	LOCAL:1 *= MAX(LOCAL:2, 1)
ENDIF
LOCAL:1 = MIN(LOCAL:1, 1000)
;補正用
TIMES LOCAL:1 , 3.50
PRINTFORML %CALLNAME:TARGET%に一晩かけて触手の魅力を教え込みますか？
PRINTFORML 現在の触手エネルギー:{BASE:MASTER:触手}τ　　({LOCAL:1}τ必要)
SIF TALENT:処女
	PRINTFORML (※はいを選ぶと%CALLNAME:TARGET%は処女を失うことになります)
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
SIF TALENT:MASTER:998
	LOCAL:1 = 0
$INPUT_LOOP_1
INPUT
IF RESULT == 1
	TARGET = LOCAL:98
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 0
	IF BASE:MASTER:触手 < LOCAL:1
		PRINTW 触手エネルギーが不足しています。
		TARGET = LOCAL:98
		ライン数:1 = LINECOUNT
		CLEARLINE ライン数:1 - ライン数:0
		GOTO PRINT_LIST
	ELSE
		BASE:MASTER:触手 -= LOCAL:1
	ENDIF
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF
DRAWLINE
;触手漬けイベント突き飛ばされた時～触手に捕まる時の口上用
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 250, LOCAL:1
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
;触手漬けイベント凌辱中～精神が崩壊したあたりの時の口上用
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 251, LOCAL:1
CALL TENTACLE_SPECIALROOM_TRAINING, TARGET
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
;触手漬けイベント屈服時(懇願しているとき？)の口上用
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 252, LOCAL:1
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
LOCAL:2 = 0
IF TALENT:精神崩壊 || TALENT:傀儡 ||MARK:反発刻印 == 0
	LOCAL:2 = 0
ELSEIF MARK:反発刻印 > 2 && JUEL:恐怖 > 50000
	MARK:反発刻印 -= 1
	JUEL:恐怖 -= 50000
	LOCAL:2 = 1
ELSEIF MARK:反発刻印 > 1 && JUEL:恐怖 > 10000
	MARK:反発刻印 -= 1
	JUEL:恐怖 -= 10000
	LOCAL:2 = 1
ELSEIF MARK:反発刻印 > 0 && JUEL:恐怖 > 5000
	MARK:反発刻印 -= 1
	JUEL:恐怖 -= 5000
	LOCAL:2 = 1
ENDIF
IF LOCAL:2 == 1
	TFLAG:232 += 1
	PRINTFORML %CALLNAME:TARGET%の瞳から反抗の意志が消えている…
	PRINTFORMW %CALLNAME:TARGET%の反発刻印がLV{MARK:反発刻印}に下がった
	IF TALENT:弱味 == 0 && TALENT:服従 == 0 && TALENT:烙印 == 0 && TALENT:隷属 == 0 && TALENT:壊造人格 == 0
		PRINTFORML %CALLNAME:TARGET%は今回の体験がトラウマになったようだ…
		PRINTFORMW %CALLNAME:TARGET%は[弱味]を得た
		TALENT:弱味 = 1
		CFLAG:12 |= 1024
	ENDIF
ENDIF

LOCAL = TFLAG:230 * (TFLAG:231 + TFLAG:232) * 10
SIF TALENT:MASTER:998
	PRINTFORML ストレス＋{LOCAL}

;ストレス値は100万以上にはならない
CFLAG:ストレス = MIN(CFLAG:ストレス + LOCAL, 999999)

MAXBASE:0 -= MAX(TFLAG:232 * 50, MAXBASE:0 - 700)
MAXBASE:1 -= MAX(TFLAG:232 * 10, MAXBASE:1 - 500)
BASE:気力 = MIN(BASE:気力, MAXBASE:1)
BASE:体力 = 1

PRINTFORML %CALLNAME:TARGET%は激しく消耗している…

IF TALENT:理性
	MAXBASE:9 = MAX(MAXBASE:9 - 3000 , 0)
	BASE:理性 = -9999
	PRINTFORMW %CALLNAME:TARGET%の気力と体力と理性は激減した
ELSE
	PRINTFORMW %CALLNAME:TARGET%の気力と体力は激減した
ENDIF

TARGET = LOCAL:98

FLAG:0 = 1
RETURN 1

;-----------------------------------------------
;触手幽閉
;-----------------------------------------------
@TENTACLE_LABORATORY_PRIVATEROOM
;上の触手漬けと効果は似ているがこっちは閉じ込めた奴隷を屈服させるのではく、
;屈服した奴隷を触手達に調教させていくことが目的。Ｊの待機コマンドに割と近いかも。
;結構強いので体力の最大値が減る、否定の珠が入るなどのデメリットをつけています。
;ただし条件や量は触手漬けよりもかなり軽めにしています。
;体力が減りきらないうちに部屋から出す、触手中毒をつける等である程度回避可能。
;でもやりすぎると…？
;表示させるキャラを抽出(LOCAL:2に人数)
$PRINT_FRESH_LIST
CALLF CLEAR_LIST
VARSET LOCAL, 0
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	SIF COUNT == MASTER
		CONTINUE
;	SIF COUNT == TARGET
;		CONTINUE
	;パートナーとなっている助手はダメ
	SIF CFLAG:COUNT:3 & 4
		CONTINUE
	SIF NO:COUNT == 5001 && (FLAG:570 & 48) == 0
		CONTINUE
	IF CFLAG:COUNT:4 == 4
		;触手部屋の中にいる分はOK(部屋に入れた直後は除く)
		SIF CFLAG:COUNT:39 < 1
			CONTINUE
	ELSE
		;触手部屋にも自室にも居ないならダメ
		SIF CFLAG:COUNT:4
			CONTINUE
		;従順が3未満だと除外
		SIF ABL:COUNT:従順 < 3
			CONTINUE
		;妊娠、育児中、抱卵中も除外
		SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中 || TALENT:COUNT:抱卵中 || TALENT:COUNT:死亡
			CONTINUE
		;反発刻印を持っていると除外
		SIF MARK:COUNT:反発刻印
			CONTINUE
		;体力が全快でないと奴隷を連れ込めない
		SIF BASE:COUNT:体力 < MAXBASE:COUNT:0
			CONTINUE
		;[養分注入触手]が無い状態では体力1000未満の奴隷を連れ込めない
		SIF BASE:COUNT:体力 < 1000 && (FLAG:75 & 32) == 0
			CONTINUE
		;体力、気力の最大値のどちらかが500未満のの場合も奴隷を連れ込めない
		SIF MAXBASE:COUNT:0 < 500 || MAXBASE:COUNT:1 < 500
			CONTINUE
	ENDIF

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
	SIF (LOCAL:2%20) == 1 && LOCAL:2 > 20
		LOCAL:5 += 1
REND

$PRINT_LIST
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL 奴隷を地下室に閉じ込め、じっくり時間をかけて触手の魅力を教え込みます。
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL+1}＞
CALL TENTACLE_LIST_CHARA, LOCAL
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:5) ? %" " * 16% # [1009]次のページ\@
PRINTL 
PRINTLC 　　　
PRINTLC [1111]メニューへ戻る
PRINTL 
$INPUT_LOOP_0
INPUT
ライン数:1 = LINECOUNT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1111
	RESULT:1 = 1
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:5
	LOCAL += 1
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF
LOCAL = RESULT
IF CFLAG:LOCAL:4 == 4
	IF CFLAG:LOCAL:39 < 1
		PRINTFORMW %CALLNAME:LOCAL%はたった今幽閉したばかりです。
		CLEARLINE ライン数:1 - ライン数:0 + 1
		GOTO PRINT_LIST
	ELSE
		IF CFLAG:LOCAL:41 & 1024
			;あまりにも長く触手幽閉を行うと奴隷が取り込まれてしまい、二度と連れ出せなくなる
			;ただしあらかじめ使い魔にしておくと予防可能
			CALL TENTACLE_LABORATORY_PRIVATEROOM_3, LOCAL
		ELSE
			CALL TENTACLE_LABORATORY_PRIVATEROOM_2, LOCAL
		ENDIF
	ENDIF
ELSE
	CALL TENTACLE_LABORATORY_PRIVATEROOM_1, LOCAL
	ライン数:1 = LINECOUNT
	SIF RESULT == 0
		CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_FRESH_LIST
ENDIF

;………………………………………………
;触手幽閉開始
;………………………………………………
@TENTACLE_LABORATORY_PRIVATEROOM_1, ARG
LOCAL = TARGET
TARGET = ARG
LOCAL:1 = 2500
;必要エネルギー減少系
;欲望
IF ABL:欲望
	LOCAL:2 = ABL:欲望
	LOCAL:1 -= LOCAL:2 * 120
ENDIF
;屈服刻印
IF MARK:屈服刻印
	LOCAL:2 = MARK:屈服刻印
	LOCAL:1 -= (1 + LOCAL:2) * 75
ENDIF
;大人しい
SIF TALENT:大人しい
	LOCAL:1 -= 150
;プライド低い
SIF TALENT:プライド低い
	LOCAL:1 -= 300
;貞操無頓着
SIF TALENT:貞操無頓着
	LOCAL:1 -= 100
;解放
SIF TALENT:解放
	LOCAL:1 -= 300
;倒錯的
SIF TALENT:倒錯的
	LOCAL:1 -= 230
;必要エネルギー増加系
;従順
IF ABL:従順 < 3
	LOCAL:2 = ABL:従順
	LOCAL:1 += (3 - LOCAL:2) * 150
ENDIF
;反抗的
SIF TALENT:反抗的
	LOCAL:1 += 120
;気丈
SIF TALENT:気丈
	LOCAL:1 += 210
;生意気
SIF TALENT:生意気
	LOCAL:1 += 120
;プライド高い
SIF TALENT:プライド高い
	LOCAL:1 += 500
;保守的
SIF TALENT:保守的
	LOCAL:1 += 300
;貞操観念
SIF TALENT:貞操観念
	LOCAL:1 += 100
;抑圧
SIF TALENT:抑圧
	LOCAL:1 += 250
;抵抗
SIF TALENT:抵抗
	LOCAL:1 += 500
;倍率変動
;淫乱
SIF TALENT:淫乱
	LOCAL:1 /= 2
;触手中毒
SIF ABL:触手中毒
	LOCAL:1 /= 2
;一線越えない
SIF TALENT:一線越えない
	LOCAL:1 *= 3
LOCAL:1 = MAX(LOCAL:1, 750)
IF TALENT:オトコ
	;ＢＬ中毒
	LOCAL:2 = ABL:ＢＬ中毒
ELSE
	;レズ中毒
	LOCAL:2 = ABL:レズ中毒
ENDIF
SIF LOCAL:2
	LOCAL:1 += LOCAL:2 * 50
;補正用
TIMES LOCAL:1 , 2.50
PRINTFORML %CALLNAME:TARGET%に時間をかけて触手の魅力を教え込みますか？
PRINTFORML 現在の触手エネルギー:{BASE:MASTER:触手}τ　　({LOCAL:1}τ必要)
SIF TALENT:処女
	PRINTFORML （※はいを選ぶと%CALLNAME:TARGET%は処女を失うことになります）
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
SIF TALENT:MASTER:998
	LOCAL:1 = 0
$INPUT_LOOP_1
INPUT
IF RESULT == 1
	TARGET = LOCAL
	RETURN 0
ELSEIF RESULT == 0
	IF BASE:MASTER:触手 < LOCAL:1
		PRINTW 触手エネルギーが不足しています
		TARGET = LOCAL
		RETURN 0
	ELSE
		BASE:MASTER:触手 -= LOCAL:1
	ENDIF
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF
DRAWLINE
;触手幽閉イベント口上用。
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 253, LOCAL:1
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
CFLAG:39 = 0
CFLAG:4 = 4
TARGET = LOCAL
IF ARG == TARGET
	TARGET = -1
ELSEIF ARG == ASSI
	ASSI = -1
ENDIF
DRAWLINE
RETURN 1

;………………………………………………
;触手幽閉終了
;………………………………………………
@TENTACLE_LABORATORY_PRIVATEROOM_2, ARG
LOCAL = TARGET
TARGET = ARG
;触手幽閉イベント終了時の口上用。
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 254
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
IF BASE:体力 < 100
	PRINTFORML %CALLNAME:TARGET%はこれ以上にないくらい消耗している…
ELSEIF BASE:体力 < 500
	PRINTFORML %CALLNAME:TARGET%は激しく消耗している…
ELSEIF BASE:体力 < 1000
	PRINTFORML %CALLNAME:TARGET%はかなり消耗している…
ELSE
	PRINTFORML %CALLNAME:TARGET%は消耗している…
ENDIF
PRINTW しばらくは休ませたほうが良いかも知れない
CFLAG:39 = 0
CFLAG:4 = 0
FLAG:0 = 1
TARGET = LOCAL
DRAWLINE
RETURN 1

;………………………………………………
;触手同化イベント
;………………………………………………
@TENTACLE_LABORATORY_PRIVATEROOM_3, ARG
LOCAL = TARGET
LOCAL:1 = ASSI
TARGET = ARG
CALL TENTACLE_ABSORB_EVENT
LOCAL:2 = TFLAG:233 * MAX(CFLAG:39 / 10, 1)
LOCAL:3 = TFLAG:234 * 100
;触手同化イベントの口上用。
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 256
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTFORMW ―――%CALLNAME:TARGET%は触手達に取り込まれました―――
PRINTW ………………
PRINTW …………
PRINTW ……
PRINTFORMW ＜%CALLNAME:TARGET%からエネルギーを作りだした＞
PRINTFORML ＜触手エネルギー＋{LOCAL:2}τ＞
PRINTFORMW ＜触手の珠＋{LOCAL:3}個(主人)＞
PRINTFORMW ＜%CALLNAME:TARGET%は二度と調教対象に選択できなくなりました＞
PRINTL 
;触手中毒Lv3以上で助手可能または[恋慕]・[服従]・[傀儡]・[壊造人格]のいずれかを持った名有りキャラが生贄になった場合、判定処理へ
SIF ((ABL:触手中毒 >= 3 && CFLAG:1 == 2) || TALENT:恋慕 || TALENT:服従 || TALENT:傀儡 || TALENT:壊造人格) && (NO:TARGET > 1000 && NO:TARGET < 3000)
	CALL CHARACHECK_TENTACLE_SACRIFICE
DRAWLINE
DELCHARA ARG

IF LOCAL < 0 || LOCAL == ARG
	TARGET = -1
ELSE
	TARGET = (LOCAL > ARG) ? (LOCAL - 1) # LOCAL
ENDIF
IF LOCAL:1 < 0 || LOCAL:1 == ARG
	ASSI = -1
ELSE
	ASSI = (LOCAL:1 > ARG) ? (LOCAL:1 - 1) # LOCAL:1
ENDIF
BASE:MASTER:触手 += LOCAL:2
JUEL:MASTER:触手 += LOCAL:3
FLAG:33 += 1
FLAG:99 += 1
RETURN 1

;-----------------------------------------------
;触手生贄
;-----------------------------------------------
@TENTACLE_LABORATORY_SACRIFICE
;効果は長時間触手幽閉したときに出る同化イベントをいきなりやるようなもの。
;調教による触手との親和性がまだ低いため手に入るエネルギーは触手幽閉よりも少なめ。
;触手同化イベントと同じく触手の生贄になったものは二度と解放されることはない＝消滅扱いになるので注意。
;表示させるキャラを抽出(LOCAL:2に人数)
CALLF CLEAR_LIST
VARSET LOCAL, 0
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	SIF COUNT == MASTER
		CONTINUE
;	SIF COUNT == TARGET
;		CONTINUE
	;パートナーとなっている助手はダメ
	SIF CFLAG:COUNT:3 & 4
		CONTINUE
	;その場に居ないならダメ
	SIF CFLAG:COUNT:4
		CONTINUE
	;使い魔不可
	SIF TALENT:COUNT:使い魔
		CONTINUE
	SIF NO:COUNT == 5001 && (FLAG:570 & 48) == 0
		CONTINUE
	IF TALENT:MASTER:998 == 0
		;従順が3未満だと除外
		SIF ABL:COUNT:従順 < 3
			CONTINUE
		;妊娠、育児中、抱卵中も除外
		SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中 || TALENT:COUNT:抱卵中 || TALENT:COUNT:死亡
			CONTINUE
		;反発刻印3を持っていると除外
		SIF MARK:COUNT:反発刻印 > 2
			CONTINUE
		;体力、気力の最大値のどちらかが1000未満の場合は奴隷を連れ込めない
		SIF MAXBASE:COUNT:0 < 1000 || MAXBASE:COUNT:1 < 1000
			CONTINUE
		;現在の体力、気力のどちらかが1000未満の場合も奴隷を連れ込めない
		SIF BASE:COUNT:体力 < 1000 || BASE:COUNT:気力 < 1000
			CONTINUE
	ENDIF

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
	SIF (LOCAL:2%20) == 1 && LOCAL:2 > 20
		LOCAL:5 += 1
REND

$PRINT_LIST
ライン数:0 = LINECOUNT
DRAWLINE
PRINTL エネルギーと引き換えに奴隷を触手の生贄に捧げます。
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL:6+1}＞
CALL TENTACLE_LIST_CHARA, LOCAL:6
DRAWLINE
PRINTFORMLC \@(LOCAL:6 <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL:6 >= LOCAL:5) ? %" " * 16% # [1009]次のページ\@
PRINTL 
PRINTLC 　　　
PRINTLC [1111]メニューへ戻る
PRINTL 
$INPUT_LOOP_0
INPUT
ライン数:1 = LINECOUNT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1111
	RESULT:1 = 1
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL:6 -= 1
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:5
	LOCAL:6 += 1
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF

LOCAL = RESULT
PRINTFORML ※触手の生贄に捧げると二度と%CALLNAME:LOCAL%に会うことができません。
PRINTL それでも実行しますか？
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 1
	ライン数:1 = LINECOUNT
	CLEARLINE ライン数:1 - ライン数:0
	GOTO PRINT_LIST
ELSEIF RESULT != 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF

LOCAL:98 = TARGET
LOCAL:97 = ASSI
TARGET = LOCAL
CALL TENTACLE_ABSORB_EVENT
TFLAG:234 *= 40
;触手生贄イベントの口上用。
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 257
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
IF TALENT:死亡
SELECTCASE RAND:3
	CASE 0
		PRINTFORMW %CALLNAME:TARGET%はこれから一生触手の糧となる運命に……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達の生贄に捧げられました―――
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%は触手を楽しませるだけの存在になるだろう……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達のオモチャにされました―――
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%は二度と日の光も、月の明かりも目にすることはないだろう……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達の繭に取り込まれました―――
ENDSELECT
ELSE
SELECTCASE RAND:4
	CASE 1
		PRINTFORMW %CALLNAME:TARGET%はこれから一生触手の糧となる運命に……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達の生贄に捧げられました―――
	CASE 2
		PRINTFORMW %CALLNAME:TARGET%は触手を楽しませるだけの存在になるだろう……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達のオモチャにされました―――
	CASE 3
		PRINTFORMW %CALLNAME:TARGET%は二度と日の光も、月の明かりも目にすることはないだろう……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達の繭に取り込まれました―――
	CASEELSE
		PRINTFORMW %CALLNAME:TARGET%は逃げることも拒むことも許されず、ただひたすら凌辱され続けることになるだろう……
		PRINTW  
		PRINTFORMW ―――%CALLNAME:TARGET%は触手達に徹底的に犯されました―――
ENDSELECT
ENDIF
PRINTW ………………
PRINTW …………
PRINTW ……
PRINTFORMW ＜触手達は%CALLNAME:TARGET%からエネルギーを搾り取った＞
PRINTFORML ＜触手エネルギー＋{TFLAG:233}τ＞
PRINTFORMW ＜触手の珠＋{TFLAG:234}個(主人)＞
PRINTFORMW ＜%CALLNAME:TARGET%は二度と調教対象に選択できなくなりました＞
PRINTL 
;触手中毒Lv3以上で助手可能または[恋慕]・[服従]・[傀儡]・[壊造人格]のいずれかを持った名有りキャラが生贄になった場合、判定処理へ
SIF ((ABL:触手中毒 >= 3 && CFLAG:1 == 2) || TALENT:恋慕 || TALENT:服従 || TALENT:傀儡 || TALENT:壊造人格) && (NO:TARGET > 1000 && NO:TARGET < 3000)
	CALL CHARACHECK_TENTACLE_SACRIFICE
DRAWLINE
DELCHARA LOCAL

IF LOCAL:98 < 0 || LOCAL:98 == LOCAL
	TARGET = -1
ELSE
	TARGET = (LOCAL:98 > LOCAL) ? (LOCAL:98 - 1) # LOCAL:98
ENDIF
IF LOCAL:97 < 0 || LOCAL:97 == LOCAL
	ASSI = -1
ELSE
	ASSI = (LOCAL:97 > LOCAL) ? (LOCAL:97 - 1) # LOCAL:97
ENDIF
BASE:MASTER:触手 += TFLAG:233
JUEL:MASTER:触手 += TFLAG:234
FLAG:33 += 1
FLAG:99 += 1
RETURN 1

;-----------------------------------------------
;触手養殖関係
;-----------------------------------------------
;触手進化拡張パッチと妊娠出産パッチを参考に作成
;奴隷に触手の種付けして繁殖させ、蒐集したり調教したりする
;基本的に種付け→懐卵→時間をおいて産卵→回復を待つ……というサイクルを行う
;間違っても某悪魔みたいに仲間を呼ぶことはない
;※なお産んだ触手を用いる機能は現段階では未実装
@TENTACLE_LABORATORY_BREED
DRAWLINE
PRINTFORML 現在の触手エネルギー({BASE:MASTER:触手}τ)
PRINTL 何をしますか？
SIF CHARANUM > 1
	PRINTL  [0] 触手種付
PRINTL [100]やめる
$INPUT_LOOP
INPUT
IF RESULT == 0 && CHARANUM > 1
	CALL SERVE_TENTACLE_LARVA
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 1
	ENDIF
ELSEIF RESULT == 100
	RETURN 0
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF
RESTART

;-----------------------------------------------
;触手種付
;-----------------------------------------------
;触手と無理やり交尾させて触手を孕ませる処理をここで行う
;(取得する素質は[妊娠]と区別するために[懐卵]と命名)
;否定の珠も大量に入ります
;いろんな所から処理・構文を流用
@SERVE_TENTACLE_LARVA
;表示させるキャラを抽出(LOCAL:2に人数)
CALLF CLEAR_LIST
VARSET LOCAL, 0
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	SIF COUNT == 0
		CONTINUE
;	SIF COUNT == TARGET
;		CONTINUE
	;その場にいないならダメ
	SIF CFLAG:COUNT:4
		CONTINUE
	;処女不可
	SIF TALENT:COUNT:処女
		CONTINUE
	;オトコ不可
	SIF TALENT:COUNT:オトコ
		CONTINUE
	;使い魔不可
	SIF TALENT:COUNT:使い魔
		CONTINUE
	;すでに妊娠or懐卵している、あるいは育児中も不可
	SIF TALENT:COUNT:妊娠 || TALENT:COUNT:育児中 || TALENT:COUNT:懐卵
		CONTINUE
	SIF NO:COUNT == 5001 && (FLAG:570 & 48) == 0
		CONTINUE
	IF TALENT:MASTER:998 == 0
		;必要体力、気力を算出(寄生されているかどうかで分岐)
		LOCAL:3 = (TALENT:COUNT:寄生) ? 1500 # 1000
		;体力、気力の最大値のどちらかが1000(触手寄生中の場合は1500)未満の場合は奴隷を連れ込めない
		SIF MAXBASE:COUNT:0 < LOCAL:3 || MAXBASE:COUNT:1 < LOCAL:3
			CONTINUE
		;現在の体力が1000(触手寄生中の場合は1500)未満の場合も奴隷を連れ込めない
		SIF BASE:COUNT:体力 < LOCAL:3
			CONTINUE
		;小柄体型は精液放出量強化必須(妊娠パッチの小柄体型は妊娠しにくいの特性より)
		SIF TALENT:COUNT:小柄体型 && (FLAG:75 & 16384) == 0
			CONTINUE
		;小人体型は精液放出量強化と極細触手両方を取得している必要がある
		SIF TALENT:COUNT:小人体型 && (FLAG:75 & 16384) == 0 && (FLAG:75 & 16) == 0
			CONTINUE
		;薬毒耐性持ちは体内適応力強化必須
		SIF TALENT:COUNT:薬毒耐性 && (FLAG:75 & 8192) == 0
			CONTINUE
		;回復遅いは養分注入触手必須
		SIF TALENT:COUNT:回復遅い && (FLAG:75 & 32) == 0
			CONTINUE
		;催眠触手を持っている場合
		IF FLAG:75 & 8
			;反発刻印3を持っていると除外
			SIF MARK:COUNT:反発刻印 > 2
				CONTINUE
			;従順が2未満だと除外
			SIF ABL:COUNT:従順 < 2
				CONTINUE
		;持っていない場合
		ELSE
			;反発刻印が1以上でも除外
			SIF MARK:COUNT:反発刻印
				CONTINUE
			;従順が4未満だと除外
			SIF ABL:COUNT:従順 < 4
				CONTINUE
		ENDIF
	ENDIF

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
	SIF (LOCAL:2%20) == 1 && LOCAL:2 > 20
		LOCAL:5 += 1
REND

$PRINT_LIST
DRAWLINE
PRINTL エネルギーを消費して奴隷に触手の子供を孕ませます。
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL+1}＞
CALL TENTACLE_LIST_CHARA, LOCAL
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:5) ? %" " * 16% # [1009]次のページ\@
PRINTL 
PRINTLC 　　　
PRINTLC [1111]メニューへ戻る
PRINTL 
$INPUT_LOOP_0
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1111
	RESULT:1 = 1
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:5
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_0
ENDIF

LOCAL = RESULT
LOCAL:1 = 500
;必要エネルギー減少系
;欲望
LOCAL:2 = ABL:LOCAL:欲望
LOCAL:1 -= LOCAL:2 * 200
;大人しい
SIF TALENT:LOCAL:大人しい
	LOCAL:1 -= 250
;プライド低い
SIF TALENT:LOCAL:プライド低い
	LOCAL:1 -= 600
;貞操無頓着
SIF TALENT:LOCAL:貞操無頓着
	LOCAL:1 -= 800
;解放
SIF TALENT:LOCAL:解放
	LOCAL:1 -= 1000
;倒錯的
SIF TALENT:LOCAL:倒錯的
	LOCAL:1 -= 900
;必要エネルギー増加系
;従順
LOCAL:2 = ABL:LOCAL:従順
LOCAL:1 += (3 - LOCAL:2) * 400
;反抗的
SIF TALENT:LOCAL:反抗的
	LOCAL:1 += 1000
;気丈
SIF TALENT:LOCAL:気丈
	LOCAL:1 += 600
;生意気
SIF TALENT:LOCAL:生意気
	LOCAL:1 += 2000
;プライド高い
SIF TALENT:LOCAL:プライド高い
	LOCAL:1 += 3000
;保守的
SIF TALENT:LOCAL:保守的
	LOCAL:1 += 4000
;貞操観念
SIF TALENT:LOCAL:貞操観念
	LOCAL:1 += 5000
;抑圧
SIF TALENT:LOCAL:抑圧
	LOCAL:1 += 2000
;抵抗
SIF TALENT:LOCAL:抵抗
	LOCAL:1 += 4000
;小人体型
SIF TALENT:LOCAL:小人体型
	LOCAL:1 += 500
;小柄体型
SIF TALENT:LOCAL:小柄体型
	LOCAL:1 += 200
;薬毒耐性
SIF TALENT:LOCAL:薬毒耐性
	LOCAL:1 += 2000
;回復遅い
SIF TALENT:LOCAL:回復遅い
	LOCAL:1 += 700
;屈服刻印
LOCAL:2 = MARK:LOCAL:屈服刻印
LOCAL:1 += (1 + LOCAL:2) * 750
;倍率変動
;淫乱
SIF TALENT:LOCAL:淫乱
	LOCAL:1 /= 2
;触手中毒
SIF ABL:LOCAL:触手中毒
	LOCAL:1 /= 2
;一線越えない＋反発刻印Lv3
IF TALENT:LOCAL:一線越えない && MARK:LOCAL:反発刻印 >= 3
	LOCAL:1 *= 35
ELSE
	;一線越えない
	SIF TALENT:LOCAL:一線越えない
		LOCAL:1 *= 3
	;反発刻印
	LOCAL:2 = 3*MARK:LOCAL:反発刻印
	SIF LOCAL:2
		LOCAL:1 *= LOCAL:2
ENDIF
;催眠触手
SIF FLAG:75 & 8
	TIMES LOCAL:1 , 0.50
;極細触手
SIF FLAG:75 & 16
	TIMES LOCAL:1 , 0.75
;精液放出量強化
SIF FLAG:75 & 16384
	TIMES LOCAL:1 , 0.60
LOCAL:1 = MAX(LOCAL:1, 100)
;補正用
LOCAL:1 /= 10
DRAWLINE
PRINTFORML ※%CALLNAME:LOCAL%に触手の幼体を孕ませます。
PRINTFORML 現在の触手エネルギー:{BASE:MASTER:触手}τ　　({LOCAL:1}τ必要)
PRINTL 実行しますか？
PRINTC [0]はい
PRINTC [1]いいえ
PRINTL 
SIF TALENT:MASTER:998
	LOCAL:1 = 0
$INPUT_LOOP_1
INPUT
IF RESULT == 1
	RESTART
ELSEIF RESULT == 0
	IF BASE:MASTER:触手 < LOCAL:1
		PRINTW 触手エネルギーが不足しています
		RESTART
	ELSE
		BASE:MASTER:触手 -= LOCAL:1
	ENDIF
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ENDIF
LOCAL:98 = TARGET
LOCAL:97 = ASSI
TARGET = LOCAL
DRAWLINE

CALL TENTACLE_LARVA_IMPREGNATE

;地の文分岐用に設定(調教中は人形愛撫に使用)
TFLAG:141 = 0
;地の文の素質変動表示用
LOCALS:0 = 
LOCALS:1 = 
;ストレス加算用
LOCAL:96 = 0
IF TALENT:精神崩壊 || TALENT:傀儡
	;触手懐卵用のイベント口上
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 259, 0
ELSEIF ABL:触手中毒 > 2
	;触手懐卵用のイベント口上
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 259, 1

	;プライド高い
	IF TALENT:プライド高い && ABL:触手中毒 >= 5
		LOCALS:0 = [%TALENTNAME:16%]
		TALENT:プライド高い = 0
		TFLAG:141 |= 1
	ENDIF
	;一線越えない
	IF TALENT:一線越えない
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:27%]
		TALENT:一線越えない = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 64
	ENDIF
	;貞操観念
	IF TALENT:貞操観念
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:30%]
		TALENT:貞操観念 = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 256
	ENDIF
	;抑圧
	IF TALENT:抑圧
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:32%]
		TALENT:抑圧 = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 1
	ENDIF
	;貞操無頓着
	IF TALENT:貞操無頓着 == 0
		LOCALS:1 = [%TALENTNAME:31%]
		TALENT:貞操無頓着 = 1
		TFLAG:141 |= 2
	ENDIF
	;汎用口上用フラグ
	CFLAG:43 = 0
ELSEIF EXP:異常経験 < 10 && ABL:触手中毒 == 0 && TALENT:烙印 == 0 && TALENT:隷属 == 0 && TALENT:傀儡 == 0 && TALENT:壊造人格 == 0
	;触手懐卵用のイベント口上
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 259, 2

	;反抗的
	IF TALENT:反抗的
		LOCALS:0 = [%TALENTNAME:11%]
		TALENT:反抗的 = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 8
	ENDIF
	;気丈
	IF TALENT:気丈
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:12%]
		TALENT:気丈 = 0
		TFLAG:141 |= 1
	ENDIF
	;プライド高い
	IF TALENT:プライド高い
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:16%]
		TALENT:プライド高い = 0
		TFLAG:141 |= 1
	ENDIF
	;自制心
	IF TALENT:自制心
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:20%]
		TALENT:自制心 = 0
		TFLAG:141 |= 1
	ENDIF
	;一線越えない
	IF TALENT:一線越えない
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:27%]
		TALENT:一線越えない = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 256
	ENDIF
	;貞操無頓着
	IF TALENT:貞操無頓着
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:31%]
		TALENT:貞操無頓着 = 0
		TFLAG:141 |= 1
	ENDIF
	;抑圧
	IF TALENT:抑圧
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:32%]
		TALENT:抑圧 = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 1
	ENDIF
	;抵抗
	IF TALENT:抵抗
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:34%]
		TALENT:抵抗 = 0
		TFLAG:141 |= 1
		CFLAG:13 |= 2
	ENDIF
	;恥らい
	IF TALENT:恥じらい
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:35%]
		TALENT:恥じらい = 0
		TFLAG:141 |= 1
	ENDIF
	;献身的
	IF TALENT:献身的
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:63%]
		TALENT:献身的 = 0
		TFLAG:141 |= 1
	ENDIF
	;快感に素直
	IF TALENT:快感に素直
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:70%]
		TALENT:快感に素直 = 0
		TFLAG:141 |= 1
	ENDIF
	;魅力
	IF TALENT:魅力
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:90%]
		TALENT:魅力 = 0
		TFLAG:141 |= 1
	ENDIF
	;魅惑
	IF TALENT:魅惑
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:91%]
		TALENT:魅惑 = 0
		TFLAG:141 |= 1
	ENDIF
	;謎の魅力
	IF TALENT:謎の魅力
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:92%]
		TALENT:謎の魅力 = 0
		TFLAG:141 |= 1
	ENDIF
	;鼓舞
	IF TALENT:鼓舞
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:95%]
		TALENT:鼓舞 = 0
		TFLAG:141 |= 1
	ENDIF
	;恋慕
	IF TALENT:恋慕
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:150%]
		TALENT:恋慕 = 0
		SIF CFLAG:14 & 1
			CFLAG:14 -= 1
		CFLAG:陥落した日 = 0
		CFLAG:MASTER:98 -= 1
		SIF TALENT:妖精 && TALENT:服従 == 0 && TALENT:淫乱 == 0
			FLAG:32 -= 1
		TFLAG:141 |= 1
	ENDIF
	;親愛
	IF TALENT:親愛
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:152%]
		TALENT:親愛 = 0
		SIF CFLAG:14 & 4
			CFLAG:14 -= 4
		TFLAG:141 |= 1
	ENDIF
	;相愛
	IF TALENT:相愛
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:153%]
		TALENT:相愛 = 0
		SIF CFLAG:14 & 8
			CFLAG:14 -= 8
		CFLAG:結婚記念日 = 0
		TFLAG:141 |= 1
	ENDIF
	;幼児退行
	IF TALENT:幼児退行
		LOCALS:0 = %LOCALS:0%[%TALENTNAME:159%]
		TALENT:幼児退行 = 0
		TFLAG:141 |= 1
	ENDIF
	;無関心
	IF TALENT:無関心 == 0
		LOCALS:1 = [%TALENTNAME:21%]
		TALENT:無関心 = 1
		TFLAG:141 |= 2
		CFLAG:13 |= 4
	ENDIF
	;感情乏しい
	IF TALENT:感情乏しい == 0
		LOCALS:1 = %LOCALS:1%[%TALENTNAME:22%]
		TALENT:感情乏しい = 1
		TFLAG:141 |= 2
	ENDIF
	;狂気
	IF TALENT:狂気 == 0
		LOCALS:1 = %LOCALS:1%[%TALENTNAME:158%]
		TALENT:狂気 = 1
		TFLAG:141 |= 2
		CFLAG:12 |= 1
		CFLAG:17 |= 2
	ENDIF
	;精神崩壊
	IF TALENT:精神崩壊 == 0
		LOCALS:1 = %LOCALS:1%[%TALENTNAME:167%]
		TALENT:精神崩壊 = 1
		TFLAG:141 |= 2
		CFLAG:17 |= 4
	ENDIF

	LOCAL:96 = 10000
	;汎用口上用フラグ
	CFLAG:43 = 0
ELSE
	;触手懐卵用のイベント口上
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 259, 3

	LOCAL:96 = 5000
ENDIF
;地の文の素質変動表示
IF TFLAG:141
	PRINTFORM %CALLNAME:TARGET%は
	SIF TFLAG:141 & 1
		PRINTFORM %LOCALS:0%\@(TFLAG:141 & 2) ? を失い、 # を失った\@
	SIF TFLAG:141 & 2
		PRINTFORM %LOCALS:1%を得た
	PRINTW  
ENDIF
;ストレス値追加処理
CFLAG:ストレス += LOCAL:96
SIF TALENT:MASTER:998 && LOCAL:96 > 0
	PRINTFORML ストレス＋{LOCAL:96}
;ストレス値は100万以上にはならない
CFLAG:ストレス = MIN(CFLAG:ストレス, 999999)

IF (CFLAG:24 & 16) == 0
	CFLAG:24 |= 16
	;異常経験
	CALL COMMON_UP_EXP, TARGET, 50, 1, 1
ENDIF

MAXBASE:0 -= 500
BASE:体力 = 500
PRINTFORMW %CALLNAME:TARGET%は疲れ果てているようだ……休ませた方が良いだろう
PRINTW  

;調教対象と助手を元に戻す
TARGET = LOCAL:98
ASSI = LOCAL:97
RETURN 1

;-------------------------------------------------
;触手研究・培養施設用キャラ一覧表示処理
;-------------------------------------------------
@TENTACLE_LIST_CHARA, ARG
DRAWLINE
REPEAT 20
	LOCAL = GET_LIST(COUNT + ARG * 20)
	SIF LOCAL == 0
		CONTINUE

	;主人公と助手対象と調教中キャラは排除
	SIF LOCAL == 0
		CONTINUE
	SIF CFLAG:LOCAL:4 && CFLAG:LOCAL:4 != 4
		CONTINUE
	SIF MARK:LOCAL:98 == 0
		CONTINUE
	IF LOCAL == TARGET
		PRINT ☆
	ELSEIF LOCAL == ASSI
		PRINT ★
	ELSEIF CFLAG:LOCAL:4 == 4
		PRINT ◆
	ELSEIF ISASSI:LOCAL == 1
		PRINT ●
	ELSEIF CFLAG:LOCAL:1 == 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	CALL ARRANGE_CHARALIST, LOCAL
	CALL ARRANGE_CHARALIFE, LOCAL
	SIF TALENT:LOCAL:寄生
		PRINT [寄生]
	SIF TALENT:LOCAL:懐卵
		PRINT [懐卵]
	SIF CFLAG:LOCAL:4 == 4
		PRINTFORM ＜幽閉{CFLAG:LOCAL:39}日目＞
	PRINTL 
REND
