;=============================================================================
;身長・体重・3サイズの生成処理
;=============================================================================
;身長・体重・3サイズを生成し、以下の変数に格納します。
;BASE:10 … 身長 = 基本値 + MAXBASE:10 + 素質 (mm)
;BASE:11 … 体重 = (素質:オトコ) ? (身長 * 30) /100 # (身長 * 28) /100 * 素質 + バストの3乗 * 2 / 10の8乗 
;BASE:12 … バスト = ( 身長 * 100 / 22 - 身長 * 100 / ウェスト ) / 10 + 素質
;BASE:13 … ウェスト = {身長(mm) * 38}/100 (mm) = 身長 * 0.38 で成人女性の平均ウエストになるとか
;BASE:14 … ヒップ = ( 身長 * 54 ) / 100 = 身長 * 0.54 * 素質
;BASE:15 … ペニスの長さ (mm)
;BASE:16 … ペニスの称号
;ちなみに
;BASE:陰核 … クリトリスの成長具合(0 ～ 250)、
;具体的なサイズは、BASE:陰核 * BASE:陰核 / 150(mm)で計算され、416(mm)が最大
;MAXBASE:10 … 身長の計算時の乱数値、キャラごとの.csvに数値をmm単位で記入されていると定数に
;MAXBASE:11 … 未使用
;MAXBASE:12 … 未使用
;MAXBASE:13 … 未使用
;MAXBASE:14 … 未使用
;MAXBASE:15 … ペニスの計算時の乱数値、とか言いつつも、前に測定した乱数を記憶させるための変数
;CFLAG:508 … 身体測定フラグ、過去では"8"を使っていたので、500番台に変更
;CFLAG:509 … ペニス測定フラグ、上のビットで判別すればいいが、面倒
;CFLAG:3500 … 妊娠による追加サイズ
;CFLAG:3510 ～ 3514 … 子供の成長増加量、順に、身長、体重、バスト、ウエスト、ヒップ
;CFLAG:3510 … [幼児]時の身長増加量
;CFLAG:3577 … 子供調教開始日
;LOCAL:0 ～ 4 … 最終計算結果の一時保存用
;LOCAL:10 ～ 14 … 計算途中の一時保存用
;LOCAL:20 ～ 24 … 前の値の一時保存用
;LOCAL:30 … 娘の身長増加量用変数
;LOCAL:50 ～ 54 … ペニスの計算用変数
;TALENT:幼児 :: 幼児
;算出順序は、 身長→ウェスト→バスト→ヒップ→体重 の順に算出します。
;
;第一引数 ･･･ 呼び出し元ARG 0:そのまま 1:キャラの登録番号有り
;第二引数 ･･･ キャラの登録番号
;
;--------------------------------------------------
;身体サイズ生成処理 引数で呼び出し元を取得
;--------------------------------------------------
@MAKE_SIZE, ARG, ARG:1
VARSET LOCAL,0
;初期値の取得 呼び出し元の場所によって変化
LOCAL:99 = (ARG == 0) ? TARGET # ARG:1
;[オトコ][ふたなり][具現]などあって、すでに身体測定をしてしまって未測定のときの抜け道
SIF (TALENT:(LOCAL:99):120 || TALENT:(LOCAL:99):121 || TALENT:(LOCAL:99):123) && CFLAG:(LOCAL:99):508 && CFLAG:(LOCAL:99):509 == 0
	CALL MAKE_PENIS_SIZE, LOCAL:99

;身体測定フラグが立っている、または、成長しきっているとき、計算しないで戻す
SIF CFLAG:(LOCAL:99):508 || (MARK:(LOCAL:99):81 && CFLAG:(LOCAL:99):508)
	RETURN 0

;===身長の算出===
;乱数値が存在しない場合は計算し、それを生涯に渡り使用する。
IF MAXBASE:(LOCAL:99):10 <= 0
	LOCAL:10 = 1 + RAND:70 + RAND:70 + RAND:70 + RAND:70 + RAND:70 + RAND:70
	MAXBASE:(LOCAL:99):10 = LOCAL:10
ELSE
	LOCAL:10 = MAXBASE:(LOCAL:99):10
ENDIF
;乱数値＋性別による基礎が身長である(mm)
;幼児のときは男女差なし
LOCAL:10 += (TALENT:(LOCAL:99):120 && TALENT:(LOCAL:99):130 == 0) ? 1500 # 1300 
;体格素質による修正
;小人体型
IF TALENT:(LOCAL:99):127
	LOCAL:10 -= 1100
	LOCAL:10 /= 2
;小人でなく小柄体型
ELSEIF TALENT:(LOCAL:99):128
	LOCAL:10 -= 400
;巨躯
ELSEIF TALENT:(LOCAL:99):129
	LOCAL:10 += 400
ENDIF

;徐々に成長する
IF FLAG:11 & 64
	;[幼児]の身長増加、[幼児]以外は、CFLAG:3510は0のまま
	IF TALENT:(LOCAL:99):130
		;女は、初めの10日間は、伸びが10、それ以降は30
		LOCAL:30 = (DAY - CFLAG:(LOCAL:99):3577 <= 10) ? 10 # 30 
		;謎の2倍の補正
		LOCAL:30 /= 2
		;しかし、21日目でストップ
		SIF (DAY - CFLAG:(LOCAL:99):3577) > 20
			LOCAL:30 = 0
		CFLAG:(LOCAL:99):3510 += (TALENT:(LOCAL:99):120) ? 19 # LOCAL:30 
		CFLAG:(LOCAL:99):3510 += RAND:10
		;入れ子に出来たら下のようになる
		;CFLAG:(LOCAL:99):3510 = (TALENT:(LOCAL:99):120) ? 19 # (DAY - CFLAG:(LOCAL:99):77 <= 30) ? 10 # (DAY - CFLAG:(LOCAL:99):77 <= 40) ? 30 # 0 
		;小人体型
		SIF TALENT:(LOCAL:99):127
			CFLAG:(LOCAL:99):3510 /= 10
		;生まれて20日目、調教開始日は成長せず
		SIF (DAY - CFLAG:(LOCAL:99):3577) == 0 
			CFLAG:(LOCAL:99):3510 = 0
	ENDIF
	;かつて[幼児]だった人(すなわち、CFLAG:3510 > 0 かつ、素質[幼児]なし)かつ、巨躯、または、小人体型、小柄体型でないのが以下を通る
	SIF CFLAG:(LOCAL:99):3510 > 0 && TALENT:(LOCAL:99):130 == 0  && ( TALENT:(LOCAL:99):129 == 0 || TALENT:(LOCAL:99):127 == 0 || TALENT:(LOCAL:99):128 == 0)
		CFLAG:(LOCAL:99):3510 -= 400
	LOCAL:10 += CFLAG:(LOCAL:99):3510
	;かつて[幼児]だった人はここで成長ストップ
	SIF CFLAG:(LOCAL:99):3510 > 0 && TALENT:(LOCAL:99):130 == 0
		CFLAG:(LOCAL:99):3510 = 0
;男、且つ、[幼児]の補正
ELSEIF TALENT:(LOCAL:99):120 && TALENT:(LOCAL:99):130
	LOCAL:10 += 150
ENDIF

;身長決定
BASE:(LOCAL:99):10 = LOCAL:10


;===ウェストの算出===
;ウェストの基礎値算出
LOCAL:13 = (LOCAL:10 * 38) / 100
;ウェスト決定
BASE:(LOCAL:99):13 = LOCAL:13

;===バストの算出===
;アンダーバスト算出
LOCAL:12 = (((LOCAL:10 * 100) / 22) - ((LOCAL:10 * 100) / LOCAL:13)) / 10

;胸系素質で基礎値変化 110:絶壁 111:貧乳 112:巨乳 113:爆乳
;小人体型の場合、このまま足すと値として完全に破綻するので補正
;呼び出し元の場所によって変化
IF TALENT:(LOCAL:99):110
	LOCAL:12 += 0
ELSEIF TALENT:(LOCAL:99):111
	LOCAL:12 += 100 / (TALENT:(LOCAL:99):127 * 3 + 1)
ELSEIF TALENT:(LOCAL:99):112
	LOCAL:12 += 200 / (TALENT:(LOCAL:99):127 * 3 + 1)
ELSEIF TALENT:(LOCAL:99):113
	LOCAL:12 += 250 / (TALENT:(LOCAL:99):127 * 3 + 1)
ELSE
	LOCAL:12 += 150 / (TALENT:(LOCAL:99):127 * 3 + 1)
ENDIF
;バスト決定
BASE:(LOCAL:99):12 = LOCAL:12

;===ヒップの算出===
;ヒップの基礎値算出
LOCAL:14 = (LOCAL:10 * 54) / 100
;小柄と小人はやや小さめに補正してみる
SIF TALENT:(LOCAL:99):127 || TALENT:(LOCAL:99):128
	TIMES LOCAL:14, 0.95
;ヒップ決定
BASE:(LOCAL:99):14 = LOCAL:14

;===体重の算出===
;体重の基礎値算出(成人オトコだけ別処理)
LOCAL:11 = (TALENT:(LOCAL:99):120 && TALENT:(LOCAL:99):130 == 0) ? (LOCAL:10 * 30) /100 # (LOCAL:10 * 28) /100
;小人体型での補正
IF TALENT:(LOCAL:99):127
	TIMES LOCAL:11, 0.2
;小柄体型での補正
ELSEIF TALENT:(LOCAL:99):128
	TIMES LOCAL:11, 0.8
;巨躯での補正
ELSEIF TALENT:(LOCAL:99):129
	TIMES LOCAL:11, 1.25
ENDIF

;バストによる重量加算
LOCAL:12 = 4 * LOCAL:12 * LOCAL:12 * LOCAL:12 / 1000000
LOCAL:12 *= 2
LOCAL:12 /= 100

;合計
LOCAL:1 = LOCAL:11 + LOCAL:12

;素質による補正
;機械(重い)
SIF TALENT:(LOCAL:99):206
	TIMES LOCAL:1 , 1.50
;霊体(極めて軽い)
SIF TALENT:(LOCAL:99):207
	TIMES LOCAL:1 , 0.60
;鬼(少し重い)
SIF TALENT:(LOCAL:99):214
	TIMES LOCAL:1, 1.10
;神霊(少し軽い)
SIF TALENT:(LOCAL:99):222
	TIMES LOCAL:1 , 0.90

;体重決定
BASE:(LOCAL:99):11 = LOCAL:1
;[オトコ][ふたなり][具現]などがある
SIF TALENT:(LOCAL:99):120 || TALENT:(LOCAL:99):121 || TALENT:(LOCAL:99):123
	CALL MAKE_PENIS_SIZE, LOCAL:99

;身体測定フラグを立てる
CFLAG:(LOCAL:99):508 = 1
;[幼児]持ちで成長見込みのある子(MARK:(LOCAL:99):81 == 0)はフラグを寝かす
SIF TALENT:(LOCAL:99):130 && MARK:(LOCAL:99):81 == 0
	CFLAG:(LOCAL:99):508 = 0


;--------------------------------------------------
;妊娠時に増加するウェスト(腹囲)と体重を操作する
;--------------------------------------------------
;LOCAL:0 ･･･ 逆算用変数
;LOCAL:1 ･･･ 乱数保存用変数
;LOCAL:10 ･･･ 前回計測身長保存
@MAKE_BABY_SIZE, ARG
LOCAL:10 = BASE:ARG:10
CFLAG:ARG:508 = 0
CALL MAKE_SIZE, 1, ARG

IF CFLAG:ARG:3500
	LOCAL:0 = (CFLAG:ARG:3500 * 100) / (LOCAL:10 / 15)
	LOCAL:0 /= MAX((FLAG:600 - (CFLAG:ARG:77 - DAY) + 1), 1)
ENDIF

LOCAL:1 = (13 + RAND:6)
;妊娠増加サイズが減少しすぎないように調整
LOCAL:1 = MAX(LOCAL:0, LOCAL:1)
CFLAG:ARG:3500 = LOCAL:1 * (FLAG:600 - (CFLAG:ARG:77 - DAY))
CFLAG:ARG:3500 *= (BASE:ARG:10 / 15)
CFLAG:ARG:3500 /= 100

;--------------------------------------------------
;ペニス系統の測定
;BASE:15 … ペニスの長さ (mm)
;BASE:16 … ペニスの称号
;ちなみに
;BASE:陰核 … クリトリスの成長具合(0 ～ 250)、
;具体的なサイズは、BASE:陰核 * BASE:陰核 / 150(mm)で計算され、416(mm)が最大
;MAXBASE:10 … 身長の計算時の乱数値、キャラごとの.csvに数値をmm単位で記入されていると定数に
;MAXBASE:11 … 未使用
;MAXBASE:12 … 未使用
;MAXBASE:13 … 未使用
;MAXBASE:14 … 未使用
;MAXBASE:15 … ペニスの計算時の乱数値、とか言いつつも、前に測定した乱数を記憶させるための変数
;CFLAG:508 … 身体測定フラグ、過去では"8"を使っていたので、500番台に変更
;CFLAG:509 … ペニス測定フラグ、上のビットで判別すればいいが、面倒
;LOCAL:50 ～ 54 … ペニスの計算用変数
;TALENT:幼児 :: 幼児
;算出順序は、 身長→ウェスト→バスト→ヒップ→体重 の順に算出します。
;
;第一引数 ･･･ キャラの登録番号
;--------------------------------------------------
@MAKE_PENIS_SIZE, ARG
#LOCALSIZE 100
;計測済みを弾く処理
SIF CFLAG:ARG:509
	RETURN 0

;念のため初期化
FOR PC_VAR1, 50, 54
	LOCAL:PC_VAR1 = 0
NEXT


;体格の素質によってペニスサイズの範囲を若干調整
;小柄体型の場合
IF TALENT:ARG:小柄体型
	LOCAL:53 = 6
	LOCAL:54 = 3
;巨躯の場合
ELSEIF TALENT:ARG:巨躯
	LOCAL:53 = 4
	LOCAL:54 = 1
ELSE
	LOCAL:53 = 5
	LOCAL:54 = 2
ENDIF
;初回設定
IF BASE:ARG:16 == 0
	;通常範囲
	;例)150cm = 10.0cm ～ 15.0cm
	IF RAND:(LOCAL:53)
		LOCAL:51 = RAND:4 + 8
		LOCAL:50 = ((BASE:ARG:10 / 12) * LOCAL:51) / 10
	;スモールサイズ
	;例)150cm = 5.0cm ～ 8.7cm
	ELSEIF RAND:(LOCAL:54)
		LOCAL:51 = RAND:3 + 4
		LOCAL:50 = ((BASE:ARG:10 / 12) * LOCAL:51) / 10
	;ビッグサイズ
	;例)150cm = 16.2cm ～ 25.0cm
	ELSE
		LOCAL:51 = RAND:7 + 13
		LOCAL:50 = MIN(((BASE:ARG:10 / 12) * LOCAL:51) / 10, 300)
	ENDIF
;薬でペニスを大きくする手段が不明なので、形だけは残しておく
;ELSE
;	;薬で大きさを変更した時の設定
;	IF BASE:ARG:16 == 1
;		;マイクロペニスの大きさ、ベース値: 2.0cm ～3.9cm
;		LOCAL:50 = RAND(24, 34)
;	ELSEIF BASE:ARG:16 == 2
;		;ポークビッツの大きさ
;		LOCAL:50 = RAND(44, 64)
;	ELSEIF BASE:ARG:16 == 3
;		;粗チンの大きさ
;		LOCAL:50 = RAND(74, 96)
;	ELSEIF BASE:ARG:16 == 4
;		;普通の大きさ
;		LOCAL:50 = RAND(104, 144)
;	ELSEIF BASE:ARG:16 == 5
;		;マグナムの大きさ
;		LOCAL:50 = RAND(154, 194)
;	ELSEIF BASE:ARG:16 == 6
;		;巨根の大きさ
;		LOCAL:50 = RAND(204, 294)
;	ELSEIF BASE:ARG:16 == 7
;		;第３の足の大きさ、ベース値:31.0cm～100.0cm
;;		LOCAL:50 = RAND(310, 500)
;		SIF LOCAL:50 == 500
;			LOCAL:50 += RAND:500
;	ENDIF
ENDIF

;最終的なミリ単位での微調整、小人体型は除く
IF TALENT:ARG:小人体型 == 0
	IF RAND:3
		LOCAL:50 += RAND:5
		SIF LOCAL:50 > 500
			LOCAL:50 += RAND:95
	ELSE
		LOCAL:50 -= RAND:3
	ENDIF
ENDIF

IF BASE:ARG:15 > 0
	LOCAL:50 = BASE:ARG:15
	LOCAL:50 += (DAY % 2 == 0) ? 1 # 0 
ENDIF

;ペニスの大きさを決定
BASE:ARG:15 = LOCAL:50
MAXBASE:ARG:15 = 1

;かつて、陰核肥大などをしていたときの処理、デカイ方を採用
SIF BASE:ARG:陰核 > 0
	BASE:ARG:15 = MAX((BASE:ARG:陰核 * BASE:ARG:陰核) / 150, LOCAL:50 )

LOCAL:50 = BASE:ARG:15

;ペニスのランク設定
;小人体型の設定
IF TALENT:ARG:小人体型
	LOCAL:52 = 8
;上記以外
ELSE
	;ポークビッツ: 4.0cm ～6.9cm
	IF LOCAL:50 < 70
		LOCAL:52 = 2
	;粗チン: 7.0cm ～9.9cm
	ELSEIF LOCAL:50 < 100
		LOCAL:52 = 3
	;普通:10.0cm ～14.9cm
	ELSEIF LOCAL:50 < 150
		LOCAL:52 = 4
	;マグナム:15.0cm ～19.9cm
	ELSEIF LOCAL:50 < 200
		LOCAL:52 = 5
	;巨根:20.0cm～30.0cm
	ELSEIF LOCAL:50 < 301
		LOCAL:52 = 6
	;第３の足の大きさ、ベース値:30.1cm～100.0cm
	ELSE
		LOCAL:52 = 7
	ENDIF
ENDIF
;ペニスのランクを決定
BASE:ARG:16 = LOCAL:52

;ペニス測定フラグ
CFLAG:ARG:509 = 1

;[幼児]持ちはフラグを寝かす
SIF TALENT:ARG:幼児 && MARK:(LOCAL:99):81 == 0
	CFLAG:ARG:509 = 0
