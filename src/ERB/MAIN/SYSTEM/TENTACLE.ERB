;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;触手関係追加要素の処理
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;調教中・待機中等自分では直接操作をしない類のものを扱う
;=============================================================================
;表示・調教中の処理
;=============================================================================
;-----------------------------------------------
;触手寄生ゲージ計算処理
;-----------------------------------------------
@CALC_TENTACLE_GAUGE
VARSET LOCAL, 0
;ABL:触手中毒をみる(触手寄生LVのゲージ)
SELECTCASE ABL:触手中毒
	CASE 0
		LOCAL = 10
	CASE 1
		LOCAL = 50
	CASE 2
		LOCAL = 100
	CASE 3
		LOCAL = 250
	CASE 4
		LOCAL = 500
	CASEELSE
		LOCAL = 1000
ENDSELECT
;触手インプラント
SIF CFLAG:41
	LOCAL = PARASITIC_TENTACLE_INPLANT(LOCAL)
SELECTCASE EXP:触手経験
	CASE IS >= 160000
		TIMES LOCAL , 5.00
	CASE IS >= 80000
		TIMES LOCAL , 4.75
	CASE IS >= 40000
		TIMES LOCAL , 4.50
	CASE IS >= 20000
		TIMES LOCAL , 4.25
	CASE IS >= 10000
		TIMES LOCAL , 4.00
	CASE IS >= 9000
		TIMES LOCAL , 3.50
	CASE IS >= 8000
		TIMES LOCAL , 3.25
	CASE IS >= 7000
		TIMES LOCAL , 3.00
	CASE IS >= 6000
		TIMES LOCAL , 2.50
	CASE IS >= 5000
		TIMES LOCAL , 2.25
	CASE IS >= 4000
		TIMES LOCAL , 2.00
	CASE IS >= 3000
		TIMES LOCAL , 1.50
	CASE IS >= 2000
		TIMES LOCAL , 1.25
ENDSELECT
LOCAL = MIN(LOCAL, 10000)
MAXBASE:6 = LOCAL

;-----------------------------------------------
;触手成長LVの表示
;-----------------------------------------------
@SHOW_INFO_TENTACLE
CALL SHOW_TENTACLE_ABL
CALL NEW_DRAWLINE
PRINTFORML 現在の触手エネルギー({BASE:MASTER:触手}τ)
SIF TALENT:寄生 && MAXBASE:6 > 0
	PRINTFORML 寄生触手成長LV(%CALLNAME:TARGET%)%BARSTR(BASE:触手,MAXBASE:6,10)%({BASE:触手, 4}/{MAXBASE:6, 4})
SIF ASSI >= 0 && TALENT:ASSI:寄生 && MAXBASE:ASSI:6 > 0
	PRINTFORML 寄生触手成長LV(%CALLNAME:ASSI%)%BARSTR(BASE:ASSI:触手,MAXBASE:ASSI:6,10)%({BASE:ASSI:触手, 4}/{MAXBASE:ASSI:6, 4})
CALL NEW_DRAWLINE
WAIT

;-----------------------------------------------
;調教中の寄生触手成長処理
;-----------------------------------------------
;触手がいないときは非常にゆっくりなペースで成長。
;触手調教中の場合は触手挿入や触手貫通などの状況で基礎値を算出、
;その後、触手経験や触手中毒で補正をかけた値が成長値になる。
;また寄生中は触手調教の体力気力理性の消費が低下、それ以外のときは増加する処理もここで行う。
@PARASITIC_TENTACLE_TARGET
IF BASE:触手 < MAXBASE:6
	LOCAL = 1
	;触手召喚中
	IF TEQUIP:触手調教
		IF SELECTCOM >= 500 && SELECTCOM < 510
			LOCAL += 1
		ELSEIF SELECTCOM >= 510 && SELECTCOM < 600
			LOCAL += 2
		ENDIF
		;触手避妊膜・触手排卵剤
		SIF TEQUIP:排卵誘発剤 == 2 || TEQUIP:緊急避妊薬 == 2
			LOCAL += 1
		;触手挿入
		SIF TEQUIP:Ｖ系装着器具
			LOCAL += 1
		;触手Ｇスポ責め・触手子宮責め
		SIF TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 3
			LOCAL += 1
		;アナル触手・触手浣腸
		SIF TEQUIP:Ａ系装着器具
			LOCAL += 1
		;触手貫通
		SIF TEQUIP:Ａ系装着器具 == 3
			LOCAL += 2
		;触手クリ責め・触手ペニス責め
		SIF TEQUIP:Ｃ系装着器具
			LOCAL += 1
		;触手カテーテル
		SIF TEQUIP:Ｕ系装着器具
			LOCAL += 1
		;触手乳首責め・触手搾乳
		SIF TEQUIP:Ｂ系装着器具
			LOCAL += 1
		;触手緊縛
		SIF TEQUIP:緊縛関連
			LOCAL += 1
		;触手口辱
		SIF TEQUIP:口装着器具 == 2
			LOCAL += 1
		;触手読心責め
		SIF TEQUIP:読心 == 2
			LOCAL += 1
		;触手強制回復
		SIF TEQUIP:触手強制回復
			LOCAL += 3
		;触手催眠
		SIF TEQUIP:触手催眠
			LOCAL += 2
		;触手膣内産卵
		SIF TEQUIP:触手膣内産卵
			LOCAL += 1
		;触手腸内産卵
		SIF TEQUIP:触手腸内産卵
			LOCAL += 1
		;触手チキチキ
		SIF TEQUIP:触手チキチキ
			LOCAL += 2
		;触手服
		SIF TEQUIP:触手服
			LOCAL += 2
	;触手服着衣中
	ELSEIF TEQUIP:触手服
		;触手挿入
		SIF TEQUIP:Ｖ系装着器具 && TEQUIP:触手服 & 8
			LOCAL += 1
		;触手Ｇスポ責め・触手子宮責め
		SIF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 3) && TEQUIP:触手服 & 8
			LOCAL += 1
		;アナル触手・触手浣腸
		SIF TEQUIP:Ａ系装着器具 && TEQUIP:触手服 & 16
			LOCAL += 1
		;触手貫通
		SIF TEQUIP:Ａ系装着器具 == 3 && TEQUIP:触手服 & 16
			LOCAL += 2
		;触手クリ責め・触手ペニス責め
		SIF TEQUIP:Ｃ系装着器具 && TEQUIP:触手服 & 2
			LOCAL += 1
		;触手カテーテル
		SIF TEQUIP:Ｕ系装着器具 && TEQUIP:触手服 & 32
			LOCAL += 1
		;触手乳首責め・触手搾乳
		SIF TEQUIP:Ｂ系装着器具 && TEQUIP:触手服 & 4
			LOCAL += 1
	ENDIF
	;触手経験(0はありえない)
	SELECTCASE EXP:触手経験
		CASE IS < EXPLV:2
			TIMES LOCAL , 1.00
		CASE IS < EXPLV:3
			TIMES LOCAL , 1.20
		CASE IS < EXPLV:4
			TIMES LOCAL , 1.50
		CASE IS < EXPLV:5
			TIMES LOCAL , 1.80
		CASEELSE
			TIMES LOCAL , 2.00
	ENDSELECT
	;触手中毒
	SELECTCASE ABL:触手中毒
		CASE 0
			TIMES LOCAL , 1.00
		CASE 1
			TIMES LOCAL , 1.50
		CASE 2
			TIMES LOCAL , 2.00
		CASE 3
			TIMES LOCAL , 2.50
		CASE 4
			TIMES LOCAL , 3.00
		CASEELSE
			TIMES LOCAL , 3.50
	ENDSELECT
	BASE:触手 += LOCAL
	BASE:触手 = MIN(BASE:触手, MAXBASE:6)
ENDIF

;触手に対する抵抗感が薄れる効果(消費体力・気力・理性軽減)
;(寄生をスムーズにするため麻薬効果があるような変なホルモンを分泌しているってことで)
IF TEQUIP:触手調教 || TEQUIP:触手服
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.60
	TIMES DOWNBASE:9 , 0.60
ELSE
	TIMES DOWNBASE:0 , 1.20
	TIMES DOWNBASE:1 , 1.10
	TIMES DOWNBASE:9 , 1.20
ENDIF

;-----------------------------------------------
;調教中の寄生触手成長処理(助手)
;-----------------------------------------------
;助手の成長処理は奴隷時と違い、基礎値が触手中毒に依存。
;その後触手調教中ならボーナス1点を追加。
@PARASITIC_TENTACLE_ASSI
LOCAL = 0
IF BASE:ASSI:触手 < MAXBASE:ASSI:6
	;触手中毒
	SELECTCASE ABL:ASSI:触手中毒
		CASE 0
			LOCAL = 1
		CASE IS <= 2
			LOCAL = 2
		CASE IS <= 4
			LOCAL = 3
		CASEELSE
			LOCAL = 4
	ENDSELECT
	SIF TEQUIP:触手調教
		LOCAL += 1
	BASE:ASSI:触手 += LOCAL
	BASE:ASSI:触手 = MIN(BASE:ASSI:触手, MAXBASE:ASSI:6)
ENDIF

;-----------------------------------------------
;触手インプラント処理(体の各部位に快楽を増幅させる『種』を植え付けられている状態)
;-----------------------------------------------
;入るソースが強化(一部減少)される代わりに好感度の上がりが悪くなる。
;また、触手幽閉時には取り込まれる危険も高くなる。
@SOURCE_INPLANT
;クリトリス(快Ｃ追加)
SIF CFLAG:41 & 1
	TIMES SOURCE:快Ｃ , 1.15
;子宮(快Ｖ・液体追加増加)
IF CFLAG:41 & 2
	TIMES SOURCE:快Ｖ , 1.15
	TIMES SOURCE:液体追加 , 1.20
ENDIF
;直腸(快Ａ・不潔増加)
IF CFLAG:41 & 4
	TIMES SOURCE:快Ａ , 1.15
	TIMES SOURCE:不潔 , 1.50
ENDIF
;乳房(快Ｂ増加)
SIF CFLAG:41 & 8
	TIMES SOURCE:快Ｂ , 1.15
;ヘソ(露出・痒み増加)
IF CFLAG:41 & 16
	TIMES SOURCE:露出 , 1.10
	TIMES SOURCE:痒み , 1.25
ENDIF
;舌根(情愛・欲情追加増加)
IF CFLAG:41 & 32
	TIMES SOURCE:情愛 , 1.15
	TIMES SOURCE:欲情追加 , 1.15
ENDIF
;膀胱(中毒充足・不潔増加)
IF CFLAG:41 & 64
	TIMES SOURCE:中毒充足 , 1.15
	TIMES SOURCE:不潔 , 1.50
ENDIF
;心臓(性行動・支配増加)
IF CFLAG:41 & 128
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:支配 , 1.25
ENDIF
;脊椎(屈従増加・反感追加減少)
IF CFLAG:41 & 256
	TIMES SOURCE:屈従 , 1.25
	TIMES SOURCE:反感追加 , 0.90
ENDIF
;額(性行動・屈従増加)
IF CFLAG:41 & 512
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:屈従 , 1.25
ENDIF

;-----------------------------------------------
;触手インプラントによる好感度変動処理
;-----------------------------------------------
@TENTACLE_INPLANT
SIF CFLAG:41 & 1
	TFLAG:97 -= 1
SIF CFLAG:41 & 2
	TFLAG:97 -= 1
SIF CFLAG:41 & 4
	TFLAG:97 -= 1
SIF CFLAG:41 & 8
	TFLAG:97 -= 1
SIF CFLAG:41 & 16
	TFLAG:97 -= 2
SIF CFLAG:41 & 32
	TFLAG:97 -= 1
SIF CFLAG:41 & 64
	TFLAG:97 -= 2
SIF CFLAG:41 & 128
	TFLAG:97 -= 3
SIF CFLAG:41 & 256
	TFLAG:97 -= 3
SIF CFLAG:41 & 512
	TFLAG:97 -= 3
	
;-----------------------------------------------
;触手インプラントによる調教度変動処理
;-----------------------------------------------
@TENTACLE_INPLANT_2
SIF CFLAG:41 & 1
	TFLAG:550 += 1
SIF CFLAG:41 & 2
	TFLAG:550 += 1
SIF CFLAG:41 & 4
	TFLAG:550 += 1
SIF CFLAG:41 & 8
	TFLAG:550 += 1
SIF CFLAG:41 & 16
	TFLAG:550 += 2
SIF CFLAG:41 & 32
	TFLAG:550 += 1
SIF CFLAG:41 & 64
	TFLAG:550 += 2
SIF CFLAG:41 & 128
	TFLAG:550 += 3
SIF CFLAG:41 & 256
	TFLAG:550 += 3
SIF CFLAG:41 & 512
	TFLAG:550 += 3

;-----------------------------------------------
;触手インプラントによる触手成長LVの限界拡張
;-----------------------------------------------
;最大で基礎値から1500増加される
@PARASITIC_TENTACLE_INPLANT(ARG)
#FUNCTION
SIF CFLAG:41 & 1
	ARG += 125
SIF CFLAG:41 & 2
	ARG += 125
SIF CFLAG:41 & 4
	ARG += 125
SIF CFLAG:41 & 8
	ARG += 125
SIF CFLAG:41 & 16
	ARG += 150
SIF CFLAG:41 & 32
	ARG += 125
SIF CFLAG:41 & 64
	ARG += 150
SIF CFLAG:41 & 128
	ARG += 150
SIF CFLAG:41 & 256
	ARG += 200
SIF CFLAG:41 & 512
	ARG += 225
RETURNF ARG

;-----------------------------------------------
;触手が獲得した能力を表示
;-----------------------------------------------
@SHOW_TENTACLE_ABL
IF FLAG:75 == 0 && FLAG:97 == 0 && FLAG:98 == 0
	PRINTL ＊特殊能力なし＊
	RETURN 0
ENDIF
PRINTL 
PL_VAR_X:1 = 0
PL_VAR_X:2 = 0
CALLF CLEAR_LIST
FOR PC_VAR1, 0, 18
	PL_VAR_X:0 = POWER( 2, PC_VAR1)
	SIF FLAG:75 & PL_VAR_X:0 == 0 && FLAG:97 & PL_VAR_X:0 == 0 && FLAG:98 & PL_VAR_X:0 == 0
		CONTINUE
	IF FLAG:75 & PL_VAR_X:0
		CALLF SET_LIST, PL_VAR_X:1, PC_VAR1 + 10
		PL_VAR_X:1++
		IF PL_VAR_X:0 == 131072
			CALLF SET_LIST, PL_VAR_X:1, PC_VAR1 + 11
			PL_VAR_X:1++
		ENDIF
	ENDIF
	IF FLAG:97 & PL_VAR_X:0 && FLAG:98 & PL_VAR_X:0
		CALLF SET_LIST, PL_VAR_X:2 + 20, PC_VAR1 + 60
		PL_VAR_X:2++
	ENDIF
NEXT
PL_VAR_X:0 = 0
PL_STR_I:0 = 
FOR PC_VAR1, 0, PL_VAR_X:1 + PL_VAR_X:2
	IF PC_VAR1 < PL_VAR_X:1
		PRINTFORM [%STR:(GET_LIST(PC_VAR1))%]　
		
			PL_VAR_X:3 = MAX(18 - STRLENS(STR:(GET_LIST(PC_VAR1))), 0)
			PL_STR_I:0 = % " " * PL_VAR_X:3 %
			PRINTFORM %PL_STR_I:0%
			PL_VAR_X:0++
	ELSE
		SIF PC_VAR1 == PL_VAR_X:1 && PC_VAR1 % 4 > 0
			PRINTL 
		PRINTFORM [%STR:(GET_LIST(PL_VAR_X:0 + 20))%]　
		
			PL_VAR_X:3 = MAX(18 - STRLENS(STR:(GET_LIST(PL_VAR_X:0 + 20))), 0)
			PL_STR_I:0 = % " " * PL_VAR_X:3 %
			PRINTFORM %PL_STR_I:0%
			
		PL_VAR_X:0++
	ENDIF
	SIF (PC_VAR1 % 4 == 3 && PC_VAR1 < PL_VAR_X:1) || (PL_VAR_X:0 % 4 == 0 && PL_VAR_X:0 >= 4)
		PRINTL 
;	ELSE
		
;	ENDIF
NEXT
SIF PL_VAR_X:0 % 4 > 0
	PRINTL 
SIF DA:1000:15
	PRINTL [現し身触手]

;SIF FLAG:75 & 1
;	PRINT [鞭触手] 
;SIF FLAG:75 & 2
;	PRINT [針状触手] 
;SIF FLAG:75 & 4
;	PRINT [放電触手] 
;SIF FLAG:75 & 8
;	PRINT [催眠触手] 
;SIF FLAG:75 & 16
;	PRINT [極細触手] 
;SIF FLAG:75 & 32
;	PRINT [養分注入触手] 
;SIF FLAG:75 & 64
;	PRINT [抗菌触手] 
;SIF FLAG:75 & 128
;	PRINT [ゴーヤ型触手] 
;SIF FLAG:75 & 256
;	PRINT [磯巾着型触手] 
;SIF FLAG:75 & 512
;	PRINT [遠隔精密制御] 
;SIF FLAG:75 & 1024
;	PRINT [特殊ホルモン分泌] 
;SIF FLAG:75 & 2048
;	PRINT [触手賢者の石] 
;SIF FLAG:75 & 4096
;	PRINT [奴隷懲罰用触手部屋] 
;SIF FLAG:75 & 8192
;	PRINT [体内適応力強化] 
;SIF FLAG:75 & 16384
;	PRINT [精液放出量強化] 
;SIF FLAG:75 & 32768
;	PRINT [精子と触手の部屋] 
;SIF FLAG:75 & 65536
;	PRINT [吸血触手] 
;SIF FLAG:75 & 131072
;	PRINT [触手保育施設] [触手分娩台] 
;
;SIF DA:1000:15
;	PRINT [現し身触手] 
;
;SIF FLAG:97 & 1 && FLAG:98 & 1
;	PRINT [SATORI触手] 
;SIF FLAG:97 & 2 && FLAG:98 & 2
;	PRINT [NITORI触手] 
;SIF FLAG:97 & 4 && FLAG:98 & 4
;	PRINT [YUYUKO触手] 
;SIF FLAG:97 & 8 && FLAG:98 & 8
;	PRINT [SUIKA触手] 
;SIF FLAG:97 & 16 && FLAG:98 & 16
;	PRINT [ALICE触手] 
;SIF FLAG:97 & 32 && FLAG:98 & 32
;	PRINT [HINA触手] 
;SIF FLAG:97 & 64 && FLAG:98 & 64
;	PRINT [AYA触手] 
;SIF FLAG:97 & 128 && FLAG:98 & 128
;	PRINT [PARSEE触手] 
;SIF FLAG:97 & 256 && FLAG:98 & 256
;	PRINT [MEDICINE触手] 
;SIF FLAG:97 & 512 && FLAG:98 & 512
;	PRINT [PATCHOULI触手] 
;SIF FLAG:97 & 1024 && FLAG:98 & 1024
;	PRINT [HIGH-FAIRY触手] 
;SIF FLAG:97 & 2048 && FLAG:98 & 2048
;	PRINT [CIRNO触手] 
;SIF FLAG:97 & 4096 && FLAG:98 & 4096
;	PRINT [NAZRIN触手] 
;SIF FLAG:97 & 8192 && FLAG:98 & 8192
;	PRINT [EIKI触手] 
;SIF FLAG:97 & 16384 && FLAG:98 & 16384
;	PRINT [SHIZUHA触手] 
;SIF FLAG:97 & 32768 && FLAG:98 & 32768
;	PRINT [MINORIKO触手] 
;SIF FLAG:97 & 65536 && FLAG:98 & 65536
;	PRINT [SUWAKO触手] 
;PRINTL 
;
;-----------------------------------------------
;触手生贄時における特定キャラ判別処理
;-----------------------------------------------
;触手生贄時特定のキャラが生贄になったのをここで判定する
;使用フラグはFLAG:98でビット演算を使用
;また購入済みキャラの判定がリセットされる関係上、このフラグは引き継ぎにて強制的にリセットされる
;(ただしこの判定を元に取得した触手の能力については引き継がない限り消滅しない)
	;新たにキャラ触手を追加されるときは、Str.csvも参照されるとありがたい
@CHARACHECK_TENTACLE_SACRIFICE
LOCAL = 0
;生贄になったキャラがさとり([SATORI触手]取得に必要)
IF NO:TARGET == 1061 && (FLAG:97 & 1) == 0
	LOCAL = 1
	LOCALS = SATORI
	LOCALS:1 = 触手読心責め
;生贄になったキャラがにとり([NITORI触手]取得に必要)
ELSEIF NO:TARGET == 1040 && (FLAG:97 & 2) == 0
	LOCAL = 2
	LOCALS = NITORI
	LOCALS:1 = 触手潮干狩り
;生贄になったキャラが幽々子([YUYUKO触手]取得に必要)
ELSEIF NO:TARGET == 1020 && (FLAG:97 & 4) == 0
	LOCAL = 4
	LOCALS = YUYUKO
	LOCALS:1 = 触手幽霊膣鏡
;生贄になったキャラが萃香([SUIKA触手]取得に必要)
ELSEIF NO:TARGET == 1023 && (FLAG:97 & 8) == 0
	LOCAL = 8
	LOCALS = SUIKA
	LOCALS:1 = 触手強制飲酒
;生贄になったキャラがアリス([ALICE触手]取得に必要)
ELSEIF NO:TARGET == 1014 && (FLAG:97 & 16) == 0
	LOCAL = 16
	LOCALS = ALICE
	LOCALS:1 = 触手精神制御
;生贄になったキャラが雛([HINA触手]取得に必要)
ELSEIF NO:TARGET == 1039 && (FLAG:97 & 32) == 0
	LOCAL = 32
	LOCALS = HINA触手
	LOCALS:1 = 触手厄落とし
;生贄になったキャラが文([AYA触手]取得に必要)
ELSEIF NO:TARGET == 1032 && (FLAG:97 & 64) == 0
	LOCAL = 64
	LOCALS = AYA
	LOCALS:1 = 触手腸内撮影
;生贄になったキャラがパルスィ([PARSEE触手]取得に必要)
ELSEIF NO:TARGET == 1059 && (FLAG:97 & 128) == 0
	LOCAL = 128
	LOCALS = PARSEE
	LOCALS:1 = 触手言葉責め
;生贄になったキャラがメディスン([MEDICINE触手]取得に必要)
ELSEIF NO:TARGET == 1033 && (FLAG:97 & 256) == 0
	LOCAL = 256
	LOCALS = MEDICINE
	LOCALS:1 = 触手鈴蘭媚薬
;生贄になったキャラがパチュリー([PATCHOULI触手]取得に必要)
ELSEIF NO:TARGET == 1008 && (FLAG:97 & 512) == 0
	LOCAL = 512
	LOCALS = PATCHOULI
	LOCALS:1 = 触手官能図書
;生贄になったキャラが大妖精([HIGH-FAIRY触手]取得に必要)
ELSEIF NO:TARGET == 1004 && (FLAG:97 & 1024) == 0
	LOCAL = 1024
	LOCALS = HIGH-FAIRY
	LOCALS:1 = 触手妖精按摩
;生贄になったキャラがチルノ([CIRNO触手]取得に必要)
ELSEIF NO:TARGET == 1005 && (FLAG:97 & 2048) == 0
	LOCAL = 2048
	LOCALS = CIRNO
	LOCALS:1 = 触手氷精手淫
;生贄になったキャラがナズーリン([NAZRIN触手]取得に必要)
ELSEIF NO:TARGET == 1071 && (FLAG:97 & 4096) == 0
	LOCAL = 4096
	LOCALS = NAZRIN
	LOCALS:1 = 触手淫脈探査
;生贄になったキャラが映姫様([EIKI触手]取得に必要)
ELSEIF NO:TARGET == 1036 && (FLAG:97 & 8192) == 0
	LOCAL = 8192
	LOCALS = EIKI
	LOCALS:1 = 触手最終審判
;生贄になったキャラが静葉([SHIZUHA触手]取得に必要)
ELSEIF NO:TARGET == 1037 && (FLAG:97 & 16384) == 0
	LOCAL = 16384
	LOCALS = SHIZUHA
	LOCALS:1 = 触手百裂紅葉
;生贄になったキャラが穣子([MINORIKO触手]取得に必要)
ELSEIF NO:TARGET == 1038 && (FLAG:97 & 32768) == 0
	LOCAL = 32768
	LOCALS = MINORIKO
	LOCALS:1 = 触手豊穣淫香
;生贄になったキャラが諏訪子([SUWAKO触手]取得に必要)
;ELSEIF NO:TARGET == 1038 && (FLAG:97 & 65536) == 0
;	LOCAL = 32768
;	LOCALS = SUWAKO
;	LOCALS:1 = 触手○○○○
ENDIF
PRINTL 
IF LOCAL > 0
	FLAG:97 |= LOCAL
	IF FLAG:98 & LOCAL
		PRINTFORML %CALLNAME:TARGET%を吸収した触手は[%LOCALS%触手]を得た
		PRINTFORMW コマンド＜%LOCALS:1%＞が使えるようになりました
	ELSE
		PRINTFORML %CALLNAME:TARGET%を吸収したことで触手は新たな能力を得た
		PRINTFORMW [%LOCALS%触手]が開発可能になりました
	ENDIF
ENDIF

;-----------------------------------------------
;同化刻印(仮)追加判定処理
;-----------------------------------------------
;ここでの同化刻印は本家触手進化拡張パッチの蓬莱の薬を開発に合わせて作成しました
;名前を変えた上、素質から刻印扱いにしたのは[蓬莱人]という1つのアイデンティティを潰さないため
;(性能が似ているから結局潰しているというツッコミは無しで。売却補正は付けないつもり)と、
;成長性を付けるため(LVが低いと条件次第ではそのまま消滅してGAMEOVERになる)
@TENTACLE_SEAL_OF_IMMORTAL
#FUNCTION
LOCAL:1 = 0
LOCAL = NO:TARGET
IF MARK:同化刻印 == 2 && EXP:異常経験 >= 20 && DA:LOCAL:11 >= 10 && MONEY >= 1000000 && ABL:MASTER:触手使役 >= 5
	;同化刻印LV3にするには奴隷の同化刻印がLV2で異常経験が20以上
	;かつ主人の触手使役がLV5以上、かつ奴隷が産んだ触手10匹と所持金100萬圓が必要
	LOCAL:1 = 3
ELSEIF MARK:同化刻印 == 1 && EXP:異常経験 >= 10 && DA:LOCAL:11 >= 5 && MONEY >= 500000 && ABL:MASTER:触手使役 >= 4
	;同化刻印LV2にするには奴隷の同化刻印がLV1かつで異常経験が10以上
	;主人の触手使役がLV4以上、かつ奴隷が産んだ触手5匹と所持金50萬圓が必要
	LOCAL:1 = 2
ELSEIF MARK:同化刻印 == 0 && DA:LOCAL:11 >= 1 && MONEY >= 100000 && ABL:MASTER:触手使役 >= 3
	;同化刻印LV1にするには主人の触手使役がLV3以上、かつ奴隷が産んだ触手1匹と所持金10萬圓が必要
	LOCAL:1 = 1
ENDIF
RETURNF LOCAL:1

;-----------------------------------------------
;同化刻印追加処理
;-----------------------------------------------
@TENTACLE_GAIN_IMMORTAL, ARG, ARG:1
IF ARG == 3
	MONEY -= 1000000
	TFLAG:201 = 10
ELSEIF ARG == 2
	MONEY -= 500000
	TFLAG:201 = 5
ELSE
	MONEY -= 100000
	TFLAG:201 = 1
ENDIF
LOCAL = NO:TARGET
;触手の幼生を消費
DA:LOCAL:11 = MAX(DA:LOCAL:11 - TFLAG:201, 0)
;触手の幼生の数が0になった場合、蒐集品の種類を減らす
IF DA:LOCAL:11 == 0
	FLAG:500 = MAX(FLAG:500 - 1, 0)
	FLAG:511 = MAX(FLAG:511 - 1, 0)
ENDIF

;口上・地の文の表示
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 270, ARG
CALL COMMON_MARK_UP, TARGET, 10, ARG, 0
BASE:触手 -= ARG:1
;同化刻印累計カウント
FLAG:409 += 1
IF FLAG:409 >= 50 && CHECK_GAMEMODE_ENDING_EXIST() == 1 && (FLAG:70 & 512) == 0
	PRINTFORML %CALLNAME:MASTER%は不死化の知識の深淵を知った…
	PRINTFORMW ※強くてニューゲーム開始時に%CALLNAME:MASTER%の種族を[呪精]に選択することができます
	FLAG:70 |= 512
ENDIF

;-----------------------------------------------
;同化刻印発動判定処理
;-----------------------------------------------
;同化刻印LV1：触手調教中か寄生または懐卵中かつ触手エネルギーが体力の最大値の1/5τ以上あれば発動する。体力の最大値が1000以上ある場合は蘇生するたびに-100
;同化刻印LV2：触手エネルギーが体力の最大値の1/10τ以上あれば発動する。体力の最大値が1000以上ある場合は蘇生するたびに-50
;同化刻印LV3：触手エネルギーが1τ以上あれば発動する。体力の最大値1000以上ある場合は蘇生するたびに-10。また、異常事態と認識しなくなるため異常経験は入らなくなる
;同化刻印LV1～2では触手エネルギーを消費してしまうが、LV3以上になるとエネルギーの『消費』は無くなる(使ったものが全て還元されるため)
;なお、妊娠や懐卵による消費の低下(体力の最大値の低下によるもの)は仕様。強制的に消滅って手もありかも知れないが処理がややこしくなりそうなのでパス。
@TENTACLIC_REVIVAL
IF !IF_ALIVE(TARGET) && MARK:同化刻印 == 3 && BASE:MASTER:触手 > 0
	LOCAL = 3
ELSEIF !IF_ALIVE(TARGET) && MARK:同化刻印 == 2 && BASE:MASTER:触手 >= MAXBASE:0 / 10
	LOCAL = 2
ELSEIF !IF_ALIVE(TARGET) && MARK:同化刻印 == 1 && BASE:MASTER:触手 >= MAXBASE:0 / 5 && (TEQUIP:触手調教 || TALENT:寄生 || TALENT:懐卵)
	LOCAL = 1
ELSE
	RETURN 0
ENDIF

PRINTW ＜リザレクション＞
PRINTFORML %CALLNAME:TARGET%は同化刻印の力によって無理やり蘇生させられた
;同化刻印(触手)リザレクション回数フラグ
CFLAG:触手での復活回数 += 1

;同化刻印LV3
IF LOCAL == 3
	IF MAXBASE:0 > 1000
		MAXBASE:0 -= 10
		MAXBASE:0 = MAX(MAXBASE:0, 1000)
		PRINTFORML %CALLNAME:TARGET%の体力の最大値が{MAXBASE:0}に下がった
	ENDIF
;同化刻印LV2
ELSEIF LOCAL == 2
	BASE:MASTER:触手 -= MAXBASE:0 / 10
	TCVAR:異常経験 += 1
	IF ABL:従順 > 0 && TALENT:恋慕 == 0
		ABL:従順 -= 1
		PRINTFORML %CALLNAME:TARGET%の%ABLNAME:10%が{ABL:従順}に下がった
	ENDIF
	IF MAXBASE:0 > 1000
		MAXBASE:0 -= 50
		MAXBASE:0 = MAX(MAXBASE:0, 1000)
		PRINTFORML %CALLNAME:TARGET%の体力の最大値が{MAXBASE:0}に下がった
	ENDIF
;同化刻印LV1
ELSE
	BASE:MASTER:触手 -= MAXBASE:0 / 5
	TCVAR:異常経験 += 1
	IF ABL:従順 > 0 && TALENT:恋慕 == 0
		ABL:従順 -= 1
		PRINTFORML %CALLNAME:TARGET%の%ABLNAME:10%が{ABL:従順}に下がった
	ENDIF
	IF MAXBASE:0 > 1000
		MAXBASE:0 -= 100
		MAXBASE:0 = MAX(MAXBASE:0, 1000)
		PRINTFORML %CALLNAME:TARGET%の体力の最大値が{MAXBASE:0}に下がった
	ENDIF
ENDIF

IF TALENT:寄生
	TALENT:寄生 = 0
	MAXBASE:6 = 0
	BASE:触手 = MAXBASE:6
	PRINTFORML %CALLNAME:TARGET%に寄生した触手は消滅してしまった
ENDIF
BASE:体力 = 1

;-----------------------------------------------
;触手回復処理
;-----------------------------------------------
@TENTACLE_HEAL
;触手中毒
LOCAL = 1 + ABL:触手中毒

;触手経験
SELECTCASE EXP:触手経験
	CASE IS < EXPLV:2
		LOCAL += 0
	CASE IS < EXPLV:3
		LOCAL += 1
	CASE IS < EXPLV:4
		LOCAL += 2
	CASE IS < EXPLV:5
		LOCAL += 3
	CASEELSE
		LOCAL += 4
ENDSELECT

LOCAL *= BASE:触手
LOCAL /= 100
BASE:体力 += LOCAL
BASE:体力 = MIN(BASE:体力, MAXBASE:0)
BASE:触手 = 0
PRINTFORMW %CALLNAME:TARGET%に寄生していた触手は全ての栄養を放出した

;=============================================================================
;イベント系の処理
;=============================================================================
;-----------------------------------------------
;触手漬けで入手する珠・経験
;-----------------------------------------------
;ARG = 触手漬けされるキャラの登録キャラ番号
;TFLAG:230が旧J,TFLAG:231が旧K,TFLAG:232が旧L,それ以外の一文字変数はみんなLOCALに
@TENTACLE_SPECIALROOM_TRAINING, ARG
;LOCAL:1=Ｖ系, LOCAL:2=Ａ系, LOCAL:3=フェラ系, LOCAL:4=Ｃ・尿道系, LOCAL:5=Ｂ系, LOCAL:6=緊縛系, LOCAL:7=苦痛系
LOCAL:1 = 20
LOCAL:2 = 15
LOCAL:3 = 10
LOCAL:4 = 10
LOCAL:5 = 15
LOCAL:6 = 10
SIF ABL:ARG:Ｖ感覚
	LOCAL:1 += 2 * ABL:ARG:4
SIF ABL:ARG:Ａ感覚
	LOCAL:2 += 3 * ABL:ARG:5
SIF ABL:ARG:奉仕精神
	LOCAL:3 += ABL:ARG:6
SIF ABL:ARG:Ｃ感覚
	LOCAL:4 += 3 * ABL:ARG:Ｂ感覚
SIF ABL:ARG:Ｂ感覚
	LOCAL:5 += 2 * ABL:ARG:露出癖
SIF ABL:ARG:8
	LOCAL:6 += ABL:ARG:8
LOCAL:7 = LOCAL:6+(LOCAL:1+LOCAL:2)/5
TFLAG:230 = LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:4+LOCAL:5+LOCAL:6
TFLAG:231 = 2 + MARK:ARG:薬物刻印
TFLAG:232 = 5+TFLAG:231
IF TALENT:ARG:処女
	PRINTW 処女喪失
	TALENT:ARG:処女 = 0
	LOCAL:7 += 50
	TFLAG:232 += 1
	IF EXP:ARG:Ｖ経験 == 0
		LOCAL:7 += 100
		TFLAG:232 += 1
	ENDIF
ENDIF
CFLAG:ARG:11 = 6
IF TALENT:ARG:オトコ
	;Ａ経験
	CALL COMMON_UP_EXP, ARG, 1, ((LOCAL:1)/2+LOCAL:2), 0
	;Ａ拡張経験
	CALL COMMON_UP_EXP, ARG, 33, ((LOCAL:1+LOCAL:2)/8), 0
ELSE
	;Ｖ経験
	CALL COMMON_UP_EXP, ARG, 0, LOCAL:1, 0
	;Ｖ拡張経験
	CALL COMMON_UP_EXP, ARG, 32, ((LOCAL:1)/5), 0
	;Ａ経験
	CALL COMMON_UP_EXP, ARG, 1, LOCAL:2, 0
	;Ａ拡張経験
	CALL COMMON_UP_EXP, ARG, 33, ((LOCAL:2)/5), 0
ENDIF
IF !TALENT:ARG:死亡
;絶頂経験
CALL COMMON_UP_EXP, ARG, 2, ((LOCAL:1+LOCAL:2)/2), 0
;射精経験
SIF TALENT:ARG:オトコ || TALENT:ARG:ふたなり
	CALL COMMON_UP_EXP, ARG, 3, LOCAL:4, 0
ENDIF
;精液経験
CALL COMMON_UP_EXP, ARG, 4, (TFLAG:230/2), 0
IF TFLAG:230/2 > 0
	CFLAG:ARG:3666 += MAX(RAND:(TFLAG:230/2), 0)
	CALL CALCULATE_LIMIT, ARG
ENDIF
;フェラ経験
CALL COMMON_UP_EXP, ARG, 22, LOCAL:3, 0
;緊縛経験
CALL COMMON_UP_EXP, ARG, 31, LOCAL:6, 0
;尿道開発経験
CALL COMMON_UP_EXP, ARG, 46, ((LOCAL:4)/2), 0
;異常経験
CALL COMMON_UP_EXP, ARG, 50, 20, 0
;触手経験
CALL COMMON_UP_EXP, ARG, 53, TFLAG:230, 0
SIF TALENT:ARG:オトコ == 0
	TIMES LOCAL:4 , 0.80
IF !TALENT:ARG:死亡
;快Ｃの珠
CALL COMMON_UP_JUEL, ARG, 0, ((LOCAL:4)*100), 0
;快Ｖの珠
SIF TALENT:ARG:オトコ == 0
	CALL COMMON_UP_JUEL, ARG, 1, ((LOCAL:1)*100), 0
;快Ａの珠
CALL COMMON_UP_JUEL, ARG, 2, ((LOCAL:2)*80), 0
;快Ｂの珠
CALL COMMON_UP_JUEL, ARG, 1, ((LOCAL:5)*70), 0
;習得の珠
CALL COMMON_UP_JUEL, ARG, 11, ((LOCAL:3)*60), 0
;欲情の珠
CALL COMMON_UP_JUEL, ARG, 13, ((TFLAG:230)*75), 0
;屈服の珠
CALL COMMON_UP_JUEL, ARG, 14, ((TFLAG:230)*300), 0
;苦痛の珠
CALL COMMON_UP_JUEL, ARG, 16, ((LOCAL:7)*60), 0
;恐怖の珠
CALL COMMON_UP_JUEL, ARG, 17, ((TFLAG:230)*250), 0
;否定の珠
CALL COMMON_UP_JUEL, ARG, 100, (TFLAG:230*(TFLAG:231)*50), 0
ENDIF
IF MARK:ARG:苦痛刻印 < 2 && TALENT:ARG:痛みに強い == 0
	TFLAG:232 += 1
	SIF MARK:ARG:苦痛刻印 == 0
		TFLAG:232 += 1
	CALL COMMON_MARK_UP, ARG, 0, MARK:ARG:苦痛刻印+1, 0
ENDIF
IF MARK:ARG:快楽刻印 < 2 && TALENT:ARG:快感の否定 == 0
	TFLAG:232 += 1
	SIF MARK:ARG:快楽刻印 == 0
		TFLAG:232 += 1
	CALL COMMON_MARK_UP, ARG, 1, MARK:ARG:快楽刻印+1, 0
ENDIF
IF MARK:ARG:屈服刻印 < 3 && TALENT:ARG:一線越えない == 0 && TALENT:ARG:快感の否定 == 0
	TFLAG:232 += 1
	IF MARK:ARG:苦痛刻印 == 0
		TFLAG:232 += 2
	ELSEIF MARK:ARG:苦痛刻印 == 1
		TFLAG:232 += 1
	ENDIF
	CALL COMMON_MARK_UP, ARG, 2, MARK:ARG:屈服刻印+1, 0
ENDIF
LOCAL:1 = 0
IF MARK:ARG:恐怖刻印 > 2 && JUEL:ARG:屈服 > 75000
	LOCAL:1 = 3
ELSEIF MARK:ARG:恐怖刻印 > 1 && JUEL:ARG:屈服 > 15000
	LOCAL:1 = 2
ELSEIF MARK:ARG:恐怖刻印 > 0 && JUEL:ARG:屈服 > 7500
	LOCAL:1 = 1
ENDIF
IF LOCAL:1 > 0 && MARK:ARG:96 > MARK:ARG:恐怖刻印
	TFLAG:232 += 1
	CALL COMMON_MARK_UP, ARG, 8, MARK:ARG:恐怖刻印+1, 0
ENDIF
WAIT

;-----------------------------------------------
;触手幽閉で入手する珠・経験
;-----------------------------------------------
@TENTACLE_PRIVATEROOM_TRAINING, ARG
;LOCAL:17 = 口上用,LOCAL:16=触手経験用他, LOCAL:15=乱数用, LOCAL:14=どのような調教が行われるのかを決定, 
;LOCAL:1=Ａ系, LOCAL:2=Ｂ系, LOCAL:3=Ｃ系, LOCAL:13=Ｖ系, LOCAL:7=フェラ系, LOCAL:9=緊縛系, LOCAL:12=尿道系, LOCAL:5=産卵系
;LOCAL:4=否定の珠･異常経験用, LOCAL:6=消費体力気力理性・インプラント関係, LOCAL:8=苦痛, LOCAL:10=精液, LOCAL:11=催眠
VARSET LOCAL,0
FOR LOCAL, 0, 3
	SELECTCASE ((FLAG:75 & 8) ? RAND:10 # RAND:9)
		CASE 0
			;Ａ系追加
			LOCAL:14 |= 1
			LOCAL:1 += 6 + RAND:5
		CASE 1
			;対象がオトコの場合はＶ系でなくＡ系かＣ系が入るように
			IF TALENT:ARG:オトコ == 0
				;Ｖ系追加
				LOCAL:14 |= 2
				LOCAL:13 += 6 + RAND:8
			ELSE
				LOCAL:15 = RAND:2
				;(LOCAL:15 == 0)Ａ系追加、(LOCAL:15 != 0)Ｃ系追加
				LOCAL:14 |= (LOCAL:15 == 0) ? 1 # 8
				LOCAL:1 += (LOCAL:15 == 0) ? 6 + RAND:5 # 3 + RAND:3
			ENDIF
		CASE 2
			;Ｂ系追加
			LOCAL:14 |= 4
			LOCAL:2 += 5 + RAND:6
		CASE 3
			;Ｃ系追加
			LOCAL:14 |= 8
			LOCAL:3 += 3 + RAND:3
		CASE 4
			;フェラ系追加
			LOCAL:14 |= 16
			LOCAL:7 += 3 + RAND:9
		CASE 5
			;緊縛系追加
			LOCAL:14 |= 32
			LOCAL:9 += 4 + RAND:7
		CASE 6
			;苦痛系追加
			LOCAL:14 |= 64
			LOCAL:8 += 8 + RAND:9
		CASE 7
			;尿道系追加
			LOCAL:14 |= 128
			LOCAL:12 += 3 + RAND:2
		CASE 8
			;産卵系追加
			LOCAL:14 |= 256
			LOCAL:5 += 1 + RAND:5
		CASEELSE
			;催眠系追加
			LOCAL:14 |= 512
			LOCAL:11 += 1 + RAND:2
	ENDSELECT
NEXT
;ＶＡ系の責めがない場合は振り直し
SIF LOCAL:13 + LOCAL:1 == 0
	RESTART

;Ａ感覚
IF LOCAL:1
	LOCAL:1 *= 10 + ABL:ARG:Ａ感覚
	LOCAL:1 /= 10
ENDIF
;Ｂ感覚
IF LOCAL:2
	LOCAL:2 *= 10 + ABL:ARG:Ｂ感覚
	LOCAL:2 /= 10
ENDIF
;Ｃ感覚
IF LOCAL:3
	LOCAL:3 *= 10 + ABL:ARG:Ｃ感覚
	LOCAL:3 /= 10
ENDIF
;Ｃ感覚(尿道系)
IF LOCAL:12
	LOCAL:12 *= 10 + ABL:ARG:Ｃ感覚
	LOCAL:12 /= 10
ENDIF
;Ｖ感覚
IF LOCAL:13
	LOCAL:13 *= 10 + ABL:ARG:Ｖ感覚
	LOCAL:13 /= 10
ENDIF
;奉仕精神(フェラ系)
IF LOCAL:7
	LOCAL:7 *= 10 + ABL:ARG:奉仕精神
	LOCAL:7 /= 10
ENDIF
;奉仕精神(催眠系)
IF LOCAL:11
	LOCAL:11 += ABL:ARG:奉仕精神
	LOCAL:11 /= 2
ENDIF
;マゾっ気(苦痛系)
IF LOCAL:8
	LOCAL:8 *= 10 + ABL:ARG:マゾっ気
	LOCAL:8 /= 10
ENDIF
;マゾっ気(緊縛系)
IF LOCAL:9
	LOCAL:9 *= 10 + ABL:ARG:マゾっ気
	LOCAL:9 /= 10
ENDIF
;産卵による苦痛・体力消費
IF LOCAL:5
	LOCAL:8 += LOCAL:5 * 2
	LOCAL:6 += LOCAL:5 * 10
ENDIF
;精液
LOCAL:10 = (LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:12+LOCAL:13)/10
;精液中毒
IF LOCAL:10
	LOCAL:10 *= 10 + ABL:ARG:精液中毒
	LOCAL:10 /= 10
ENDIF

;地の文章表示
IF CFLAG:ARG:99 == 0
	PRINTFORM 触手達は%CALLNAME:ARG%の
	SIF LOCAL:14 & 256
		PRINT 体に卵を産み着けながら、
	SIF LOCAL:14 & 512
		PRINT 意思を催眠術で削ぎ、
	IF LOCAL:14 & 1 || LOCAL:14 & 2
		IF LOCAL:14 & 1
			PRINTFORM \@(LOCAL:14 & 2) ? アナルとヴァギナ # アナル\@を犯し
		ELSE
			PRINT ヴァギナを犯し
		ENDIF
		IF LOCAL:14 & 4 || LOCAL:14 & 8
			PRINT つつ、
		ELSE
			PRINTL 続けている…
		ENDIF
	ENDIF
	IF LOCAL:14 & 4
		IF LOCAL:14 & 8
			PRINTFORM 乳首と\@(TALENT:ARG:オトコ || TALENT:ARG:ふたなり) ? ペニス # クリトリス\@
		ELSE
			PRINT 乳首
		ENDIF
		PRINTL を責め続けている…
	ELSEIF LOCAL:14 & 8
		PRINTFORML \@(TALENT:ARG:オトコ || TALENT:ARG:ふたなり) ? ペニス # クリトリス\@を責め続けている…
	ENDIF
	IF LOCAL:14 & 64
		PRINTFORM %CALLNAME:ARG%の体は触手に打たれた痕が、
	ELSEIF LOCAL:14 & 16 || LOCAL:14 & 128
		PRINTFORM %CALLNAME:ARG%の
	ELSE
		PRINTFORM %CALLNAME:ARG%は
	ENDIF
	IF LOCAL:14 & 16
		PRINTFORML \@(LOCAL:14 & 128) ? 口と尿道 # 口\@には深々と触手が咥えこまれている…
	ELSEIF LOCAL:14 & 128
		PRINTL 尿道には深々と触手が咥えこまれている…
	ENDIF
	PRINT 四肢をがっちりと拘束されているため、あらゆる責めから逃れることができず、
	SIF (LOCAL:14 & 16) == 0 && (LOCAL:14 & 64) == 0 && (LOCAL:14 & 128) == 0
		PRINTFORM %CALLNAME:ARG%は
	PRINTL 悶え続けていた……
	WAIT
ENDIF
IF LOCAL:13
	IF TALENT:ARG:処女
		PRINTL 処女喪失
		LOCAL:8 += 50
		LOCAL:6 += 1
		TALENT:ARG:処女 = 0
		IF EXP:ARG:Ｖ経験 == 0
			LOCAL:8 += 100
			LOCAL:6 += 1
		ENDIF
	ENDIF
	;Ｖ経験
	CALL COMMON_UP_EXP, ARG, 0, LOCAL:13, 0
	;Ｖ拡張経験
	CALL COMMON_UP_EXP, ARG, 32, ((LOCAL:13)/5), 0
ENDIF
;Ａ経験
CALL COMMON_UP_EXP, ARG, 1, LOCAL:1, 0
;Ａ拡張経験
CALL COMMON_UP_EXP, ARG, 33, ((LOCAL:1)/5), 0
;絶頂経験
CALL COMMON_UP_EXP, ARG, 2, ((LOCAL:13+LOCAL:1+LOCAL:2+LOCAL:3)/2), 0
;精液経験
CALL COMMON_UP_EXP, ARG, 4, LOCAL:10, 0
IF LOCAL:10 > 0
	CFLAG:ARG:3666 += MAX(RAND:(LOCAL:10), 0)
	CALL CALCULATE_LIMIT, ARG
ENDIF
;フェラ経験
CALL COMMON_UP_EXP, ARG, 22, LOCAL:7, 0
;緊縛経験
CALL COMMON_UP_EXP, ARG, 31, LOCAL:9, 0
;尿道開発経験
CALL COMMON_UP_EXP, ARG, 46, LOCAL:12, 0
IF LOCAL:5
	SIF EXP:ARG:60 == 0
		LOCAL:4 = 1
	;産卵経験
	CALL COMMON_UP_EXP, ARG, 51, LOCAL:5, 0
ENDIF
IF (CFLAG:ARG:24 & 4) == 0
	CFLAG:ARG:24 |= 4
	LOCAL:4 += 1
	;異常経験
	CALL COMMON_UP_EXP, ARG, 50, LOCAL:4, 0
ENDIF
LOCAL:4 = (LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:5+LOCAL:7+LOCAL:9+LOCAL:13) * 50
LOCAL:16 = 5 + (LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:5+LOCAL:7+LOCAL:9+LOCAL:13)/20
LOCAL:6 += (LOCAL:16)/2
;触手経験
CALL COMMON_UP_EXP, ARG, 53, LOCAL:16, 0

;能力(触手中毒)
SELECTCASE ABL:ARG:触手中毒
	CASE 0
		TIMES LOCAL:16 , 0.80
		TIMES LOCAL:4 , 2.40
	CASE 1
		TIMES LOCAL:16 , 1.00
		TIMES LOCAL:4 , 1.20
	CASE 2
		TIMES LOCAL:16 , 1.20
		TIMES LOCAL:4 , 1.00
	CASE 3
		TIMES LOCAL:16 , 1.50
		TIMES LOCAL:4 , 0.90
	CASE 4
		TIMES LOCAL:16 , 1.80
		TIMES LOCAL:4 , 0.60
	CASEELSE
		TIMES LOCAL:16 , 2.50
		TIMES LOCAL:4 , 0.10
ENDSELECT
;好感度減少
IF CFLAG:ARG:好感度 > 0 && LOCAL:16
	CFLAG:ARG:好感度 -= LOCAL:16
	CFLAG:ARG:好感度 = MAX(CFLAG:ARG:好感度, 0)
ENDIF
;能力(欲望)
SELECTCASE ABL:ARG:欲望
	CASE 0
		TIMES LOCAL:16 , 1.00
		TIMES LOCAL:4 , 2.50
	CASE 1
		TIMES LOCAL:16 , 1.20
		TIMES LOCAL:4 , 1.80
	CASE 2
		TIMES LOCAL:16 , 1.40
		TIMES LOCAL:4 , 1.50
	CASE 3
		TIMES LOCAL:16 , 1.60
		TIMES LOCAL:4 , 1.00
	CASE 4
		TIMES LOCAL:16 , 1.80
		TIMES LOCAL:4 , 0.80
	CASEELSE
		TIMES LOCAL:16 , 2.00
		TIMES LOCAL:4 , 0.50
ENDSELECT
;触手エネルギーが切れていると効果ダウン
IF BASE:MASTER:触手 <= 0
	LOCAL:3 /= 2
	LOCAL:13 /= 2
	LOCAL:1 /= 2
	LOCAL:2 /= 2
	LOCAL:16 /= 2
	LOCAL:7 /= 2
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:6 , 1.50
ENDIF

;快Ｃの珠
CALL COMMON_UP_JUEL, ARG, 0, ((LOCAL:3)*80), 0
;快Ｖの珠
CALL COMMON_UP_JUEL, ARG, 1, ((LOCAL:13)*80), 0
;快Ａの珠
CALL COMMON_UP_JUEL, ARG, 2, ((LOCAL:1)*60), 0
;快Ｂの珠
CALL COMMON_UP_JUEL, ARG, 3, ((LOCAL:2)*80), 0
;習得の珠
CALL COMMON_UP_JUEL, ARG, 11, ((LOCAL:7)*30), 0
;欲情の珠
CALL COMMON_UP_JUEL, ARG, 13, ((LOCAL:16)*35), 0
;屈服の珠
CALL COMMON_UP_JUEL, ARG, 14, ((LOCAL:16)*120), 0
;苦痛の珠
SIF LOCAL:11 && LOCAL:9
	LOCAL:9 /= 2
IF (LOCAL:8)+((LOCAL:9)/5) > 0
	CALL COMMON_UP_JUEL, ARG, 16, ((LOCAL:8)+((LOCAL:9)/5)*40), 0
	LOCAL:4 += ((LOCAL:8)+((LOCAL:9)/5)) / 2
ENDIF
;恐怖の珠
CALL COMMON_UP_JUEL, ARG, 17, ((LOCAL:16)*110), 0
;否定の珠・ストレス
;触手経験
SELECTCASE EXP:ARG:触手経験
	CASE IS < EXPLV:1
		TIMES LOCAL:4 , 3.00
	CASE IS < EXPLV:2
		TIMES LOCAL:4 , 2.50
	CASE IS < EXPLV:3
		TIMES LOCAL:4 , 2.00
	CASE IS < EXPLV:4
		TIMES LOCAL:4 , 1.00
	CASE IS < EXPLV:5
		TIMES LOCAL:4 , 0.80
	CASEELSE
		TIMES LOCAL:4 , 0.60
ENDSELECT
;寄生
SIF TALENT:ARG:寄生
	LOCAL:4 -= 500
;服従系・崩壊系
SIF TALENT:ARG:服従 || TALENT:ARG:烙印 || TALENT:ARG:隷属 || TALENT:ARG:傀儡 || TALENT:ARG:壊造人格
	LOCAL:4 -= 100
;使い魔
SIF TALENT:ARG:使い魔
	LOCAL:4 -= 250
IF LOCAL:11
	LOCAL:4 -= 100 * LOCAL:11
	LOCAL:6 /= LOCAL:11
ENDIF
LOCAL:4 = MAX(LOCAL:4, 0)
IF LOCAL:4 > 0
	;否定の珠
	CALL COMMON_UP_JUEL, ARG, 100, LOCAL:4, 0
	SIF TALENT:MASTER:998
		PRINTFORML ストレス＋{(LOCAL:4) / 5}
	CFLAG:ARG:ストレス += (LOCAL:4) / 5
	;ストレス値は100万以上にはならない
	CFLAG:ARG:ストレス = MIN(CFLAG:ARG:ストレス , 999999)
ENDIF
;気力・体力・理性の消費
IF LOCAL:6
	IF BASE:ARG:体力 > 1
		BASE:ARG:体力 = MAX(BASE:ARG:体力 - LOCAL:6, 1)
	ELSEIF BASE:ARG:体力 <= 1 && MAXBASE:ARG:0 > 1000
		BASE:ARG:体力 = 1
		MAXBASE:ARG:0 = MAX(MAXBASE:ARG:0 - (LOCAL:6)/5, 750)
	ENDIF
	IF BASE:ARG:気力 > 0
		BASE:ARG:気力 = MAX(BASE:ARG:気力 - (LOCAL:6)/2, 0)
	ELSEIF BASE:ARG:気力 <= 0 && MAXBASE:ARG:1 > 750
		BASE:ARG:気力 = 0
		MAXBASE:ARG:1 = MAX(MAXBASE:ARG:1 - (LOCAL:6)/10, 500)
	ENDIF
	IF TALENT:ARG:理性
		IF BASE:ARG:理性 > 0
			BASE:ARG:理性 = MAX(BASE:ARG:理性 - (LOCAL:6) * 5 / 3, 0)
		ELSEIF BASE:ARG:理性 <= 0
			BASE:ARG:理性 = 0
			MAXBASE:ARG:9 = MAX(MAXBASE:ARG:9 - (LOCAL:6)*5 / 11, 1)
		ENDIF
	ENDIF
ENDIF
;触手エネルギーの消費(1人につき-100～50ずつ。0未満にはならない)
SIF BASE:MASTER:触手
	BASE:MASTER:触手 = MAX(BASE:MASTER:触手 - (100 - 50 * ABL:ARG:触手中毒), 0)
WAIT

;-----------------------------------------------
;触手インプラント・同化フラグ
;-----------------------------------------------
;幽閉期間が30日以上または7箇所以上インプラントされているかつ幽閉期間が15日以上のとき
;時間が進む度に同化フラグが発生する判定を行う。
;この確率は取り込まれたキャラが多いほど上昇し、十人以降は確実に発生するようになる。
;ただし幽閉する前に予め使い魔の契約を結んでいると触手同化のイベントの回避は可能。
;また、使い魔の契約を結んでいない場合でも幽閉してから14日経つ前に開放することで回避することが可能。
;同化イベントに関するメッセージを追加。同時に期間が危険水域に達した場合のメッセージも表示。
IF TALENT:ARG:使い魔 == 0 && ((COUNTBIT(CFLAG:ARG:41, 1, 10) >= 7 && CFLAG:ARG:39 > 14) || CFLAG:ARG:39 > 30)
	IF MAX(RAND:11 - FLAG:33, 0) == 0
		PRINTFORM 触手達はすっかり
		PRINTFORMW %SHOW_CALLNAME(ARG)%のことを気に入ってしまったようだ……
		CFLAG:ARG:41 |= 1024
	ELSE
		SELECTCASE RAND:3
			CASE 0
				PRINTFORM 触手達は
				PRINTFORMW %SHOW_CALLNAME(ARG)%のことを気に入ったようだ……
			CASE 1
				PRINTFORMW %SHOW_CALLNAME(ARG)%の体は大分触手に慣れてきたようだ……
			CASEELSE
				PRINTFORM 触手達は
				PRINTFORMW %SHOW_CALLNAME(ARG)%の体の開発をさらに進めている……
		ENDSELECT
	ENDIF
ENDIF
;触手インプラントに関しては触手を寄生させた状態では幽閉から8日以降、そうでない場合は15日以降に発生。
;触手により適応する効果を持つ『種』を植え付けられる。植え付けられた箇所に応じて調教時のPALAM:の伸びが良くなるが、
;前述の通り同化フラグが発生する確率も上昇してしまう。
;触手寄生と同様メディスン、雛、蓬莱人は[体内適応力強化]がないとインプラントが発生しないように。
LOCAL:6 = (TALENT:ARG:寄生) ? 8 # 15
LOCAL:17 = 0
IF COUNTBIT(CFLAG:ARG:41, 1, 10) < 10 && CFLAG:ARG:39 > LOCAL:6 && ((TALENT:ARG:蓬莱人 == 0 && NO:ARG != 1033 && NO:ARG != 1039) || FLAG:75 & 8192)
	IF (CFLAG:ARG:41 & 1) == 0 && LOCAL:14 & 8 && TALENT:ARG:オトコ == 0 && TALENT:ARG:ふたなり == 0
		;触手インプラント(クリトリス)
		PRINTFORMW %SHOW_CALLNAME(ARG)%はクリトリスに何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 1
		LOCAL:17 = 1
	ELSEIF (CFLAG:ARG:41 & 2) == 0 && LOCAL:14 & 2 && TALENT:ARG:オトコ == 0
		;触手インプラント(子宮)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は子宮の奥深くに何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 2
		LOCAL:17 = 2
	ELSEIF (CFLAG:ARG:41 & 4) == 0 && LOCAL:14 & 1
		;触手インプラント(直腸)
		PRINTFORMW %SHOW_CALLNAME(ARG)%はお尻の穴に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 4
		LOCAL:17 = 3
	ELSEIF (CFLAG:ARG:41 & 8) == 0 && LOCAL:14 & 4 && TALENT:ARG:オトコ == 0
		;触手インプラント(乳房)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は乳腺を通し、乳房の奥底に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 8
		LOCAL:17 = 4
	ELSEIF (CFLAG:ARG:41 & 16) == 0 && LOCAL:14 & 256
		;触手インプラント(ヘソ)
		PRINTFORMW %SHOW_CALLNAME(ARG)%はヘソに何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 16
		LOCAL:17 = 5
	ELSEIF (CFLAG:ARG:41 & 32) == 0 && LOCAL:14 & 16
		;触手インプラント(舌根)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は舌の付け根に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 32
		LOCAL:17 = 6
	ELSEIF (CFLAG:ARG:41 & 64) == 0 && LOCAL:14 & 128
		;触手インプラント(膀胱)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は膀胱壁に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 64
		LOCAL:17 = 7
	ELSEIF (CFLAG:ARG:41 & 128) == 0 && LOCAL:14 & 64
		;触手インプラント(心臓)
		PRINTFORMW %SHOW_CALLNAME(ARG)%の心臓に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 128
		LOCAL:17 = 8
	ELSEIF (CFLAG:ARG:41 & 256) == 0 && LOCAL:14 & 32
		;触手インプラント(脊椎)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は脊椎の中に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 256
		LOCAL:17 = 9
	ELSEIF (CFLAG:ARG:41 & 512) == 0 && LOCAL:14 & 512
		;触手インプラント(額)
		PRINTFORMW %SHOW_CALLNAME(ARG)%は額の中心に何かを植え付けられたようだ……
		CFLAG:ARG:41 |= 512
		LOCAL:17 = 10
	ENDIF
ENDIF

CFLAG:ARG:39 += 1
;触手幽閉イベントの口上用。
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, ARG, 255, LOCAL:17
RETURN 0

;-----------------------------------------------
;触手同化イベントで入手するτ、触手の珠の計算
;-----------------------------------------------
@TENTACLE_ABSORB_EVENT
TFLAG:233 = 0
TFLAG:234 = 0
;体力・気力・理性
	TFLAG:233 += MAXBASE:0 / 50
	TFLAG:233 += MAXBASE:1 / 50
	SIF TALENT:理性
		TFLAG:233 += MAXBASE:9 / 300

;従順
	TFLAG:233 += 120 * ABL:従順
	TFLAG:234 += 2 * ABL:従順
;欲望
	TFLAG:233 += 150 * ABL:欲望
	TFLAG:234 += 3 * ABL:欲望
;技巧
	TFLAG:233 += 70 * ABL:技巧
	TFLAG:234 += ABL:技巧

;自慰中毒
	TFLAG:233 += 100 * ABL:自慰中毒
	TFLAG:234 += 2 * ABL:自慰中毒
;精液中毒
	TFLAG:233 += 180 * ABL:精液中毒
	TFLAG:234 += 3 * ABL:精液中毒
;噴乳中毒
	TFLAG:233 += 120 * ABL:噴乳中毒
	TFLAG:234 += 3 * ABL:噴乳中毒
;触手中毒
	TFLAG:233 += 200 * ABL:触手中毒
	TFLAG:234 += 5 * ABL:触手中毒

;Ｃ感覚
	TFLAG:233 += 130 * ABL:Ｃ感覚
	TFLAG:234 += 2 * ABL:Ｃ感覚
;Ｖ感覚
	TFLAG:233 += 140 * ABL:Ｖ感覚
	TFLAG:234 += 2 * ABL:Ｖ感覚
;Ａ感覚
	TFLAG:233 += 140 * ABL:Ａ感覚
	TFLAG:234 += 2 * ABL:Ａ感覚
;Ｂ感覚
	TFLAG:233 += 130 * ABL:Ｂ感覚
	TFLAG:234 += 2 * ABL:Ｂ感覚

;奉仕精神
	TFLAG:233 += 140 * ABL:奉仕精神
	TFLAG:234 += 3 * ABL:奉仕精神
;露出癖
	TFLAG:233 += 120 * ABL:露出癖
	TFLAG:234 += ABL:露出癖
;サドっ気
	TFLAG:233 += 160 * ABL:サドっ気
	TFLAG:234 += ABL:サドっ気
;マゾっ気
	TFLAG:233 += 160 * ABL:マゾっ気
	TFLAG:234 += ABL:マゾっ気
;レズっ気
	TFLAG:233 += 110 * ABL:レズっ気
;ＢＬっ気
	TFLAG:233 += 110 * ABL:ＢＬっ気

;料理技能
	TFLAG:233 += 60 * ABL:料理技能
;歌唱技能
	TFLAG:233 += 60 * ABL:歌唱技能
;工作技能
	TFLAG:233 += 60 * ABL:工作技能

;触手インプラント(触手成長LV最大値引き上げ処理を流用)
IF CFLAG:41
	LOCAL = PARASITIC_TENTACLE_INPLANT(0)
	TFLAG:233 += LOCAL / 2
	TFLAG:234 += LOCAL / 10
ENDIF

;処女
SIF TALENT:処女
	TIMES TFLAG:233 , 2.50
;蓬莱人
IF TALENT:蓬莱人
	TIMES TFLAG:233 , 4.00
;呪精
ELSEIF TALENT:呪精
	TIMES TFLAG:233 , 2.50
ENDIF
;薬毒耐性
SIF TALENT:薬毒耐性
	TIMES TFLAG:233 , 0.50
;献身的
SIF TALENT:献身的
	TIMES TFLAG:233 , 1.20
;快感に素直
IF TALENT:快感に素直 && ABL:欲望 > 2
	TIMES TFLAG:233 , 1.20
;快感の否定
ELSEIF TALENT:快感の否定 && ABL:欲望 < 4
	TIMES TFLAG:233 , 0.60
ENDIF
;倒錯的
SIF TALENT:倒錯的 && ABL:欲望 > 2
	TIMES TFLAG:233 , 1.20
;魅力
SIF TALENT:魅力
	TIMES TFLAG:233 , 1.50
;魅惑
SIF TALENT:魅惑
	TIMES TFLAG:233 , 1.50
;謎の魅力
SIF TALENT:謎の魅力
	TIMES TFLAG:233 , 2.00
;オトコ
IF TALENT:オトコ
	TIMES TFLAG:233 , 0.75
;ふたなり
ELSEIF TALENT:ふたなり
	TIMES TFLAG:233 , 1.50
ENDIF
;母乳体質
SIF TALENT:母乳体質
	TIMES TFLAG:233 , 1.40
;回復早い
IF TALENT:回復早い
	TIMES TFLAG:233 , 1.20
;回復遅い
ELSEIF TALENT:回復遅い
	TIMES TFLAG:233 , 1.20
ENDIF
;にんっしんっ
IF TALENT:妊娠
	TIMES TFLAG:233 , 0.75
;懐卵
ELSEIF TALENT:懐卵
	TIMES TFLAG:233 , 1.75
	TIMES TFLAG:234 , 1.75
ENDIF
;寄生
IF TALENT:寄生
	TFLAG:233 *= 2
	TFLAG:234 *= 2
ENDIF
;狂気
SIF TALENT:狂気
	TIMES TFLAG:233 , 1.10
;淫乱
SIF TALENT:淫乱
	TIMES TFLAG:233 , 1.20
;淫核／淫茎
SIF TALENT:淫核／淫茎
	TIMES TFLAG:233 , 1.10
;淫壷
SIF TALENT:淫壷
	TIMES TFLAG:233 , 1.10
;淫尻
SIF TALENT:淫尻
	TIMES TFLAG:233 , 1.10
;淫乳
SIF TALENT:淫乳
	TIMES TFLAG:233 , 1.10
;尿道狂
SIF TALENT:尿道狂
	TIMES TFLAG:233 , 1.10
;キス魔
SIF TALENT:キス魔
	TIMES TFLAG:233 , 1.10
;ドＳ
SIF TALENT:ドＳ
	TIMES TFLAG:233 , 1.10
;ドＭ
SIF TALENT:ドＭ
	TIMES TFLAG:233 , 1.10
;神霊
IF TALENT:神霊
	TIMES TFLAG:233 , 1.50
	TIMES TFLAG:234 , 1.25
ENDIF
;半神
IF TALENT:半神
	TIMES TFLAG:233 , 1.25
	TIMES TFLAG:234 , 1.20
ENDIF
;死神
IF TALENT:死神
	TIMES TFLAG:233 , 1.50
	TIMES TFLAG:234 , 1.25
ENDIF
;数値調整用
TFLAG:233 = MAX(TFLAG:233 / 10, 100)

RETURN 0

;-----------------------------------------------
;触手種付の珠・経験入手処理
;-----------------------------------------------
;触手出産の処理を流用
@TENTACLE_LARVA_IMPREGNATE
TFLAG:201 = 0
LOCAL = 32000
LOCAL:1 = 30

;ABL:Ｖ感覚をみる
LOCAL:2 = 200 * (1+ABL:Ｖ感覚)

;EXP:Ｖ経験をみる
SELECTCASE EXP:Ｖ経験
	CASE IS < EXPLV:1
		TIMES LOCAL:2 , 0.20
		TIMES LOCAL , 2.50
		LOCAL:3 = 3000
	CASE IS < EXPLV:2
		TIMES LOCAL:2 , 0.60
		TIMES LOCAL , 1.60
		LOCAL:3 = 2000
	CASE IS < EXPLV:3
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL , 1.00
		LOCAL:3 = 1500
	CASE IS < EXPLV:4
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL , 0.90
		LOCAL:3 = 1000
	CASE IS < EXPLV:5
		TIMES LOCAL:2 , 1.40
		TIMES LOCAL , 0.80
		LOCAL:3 = 500
	CASEELSE
		TIMES LOCAL:2 , 1.60
		TIMES LOCAL , 0.60
		LOCAL:3 = 100
ENDSELECT

;EXP:産卵経験をみる
SELECTCASE EXP:産卵経験
	CASE IS < EXPLV:2
		TIMES LOCAL:2 , 0.80
		TIMES LOCAL:3 , 1.50
		TIMES LOCAL , 1.80
		LOCAL:4 = 0
	CASE IS < EXPLV:4
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL:3 , 1.00
		TIMES LOCAL , 1.20
		LOCAL:4 = 1
	CASE IS < EXPLV:5
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL:3 , 0.80
		TIMES LOCAL , 1.00
		LOCAL:4 = 2
	CASEELSE
		TIMES LOCAL:2 , 1.50
		TIMES LOCAL:3 , 0.60
		TIMES LOCAL , 0.90
		LOCAL:4 = 3
ENDSELECT

;ABL:触手中毒をみる
SELECTCASE ABL:触手中毒
	CASE 0
		TIMES LOCAL:2 , 0.50
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL:2 , 0.66
		TIMES LOCAL , 1.00
	CASE 2
		TIMES LOCAL:2 , 0.80
		TIMES LOCAL , 0.70
	CASE 3
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL , 0.40
	CASE 4
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL , 0.10
	CASEELSE
		TIMES LOCAL:2 , 1.50
		TIMES LOCAL , 0.01
ENDSELECT

;催眠触手
SIF FLAG:75 & 8
	TIMES LOCAL , 0.75
;極細触手
SIF FLAG:75 & 16
	TIMES LOCAL:3 , 0.60
;精液放出量強化
IF FLAG:75 & 16384
	IF TALENT:小人体型
		LOCAL:1 *= 2
	ELSEIF TALENT:小柄体型
		LOCAL:1 *= 3
	ELSE
		LOCAL:1 *= 5
	ENDIF
ENDIF

LOCAL:5 = LOCAL:2 + LOCAL:3 + (LOCAL/50)
TIMES LOCAL:5 , 0.75

;従順
SELECTCASE ABL:従順
	CASE 0
		TIMES LOCAL , 2.50
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 0.90
	CASE 4
		TIMES LOCAL , 0.75
	CASEELSE
		TIMES LOCAL , 0.50
ENDSELECT

;保守的
SIF TALENT:保守的
	TIMES LOCAL , 2.50
;恋慕
SIF TALENT:恋慕
	TIMES LOCAL , 0.90
;妄信
SIF TALENT:妄信
	TIMES LOCAL , 0.20
;親愛
SIF TALENT:親愛
	TIMES LOCAL , 0.80
;相愛
SIF TALENT:相愛
	TIMES LOCAL , 0.80
;服従
SIF TALENT:服従
	TIMES LOCAL , 0.60
;烙印
SIF TALENT:烙印
	TIMES LOCAL , 0.30
;隷属
SIF TALENT:隷属
	TIMES LOCAL , 0.50
;淫乱
SIF TALENT:淫乱
	TIMES LOCAL , 0.80

TFLAG:201 = LOCAL:1
;触手種付用のイベント口上
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 258
PRINTL 

;快Ｖの珠
CALL COMMON_UP_JUEL, TARGET, 1, LOCAL:2, 0
;屈服の珠
CALL COMMON_UP_JUEL, TARGET, 14, ((LOCAL:2+LOCAL:3)/2), 0
;苦痛の珠
CALL COMMON_UP_JUEL, TARGET, 16, (LOCAL:3/3), 0
;恐怖の珠
CALL COMMON_UP_JUEL, TARGET, 17, LOCAL:5, 0
;否定の珠
CALL COMMON_UP_JUEL, TARGET, 100, LOCAL, 0
;Ｖ経験
CALL COMMON_UP_EXP, TARGET, 0, 50, 0
;Ｖ拡張経験
CALL COMMON_UP_EXP, TARGET, 32, 2, 0
;精液経験
CALL COMMON_UP_EXP, TARGET, 4, LOCAL:1, 0
;緊縛経験
CALL COMMON_UP_EXP, TARGET, 31, 7, 0
IF (CFLAG:24 & 16) == 0
	CFLAG:24 |= 16
	;異常経験
	CALL COMMON_UP_EXP, TARGET, 50, 1, 0
ENDIF
;触手経験
CALL COMMON_UP_EXP, TARGET, 53, 10, 1
PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:145%]を得た
TALENT:懐卵 = 1

;-----------------------------------------------
;触手出産処理
;-----------------------------------------------
;黄色い飴玉の産卵処理を流用
@SPAWN_TENTACLE_LARVA
LOCAL = 35000

;ABL:Ｖ感覚をみる
LOCAL:2 = 150 * (1+ABL:Ｖ感覚)

;EXP:Ｖ経験をみる
SELECTCASE EXP:Ｖ経験
	CASE IS < EXPLV:2
		TIMES LOCAL:2 , 0.60
		TIMES LOCAL , 1.60
		LOCAL:3 = 3000
	CASE IS < EXPLV:3
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL , 1.00
		LOCAL:3 = 1500
	CASE IS < EXPLV:4
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL , 0.90
		LOCAL:3 = 1000
	CASE IS < EXPLV:5
		TIMES LOCAL:2 , 1.40
		TIMES LOCAL , 0.80
		LOCAL:3 = 500
	CASEELSE
		TIMES LOCAL:2 , 1.60
		TIMES LOCAL , 0.60
		LOCAL:3 = 100
ENDSELECT

;EXP:産卵経験をみる
SELECTCASE EXP:産卵経験
	CASE IS < EXPLV:2
		TIMES LOCAL:2 , 0.80
		TIMES LOCAL:3 , 1.50
		TIMES LOCAL , 1.80
		LOCAL:4 = 0
	CASE IS < EXPLV:4
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL:3 , 1.00
		TIMES LOCAL , 1.20
		LOCAL:4 = 1
	CASE IS < EXPLV:5
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL:3 , 0.80
		TIMES LOCAL , 1.00
		LOCAL:4 = 2
	CASEELSE
		TIMES LOCAL:2 , 1.50
		TIMES LOCAL:3 , 0.60
		TIMES LOCAL , 0.90
		LOCAL:4 = 3
ENDSELECT

;ABL:触手中毒をみる
SELECTCASE ABL:触手中毒
	CASE 0
		TIMES LOCAL:2 , 0.50
		TIMES LOCAL , 1.00
	CASE 1
		TIMES LOCAL:2 , 0.66
		TIMES LOCAL , 1.00
	CASE 2
		TIMES LOCAL:2 , 0.80
		TIMES LOCAL , 0.70
	CASE 3
		TIMES LOCAL:2 , 1.00
		TIMES LOCAL , 0.40
	CASE 4
		TIMES LOCAL:2 , 1.20
		TIMES LOCAL , 0.10
	CASEELSE
		TIMES LOCAL:2 , 1.50
		TIMES LOCAL , 0.01
ENDSELECT

;催眠触手
SIF FLAG:75 & 8
	TIMES LOCAL , 0.75

LOCAL:1 = LOCAL:2 + LOCAL:3 + (LOCAL/50)
TIMES LOCAL:1 , 1.25

;従順
SELECTCASE ABL:従順
	CASE 0
		TIMES LOCAL , 2.50
	CASE 1
		TIMES LOCAL , 1.50
	CASE 2
		TIMES LOCAL , 1.00
	CASE 3
		TIMES LOCAL , 0.90
	CASE 4
		TIMES LOCAL , 0.75
	CASEELSE
		TIMES LOCAL , 0.50
ENDSELECT

;保守的
SIF TALENT:保守的
	TIMES LOCAL , 2.50
;恋慕
SIF TALENT:恋慕
	TIMES LOCAL , 0.90
;妄信
SIF TALENT:妄信
	TIMES LOCAL , 0.20
;親愛
SIF TALENT:親愛
	TIMES LOCAL , 0.80
;相愛
SIF TALENT:相愛
	TIMES LOCAL , 0.80
;服従
SIF TALENT:服従
	TIMES LOCAL , 0.50
;烙印
SIF TALENT:烙印
	TIMES LOCAL , 0.20
;隷属
SIF TALENT:隷属
	TIMES LOCAL , 0.30
;淫乱
SIF TALENT:淫乱
	TIMES LOCAL , 0.80
;理性
SIF TALENT:理性
	TIMES LOCAL , 1.20

;触手出産用のイベント口上
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 260
PRINTL 
PRINTFORMW %CALLNAME:TARGET%は[%TALENTNAME:145%]を失った
TALENT:懐卵 = 0
SIF TALENT:MASTER:998
	PRINTFORML ストレス＋{LOCAL / 5}
CFLAG:ストレス += LOCAL / 5

;快Ｖの珠
CALL COMMON_UP_JUEL, TARGET, 1, LOCAL:2, 0
;屈服の珠
CALL COMMON_UP_JUEL, TARGET, 14, ((LOCAL:2+LOCAL:3)/2), 0
;苦痛の珠
CALL COMMON_UP_JUEL, TARGET, 16, LOCAL:3, 0
;恐怖の珠
CALL COMMON_UP_JUEL, TARGET, 17, LOCAL:1, 0
;否定の珠
CALL COMMON_UP_JUEL, TARGET, 100, LOCAL, 0
;Ｖ経験
CALL COMMON_UP_EXP, TARGET, 0, 5, 0
;Ｖ拡張経験
CALL COMMON_UP_EXP, TARGET, 32, 1, 0
;緊縛経験
CALL COMMON_UP_EXP, TARGET, 31, 5, 0
;異常経験
SIF CFLAG:触手出産数 == 0
	CALL COMMON_UP_EXP, TARGET, 50, 1, 0
;出産経験
CALL COMMON_UP_EXP, TARGET, 52, 1, 0
;触手経験
CALL COMMON_UP_EXP, TARGET, 53, 7, 1

;ストレス値は100万以上にはならない
CFLAG:ストレス = MIN(CFLAG:ストレス, 999999)
PRINTL 
;出産した触手(幼体)は展示室に持って行かれる
SIF FLAG:12 & 8
	CALL GET_LARVA

;[烙印]、[精神崩壊]、[傀儡]、[壊造人格]のいずれも取得していない状態で異常経験10未満かつ触手中毒3未満だと狂気が付く(判定は触手出産する度に行う)
IF (EXP:異常経験 < 10 && ABL:触手中毒 < 3) && TALENT:狂気 == 0 && TALENT:烙印 == 0 && TALENT:精神崩壊 == 0 && TALENT:傀儡 == 0 && TALENT:壊造人格 == 0
	PRINTFORMW %CALLNAME:TARGET%は発狂してしまった
	TALENT:狂気 = 1
	CFLAG:12 |= 1
	CFLAG:17 |= 2
ENDIF

;懐卵日数リセット及び触手出産回数カウント処理
CFLAG:触手出産数 += 1
CFLAG:35 = 0

;触手種付イベントで減った体力の最大値が回復
MAXBASE:0 += 500
BASE:体力 = 100
BASE:体力 -= 10*(9-LOCAL:4)
PRINTFORMW %SHOW_CALLNAME(TARGET)%は体力を激しく消耗している
PRINTW 少し休ませたほうがよさそうだ……

;調教対象と助手を元に戻す
TARGET = TFLAG:901
ASSI = TFLAG:902
