;YM→YMAEコンバータによって作成されました
;================================================
;調教コマンドの実行回数（ターン数）を返す
;------------------------------------------------
;■引数
;	"RESET"		ターン数を初期化（システムから呼び出し）
;	"GET"		現在のターン数を取得
;	"COUNTUP"	ターン数を加算（システムから呼び出し）
;------------------------------------------------
;■戻り値
;	GET時のみ回数を返す。
;	それ以外は-1が戻る（普通はCALLFで呼ぶので意味なし）
;================================================
@K2008F_COMCOUNT(ARGS = "")
#FUNCTION
SELECTCASE ARGS
	CASE "RESET"
		CFLAG:__K2008N_CFLAG_COUNTCOM__ = 0
		RETURNF -1
	CASE "GET", ""
		RETURNF CFLAG:__K2008N_CFLAG_COUNTCOM__
	CASE "COUNTUP"
		CFLAG:__K2008N_CFLAG_COUNTCOM__ += 1
		RETURNF -1
	CASEELSE
		THROW 【K2008エラー】@未登録の引数 %ARGS% が使用されました
ENDSELECT


;================================================
;SELECTCASEでRAND分岐にIF文を混ぜるための関数
;【注意】
;	RAND分岐用なので、FALSEの時-1（RANDでは絶対に出ない値）が戻ります。
;	ERBの仕様上、-1はTRUEとして扱われます。
;------------------------------------------------
;■戻り値
;	_EXPが正（_EXP > 0）なら_RESULT
;	それ以外は-1
;------------------------------------------------
;■引数
;_EXP
;	判定したい変数や数式
;_RESULT
;	予約戻り値（省略時1）
;------------------------------------------------
;■使用方法
;	SELECTCASE RAND:4
;		CASE 3
;			PRINTL RAND:4==3の時に表示されます
;		CASE 2 * K2008F_CASEIF(TALENT:処女 == 1)
;			PRINTL 生娘の時、かつRAND:4==2の時に表示されます
;			PRINTL 生娘でない場合、CASE -2となるため、RAND:4の条件では絶対に呼ばれません
;			PRINTL CASE nの見出しで整列させる際には、こちらの表記が便利です
;		CASE K2008F_CASEIF(TALENT:処女 == 2, 1)
;			PRINTL 再生処女の時、かつRAND:4==1の時に表示されます
;			PRINTL 数式ですっきりさせるメリットはありますが、この方式は使い勝手は悪いかも
;		CASEELSE
;			PRINTL RAND:4==0のとき、もしくは1～2だったが条件を満たさなかった場合に表示されます
;	ENDSELECT
;================================================
@K2008F_CASEIF(_EXP = 0, _RESULT = 1)
#FUNCTION
#DIM _EXP
#DIM _RESULT
RETURNF ( (_EXP > 0)? _RESULT # -1)

;================================================
;胸の大きさを返す関数
;------------------------------------------------
;■戻り値
;	-2: 絶壁
;	-1: 貧乳
;	 0: 並乳
;	 1: 巨乳
;	 2: 爆乳
;================================================
@K2008F_BSIZE()
#FUNCTION
#DIM _SIZE
_SIZE = 0
IF TALENT:絶壁 > 0
	_SIZE = -2
ELSEIF TALENT:貧乳 > 0
	_SIZE = -1
ELSEIF TALENT:巨乳 > 0
	_SIZE = 1
ELSEIF TALENT:爆乳 > 0
	_SIZE = 2
ENDIF
RETURNF _SIZE

;================================================
;陰毛の濃さを返す関数
;================================================
@K2008F_PUBIC_HAIR_DENSITY()
#FUNCTION
#DIM _DENSITY
_DENSITY = 0
SELECTCASE BASE:陰毛
	CASE 0
		_DENSITY = 0
	CASE 1 TO 50
		_DENSITY = 1
	CASE 51 TO 250
		_DENSITY = 2
	CASE 251 TO 500
		_DENSITY = 3
	CASE 501 TO 1000
		;本当はMAXBASE:5で比較したいが、使われていない？
		_DENSITY = 4
		SIF K2008F_DEVRANK("陰毛") >= 5
			_DENSITY += 1
	CASEELSE
		THROW 【K2008エラー】予期せぬ値BASE:陰毛 = {BASE:陰毛}がありました
ENDSELECT
RETURNF _DENSITY

;================================================
;調教コマンドの番号を、コマンド名で参照する
;	なんでGETNUMはTRAINに使えないんだ……
;------------------------------------------------
;■引数
;	調教コマンド名称
;================================================
@K2008F_GETNUM_TRAIN(ARGS)
#FUNCTION
#DIM _COUNT
FOR _COUNT, 0, VARSIZE("TRAINNAME")
	SIF TRAINNAME:_COUNT == ARGS
		RETURNF _COUNT
NEXT
THROW 【K2008エラー】定義されていないコマンド名を参照しようとしました

;================================================
;調教コマンドのCFLAG内容を、コマンド名で参照する
;------------------------------------------------
;■引数
;	調教コマンド名称
;================================================
@K2008F_CFLAGCOM(ARGS)
#FUNCTION
;GETNUM(TRAIN, ARGS)したいけど、TRAINには使えないようなので手作業探索
ASSERT INRANGE(K2008F_GETNUM_TRAIN(ARGS), 0, 999)
RETURNF CFLAG:(8000 + K2008F_GETNUM_TRAIN(ARGS))

;================================================
;引数のキャラが射精可能かを返す
;------------------------------------------------
;■引数
;	TARGET/ASSI/MASTERのいずれか
;	省略時はTARGET（つまり夢美）
;================================================
@K2008F_CAN_EJAC(_CHARA = __INT_MIN__)
#FUNCTION
#DIM _CHARA
SELECTCASE _CHARA
	CASE __INT_MIN__, TARGET
		_CHARA = TARGET
	CASE IS < 0
		THROW ASSIなどの存在確認を先に行ってください。存在しないキャラ変数は受け付けません
	CASE ASSI, MASTER
		;_CHARAをそのまま使う
ENDSELECT

RETURNF TALENT:_CHARA:オトコ || TALENT:_CHARA:ふたなり



;================================================
;睡眠薬を服用した時の睡眠深さを返す
;------------------------------------------------
;■引数
;	ARGS	GETまたは各状態
;------------------------------------------------
;■戻り値
;	GETなら現在の状態（LV0～4）
;	各状態なら基準値の数値を返す
;================================================
@K2008F_SLEEPLV(ARGS)
#FUNCTION
#DIM _LV
#DIM CONST _SLEEPLV_, 5 = 0, 1, 2001, 5001, 9001

SELECTCASE ARGS
	CASE "GET"
		SELECTCASE EQUIP:睡眠薬
			CASE IS >= _SLEEPLV_:4
				_LV = 4
			CASE IS >= _SLEEPLV_:3
				_LV = 3
			CASE IS >= _SLEEPLV_:2
				_LV = 2
			CASE IS >= _SLEEPLV_:1
				_LV = 1
			CASE IS >= _SLEEPLV_:0
				_LV = 0
			CASEELSE
				THROW 【K2008エラー】予期せぬ値が EQUIP:睡眠薬 に入っています。
		ENDSELECT
	CASE "昏睡"
		_LV = _SLEEPLV_:4
	CASE "熟睡"
		_LV = _SLEEPLV_:3
	CASE "睡眠"
		_LV = _SLEEPLV_:2
	CASE "まどろみ"
		_LV = _SLEEPLV_:1
	CASE "目覚め"
		_LV = _SLEEPLV_:0
	CASEELSE
		THROW 【K2008エラー】未登録の引数 %ARGS% が使用されました
ENDSELECT

RETURNF _LV


;================================================
;発言可能な状態かを調べる
;------------------------------------------------
;■戻り値
;	0：発言不能
;	1：発言可能
;================================================
@K2008F_CAN_TALK()
#FUNCTION

;眠ってる場合はNG
SIF EQUIP:睡眠薬
	RETURNF 0
;口枷がある場合はNG
SIF TEQUIP:口装着器具
	RETURNF 0
;時止め中はNG
SIF TEQUIP:時間止め
	RETURNF 0
;失神中はNG
SIF TFLAG:899 >= 1
	RETURNF 0
;精神崩壊はNG
SIF TALENT:精神崩壊
	RETURNF 0

;最後まで来た＝発言可能
RETURNF 1


;================================================
;TEQUIP変数内で、値による分岐があるものについて、名前に対応した数値を返す
;------------------------------------------------
;■引数
;	ARGS	対応したアイテム名・行為名
;------------------------------------------------
;■戻り値
;	設定された数値
;================================================
@K2008F_TEQUIPTYPE(ARGS)
#FUNCTION
SELECTCASE ARGS
	;------------------------------------------------
	;TEQUIP:坐剤 坐剤
	;ビット演算……どのように対応すればよいですか？
	;------------------------------------------------
	CASE "アナルに座薬", "アナル座薬"
		RETURNF 1
	CASE "膣に座薬", "膣座薬"
		RETURNF 2
	CASE "アナルにフリスク", "アナリスク"
		RETURNF 3
	;------------------------------------------------
	;TEQUIP:排卵誘発剤 排卵誘発剤
	;------------------------------------------------
	CASE "排卵誘発剤"
		RETURNF 1
	CASE "触手排卵剤"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:緊急避妊薬 緊急避妊薬
	;------------------------------------------------
	CASE "緊急避妊薬"
		RETURNF 1
	CASE "触手避妊膜"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:Ｖ系装着器具 Ｖ系装着器具
	;------------------------------------------------
	CASE "バイブ", "バイブ装着", "触手Ｖ挿入"
		RETURNF 1
	CASE "ヴァギナ要石", "触手Ｇスポ責め"
		RETURNF 2
	CASE "ローターＶ挿入", "触手子宮責め"
		RETURNF 3
	CASE "ヴァギナ電極"
		RETURNF 4
	CASE "ヴァギナバーベル"
		RETURNF 5
	CASE "ヴァギナバルーン"
		RETURNF 6
	CASE "愛液採集器", "触手愛液採集"
		RETURNF 7
	;CASE "未定義"
	;	RETURNF 8
	CASE "焼芋挿入中"
		RETURNF 9
	;------------------------------------------------
	;TEQUIP:クスコ クスコ
	;------------------------------------------------
	CASE "クスコ"
		RETURNF 1
	CASE "触手幽霊膣鏡"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:Ｖ系装着器具 Ａ系装着器具
	;------------------------------------------------
	CASE "アナルバイブ", "アナルバイブ装着", "触手Ａ挿入"
		RETURNF 1
	CASE "アナル要石", "触手小腸責め"
		RETURNF 2
	CASE "アナルローター", "触手貫通"
		RETURNF 3
	CASE "アナル電極", "触手腸内撮影"
		RETURNF 4
	CASE "アナルビーズ"
		RETURNF 5
	CASE "アナルバルーン"
		RETURNF 6
	CASE "浣腸＋アナルプラグ装着", "浣腸"
		RETURNF 7
	CASE "空気浣腸"
		RETURNF 8
	CASE "巨峰挿入中"
		RETURNF 9
	CASE "アナルバーベル"
		RETURNF 10
	CASE "浣腸垂れ流し中"
		RETURNF 11
	;------------------------------------------------
	;TEQUIP:Ｃ系装着器具 Ｃ系装着器具
	;------------------------------------------------
	CASE "クリキャップ", "クリキャップ装着", "触手クリ責め"
		RETURNF 1
	CASE "オナホール", "オナホール装着", "触触手ペニス責め"
		RETURNF 2
	;CASE "未定義"
	;	RETURNF 3
	CASE "陰極電極"
		RETURNF 4
	CASE "洗濯バサミ"
		RETURNF 5
	CASE "電極オナホール", "触手放電ペニス責め"
		RETURNF 6
	CASE "触手妖精按摩"
		RETURNF 7
	CASE "触手氷精手淫"
		RETURNF 8
	;------------------------------------------------
	;TEQUIP:Ｕ系装着器具 Ｕ系装着器具
	;------------------------------------------------
	CASE "カテーテル"
		RETURNF 1
	CASE "採尿器"
		RETURNF 2
	CASE "バルーンカテーテル"
		RETURNF 3
	CASE "尿道系電極"
		RETURNF 4
	CASE "アスパラ挿入中"
		RETURNF 9
	;------------------------------------------------
	;TEQUIP:Ｂ系装着器具 Ｂ系装着器具
	;------------------------------------------------
	CASE "二プルキャップ", "二プルキャップ装着", "触手乳首責め"
		RETURNF 1
	CASE "搾乳器装着", "触手搾乳"
		RETURNF 2
	CASE "触手乳腺挿入"
		RETURNF 3
	CASE "乳房電極"
		RETURNF 4
	CASE "洗濯バサミ"
		RETURNF 5
	;------------------------------------------------
	;TEQUIP:緊縛関連 緊縛関連
	;------------------------------------------------
	CASE "縄で緊縛"
		RETURNF 1
	CASE "四肢緊縛"
		RETURNF 2
	CASE "亀甲縛り"
		RETURNF 3
	CASE "吊り下げ"
		RETURNF 4
	CASE "荊"
		RETURNF 5
	CASE "蜘蛛の糸"
		RETURNF 6
	CASE "蜘蛛の巣"
		RETURNF 7
	CASE "戦闘中拘束"
		RETURNF 8
	CASE "戦闘中瞬間拘束"
		RETURNF 9
	;------------------------------------------------
	;TEQUIP:目装着器具 目装着器具
	;------------------------------------------------
	CASE "アイマスク", "アイマスク装着"
		RETURNF 1
	CASE "蜘蛛の糸で目隠し"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:口装着器具 口装着器具
	;------------------------------------------------
	CASE "ボールギャグ装着", "ボールギャグ"
		RETURNF 1
	CASE "触手口辱"
		RETURNF 2
	CASE "強制開口器", "猿轡"
		RETURNF 3
	CASE "触手強制飲酒"
		RETURNF 4
	;------------------------------------------------
	;TEQUIP:鼻装着器具 鼻装着器具
	;------------------------------------------------
	CASE "鼻フック装着", "鼻フック装着"
		RETURNF 1
	CASE "触手鼻穴挿入"
		RETURNF 2
	;CASE "未定義"
	;	RETURNF 3
	;CASE "未定義"
	;	RETURNF 4
	CASE "洗濯バサミ"
		RETURNF 5
	;------------------------------------------------
	;TEQUIP:耳装着器具 耳装着器具
	;------------------------------------------------
	CASE "耳栓装着"
		RETURNF 1
	CASE "触手耳穴挿入"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:シャワープレイ シャワープレイ
	;------------------------------------------------
	CASE "温水シャワー"
		RETURNF 1
	CASE "冷水シャワー"
		RETURNF 2
	CASE "熱湯シャワー"
		RETURNF 3
	;------------------------------------------------
	;TEQUIP:特殊風呂入浴中 特殊風呂入浴中
	;------------------------------------------------
	CASE "蟲風呂"
		RETURNF 1
	CASE "鰻風呂"
		RETURNF 2
	CASE "ローション風呂"
		RETURNF 3
	CASE "スライム風呂"
		RETURNF 4
	CASE "媚薬風呂"
		RETURNF 5
	CASE "媚薬漬け"
		RETURNF 6
	CASE "水牢"
		RETURNF 7
	CASE "精液風呂"
		RETURNF 8
	CASE "酒風呂"
		RETURNF 9
	CASE "トロロ風呂"
		RETURNF 10
	CASE "毛玉風呂"
		RETURNF 11
	CASE "蒸し風呂"
		RETURNF 12
	CASE "国士無双の薬風呂"
		RETURNF 13
	CASE "厄毒風呂"
		RETURNF 14
	CASE "岩盤浴"
		RETURNF 15
	CASE "札束風呂"
		RETURNF 16
	CASE "酒漬け"
		RETURNF 17
	;------------------------------------------------
	;TEQUIP:羞恥プレイ 羞恥プレイ
	;------------------------------------------------
	CASE "大鏡"
		RETURNF 1
	CASE "浄玻璃の鏡"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:コスチュームプレイ コスチュームプレイ
	;------------------------------------------------
	CASE "裸エプロン"
		RETURNF 1
	CASE "スク水プレイ"
		RETURNF 2
	;------------------------------------------------
	;TEQUIP:89 お香使用中
	;------------------------------------------------
	CASE "シダーウッド"
		RETURNF 1
	CASE "ペパーミント"
		RETURNF 2
	CASE "ジャスミン"
		RETURNF 3
	CASE "ラヴェンダー"
		RETURNF 4
	CASE "ムスク"
		RETURNF 5
	CASE "フランキンセンス"
		RETURNF 6
	CASE "触手豊穣淫香"
		RETURNF 99
	;------------------------------------------------
	;TEQUIP:触手強制回復 触手強制回復
	;------------------------------------------------
	CASE "口で触手強制回復"
		RETURNF 1
	CASE "注射で触手強制回復"
		RETURNF 2
ENDSELECT
THROW 【K2008エラー】未登録の引数 %ARGS% が使用されました
