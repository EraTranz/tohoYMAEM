;--------------------------------------------------
;コマンド判定関数@COM_TRY_BATTLE
;ARGの中身のコマンドが実行可能かどうかを判定。
;RESULT = (0=実行可、1=実行判定で拒否される)
;--------------------------------------------------
@COM_TRY_BATTLE, ARG
IF EQUIP:戦闘中 && !TFLAG:699
	;同一コマンド連続実行ペナルティ回避フラグ
	TFLAG:96 = 1
	
	TFLAG:551 = 0
	TFLAG:552 = 0
	CALL COM_ORDER_BATTLE, TARGET, PLAYER, ARG, 1
	IF RESULT:0 == -2 || RESULT:0 == -1
		TFLAG:552 = RESULT:0 * -1
		IF RESULT:1 > 6
			CALL COM_ORDER_BATTLE, PLAYER, TARGET, ARG, 2, RESULT:1
		ELSE
			CALL COM_ORDER_BATTLE, PLAYER, TARGET, ARG, 2
		ENDIF
		;ファンブル＋ファンブル＝失敗
		IF TFLAG:552 == 2 && RESULT:0 == -2
			TFLAG:552 = 1
		;失敗＋ファンブル＝成功
		ELSEIF TFLAG:552 == 1 && RESULT:0 == -2
			TFLAG:552 = 0
			GOTO ATTACK_SUCCESS
		;失敗＋スペシャル＝ファンブル
		ELSEIF TFLAG:552 == 1 && RESULT:0 == 2
			TFLAG:552 = 2
		ENDIF
		$ATTACK_MISS
		PRINTFORM  対抗結果：
		FONTBOLD
		SETCOLOR 255,0,0
		IF TFLAG:552 == 2
			PRINTFORMW 『大失敗』
		ELSE
			PRINTFORMW 『失敗』
		ENDIF
		RESETCOLOR
		FONTREGULAR
		PRINTL 
		;調教者の攻撃（セクハラ）が失敗した時の地の文・口上
		CALL DISPLAY_KOJO_DODGE_MESSAGE_COM, TARGET
		;コマンドを実行できなかったときのパラ変動処理
		IF TFLAG:241 - TFLAG:240 <= 0
			TFLAG:241 = 1
			TFLAG:240 = 0
		ENDIF
		CALL DENIAL_PALAM_MOVE
		RETURN 1
	ELSEIF RESULT:0 == 2 || RESULT:0 == 1
		SIF RESULT:0 == 2
			TFLAG:551 = 1
		IF RESULT:1 > 6
			CALL COM_ORDER_BATTLE, PLAYER, TARGET, ARG, 2, RESULT:1
		ELSE
			CALL COM_ORDER_BATTLE, PLAYER, TARGET, ARG, 2
		ENDIF
		;スペシャル＋スペシャル＝失敗
		IF TFLAG:551 == 1 && RESULT:0 == 2
			TFLAG:551 = 0
			TFLAG:552 = 1
			GOTO ATTACK_MISS
		;成功＋成功＝失敗
		ELSEIF TFLAG:551 == 0 && RESULT:0 == 1
			TFLAG:552 = 1
			GOTO ATTACK_MISS
		;成功＋ファンブル＝大成功
		ELSEIF TFLAG:551 == 0 && RESULT:0 == -2
			TFLAG:551 = 1
		ENDIF
		$ATTACK_SUCCESS
		PRINTFORM  対抗結果：
		FONTBOLD
		SETCOLOR 0x66FFFF
		IF TFLAG:551
			PRINTFORMW 『大成功』
		ELSE
			PRINTFORMW 『成功』
		ENDIF
		RESETCOLOR
		FONTREGULAR
		PRINTL 
	ENDIF
ENDIF
RETURN 0

;--------------------------------------------------
;奴隷用
;--------------------------------------------------
@COM_TRY_BATTLE_TARGET, ARG
IF EQUIP:戦闘中
	LOCAL:1 = 0
	LOCAL:2 = 0
	CALL COM_ORDER_BATTLE, PLAYER, TARGET, ARG, 1
	IF RESULT:0 == -2 || RESULT:0 == -1
		LOCAL:2 = RESULT:0 * -1
		IF RESULT:1 > 6
			CALL COM_ORDER_BATTLE, TARGET, PLAYER, ARG, 2, RESULT:1
		ELSE
			CALL COM_ORDER_BATTLE, TARGET, PLAYER, ARG, 2
		ENDIF
		;ファンブル＋ファンブル＝失敗
		IF LOCAL:2 == 2 && RESULT:0 == -2
			LOCAL:2 = 1
		;失敗＋ファンブル＝成功
		ELSEIF LOCAL:2 == 1 && RESULT:0 == -2
			LOCAL:2 = 0
			GOTO ATTACK_SUCCESS
		;失敗＋スペシャル＝ファンブル
		ELSEIF LOCAL:2 == 1 && RESULT:0 == 2
			LOCAL:2 = 2
		ENDIF
		$ATTACK_MISS
		PRINTFORM  対抗結果：
		FONTBOLD
		SETCOLOR 255,0,0
		IF LOCAL:2 == 2
			PRINTFORMW 『大失敗』
		ELSE
			PRINTFORMW 『失敗』
		ENDIF
		RESETCOLOR
		FONTREGULAR
		PRINTL 
		;奴隷の攻撃が失敗した時の地の文・口上
		CALL DISPLAY_KOJO_MISS_MESSAGE_COM, TARGET
		RETURN 1, (LOCAL:1 == 1), (LOCAL:2 == 2)
	ELSEIF RESULT:0 == 2 || RESULT:0 == 1
		SIF RESULT:0 == 2
			LOCAL:1 = 1
		IF RESULT:1 > 6
			CALL COM_ORDER_BATTLE, TARGET, PLAYER, ARG, 2, RESULT:1
		ELSE
			CALL COM_ORDER_BATTLE, TARGET, PLAYER, ARG, 2
		ENDIF
		;スペシャル＋スペシャル＝失敗
		IF LOCAL:1 == 1 && RESULT:0 == 2
			LOCAL:1 = 0
			LOCAL:2 = 1
			GOTO ATTACK_MISS
		;成功＋成功＝失敗
		ELSEIF LOCAL:1 == 0 && RESULT:0 == 1
			LOCAL:2 = 1
			GOTO ATTACK_MISS
		;成功＋ファンブル＝大成功
		ELSEIF LOCAL:1 == 0 && RESULT:0 == -2
			LOCAL:1 = 1
		ENDIF
		$ATTACK_SUCCESS
		PRINTFORM  対抗結果：
		FONTBOLD
		SETCOLOR 0x66FFFF
		IF LOCAL:1
			PRINTFORMW 『大成功』
		ELSE
			PRINTFORMW 『成功』
		ENDIF
		RESETCOLOR
		FONTREGULAR
		PRINTL 
	ENDIF
ENDIF
RETURN 0, (LOCAL:1 == 1), (LOCAL:2 == 2)

;-------------------------------------------------
;コマンドを実行できるか判定の処理　-2：大失敗「逆凪」　-1：失敗　0：SKIP　1：成功　2：大成功
;-------------------------------------------------
;ARG＝行動の対象、ARG:1＝行動者、ARG:2＝コマンド、ARG:3＝出目/出力/抵抗、ARG:4＝難易度、ARG:5＝大失敗値
@COM_ORDER_BATTLE(ARG, ARG:1, ARG:2, ARG:3 = 0, ARG:4 = 6, ARG:5 = 0)
;何もしない、薀蓄語り、映姫様の説教
IF ARG:2 == 9 || ARG:2 == 445 || ARG:2 == 446
	PRINTL 
	RETURN 0, 0
ENDIF

;出目１
LOCAL:1 = 1 + RAND:6
;出目２
LOCAL:2 = 1 + RAND:6
;コマンドの難易度
LOCAL:3 = ARG:4
;大失敗値補正
LOCAL:4 = ARG:5
;テキスト用
LOCAL:5 = 0

;コマンド補正
;近寄る、距離をとる、背後に回る、見切り、防御
SIF ARG:3 == 1 && GROUPMATCH(ARG:2, 700, 701, 702, 703, 704)
	LOCAL:3 = MAX(LOCAL:3 - 1, 0)

LOCAL = LOCAL:1 + LOCAL:2
PRINTFORM  
PRINTFORM %SHOW_CALLNAME(ARG:1)%の
IF ARG:3 == 1
	PRINTFORM 出力：
ELSEIF ARG:3 == 2
	PRINTFORM 抵抗：
ELSE
	PRINTFORM 出目：
ENDIF
PRINTFORM 2D6 = 
PRINTFORM {LOCAL:1}
RESETCOLOR
PRINTFORM  + 
PRINTFORM {LOCAL:2}
RESETCOLOR
PRINTFORM  = 
;感情修正、初動ボーナス
IF ( ARG:1 == MASTER && ARG:3 != 2 && TFLAG:107 == 60 + EQUIP:戦闘中 * 10 ) || ( ASSI > 0 && ARG:1 == MASTER && (TALENT:ASSI:恋慕 || TALENT:ASSI:服従 || TALENT:ASSI:淫乱) )
	SIF !LOCAL:5
		PRINTFORM {LOCAL}
	PRINTFORM  + 
	SETCOLOR 0x66FFFF
	PRINTFORM 1
	RESETCOLOR
	LOCAL ++
	LOCAL:5 ++
ENDIF
;見切り
IF TEQUIP:(ARG:1):戦闘状態 == 4
	SIF !LOCAL:5
		PRINTFORM {LOCAL}
	PRINTFORM  + 
	SETCOLOR 0x66FFFF
	PRINTFORM 1
	RESETCOLOR
	LOCAL ++
	LOCAL:5 ++
;受け入れる、奉仕する、Ｖ挿入防御
ELSEIF ARG:3 == 2 && (TEQUIP:(ARG:1):戦闘状態 == 9 || TEQUIP:(ARG:1):戦闘状態 == 10 || TEQUIP:(ARG:1):戦闘状態 == 11)
	SIF !LOCAL:5
		PRINTFORM {LOCAL}
	PRINTFORM  - 
	SETCOLOR 255,0,0
	PRINTFORM 1
	RESETCOLOR
	LOCAL --
	LOCAL:5 ++
ENDIF
;TARGETの弱点
IF ARG:1 == TARGET && TARGET_WEAKNESS() > 0
	SIF !LOCAL:5
		PRINTFORM {LOCAL}
	PRINTFORM  - 
	SETCOLOR 255,0,0
	PRINTFORM {TARGET_WEAKNESS()}
	RESETCOLOR
	LOCAL -= TARGET_WEAKNESS()
	LOCAL:5 ++
ENDIF
SIF LOCAL:5
	PRINTFORM  = 
SIF LOCAL >= MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4+1)
	FONTBOLD
PRINTFORM {LOCAL}
FONTREGULAR
IF LOCAL > MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4)
	PRINTFORM 　>　
ELSEIF LOCAL == MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4)
	PRINTFORM 　=　
ELSE
	PRINTFORM 　<　
ENDIF
IF LOCAL < MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4+1)
	FONTBOLD
	;SIF LOCAL:3 <= 距離:(ARG:1)+LOCAL:4
	;	SETCOLOR 255,0,0
ENDIF
PRINTFORM {MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4)}
RESETCOLOR
FONTREGULAR

IF LOCAL >= MAX(LOCAL:3, 距離:(ARG:1)+LOCAL:4+1)
	;睡眠・拘束・時止め・失神（抵抗）
	IF ARG:1 == TARGET && ARG:3 == 2 && (EQUIP:睡眠薬 || TEQUIP:緊縛関連 || TEQUIP:時間止め || TFLAG:899 >= 1)
		FONTBOLD
		SETCOLOR 255,0,0
		PRINTFORML 　『強制失敗』
		RESETCOLOR
		FONTREGULAR
		RETURN -1, LOCAL
	ENDIF

	IF LOCAL:1 == 6 && LOCAL:2 == 6
		FONTBOLD
		SETCOLOR 0x66FFFF
		PRINTFORML 　『スペシャル』
		RESETCOLOR
		FONTREGULAR
		RETURN 2, LOCAL
	ELSE
		FONTBOLD
		SETCOLOR 0x66FFFF
		PRINTFORML 　『成功』
		RESETCOLOR
		FONTREGULAR
		RETURN 1, LOCAL
	ENDIF
ELSE
	;睡眠・拘束・時止め・失神（出力）
	IF ARG == TARGET && ARG:3 == 1 && (EQUIP:睡眠薬 || TEQUIP:緊縛関連 || TEQUIP:時間止め || TFLAG:899 >= 1)
		FONTBOLD
		SETCOLOR 0xFFA500
		PRINTFORML 　『判定無視』
		RESETCOLOR
		FONTREGULAR
		RETURN 1, LOCAL
	ENDIF
	;助手を犯す、助手のＡを犯す、助手避妊薬、コンドーム、コンドーム精飲(助)、助手を犯させる、助手のＡを犯させる、助手とキスする
	IF (ARG:1 == MASTER || ARG:1 == ASSI) && GROUPMATCH(ARG:2, 26, 36, 71, 290, 292, 307, 308, 309)
		FONTBOLD
		SETCOLOR 0x66FFFF
		PRINTFORML 　『強制成功』
		RESETCOLOR
		FONTREGULAR
		RETURN 1, LOCAL
	ENDIF
	
	IF LOCAL <= 距離:(ARG:1) + LOCAL:4
		IF (ARG:1 == MASTER || ARG:1 == ASSI) && ARG:3 != 2
			TFLAG:241 = LOCAL:4 + 距離:(ARG:1) * 2 + 1
			TFLAG:240 = LOCAL
		ENDIF
		FONTBOLD
		SETCOLOR 255,0,0
		PRINTFORML 　『ファンブル』
		RESETCOLOR
		FONTREGULAR
		RETURN -2, LOCAL
	ELSE
		IF (ARG:1 == MASTER || ARG:1 == ASSI) && ARG:3 != 2
			TFLAG:241 = 距離:(ARG:1) * 2 + 1
			TFLAG:240 = LOCAL
		ENDIF
		FONTBOLD
		SETCOLOR 255,0,0
		PRINTFORML 　『失敗』
		RESETCOLOR
		FONTREGULAR
		RETURN -1, LOCAL
	ENDIF
ENDIF

;TARGETの弱点
@TARGET_WEAKNESS
#FUNCTION
SIF TARGET <= 0
	RETURNF 0
LOCAL = 0
;放心（理性）
SIF BASE:理性 <= 5000
	LOCAL ++
;酩酊（酔い）
SIF (BASE:酔い * 100) / MAXBASE:8 >= 80
	LOCAL ++
;媚薬
SIF TEQUIP:媚薬
	LOCAL ++
;鈴蘭の毒
SIF TEQUIP:鈴蘭の毒
	LOCAL ++
;眼隠し
SIF TEQUIP:目装着器具
	LOCAL ++
;暗闇
SIF TEQUIP:暗闇
	LOCAL ++
;読心
SIF TEQUIP:読心
	LOCAL ++
;操りの糸
SIF TEQUIP:精神制御系
	LOCAL ++
;触手催眠
SIF TEQUIP:触手催眠
	LOCAL ++
SIF LOCAL > 2
	LOCAL /= 2
SIF LOCAL > 3
	LOCAL = 3
RETURNF LOCAL