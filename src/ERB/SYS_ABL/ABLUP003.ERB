;=============================================================================
;Ｂ感覚
;=============================================================================
@ABLUP3
DRAWLINE
SIF ABL:Ｂ感覚 >= 5
	JUMP ABLUP_MAX

PRINTFORM [0] - %PALAMNAME:3%の珠×{COST3(0)}……
CALL SHOW_ISABLUP_COMMON, CAN3(0)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0 && CAN3(0) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	JUEL:快Ｂ -= COST3(0)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:Ｂ感覚 += 1
PRINTFORMW %ABLNAME:3%のレベルが{ABL:Ｂ感覚}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP3
SIF ABL:Ｂ感覚 >= 5
	RETURN 0

CALLF CALC_COST3, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP3("CHECK")

@COST3(ARG)
#FUNCTION
RETURNF CALC_COST3("GET", ARG)

@CALC_COST3(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:Ｂ感覚 == 0
	LOCAL:0 = 1
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:0 = 20
ELSEIF ABL:Ｂ感覚 == 2
	LOCAL:0 = 400
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:0 = 5000
	;一線越えない
	SIF TALENT:一線越えない
		TIMES LOCAL:0 , 1.50
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:0 = 20000
	;一線越えない
	SIF TALENT:一線越えない
		TIMES LOCAL:0 , 2.50
ENDIF

;Ｂ敏感
IF TALENT:Ｂ敏感
	TIMES LOCAL:0 , 0.80
;Ｂ鈍感
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:0 , 1.20
ENDIF
;オトコ
IF TALENT:オトコ
	TIMES LOCAL:0 , 1.20
;絶壁
ELSEIF TALENT:絶壁
	TIMES LOCAL:0 , 0.80
;貧乳
ELSEIF TALENT:貧乳
	TIMES LOCAL:0 , 0.90
;巨乳
ELSEIF TALENT:巨乳
	TIMES LOCAL:0 , 1.10
;爆乳
ELSEIF TALENT:爆乳
	TIMES LOCAL:0 , 1.15
ENDIF
;淫乱
SIF TALENT:淫乱
	TIMES LOCAL:0 , 0.80

;淫乱無しかつ必要珠数が0以下の場合修正
SIF TALENT:淫乱 == 0 && LOCAL:0 < 1
	LOCAL:0 = 1

RETURNF 0

@CAN3(ARG)
#FUNCTION
RETURNF CAN_ABLUP3("GET", ARG)

@CAN_ABLUP3(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;快Ｂの珠で上げる
SIF JUEL:快Ｂ < COST3(0)
	LOCAL:0 |= 1

RETURNF (LOCAL:0 == 0)
