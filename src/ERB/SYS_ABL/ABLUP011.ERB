;=============================================================================
;欲望
;=============================================================================
;先導の珠でも上げられるように変更
@ABLUP11
DRAWLINE
SIF ABL:欲望 >= 5
	JUMP ABLUP_MAX

;ＬＶ４から５に上げるときは異常経験必要(素質：[快感に素直][淫乱]なら無視できる)
SIF COST11(2)
	PRINTFORML %EXPNAME:50%{COST11(2)}以上、かつ

PRINTFORM [0] - %PALAMNAME:13%の珠×{COST11(0)}……
CALL SHOW_ISABLUP_COMMON, CAN11(0)

;サドっ気が3以上だと先導の珠でも上昇させることができる
IF ABL:サドっ気 >= 3
	PRINTFORM [1] - %PALAMNAME:13%の珠×{COST11(1)}、%PALAMNAME:32%の珠×{COST11(0)}……
	CALL SHOW_ISABLUP_COMMON, CAN11(1)
ENDIF
PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF (RESULT == 0 || RESULT == 1) && CAN11(RESULT) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	JUEL:欲情 -= COST11(0)
ELSEIF RESULT == 1
	JUEL:欲情 -= COST11(1)
	JUEL:先導 -= COST11(0)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:欲望 += 1
PRINTFORMW %ABLNAME:11%のレベルが{ABL:欲望}になりました。
SIF ABL:欲望 >= 3 && (TALENT:抑圧 || TALENT:抵抗)
	CALL YOKUBO_UP_CHECK

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP11
SIF ABL:欲望 >= 5
	RETURN 0

CALLF CALC_COST11, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP11("CHECK")

@COST11(ARG)
#FUNCTION
RETURNF CALC_COST11("GET", ARG)

@CALC_COST11(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:欲望 == 0
	LOCAL:0 = 5
ELSEIF ABL:欲望 == 1
	LOCAL:0 = 50
ELSEIF ABL:欲望 == 2
	LOCAL:0 = 1000
ELSEIF ABL:欲望 == 3
	LOCAL:0 = 8000
	;一線越えない
	SIF TALENT:一線越えない
		TIMES LOCAL:0 , 2.00
ELSEIF ABL:欲望 == 4
	LOCAL:0 = 24000
	;一線越えない
	SIF TALENT:一線越えない
		TIMES LOCAL:0 , 3.00
ENDIF

;淫乱
SIF TALENT:淫乱
	TIMES LOCAL:0 , 0.80
;常時発情
SIF TALENT:常時発情
	TIMES LOCAL:0 , 0.80
;悪魔
SIF TALENT:悪魔
	TIMES LOCAL:0 , 0.80

LOCAL:1 = LOCAL:0 / 2

;異常経験数により軽減
IF !MARK:恐怖刻印 && !MARK:反発刻印
	FOR PC_VAR1, 0, 2
		LOCAL:PC_VAR1 = LOCAL:PC_VAR1 * (21 - LIMIT(EXP:異常経験/3, 1, 11)) / 20
	NEXT
ENDIF

;ＬＶ４から５に上げるときは異常経験必要(素質：[快感に素直][淫乱]なら無視できる)
SIF ABL:欲望 == 4 && TALENT:快感に素直 == 0 && TALENT:淫乱 == 0
	LOCAL:2 = 1

RETURNF 0

@CAN11(ARG)
#FUNCTION
RETURNF CAN_ABLUP11("GET", ARG)

@CAN_ABLUP11(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
IF EXP:異常経験 < COST11(2)
	LOCAL:0 |= 2
	LOCAL:1 |= 2
ENDIF

;欲情の珠で上げる
SIF JUEL:欲情 < COST11(0)
	LOCAL:0 |= 1

;サドっ気が3以上だと先導の珠でも上昇させることができる
IF ABL:サドっ気 >= 3
	;欲情の珠で上げる
	SIF JUEL:欲情 < COST11(1)
		LOCAL:1 |= 1
	;先導の珠で上げる
	SIF JUEL:先導 < COST11(0)
		LOCAL:1 |= 1
ELSE
	LOCAL:1 = 256
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0)
