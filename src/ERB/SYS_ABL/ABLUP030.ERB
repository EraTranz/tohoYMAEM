;=============================================================================
;自慰中毒
;=============================================================================
@ABLUP30
DRAWLINE
SIF ABL:自慰中毒 >= 5
	JUMP ABLUP_MAX

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[自慰しやすい][中毒しやすい][淫乱]なら無視できる）
SIF COST30(6)
	PRINTFORM %EXPNAME:50%{COST30(6)}以上、かつ
;Ｃ感覚と露出癖が必要
PRINTFORML %ABLNAME:0%{ABL:自慰中毒 + 1}LV以上、かつ%ABLNAME:14%{ABL:自慰中毒 + 1}LV以上、かつ

PRINTFORM [0] - %PALAMNAME:0%の珠×{COST30(0)}、%PALAMNAME:13%の珠×{COST30(2)}、%PALAMNAME:15%の珠×{COST30(3)}、%EXPNAME:10%{COST30(4)}以上……
CALL SHOW_ISABLUP_COMMON, CAN30(0)

PRINTFORM [1] - %PALAMNAME:0%の珠×{COST30(0)}、%PALAMNAME:13%の珠×{COST30(2)}、%PALAMNAME:15%の珠×{COST30(3)}、%EXPNAME:11%{COST30(5)}以上……
CALL SHOW_ISABLUP_COMMON, CAN30(1)

;以下快Ａの珠対応版
PRINTL または、

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[自慰しやすい][中毒しやすい][淫乱]なら無視できる）
SIF COST30(6)
	PRINTFORM %EXPNAME:50%{COST30(6)}以上、かつ
;Ａ感覚と露出癖が必要(Ｃ感覚よりは厳しめに、尻穴狂いがあればＣ感覚と同等)
PRINTFORML %ABLNAME:2%{COST30(7)}LV以上、かつ%ABLNAME:14%{ABL:自慰中毒 + 1}LV以上、かつ

PRINTFORM [2] - %PALAMNAME:2%の珠×{COST30(1)}、%PALAMNAME:13%の珠×{COST30(2)}、%PALAMNAME:15%の珠×{COST30(3)}、%EXPNAME:10%{COST30(4)}以上……
CALL SHOW_ISABLUP_COMMON, CAN30(2)

PRINTFORM [3] - %PALAMNAME:2%の珠×{COST30(1)}、%PALAMNAME:13%の珠×{COST30(2)}、%PALAMNAME:15%の珠×{COST30(3)}、%EXPNAME:11%{COST30(5)}以上……
CALL SHOW_ISABLUP_COMMON, CAN30(3)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF (RESULT >= 0 && RESULT <= 3) && CAN30(RESULT) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0 || RESULT == 1
	JUEL:快Ｃ -= COST30(0)
	JUEL:欲情 -= COST30(2)
	JUEL:恥情 -= COST30(3)
ELSEIF RESULT == 2 || RESULT == 3
	JUEL:快Ａ -= COST30(1)
	JUEL:欲情 -= COST30(2)
	JUEL:恥情 -= COST30(3)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:自慰中毒 += 1
PRINTFORMW %ABLNAME:30%のレベルが{ABL:自慰中毒}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP30
SIF ABL:自慰中毒 >= 5
	RETURN 0

CALLF CALC_COST30, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP30("CHECK")

@COST30(ARG)
#FUNCTION
RETURNF CALC_COST30("GET", ARG)

@CALC_COST30(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:自慰中毒 == 0
	LOCAL:0 = 10000
	LOCAL:1 = 10000
	LOCAL:2 = 3000
	LOCAL:3 = 1000
	LOCAL:4 = 100
	LOCAL:5 = 20
ELSEIF ABL:自慰中毒 == 1
	LOCAL:0 = 25000
	LOCAL:1 = 25000
	LOCAL:2 = 6000
	LOCAL:3 = 3000
	LOCAL:4 = 250
	LOCAL:5 = 40
ELSEIF ABL:自慰中毒 == 2
	LOCAL:0 = 50000
	LOCAL:1 = 50000
	LOCAL:2 = 12000
	LOCAL:3 = 6000
	LOCAL:4 = 500
	LOCAL:5 = 60
ELSEIF ABL:自慰中毒 == 3
	LOCAL:0 = 100000
	LOCAL:1 = 100000
	LOCAL:2 = 20000
	LOCAL:3 = 15000
	LOCAL:4 = 1000
	LOCAL:5 = 100
	;一線越えない
	IF TALENT:一線越えない
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
		TIMES LOCAL:2 , 2.00
		TIMES LOCAL:3 , 2.00
		TIMES LOCAL:4 , 2.00
		TIMES LOCAL:5 , 2.00
	ENDIF
ELSEIF ABL:自慰中毒 == 4
	LOCAL:0 = 300000
	LOCAL:1 = 300000
	LOCAL:2 = 40000
	LOCAL:3 = 40000
	LOCAL:4 = 2000
	LOCAL:5 = 160
	;一線越えない
	IF TALENT:一線越えない
		TIMES LOCAL:0 , 3.00
		TIMES LOCAL:1 , 3.00
		TIMES LOCAL:2 , 3.00
		TIMES LOCAL:3 , 3.00
		TIMES LOCAL:4 , 3.00
		TIMES LOCAL:5 , 3.00
	ENDIF
ENDIF

;自慰しやすい
IF TALENT:自慰しやすい
	TIMES LOCAL:0 , 0.25
	TIMES LOCAL:1 , 0.25
	TIMES LOCAL:2 , 0.25
	TIMES LOCAL:3 , 0.25
	TIMES LOCAL:4 , 0.25
ENDIF
;倒錯的
IF TALENT:倒錯的
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
	TIMES LOCAL:3 , 0.75
	TIMES LOCAL:4 , 0.75
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
	TIMES LOCAL:3 , 0.50
	TIMES LOCAL:4 , 0.50
	TIMES LOCAL:5 , 0.50
;中毒しにくい
ELSEIF TALENT:中毒しにくい
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:3 , 1.50
	TIMES LOCAL:4 , 1.50
	TIMES LOCAL:5 , 1.50
ENDIF
;[中毒扇動]有効
IF TFLAG:995 == 1
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
	TIMES LOCAL:3 , 0.80
	TIMES LOCAL:4 , 0.80
	TIMES LOCAL:5 , 0.80
ENDIF
;淫乱
IF TALENT:淫乱
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
	TIMES LOCAL:3 , 0.50
	TIMES LOCAL:4 , 0.50
ENDIF
;淫核／淫茎・淫尻
SIF TALENT:淫核／淫茎
	TIMES LOCAL:0 , 0.50
SIF TALENT:淫尻
	TIMES LOCAL:1 , 0.50

;異常経験数により軽減
IF !MARK:恐怖刻印 && !MARK:反発刻印
	FOR PC_VAR1, 0, 6
		LOCAL:PC_VAR1 = LOCAL:PC_VAR1 * (21 - LIMIT(EXP:異常経験/3, 1, 11)) / 20
	NEXT
ENDIF

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[自慰しやすい][中毒しやすい][淫乱]なら無視できる）
SIF ABL:自慰中毒 >= 2 && TALENT:自慰しやすい == 0 && TALENT:中毒しやすい == 0 && TALENT:淫乱 == 0
	LOCAL:6 = ABL:自慰中毒 - 1

;Ａ感覚
LOCAL:7 = ABL:自慰中毒 + 2 - TALENT:淫乱 - ((ABL:自慰中毒 + 2 - TALENT:淫尻) / 6)

RETURNF 0

@CAN30(ARG)
#FUNCTION
RETURNF CAN_ABLUP30("GET", ARG)

@CAN_ABLUP30(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
IF EXP:異常経験 < COST30(6)
	LOCAL:0 |= 2
	LOCAL:1 |= 2
	LOCAL:2 |= 2
	LOCAL:3 |= 2
ENDIF

;Ｃ感覚が必要
IF ABL:Ｃ感覚 < ABL:自慰中毒 + 1
	LOCAL:0 |= 4
	LOCAL:1 |= 4
ENDIF
;Ａ感覚が必要
IF ABL:Ａ感覚 < COST30(7)
	LOCAL:2 |= 4
	LOCAL:3 |= 4
ENDIF
;露出癖が必要
IF ABL:露出癖 < ABL:自慰中毒 + 1
	LOCAL:0 |= 4
	LOCAL:1 |= 4
	LOCAL:2 |= 4
	LOCAL:3 |= 4
ENDIF

;快Ｃの珠で上げる
IF JUEL:快Ｃ < COST30(0)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
ENDIF
;快Ａの珠で上げる
IF JUEL:快Ａ < COST30(1)
	LOCAL:2 |= 1
	LOCAL:3 |= 1
ENDIF
;欲情の珠で上げる
IF JUEL:欲情 < COST30(2)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
	LOCAL:2 |= 1
	LOCAL:3 |= 1
ENDIF
;恥情の珠で上げる
IF JUEL:恥情 < COST30(3)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
	LOCAL:2 |= 1
	LOCAL:3 |= 1
ENDIF

;自慰経験が必要
IF EXP:自慰経験 < COST30(4)
	LOCAL:0 |= 2
	LOCAL:2 |= 2
ENDIF
;調教自慰経験が必要
IF EXP:調教自慰経験 < COST30(5)
	LOCAL:1 |= 2
	LOCAL:3 |= 2
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0 || LOCAL:2 == 0 || LOCAL:3 == 0)
