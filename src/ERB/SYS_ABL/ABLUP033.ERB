;=============================================================================
;ＢＬ中毒
;=============================================================================
@ABLUP33
DRAWLINE
IF TALENT:オトコ == 0
	RETURN 0
ELSEIF ABL:ＢＬ中毒 >= 5
	JUMP ABLUP_MAX
ENDIF

;ＬＶ２以上に上げるときは異常経験必要（素質：[両刀][中毒しやすい]なら無視できる）
SIF COST33(3)
	PRINTFORM %EXPNAME:50%{COST33(3)}以上、かつ
;奉仕精神が必要
PRINTFORML %ABLNAME:13%{ABL:ＢＬ中毒 + 1}LV以上、かつ

PRINTFORM [0] - %PALAMNAME:0%の珠×{COST33(0)}、%PALAMNAME:13%の珠×{COST33(1)}、%PALAMNAME:14%の珠×{COST33(1)}、%EXPNAME:13%{COST33(2)}以上……
CALL SHOW_ISABLUP_COMMON, CAN33(0)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0 && CAN33(0) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	JUEL:快Ｃ -= COST33(0)
	JUEL:欲情 -= COST33(1)
	JUEL:屈服 -= COST33(1)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:ＢＬ中毒 += 1
PRINTFORMW %ABLNAME:33%のレベルが{ABL:ＢＬ中毒}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP33
SIF ABL:ＢＬ中毒 >= 5 || TALENT:オトコ == 0
	RETURN 0

CALLF CALC_COST33, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP33("CHECK")

@COST33(ARG)
#FUNCTION
RETURNF CALC_COST33("GET", ARG)

@CALC_COST33(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:ＢＬ中毒 == 0
	LOCAL:0 = 10000
	LOCAL:1 = 3000
	LOCAL:2 = 250
ELSEIF ABL:ＢＬ中毒 == 1
	LOCAL:0 = 25000
	LOCAL:1 = 8000
	LOCAL:2 = 550
ELSEIF ABL:ＢＬ中毒 == 2
	LOCAL:0 = 50000
	LOCAL:1 = 15000
	LOCAL:2 = 1000
ELSEIF ABL:ＢＬ中毒 == 3
	LOCAL:0 = 100000
	LOCAL:1 = 30000
	LOCAL:2 = 1500
ELSEIF ABL:ＢＬ中毒 == 4
	LOCAL:0 = 300000
	LOCAL:1 = 55000
	LOCAL:2 = 2100
ENDIF
;一線越えない
IF TALENT:一線越えない
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
	TIMES LOCAL:2 , 2.00
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
ENDIF
;両刀
IF TALENT:両刀
	TIMES LOCAL:0 , 0.25
	TIMES LOCAL:1 , 0.25
	TIMES LOCAL:2 , 0.25
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
;中毒しにくい
ELSEIF TALENT:中毒しにくい
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ENDIF
;[中毒扇動]有効
IF TFLAG:995 == 1
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
ENDIF

;異常経験数により軽減
IF !MARK:恐怖刻印 && !MARK:反発刻印
	FOR PC_VAR1, 0, 3
		LOCAL:PC_VAR1 = LOCAL:PC_VAR1 * (21 - LIMIT(EXP:異常経験/3, 1, 11)) / 20
	NEXT
ENDIF

;ＬＶ２以上に上げるときは異常経験必要（素質：[両刀][中毒しやすい]なら無視できる）
SIF ABL:ＢＬ中毒 >= 2 && TALENT:両刀 == 0 && TALENT:中毒しやすい == 0
	LOCAL:3 = ABL:ＢＬ中毒 - 1

RETURNF 0

@CAN33(ARG)
#FUNCTION
RETURNF CAN_ABLUP33("GET", ARG)

@CAN_ABLUP33(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
SIF EXP:異常経験 < COST33(3)
	LOCAL:0 |= 2

;奉仕精神が必要
SIF ABL:奉仕精神 < ABL:ＢＬ中毒 + 1
	LOCAL:0 |= 4

;快Ｃの珠で上げる
SIF JUEL:快Ｃ < COST33(0)
	LOCAL:0 |= 1
;欲情の珠で上げる
SIF JUEL:欲情 < COST33(1)
	LOCAL:0 |= 1
;屈服の珠で上げる
SIF JUEL:屈服 < COST33(1)
	LOCAL:0 |= 1
;ＢＬ経験が必要
SIF EXP:ＢＬ経験 < COST33(2)
	LOCAL:0 |= 2

RETURNF (LOCAL:0 == 0)
