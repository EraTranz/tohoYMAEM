;=============================================================================
;噴乳中毒
;=============================================================================
@ABLUP34
DRAWLINE
IF TALENT:オトコ
	RETURN 0
ELSEIF ABL:噴乳中毒 >= 5
	JUMP ABLUP_MAX
ENDIF

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要(素質：[中毒しやすい][Ｂ敏感][淫乱]なら無視できる)
SIF COST34(4)
	PRINTFORM %EXPNAME:50%{COST34(4)}以上、かつ
;Ｂ感覚が必要
PRINTFORML %ABLNAME:3%{ABL:噴乳中毒 + 1}LV以上、かつ

PRINTFORM [0] - %PALAMNAME:3%の珠×{COST34(0)}、%PALAMNAME:13%の珠×{COST34(1)}、%PALAMNAME:14%の珠×{COST34(2)}、%EXPNAME:5%{COST34(3)}以上……
CALL SHOW_ISABLUP_COMMON, CAN34(0)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0 && CAN34(0) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	JUEL:快Ｂ -= COST34(0)
	JUEL:欲情 -= COST34(1)
	JUEL:屈服 -= COST34(2)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:噴乳中毒 += 1
PRINTFORMW %ABLNAME:34%のレベルが{ABL:噴乳中毒}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP34
SIF ABL:噴乳中毒 >= 5 || TALENT:オトコ
	RETURN 0

CALLF CALC_COST34, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP34("CHECK")

@COST34(ARG)
#FUNCTION
RETURNF CALC_COST34("GET", ARG)

@CALC_COST34(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:噴乳中毒 == 0
	LOCAL:0 = 2500
	LOCAL:1 = 6000
	LOCAL:2 = 1000
	LOCAL:3 = 10
ELSEIF ABL:噴乳中毒 == 1
	LOCAL:0 = 5000
	LOCAL:1 = 14000
	LOCAL:2 = 2500
	LOCAL:3 = 25
ELSEIF ABL:噴乳中毒 == 2
	LOCAL:0 = 9000
	LOCAL:1 = 27000
	LOCAL:2 = 4800
	LOCAL:3 = 80
ELSEIF ABL:噴乳中毒 == 3
	LOCAL:0 = 17000
	LOCAL:1 = 65000
	LOCAL:2 = 10000
	LOCAL:3 = 150
	;一線越えない
	IF TALENT:一線越えない
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
		TIMES LOCAL:2 , 2.00
		TIMES LOCAL:3 , 2.00
	ENDIF
ELSEIF ABL:噴乳中毒 == 4
	LOCAL:0 = 30000
	LOCAL:1 = 130000
	LOCAL:2 = 25000
	LOCAL:3 = 300
	;一線越えない
	IF TALENT:一線越えない
		TIMES LOCAL:0 , 3.00
		TIMES LOCAL:1 , 3.00
		TIMES LOCAL:2 , 3.00
		TIMES LOCAL:3 , 3.00
	ENDIF
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
	TIMES LOCAL:3 , 0.75
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
	TIMES LOCAL:3 , 0.50
;中毒しにくい
ELSEIF TALENT:中毒しにくい
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:3 , 1.50
ENDIF
;[中毒扇動]有効
IF TFLAG:995 == 1
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:2 , 0.80
	TIMES LOCAL:3 , 0.80
ENDIF
;淫乱
IF TALENT:淫乱
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 0.50
	TIMES LOCAL:3 , 0.50
ENDIF

;Ｂ鈍感
IF TALENT:Ｂ鈍感
	TIMES LOCAL:0 , 1.20
;Ｂ敏感
ELSEIF TALENT:Ｂ敏感
	TIMES LOCAL:0 , 0.80
ENDIF
;絶壁
IF TALENT:絶壁
	TIMES LOCAL:0 , 0.50
;貧乳
ELSEIF TALENT:貧乳
	TIMES LOCAL:0 , 0.80
;巨乳
ELSEIF TALENT:巨乳
	TIMES LOCAL:0 , 1.10
;爆乳
ELSEIF TALENT:爆乳
	TIMES LOCAL:0 , 1.30
ENDIF

;異常経験数により軽減
IF !MARK:恐怖刻印 && !MARK:反発刻印
	FOR PC_VAR1, 0, 4
		LOCAL:PC_VAR1 = LOCAL:PC_VAR1 * (21 - LIMIT(EXP:異常経験/3, 1, 11)) / 20
	NEXT
ENDIF

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要(素質：[中毒しやすい][Ｂ敏感][淫乱]なら無視できる)
SIF ABL:噴乳中毒 >= 2 && TALENT:中毒しやすい == 0 && TALENT:Ｂ敏感 == 0 && TALENT:淫乱 == 0
	LOCAL:4 = ABL:噴乳中毒 - 1

RETURNF 0

@CAN34(ARG)
#FUNCTION
RETURNF CAN_ABLUP34("GET", ARG)

@CAN_ABLUP34(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;異常経験が必要な場合
SIF EXP:異常経験 < COST34(4)
	LOCAL:0 |= 2

;Ｂ感覚が必要
SIF ABL:Ｂ感覚 < ABL:噴乳中毒 + 1
	LOCAL:0 |= 4

;快Ｂの珠で上げる
SIF JUEL:快Ｂ < COST34(0)
	LOCAL:0 |= 1
;欲情の珠で上げる
SIF JUEL:欲情 < COST34(1)
	LOCAL:0 |= 1
;屈服の珠で上げる
SIF JUEL:屈服 < COST34(2)
	LOCAL:0 |= 1
;噴乳経験が必要
SIF EXP:噴乳経験 < COST34(3)
	LOCAL:0 |= 2

RETURNF (LOCAL:0 == 0)