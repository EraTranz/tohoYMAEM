;=============================================================================
;料理技能
;=============================================================================
@ABLUP70
SIF (FLAG:12 & 16) == 0
	RETURN 0
DRAWLINE
SIF ABL:料理技能 >= 5
	JUMP ABLUP_MAX

PRINTFORM [0] - %PALAMNAME:11%の珠×{COST70(0)}、%EXPNAME:70%{COST70(1)}以上……
CALL SHOW_ISABLUP_COMMON, CAN70(0)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0 && CAN70(0) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	JUEL:習得 -= COST70(0)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:料理技能 += 1
PRINTFORMW %ABLNAME:70%のレベルが{ABL:料理技能}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP70
SIF ABL:料理技能 >= 5
	RETURN 0

CALLF CALC_COST70, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP70("CHECK")

@COST70(ARG)
#FUNCTION
RETURNF CALC_COST70("GET", ARG)

@CALC_COST70(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:料理技能 == 0
	LOCAL:0 = 75
	LOCAL:1 = 1
ELSEIF ABL:料理技能 == 1
	LOCAL:0 = (EXP:調理経験 >= EXPLV:3) ? 250 # 500
	LOCAL:1 = 10
ELSEIF ABL:料理技能 == 2
	LOCAL:0 = (EXP:調理経験 >= EXPLV:4) ? 1500 # 3000
	LOCAL:1 = 25
ELSEIF ABL:料理技能 == 3
	LOCAL:0 = (EXP:調理経験 >= EXPLV:5) ? 4500 # 9000
	LOCAL:1 = 50
ELSEIF ABL:料理技能 == 4
	LOCAL:0 = (EXP:調理経験 >= EXPLV:5) ? 12000 # 24000
	LOCAL:1 = 100
ENDIF

;習得早い
IF TALENT:習得早い
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.75
;習得遅い
ELSEIF TALENT:習得遅い
	TIMES LOCAL:0 , 1.20
	TIMES LOCAL:1 , 1.10
ENDIF
;調合知識
IF TALENT:調合知識
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.90
ENDIF
;恋慕
IF TALENT:恋慕
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.80
ENDIF
;調理名人
IF TALENT:調理名人
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
;料理得意
ELSEIF TALENT:料理得意
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
;料理苦手
ELSEIF TALENT:料理苦手
	TIMES LOCAL:0 , 1.20
	TIMES LOCAL:1 , 1.20
ENDIF

;異常経験数により軽減
IF !MARK:恐怖刻印 && !MARK:反発刻印
	FOR PC_VAR1, 0, 2
		LOCAL:PC_VAR1 = LOCAL:PC_VAR1 * (21 - LIMIT(EXP:異常経験/3, 1, 11)) / 20
	NEXT
ENDIF

RETURNF 0

@CAN70(ARG)
#FUNCTION
RETURNF CAN_ABLUP70("GET", ARG)

@CAN_ABLUP70(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;習得の珠で上げる
SIF JUEL:習得 < COST70(0)
	LOCAL:0 |= 1
;調理経験が必要
SIF EXP:調理経験 < COST70(1)
	LOCAL:0 |= 2

RETURNF (LOCAL:0 == 0)
