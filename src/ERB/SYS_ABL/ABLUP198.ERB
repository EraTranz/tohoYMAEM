;=============================================================================
;触手使役
;=============================================================================
;基本主人専用能力。通常の能力上昇と違い、「触手の珠」、触手エネルギー、資金が必要
;また触手生贄人数(触手部屋に取る取り込み含む)が必要な場合もある
;「触手の珠」は奴隷の触手中毒LVを上昇させたり生贄に捧げたりすることで入手できる
@ABLUP198
DRAWLINE
SIF ABL:MASTER:触手使役 >= 5
	JUMP ABLUP_MAX
;技巧が必要
PRINTFORML %ABLNAME:12%{ABL:MASTER:触手使役 + 1}LV以上(現在:{ABL:MASTER:技巧}LV)、かつ

PRINTFORM [0] - %PALAMNAME:198%の珠×{COST198(0)}、触手エネルギー{COST198(1)}τ、触手生贄人数{COST198(2)}以上(現在:{FLAG:33}人)、資金{COST198(3)}圓以上……
CALL SHOW_ISABLUP_COMMON, CAN198(0)

PRINTFORM [1] - %PALAMNAME:198%の珠×{COST198(0)}、触手エネルギー{COST198(1)}τ、触手生贄人数累計{COST198(2) * 4}以上(現在:{FLAG:99}人)、資金{COST198(3)}圓以上……
CALL SHOW_ISABLUP_COMMON, CAN198(1)

PRINTL [100] - やめる
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF (RESULT == 0 || RESULT == 1) && CAN198(RESULT) != 0
	CLEARLINE 1
	REUSELASTLINE 条件を満たしていません。
	GOTO INPUT_LOOP
ELSEIF RESULT == 0 || RESULT == 1
	JUEL:MASTER:触手 -= COST198(0)
	BASE:MASTER:触手 -= COST198(1)
	MONEY -= COST198(3)
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

ABL:MASTER:触手使役 += 1
PRINTFORMW %CALLNAME:MASTER%の%ABLNAME:98%のレベルが{ABL:MASTER:触手使役}になりました。

;--------------------------------------------------
;レベルアップに必要な珠計算、レベルアップ予告処理
;--------------------------------------------------
@ISABLUP198
SIF ABL:MASTER:触手使役 >= 5
	RETURN 0

CALLF CALC_COST198, "CALC"

;条件別にＯＫかダメかを記録する
RETURN CAN_ABLUP198("CHECK")

@COST198(ARG)
#FUNCTION
RETURNF CALC_COST198("GET", ARG)

@CALC_COST198(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
;必要な条件の算出
VARSET LOCAL, 0

IF ABL:MASTER:触手使役 == 0
	LOCAL:0 = 500
	LOCAL:1 = 2000
	LOCAL:2 = 0
	LOCAL:3 = 7000
ELSEIF ABL:MASTER:触手使役 == 1
	LOCAL:0 = 2000
	LOCAL:1 = 5000
	LOCAL:2 = 1
	LOCAL:3 = 20000
ELSEIF ABL:MASTER:触手使役 == 2
	LOCAL:0 = 5000
	LOCAL:1 = 12000
	LOCAL:2 = 4
	LOCAL:3 = 80000
ELSEIF ABL:MASTER:触手使役 == 3
	LOCAL:0 = 15000
	LOCAL:1 = 25000
	LOCAL:2 = 20
	LOCAL:3 = 100000
	;一線越えない
	IF TALENT:MASTER:一線越えない
		LOCAL:0 *= 2
		LOCAL:1 *= 2
		LOCAL:2 *= 2
	ENDIF
ELSEIF ABL:MASTER:触手使役 == 4
	LOCAL:0 = 60000
	LOCAL:1 = 75000
	LOCAL:2 = 50
	LOCAL:3 = 250000
	;一線越えない
	IF TALENT:MASTER:一線越えない
		LOCAL:0 *= 3
		LOCAL:1 *= 3
		LOCAL:2 *= 3
	ENDIF
ENDIF

;臆病
IF TALENT:MASTER:臆病
	TIMES LOCAL:0 , 2.50
	TIMES LOCAL:1 , 2.50
	TIMES LOCAL:2 , 0.75
ENDIF
;倒錯的
IF TALENT:MASTER:倒錯的
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
	TIMES LOCAL:2 , 0.75
ENDIF
;中毒しやすい
IF TALENT:MASTER:中毒しやすい
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
;中毒しにくい
ELSEIF TALENT:MASTER:中毒しにくい
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
ENDIF
;魅力
IF TALENT:MASTER:魅力
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
ENDIF
;魅惑
IF TALENT:MASTER:魅惑
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
ENDIF
;謎の魅力
IF TALENT:MASTER:謎の魅力
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
ENDIF
;狂気
IF TALENT:MASTER:狂気
	TIMES LOCAL:0 , 0.25
	TIMES LOCAL:1 , 0.25
	TIMES LOCAL:2 , 1.50
ENDIF
;禁断の知識＋調合知識＋妖精知識
IF TALENT:MASTER:調合知識 && TALENT:MASTER:禁断の知識 && TALENT:MASTER:妖精知識
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
ENDIF

RETURNF 0

@CAN198(ARG)
#FUNCTION
RETURNF CAN_ABLUP198("GET", ARG)

@CAN_ABLUP198(ARGS, ARG)
#FUNCTION
SIF ARGS == "GET"
	RETURNF LOCAL:ARG
VARSET LOCAL, 0
;技巧が必要
IF ABL:MASTER:技巧 < ABL:MASTER:触手使役 + 1
	LOCAL:0 |= 2
	LOCAL:1 |= 2
ENDIF

;触手の珠で上げる
IF JUEL:MASTER:触手 < COST198(0)
	LOCAL:0 |= 1
	LOCAL:1 |= 1
ENDIF

;触手エネルギーが必要
IF BASE:MASTER:触手 < COST198(1)
	LOCAL:0 |= 16
	LOCAL:1 |= 16
ENDIF
;触手生贄が必要
SIF FLAG:33 < COST198(2)
	LOCAL:0 |= 32
;触手生贄(累計)が必要
SIF FLAG:99 < COST198(2) * 4
	LOCAL:1 |= 32
;お金が必要
IF MONEY < COST198(3)
	LOCAL:0 |= 64
	LOCAL:1 |= 64
ENDIF

RETURNF (LOCAL:0 == 0 || LOCAL:1 == 0)
