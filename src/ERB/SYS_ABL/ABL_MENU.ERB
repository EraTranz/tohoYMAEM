;=============================================================================
;能力アップ可否判定汎用処理
;=============================================================================
@SHOW_JUEL
CALLFORM SHOW_JUEL_{(FLAG:12 & 2) / 2 + 1}

@SHOW_ABLUP_SELECT
CALLFORM SHOW_ABLUP_SELECT_{(FLAG:12 & 2) / 2 + 1}

@ABL_AFTERTRAIN_MAIN
ライン数:2 = LINECOUNT
CALL NEW_DRAWLINE
PRINTFORML %SHOW_CALLNAME(TARGET)%のどの能力を上昇させますか？
CALL SHOW_JUEL
CALL SHOW_SPOILER_SELL
CALL SHOW_SPOILER_FALLTALENT
CALL SHOW_ABLUP_SELECT
CALL NEW_DRAWLINE
PRINT [100]一括レベルアップ(基礎系)
PRINT   
PRINTL [101]能力値アップの終了
$INPUT_LOOP1
INPUT
IF RESULT == 100
	CALL AUTO_ABLUP
ELSEIF RESULT == 101
	CALL JUJUN_UP_CHECK
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 101
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP1
ELSE
	ライン数:0 = RESULT
	ライン数:1 = EXIST_ABLUP(RESULT)
	IF ライン数:1 == -1
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP1
	ENDIF
	TRYCALLFORM ABLUP{ライン数:0}
ENDIF
ライン数:3 = LINECOUNT
CLEARLINE ライン数:3 - ライン数:2
RESTART

;-------------------------------------------------
;COUNT→ABL番号の変換関数（無効のABLについては-1が返る）
;-------------------------------------------------
@GET_ABL_NUMBER(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0 TO 3
		;Ｃ感覚(0)、Ｖ感覚(1)、Ａ感覚(2)、Ｂ感覚(3)
		RETURNF COUNT
	CASE 4 TO 8
		;従順(10)、欲望(11)、技巧(12)、奉仕精神(13)、露出癖(14)
		RETURNF COUNT + 6
	CASE 9, 10
		;サドっ気(20)、マゾっ気(21)
		RETURNF COUNT + 11
	CASE 11
		;レズっ気(22)、ＢＬっ気(23)
		RETURNF 22 + TALENT:オトコ
	CASE 12, 13
		;自慰中毒(30)、精液中毒(31)
		RETURNF COUNT + 18
	CASE 14
		;レズ中毒(32)、ＢＬ中毒(33)
		RETURNF 32 + TALENT:オトコ
	CASE 15
		;噴乳中毒
		SIF TALENT:オトコ == 0 && FLAG:15 & 8
			RETURNF 34
	CASE 16
		;触手中毒
		SIF FLAG:15 & 4096
			RETURNF 35
	CASE 17
		;排泄中毒
		SIF FLAG:15 & 4
			RETURNF 36
	CASE 18
		;射精中毒
		SIF TALENT:オトコ || TALENT:ふたなり
			RETURNF 37
	CASE 19
		;獣姦中毒
		SIF FLAG:15 & 8192
			RETURNF 38
	CASE 20
		;料理技能
		SIF FLAG:12 & 16
			RETURNF 70
	CASE 21
		;撮影技能
		SIF FLAG:15 & 128 && (ITEM:ビデオカメラ || ITEM:カメラ)
			RETURNF 71
	CASE 22
		;歌唱技能
		SIF FLAG:12 & 16 && ITEM:マイク
			RETURNF 72
	CASE 23
		;工作技能
			RETURNF 73
ENDSELECT
RETURNF -1

;-------------------------------------------------
;能力可否判定処理
;-------------------------------------------------
@CHECK_ISABLUP, ARG
RESULT = -1
SIF ARG != -1
	CALLFORM ISABLUP{ARG}
RETURN RESULT

;-------------------------------------------------
;能力上昇可否の表示汎用処理
;-------------------------------------------------
@SHOW_ISABLUP_COMMON, ARG
IF ARG == 0
	PRINT ＯＫ
ELSE
	SIF ARG & 1
		PRINT 「宝珠」
	SIF ARG & 2
		PRINT 「経験」
	SIF ARG & 4
		PRINT 「能力」
	SIF ARG & 8
		PRINT 「刻印」
	SIF ARG & 16
		PRINT 「τ」
	SIF ARG & 32
		PRINT 「生贄」
	SIF ARG & 64
		PRINT 「資金」
	PRINT 不足
ENDIF
PRINTL 

;-------------------------------------------------
;限界による能力上昇不可の表示汎用処理
;-------------------------------------------------
@ABLUP_MAX
PRINTW 既にMAXです。

;=============================================================================
;能力アップ可否判定(壱型表示)
;=============================================================================
@SHOW_JUEL_1
DRAWLINE
CALL SHOW_INFO_EXP, TARGET
CALL SHOW_INFO_JUEL, TARGET

@SHOW_ABLUP_SELECT_1

LOCALS:0 = \@(TALENT:MASTER:野菜の錬金術師) ? 飢饉 # ×\@
LOCALS:1 = \@(TALENT:MASTER:野菜の錬金術師) ? 豊作 # ○\@
LOCALS:2 = \@(TALENT:MASTER:野菜の錬金術師) ? 休耕 # ☆\@
REPEAT 24
	LOCAL = GET_ABL_NUMBER(COUNT)
	IF LOCAL != -1
		CALL CHECK_ISABLUP, LOCAL

		;能力の数値が5以上なら表示しない
		IF ABL:LOCAL >= 5
			PRINTFORMLC  [{LOCAL, 2}]%ABLNAME:LOCAL, 8, LEFT%-LV{ABL:LOCAL}　%LOCALS:2%
		ELSE
			PRINTFORMLC  [{LOCAL, 2}]%ABLNAME:LOCAL, 8, LEFT%-LV{ABL:LOCAL}　%LOCALS:RESULT%
		ENDIF
	ENDIF
REND
CALL ISABLUP93
IF MARK:薬物刻印 >= 3
	PRINTFORMLC  [93]%MARKNAME:3%-LV{MARK:薬物刻印}　%LOCALS:2%
ELSE
	PRINTFORMLC  [93]%MARKNAME:3%-LV{MARK:薬物刻印}　%LOCALS:RESULT%
ENDIF
REPEAT 2
	CALLFORM ISABLUP{98 + COUNT}
	IF MARK:(8 + COUNT) == 0
		PRINTFORMLC  [{98 + COUNT}]%MARKNAME:(8 + COUNT)%-LV{MARK:(8 + COUNT)}　%LOCALS:2%
	ELSE
		PRINTFORMLC  [{98 + COUNT}]%MARKNAME:(8 + COUNT)%-LV{MARK:(8 + COUNT)}　%LOCALS:RESULT%
	ENDIF
REND
PRINTL 
;=============================================================================
;能力アップ可否判定(弐型表示)
;=============================================================================
@SHOW_JUEL_2
CUSTOMDRAWLINE ･
CALL NEW_PRINT_EXP, TARGET
CALL NEW_PRINT_JUEL, TARGET

@SHOW_ABLUP_SELECT_2
LOCAL:1 = 0
REPEAT 24
    LOCAL = GET_ABL_NUMBER(COUNT)
	SIF LOCAL < 0
		CONTINUE
	
	PRINTFORM [{LOCAL, 2}] %ABLNAME:LOCAL, 8, LEFT%:LV{ABL:LOCAL}  
	CALL PRINT_COLORBAR, ABL:LOCAL, 5, 5, UNICODE(0x2584), UNICODE(0x2584), 0x7070C0, 0x202050
	LOCAL = GET_ABL_NUMBER(COUNT)
	CALL CHECK_ISABLUP, LOCAL
	IF ABL:LOCAL >= 5
	SETCOLOR COLOR("ORANGE")
		PRINT 　　☆　
	ELSE
    IF RESULT == 1
	SETCOLOR COLOR("aqua")
	PRINT　　◇OK◇
	ELSE
	SETCOLOR COLOR("light-gray")
	PRINT　　―NG―
	ENDIF
	ENDIF
	RESETCOLOR
	;改行判定
	LOCAL:1 += 1
	IF (LOCAL:1)%2 == 0
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
REND
IF MARK:薬物刻印 < 3
	CALL ISABLUP93
	PRINTFORM [93] %MARKNAME:3%:LV{MARK:薬物刻印}  
	CALL PRINT_COLORBAR, MARK:薬物刻印, 3, 3, UNICODE(0x2584), UNICODE(0x2584), 0x7070C0, 0x202050
	CALL ISABLUP93
	IF RESULT == 1
	SETCOLOR COLOR("aqua")
	PRINT　　　◇OK◇
	ELSE
	SETCOLOR COLOR("light-gray")
	PRINT　　　―NG―
	ENDIF
	RESETCOLOR
	;改行判定
	LOCAL:1 += 1
	IF (LOCAL:1)%2 == 0
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
ENDIF
REPEAT 2
	IF MARK:(8 + COUNT)
		CALLFORM ISABLUP{98 + COUNT}
		PRINTFORM [{98 + COUNT}] %MARKNAME:(8 + COUNT)%:LV{MARK:(8 + COUNT)}  
		CALL PRINT_COLORBAR, MARK:(8 + COUNT), 3, 3, UNICODE(0x2584), UNICODE(0x2584), 0x7070C0, 0x202050
		CALLFORM ISABLUP{98 + COUNT}
	IF RESULT == 1
	SETCOLOR COLOR("aqua")
	PRINT　　　◇OK◇
	ELSE
	SETCOLOR COLOR("light-gray")
	PRINT　　　―NG―
	ENDIF
	RESETCOLOR
		;改行判定
		LOCAL:1 += 1
		IF (LOCAL:1)%2 == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	ENDIF
REND
;最後のループで改行してなかったら、ここで改行しておく
SIF LOCAL:1%2 != 0
	PRINTL 

;=============================================================================
;手動能力アップ
;=============================================================================
;-------------------------------------------------
;能力上昇キャラ選択処理
;-------------------------------------------------
@ABL_MANUAL_MENU
;ターゲット退避(夜イベと共通。TFLAG:210)
TFLAG:210 = TARGET
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
CALLF CLEAR_LIST
REPEAT CHARANUM
	MARK:COUNT:98 = 0
	;主人は除外
	SIF COUNT == MASTER
		CONTINUE
	;失踪中などこの場にいないなら除外
	SIF CFLAG:COUNT:4
		CONTINUE
	;死亡中などこの場にいないなら除外
	SIF TALENT:COUNT:死亡
		CONTINUE

	CALLF SET_LIST, LOCAL:2, COUNT
	LOCAL:2 += 1
	MARK:COUNT:98 = 1
REND
LOCAL:1 = (LOCAL:2 - 1) / 20

$PRINT_LIST
ライン数:2 = LINECOUNT
DRAWLINE
PRINTFORML 誰の能力を上昇させますか？ ＜page.{LOCAL+1}＞
DRAWLINE
;………………………………………………
;能力上昇キャラの表示処理
;………………………………………………
;主人は能力上昇出来ないことに
REPEAT 20
	SIF COUNT + LOCAL * 20 >= CHARANUM
		BREAK
	LOCAL:3 = GET_LIST(COUNT + LOCAL * 20)
	SIF LOCAL:3 == 0
		CONTINUE

	PRINT 　
	CALL ARRANGE_CHARALIST, LOCAL:3
	CALL ARRANGE_CHARALIFE, LOCAL:3
	PRINTL 
REND
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1111]前のページ\@
PRINTLC [5555]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [9999]次のページ\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 5555
	TARGET = TFLAG:210
	RETURN 0
ELSEIF RESULT == 1111 && LOCAL > 0
	LOCAL -= 1
	ライン数:3 = LINECOUNT
	CLEARLINE ライン数:3 - ライン数:2
	GOTO PRINT_LIST
ELSEIF RESULT == 9999 && LOCAL < LOCAL:1
	LOCAL += 1
	ライン数:3 = LINECOUNT
	CLEARLINE ライン数:3 - ライン数:2
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MARK:RESULT:98 == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

TARGET = RESULT
CALL ABL_MANUAL_MAIN
GOTO PRINT_LIST

;-------------------------------------------------
;上昇させる能力選択の処理
;-------------------------------------------------
@ABL_MANUAL_MAIN
#LOCALSIZE 3
ライン数:2 = LINECOUNT
;《否定の珠》の打ち消しチェック
SIF JUEL:否定 > 0
	CALL DENIAL_CHECK

;前後のキャラを表示できるか判定
LOCAL:1 = -1
LOCAL:2 = -1
FOR LOCAL, 0, CHARANUM
	SIF MARK:LOCAL:98 == 0 || LOCAL == TARGET
		CONTINUE
	IF LOCAL < TARGET
		LOCAL:1 = LOCAL
	ELSE
		LOCAL:2 = LOCAL
		BREAK
	ENDIF
NEXT

CALL NEW_DRAWLINE
PRINTFORML %SHOW_CALLNAME(TARGET)%のどの能力を上昇させますか？
CALL SHOW_JUEL
CALL SHOW_SPOILER_SELL
CALL SHOW_SPOILER_FALLTALENT
CALL SHOW_ABLUP_SELECT
CALL NEW_DRAWLINE
PRINTFORMLC %" " * 16%
PRINTLC [5550]一括レベルアップ(基礎系)
PRINTL 
PRINTFORMLC \@(LOCAL:1 < 0) ? %" " * 16% # [1111]前のキャラ\@
PRINTLC [5555]能力値アップの終了
PRINTFORMLC \@(LOCAL:2 < 0) ? %" " * 16% # [9999]次のキャラ\@
PRINTL 
$INPUT_LOOP1
INPUT
IF RESULT == 5550
	CALL AUTO_ABLUP
ELSEIF RESULT == 5555
	RETURN 0
ELSEIF RESULT == 1111 && LOCAL:1 >= 0
	TARGET = LOCAL:1
ELSEIF RESULT == 9999 && LOCAL:2 >= 0
	TARGET = LOCAL:2
ELSEIF RESULT < 0 || RESULT > 100
	REUSELASTLINE
	CLEARLINE 1
	GOTO INPUT_LOOP1
ELSE
	ライン数:0 = RESULT
	ライン数:1 = EXIST_ABLUP(RESULT)
	IF ライン数:1 == -1
		REUSELASTLINE 
		CLEARLINE 1
		GOTO INPUT_LOOP1
	ENDIF
	TRYCALLFORM ABLUP{RESULT}
ENDIF
ライン数:3 = LINECOUNT
CLEARLINE ライン数:3 - ライン数:2
RESTART

;-------------------------------------------------
;COUNT→ABL番号の変換関数（無効のABLについては-1が返る）
;-------------------------------------------------
@EXIST_ABLUP(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0 TO 3
		;Ｃ感覚(0)、Ｖ感覚(1)、Ａ感覚(2)、Ｂ感覚(3)
		RETURNF ARG
	CASE 10 TO 14
		;従順(10)、欲望(11)、技巧(12)、奉仕精神(13)、露出癖(14)
		RETURNF ARG
	CASE 20, 21
		;サドっ気(20)、マゾっ気(21)
		RETURNF ARG
	CASE 22, 32
		;レズっ気(22)、レズ中毒(32)
		SIF TALENT:オトコ == 0
			RETURNF ARG
	CASE 23, 33
		;ＢＬっ気(23)、ＢＬ中毒(33)
		SIF TALENT:オトコ
			RETURNF ARG
	CASE 30, 31
		;自慰中毒(30)、精液中毒(31)
		RETURNF ARG
	CASE 34
		;噴乳中毒(34)
		SIF TALENT:オトコ == 0 && FLAG:15 & 8
			RETURNF 34
	CASE 35
		;触手中毒
		SIF FLAG:15 & 4096
			RETURNF 35
	CASE 36
		;排泄中毒
		SIF FLAG:15 & 4
			RETURNF 36
	CASE 37
		;射精中毒
		SIF TALENT:オトコ || TALENT:ふたなり
			RETURNF 37
	CASE 38
		;獣姦中毒
		SIF FLAG:15 & 8192
			RETURNF 38
	CASE 70
		;料理技能
		SIF FLAG:12 & 16
			RETURNF 70
	CASE 71
		;撮影技能
		SIF FLAG:15 & 128 && (ITEM:ビデオカメラ || ITEM:カメラ)
			RETURNF 71
	CASE 72
		;歌唱技能
		SIF FLAG:12 & 16 && ITEM:マイク
			RETURNF 72
	CASE 73
		;工作技能
			RETURNF 73
	CASE 93, 98, 99
		RETURNF ARG
ENDSELECT
RETURNF -1

