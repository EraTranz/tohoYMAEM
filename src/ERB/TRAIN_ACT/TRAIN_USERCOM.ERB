;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;調教コマンドカスタムメニュー関連
;
;能力表示や調教者交代などの調教コマンド以外のコマンドを表示・実行するものが中心
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;=============================================================================
;カスタムメニュー表示処理
;=============================================================================
@SHOW_USERCOM
;調教メニュー実行中はコマンド表示を省略する
IF TFLAG:224 != 555
	DRAWLINE
	PRINTC 能力表示(奴隷)[800]
	PRINTC 能力表示(調教者)[801]
	IF FLAG:15 & 4096 && TEQUIP:触手調教
		PRINTC 能力表示(触手)[802]
	ELSEIF ASSI >= 0 && TEQUIP:コスチュームプレイ == 0 && TEQUIP:触手調教 == 0 && TEQUIP:ASSI:コスチュームプレイ == 0 
		PRINTFORMC \@(ASSIPLAY) ? 主人 # 助手\@と交代[810]
	ENDIF
	PRINTC 汚れ表示[811]
	IF TALENT:MASTER:オトコ == 0 && (CFLAG:MASTER:42 & 1) == 0 && (FLAG:14 & 128) == 0
		;[具現]を持つ
		IF TALENT:MASTER:ふたなり == 0 && TALENT:MASTER:具現
			PRINTC 具現化(主人)[820]
		;具現による[ふたなり]を持つ
		ELSEIF TALENT:MASTER:ふたなり && TALENT:MASTER:顕在化 & 2
			PRINTC 具現化解除(主人)[820]
		ENDIF
	ENDIF
	IF ASSI >= 0 && TALENT:ASSI:オトコ == 0 && (ABL:ASSI:欲望 >= 5 || ABL:ASSI:レズっ気 >= 4) && (CFLAG:ASSI:42 & 1) == 0 && (FLAG:14 & 128) == 0
		;[具現]を持ち、欲望LV5かレズっ気LV4以上
		IF TALENT:ASSI:ふたなり == 0 && TALENT:ASSI:具現
			PRINTC 具現化(助手)[821]
		;具現による[ふたなり]を持ち、欲望LV5かレズっ気LV4以上
		ELSEIF TALENT:ASSI:ふたなり && TALENT:ASSI:顕在化 & 2
			PRINTC 具現化解除(助手)[821]
		ENDIF
	ENDIF
	SIF TALENT:PLAYER:野菜の錬金術師 && TEQUIP:YASAIモードで使用 < 1 && FLAG:574 >= 10000
		PRINTC Exモード化[830]
	SIF TALENT:PLAYER:野菜の錬金術師 && TEQUIP:YASAIモードで使用 < 2 && FLAG:574 >= 1000000; NO:PLAYER == 5000 &&
		PRINTC Phモード化[831]
	PRINTC 表示形式変更[876]
	PRINTC コマンド拡張[888]
	PRINTC コマンドフィルタ[889]
	IF FLAG:14 & 1
		;別名：博麗の貞操を守るためのフィルター
		PRINTFORMC Ｖ系調教フィルタ:\@(FLAG:2 & 1) ? 切 # 入\@[890]
		;別名：厄い調教コマンド防止フィルター
		PRINTFORMC Ａ系調教フィルタ:\@(FLAG:2 & 2) ? 切 # 入\@[891]
		;別名：紳士的なコマンド限定フィルター
		PRINTFORMC 異常調教フィルタ:\@(FLAG:2 & 4) ? 切 # 入\@[892]
	ENDIF
	SIF FLAG:11 & 1
		PRINTC 自動射精箇所変更[893]
	;[遠隔精密制御]を持っていると寄生中に回復が可能
	SIF FLAG:15 & 4096 && FLAG:75 & 512 && TALENT:寄生 && BASE:触手
		PRINTC 寄生触手栄養放出[900]
	;変幻自在を持っていると縮小化・巨大化が可能
	;調教対象が変幻自在のときは従順が3以上かつ何らかの道具を身につけてなくて、緊縛中や乗馬中や特殊風呂入浴中でないことが条件
	SIF CHANGE_SIZE_ABLE_SLAVE() == 1
		PRINTC 巨大/縮小化(奴隷)[901]
	;主人はコンドームをつけていたら体格変化できない。
	SIF CHANGE_SIZE_ABLE_MASTER() == 1
		PRINTC 巨大/縮小化(主人)[902]
	;助手コスプレ中もしくはコンドーム装着中は体格変化できない。
	SIF ASSI >= 0 && CHANGE_SIZE_ABLE_ASSI() == 1
		PRINTC 巨大/縮小化(助手)[903]
	;雌雄可変を持っていると性別変化が可能
	;調教対象が雌雄可変のときは従順が3以上かつ何らかの道具を身につけてなくて、緊縛中や乗馬中や特殊風呂入浴中でなくて、触手召還もしていないことが条件
	SIF TALENT:雌雄可変 && TFLAG:899 <= 0 && EQUIP:睡眠薬 == 0 && ABL:従順 > 2 && TEQUIP:Ｖ系装着器具 == 0 && TEQUIP:クスコ == 0 && TEQUIP:Ａ系装着器具 == 0 && TEQUIP:氷柱 == 0 && TEQUIP:Ｃ系装着器具 == 0 && TEQUIP:コンドーム == 0 && TEQUIP:Ｕ系装着器具 == 0 && TEQUIP:Ｂ系装着器具 == 0 && TEQUIP:サラシ == 0 && TEQUIP:緊縛関連 == 0 && TEQUIP:目装着器具 == 0 && TEQUIP:口装着器具 == 0 && TEQUIP:三角木馬乗馬中 == 0 && TEQUIP:鼻装着器具 == 0 && TEQUIP:耳装着器具 == 0 && TEQUIP:特殊風呂入浴中 == 0 && TEQUIP:オムツプレイ == 0 && TEQUIP:コスチュームプレイ == 0 && TEQUIP:射精止め == 0 && TEQUIP:触手調教 == 0
		PRINTC 性別変更(奴隷)[911]
	;主人はコンドームをつけていたら雌雄可変できない。
	SIF TALENT:MASTER:雌雄可変 && TEQUIP:MASTER:コンドーム == 0
		PRINTC 性別変更(主人)[912]
	;助手コスプレ中もしくはコンドーム装着中は雌雄可変できない。
	SIF ASSI >= 0 && TALENT:ASSI:雌雄可変 && TEQUIP:ASSI:コンドーム == 0 && TEQUIP:ASSI:コスチュームプレイ == 0
		PRINTC 性別変更(助手)[913]
	IF FLAG:14 & 64
		PRINTC 調教メニュー登録[990]
		IF FLAG:550 > 0
			PRINTC 調教メニュー表示[991]
			PRINTC 調教メニュー実行[992]
		ENDIF
	ENDIF
	SIF ASSI >= 0 && ASSIPLAY 
		PRINTC 助手に選ばせる[994]
	PRINTC 東方調教典[997]
	SIF TALENT:MASTER:998
		PRINTC デバッグメニュー[998]
	SIF TEQUIP:時間止め == 0
		PRINTC 調教終了[999]
ENDIF
PRINTL 
IF PREVCOM >= 0
	PRINTFORM ＜前回調教コマンド:%TSTR:0%
	IF TFLAG:958 || TFLAG:959 || TFLAG:960 || TFLAG:961
		PRINT [
		;絶頂
	    IF TFLAG:961
			SETCOLOR COLOR("YELLOW")
	    	PRINTFORM %TSTR:89%
	    	RESETCOLOR
		ENDIF
		;射精
	    IF TFLAG:960
			SIF TFLAG:961
				PRINT  
			SETCOLOR COLOR("AQUA2")
	    	PRINTFORM %TSTR:90%
	    	RESETCOLOR
		ENDIF
		;噴乳
		IF TFLAG:959
			SIF TFLAG:960 || TFLAG:961
				PRINT  
			SETCOLOR COLOR("WHITE")
	    	PRINTFORM %TSTR:91%
	    	RESETCOLOR
		ENDIF
		;放尿
		IF TFLAG:958
			SIF TFLAG:959 || TFLAG:960 || TFLAG:961
				PRINT  
			SETCOLOR 0xC0C070
	    	PRINTFORM %TSTR:92%
	    	RESETCOLOR
		ENDIF
		PRINT ]
	ENDIF
	SIF TFLAG:133
	PRINT (助手)
	PRINTL ＞
ENDIF
;=============================================================================
;カスタムメニュー入力・実行処理
;=============================================================================
@USERCOM
IF RESULT == 800 || RESULT == 801
	LOCAL = (RESULT == 800) ? TARGET # PLAYER
	CALL SHOW_INFO_TRAIN_MAIN, LOCAL
	RETURN 1
ELSEIF RESULT == 802 && FLAG:15 & 4096 && TEQUIP:触手調教
	SIF FLAG:48 > 0
		PRINT 紳士的な
	PRINTL 触手
	CALL SHOW_INFO_TENTACLE
	WAIT
	RETURN 1
ELSEIF RESULT == 810 && ASSI >= 0 && TEQUIP:コスチュームプレイ == 0 && TEQUIP:触手調教 == 0 && TEQUIP:ASSI:コスチュームプレイ == 0
	CALL DISPLAY_KOJO_MESSAGE_CHANGEASSI
	CALL DISPLAY_KOJO_MESSAGE_PLAYERCHANGE
	PLAYER = (ASSIPLAY) ? MASTER # ASSI
	ASSIPLAY = (ASSIPLAY) ? 0 # 1
	PRINTFORMW %CALLNAME:PLAYER%と交代しました
	;時間経過処理
	SIF !EQUIP:戦闘中
		CALL ADD_TRAINING_TIME, 2
	RETURN 1
ELSEIF RESULT == 811
	CALL STAIN_INFO
	RETURN 1
ELSEIF RESULT == 820 && (CFLAG:MASTER:42 & 1) == 0 && TALENT:MASTER:オトコ == 0 && ((TALENT:MASTER:ふたなり == 0 && TALENT:MASTER:具現) || (TALENT:MASTER:ふたなり && TALENT:MASTER:顕在化 & 2))
	CALL COMMAND_TRAIN_F,MASTER
	RETURN 1
ELSEIF RESULT == 821 && ASSI >= 0 && (ABL:ASSI:欲望 >= 5 || ABL:ASSI:レズっ気 >= 4) && (CFLAG:ASSI:42 & 1) == 0 && TALENT:ASSI:オトコ == 0 && ((TALENT:ASSI:ふたなり == 0 && TALENT:ASSI:具現) || (TALENT:ASSI:ふたなり && TALENT:ASSI:顕在化 & 2))
	CALL COMMAND_TRAIN_F,ASSI
	RETURN 1
ELSEIF RESULT == 830 && TALENT:PLAYER:野菜の錬金術師 && TEQUIP:YASAIモードで使用 < 1 && FLAG:574 >= 10000
	CALL IRC_ANATA_EXMODE
	RETURN 1
ELSEIF RESULT == 831 && TALENT:PLAYER:野菜の錬金術師 && TEQUIP:YASAIモードで使用 < 2 && FLAG:574 >= 1000000; && NO:PLAYER == 5000
	CALL IRC_ANATA_PHMODE
	RETURN 1
ELSEIF RESULT == 876
	PRINTFORMW パラメータを\@(FLAG:12 & 2) ? 弐 # 暮\@型形式で表示するようにしました。
	FLAG:12 ^= 2
	RETURN 1
ELSEIF RESULT == 888
	PRINTL コマンド拡張の設定を変更します
	CALL CONFIGURATION_5
	RETURN 1
ELSEIF RESULT == 889
	PRINTL コマンドフィルタの個別設定を変更します
	CALL CUSTOM_COMMAND_FILTER
	RETURN 1
ELSEIF RESULT == 890 && FLAG:14 & 1
	PRINTFORMW Ｖ系調教フィルタを\@(FLAG:2 & 1) ? オフ # オン\@にしました。
	FLAG:2 ^= 1
	RETURN 1
ELSEIF RESULT == 891 && FLAG:14 & 1
	PRINTFORMW Ａ系調教フィルタを\@(FLAG:2 & 2) ? オフ # オン\@にしました。
	FLAG:2 ^= 2
	RETURN 1
ELSEIF RESULT == 892 && FLAG:14 & 1
	PRINTFORMW 異常調教フィルタを\@(FLAG:2 & 4) ? オフ # オン\@にしました。
	FLAG:2 ^= 4
	RETURN 1
ELSEIF RESULT == 893 && FLAG:11 & 1
	CALL CHANGE_SAMEN_PLACE
	RETURN 1
ELSEIF RESULT == 900 && FLAG:15 & 4096 && FLAG:75 & 512 && TALENT:寄生
	CALL TENTACLE_HEAL
	RETURN 1
ELSEIF RESULT == 901 && CHANGE_SIZE_ABLE_SLAVE() == 1
	CALL CHANGE_SIZE_SLAVE
	RETURN 1
ELSEIF RESULT == 902 && CHANGE_SIZE_ABLE_MASTER() == 1
	CALL CHANGE_SIZE_MASTER
	RETURN 1
ELSEIF RESULT == 903 && CHANGE_SIZE_ABLE_ASSI() == 1
	CALL CHANGE_SIZE_ASSI
	RETURN 1
ELSEIF RESULT == 911 && TALENT:雌雄可変 && TFLAG:899 <= 0 && EQUIP:睡眠薬 == 0 && ABL:従順 > 2 && TEQUIP:Ｖ系装着器具 == 0 && TEQUIP:クスコ == 0 && TEQUIP:Ａ系装着器具 == 0 && TEQUIP:氷柱 == 0 && TEQUIP:Ｃ系装着器具 == 0 && TEQUIP:コンドーム == 0 && TEQUIP:Ｕ系装着器具 == 0 && TEQUIP:Ｂ系装着器具 == 0 && TEQUIP:サラシ == 0 && TEQUIP:緊縛関連 == 0 && TEQUIP:目装着器具 == 0 && TEQUIP:口装着器具 == 0 && TEQUIP:三角木馬乗馬中 == 0 && TEQUIP:鼻装着器具 == 0 && TEQUIP:耳装着器具 == 0 && TEQUIP:特殊風呂入浴中 == 0 && TEQUIP:オムツプレイ == 0 && TEQUIP:コスチュームプレイ == 0 && TEQUIP:触手調教 == 0
	CALL CHANGE_SEX, TARGET
	RETURN 1
ELSEIF RESULT == 912 && TALENT:MASTER:雌雄可変 && TEQUIP:MASTER:コンドーム == 0
	CALL CHANGE_SEX, MASTER
	RETURN 1
ELSEIF RESULT == 913 && ASSI >= 0 && TALENT:ASSI:雌雄可変 && TEQUIP:ASSI:コンドーム == 0 && TEQUIP:ASSI:コスチュームプレイ == 0
	CALL CHANGE_SEX, ASSI
	RETURN 1
ELSEIF RESULT == 990 && FLAG:14 & 64
	CALL COMSEQ_REGISTER
	RETURN 1
ELSEIF RESULT == 991 && FLAG:550 > 0 && FLAG:14 & 64
	CALL COMSEQ_SHOW
	WAIT
	RETURN 1
ELSEIF RESULT == 992 && FLAG:550 > 0 && FLAG:14 & 64
	CALL COMSEQ_TRAIN
	RETURN 1
ELSEIF RESULT == 994 && ASSI >= 0 && ASSIPLAY
	CALL ASSI_SELECT_TRAIN
	IF RESULT != 99
		SELECTCOM:1 = RESULT
		CALLTRAIN 1
	ENDIF
	RETURN 1
ELSEIF RESULT == 997
	CALL DICTIONARY_MENU
	RETURN 1
ELSEIF (RESULT == 998 && TALENT:MASTER:998) || RESULT == 3054635427
	;デバッグモード用メニュー
	PRINTL デバッグメニューを呼び出します
	CALL DEBUG_MENU_TRAIN
	RETURN 1
ELSEIF RESULT == 999 && TEQUIP:時間止め == 0
	BEGIN AFTERTRAIN
	RETURN 1
ELSEIF RESULT == -1 && TARGET
	CALL SHOW_INFO_TRAIN_MAIN, TARGET
	RETURN 1
ELSE
	REUSELASTLINE  
ENDIF
RETURN 0

;=============================================================================
;カスタムコマンドフィルタ関連
;=============================================================================
@CUSTOM_COMMAND_FILTER
;変数のリセット
LOCALS:0 = ○
LOCALS:1 = ×
VARSET LOCAL, 0
CALLF CLEAR_LIST

REPEAT 600
	;コマンドが存在しているかどうか
	STRLENS TRAINNAME:COUNT
	SIF RESULT < 1
		CONTINUE
	CALLF SET_LIST, COUNT, 1
	LOCAL:3 += 1
	SIF (LOCAL:3%60) == 1 && LOCAL:3 > 60
		LOCAL:1 += 1
REND

;ページをめくったときなどはここから
$PRINT_LIST_PAGE
ライン数:0 = LINECOUNT
LOCAL:2 = 0
DRAWLINE
PRINTFORML カスタムコマンドフィルタ ＜page.{LOCAL + 1}/{LOCAL:1 + 1}＞
PRINTL フィルタをかけたい(解除したい)コマンドを選択してください。
DRAWLINE
REPEAT 600
	;(表示可否)が0の場合は除外
	SIF GET_LIST(COUNT) == 0
		CONTINUE
	;どこからどこまでを陳列するか
	LOCAL:2 += 1
	SIF LOCAL:2 <= (LOCAL * 60) || LOCAL:2 > ((LOCAL + 1) * 60)
		CONTINUE
	PRINTFORMLC %LOCALS:((DB:0:COUNT) > 0)%[{COUNT,3}]%TRAINNAME:COUNT%
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1111]前のページ\@
PRINTLC [5555]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [9999]次のページ\@
PRINTL 
PRINTLC [3333]フィルタを初期化
PRINTFORMLC %" " * 16%
PRINTLC [7777]全部にフィルタ
PRINTL 
$INPUT_LOOP_1
INPUT
IF RESULT == 5555
	RETURN 0
ELSEIF RESULT == 1111 && LOCAL > 0
	LOCAL -= 1
ELSEIF RESULT == 9999 && LOCAL < LOCAL:1
	LOCAL += 1
ELSEIF RESULT == 3333 || RESULT == 7777
	PRINTFORMW \@(RESULT == 3333) ? 調教コマンドのフィルタを全て解除し # 全調教コマンドにフィルタをかけ\@ました
	FOR PC_VAR1, 0, 600
		DB:0:PC_VAR1 = (RESULT == 3333) ? 0 # 1
		DITEMTYPE:999:PC_VAR1 = (RESULT == 3333) ? 0 # 1
	NEXT
ELSEIF RESULT < 0 || RESULT > 600 || GET_LIST(RESULT) == 0
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP_1
ELSE
	PRINTFORMW 調教コマンド「%TRAINNAME:RESULT%」\@(DB:0:RESULT) ? のフィルタを解除し # にフィルタを掛け\@ました
	DB:0:RESULT = (DB:0:RESULT) ? 0 # 1
	DITEMTYPE:999:RESULT = (DITEMTYPE:999:RESULT) ? 0 # 1
ENDIF
ライン数:1 = LINECOUNT
CLEARLINE ライン数:1 - ライン数:0
GOTO PRINT_LIST_PAGE

;=============================================================================
;助手のコマンド選択処理
;=============================================================================
@ASSI_SELECT_TRAIN
;ランダム選択による行き先がすべて実行できない場合は戻すので。
LOCAL = 0
FOR PC_VAR1 , 0, 500
	SIF STRLENS(TRAINNAME:PC_VAR1) <= 0
		CONTINUE
	IF DB:0:PC_VAR1 == 0
		CALLFORM COM_ABLE{PC_VAR1}
	ELSE
		RESULT = 0
	ENDIF
	SIF RESULT == 1
		LOCAL += 1
NEXT
IF LOCAL <= 0
	PRINTFORML %CALLNAME:ASSI%には、特にやりたい調教はないようです…
	RETURN 99
ENDIF

;サドっ気高い助手
IF ABL:ASSI:サドっ気 > 3 && RAND:3 == 0
	LOCAL = RAND:25 + 220
	SELECTCASE LOCAL
		CASE 237
			LOCAL = 240
		CASE 238
			LOCAL = 241
		CASE 239
			LOCAL = 242
		CASE 240
			LOCAL = 243
		CASE 241
			LOCAL = 244
		CASE 242
			LOCAL = 245
		CASE 243
			LOCAL = 251
		CASE 244
			LOCAL = 252
	ENDSELECT
;マゾっ気高い助手
ELSEIF ABL:ASSI:マゾっ気 > 3 && RAND:3 == 0
	SELECTCASE RAND:5
		CASE 0
			LOCAL = 270
		CASE 1
			LOCAL = 271
		CASE 2
			LOCAL = 262
		CASE 3
			LOCAL = 445
		CASE 4
			LOCAL = 446
	ENDSELECT
;精液中毒高い助手
ELSEIF ABL:ASSI:精液中毒 > 3 && RAND:3 == 0
	SELECTCASE RAND:21
		CASE 0 TO 2
			LOCAL = 5
		CASE 3
			LOCAL = 292
		CASE 4
			LOCAL = 293
		CASE 5
			LOCAL = 303
		CASE 6
			LOCAL = 304
		CASE 7
			LOCAL = 305
		CASE 8
			LOCAL = 307
		CASE 9
			LOCAL = 308
		CASE 10
			LOCAL = 321
		CASE 11
			LOCAL = 322
		CASE 12
			LOCAL = 323
		CASE 13
			LOCAL = 324
		CASE 14
			LOCAL = 325
		CASE 15
			LOCAL = 326
		CASE 16
			LOCAL = 331
		CASE 17
			LOCAL = 332
		CASE 18
			LOCAL = 333
		CASE 19
			LOCAL = 334
		CASE 20
			LOCAL = 335
	ENDSELECT
;自慰中毒高い助手
ELSEIF ABL:ASSI:自慰中毒 > 3 && RAND:3 == 0
	LOCAL = RAND:4 + 115
;その他
ELSE
	LOCAL = RAND:500
ENDIF
SIF STRLENS(TRAINNAME:LOCAL) <= 0
	RESTART
IF DB:0:LOCAL == 0
	CALLFORM COM_ABLE{LOCAL}
ELSE
	RESULT = 0
ENDIF
SIF RESULT == 0
	RESTART
RETURN LOCAL
