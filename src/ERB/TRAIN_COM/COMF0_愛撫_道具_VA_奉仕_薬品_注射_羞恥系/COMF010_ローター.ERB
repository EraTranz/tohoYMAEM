;=============================================================================
;ローター
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE10
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:10 > 0
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:ローター == 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 10) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM10
PRINTL ローター

;戦闘判定
CALL COM_TRY_BATTLE, 10
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ローター
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 10
DOWNBASE:1 += 80

SOURCE:露出 = 120
SOURCE:逸脱 = 70

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 200
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 400
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 900
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1600
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2400
ELSE
	SOURCE:快Ｃ = 3000
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

SIF PLAYER == MASTER
	TCVAR:Ｃ調律経験 += 1

;コマンド属性：道具(調教者)
TFLAG:98 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_10
PRINTFORM %CALLNAME:TARGET%の
IF EXIST_PENIS(TARGET) && PALAM:欲情 >= PALAMLV:3
	IF TALENT:オトコ
		PRINT 硬くそそり立ったペニス
	ELSE
		PRINT ぬかるんだ秘唇
	ENDIF
	PRINTL に震えるローターを押し当てた…
ELSEIF TEQUIP:焦らしプレイ
	SIF EX:20
		PRINT 膨張した
	PRINTL クリトリスにローターを近づけ、その周囲だけを嬲った…
ELSEIF EXIST_PENIS(TARGET)
	PRINTFORML \@(TALENT:オトコ) ? ペニス # 秘唇\@に震えるローターを押し当てた…
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINTL 硬く尖ったクリトリスに震えるローターを押し当てた…
ELSE
	PRINTL クリトリスに震えるローターを押し当てた…
ENDIF
