;=============================================================================
;バイブ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE12
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:12 > 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;Ｖ経験が低いうちに触手調教中だと、異常系調教フィルタがオフになってないとだめ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1 && (TEQUIP:触手調教 || TEQUIP:触手服)
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;触手服が先に責めてるときはダメ
SIF TEQUIP:触手服 & 8
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ｖ系装着器具 == 1
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:バイブ == 0
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;調教対象が[小人体型]の場合は主人が[禁断の知識]持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;対象が[処女]
IF TALENT:処女 == 1
	;対象の処女の判定
	SIF COM_ABLE_VIRGIN(TARGET, PLAYER) == 0
		RETURN 0
ELSE
	;対象の《潤滑》は大丈夫かチェック
	SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
		RETURN 0
ENDIF
;他のＶ系装着具使用中は不可
SIF (TEQUIP:Ｖ系装着器具 && TEQUIP:Ｖ系装着器具 != 1) || TEQUIP:クスコ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 12) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM12
;………………………………………………
;コマンドの派生判定
;………………………………………………
;調教者がレミリアでバイブ未装着のとき性感槍ぐんぐにるへ
IF NO:PLAYER == 1010 && TEQUIP:Ｖ系装着器具 == 0
	SIF EXCOM_ABLE660() == 1
		JUMP COM660
ENDIF

PRINTL バイブ

;戦闘判定
CALL COM_TRY_BATTLE, 12
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = バイブ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 30
DOWNBASE:1 += 100

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 80
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1000
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1300
ELSE
	SOURCE:快Ｖ = 1700
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 5500
	SIF TEQUIP:触手調教
		TCVAR:異常経験 += 1
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 400
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.10
ELSE
	TIMES SOURCE:快Ｖ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;バイブの着脱
LOCAL = 1
IF TEQUIP:Ｖ系装着器具
	TEQUIP:Ｖ系装着器具 = 0
ELSE
	TEQUIP:Ｖ系装着器具 = 1
ENDIF

TCVAR:Ｖ経験 += LOCAL

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;コマンド属性：道具(調教者)
TFLAG:98 = 1
RETURN 1

;-------------------------------------------------
;バイブ挿入中
;-------------------------------------------------
@EQUIP_COM12
IF TEQUIP:触手調教 || TEQUIP:触手服 & 8
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

DOWNBASE:0 += 10
DOWNBASE:1 += 50

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 40
	LOCAL:2 = 100
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 120
	LOCAL:2 = 300
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 300
	LOCAL:2 = 600
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 500
	LOCAL:2 = 1000
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 650
	LOCAL:2 = 1500
ELSE
	LOCAL = 850
	LOCAL:2 = 2100
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:1 = 150
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:1 = 20
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:1 = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:1 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:1 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.10
	LOCAL:1 += 400
	TIMES LOCAL:1 , 3.00
	LOCAL:2 += 1000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.40
	LOCAL:1 += 150
	TIMES LOCAL:1 , 1.00
	LOCAL:2 += 200
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 0.50
	LOCAL:2 += 80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:1 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:1 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;貞操観念
IF TALENT:貞操観念
	;処女はありえない
	TIMES LOCAL:2, 3.00
ENDIF
;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:1 , 2.00

SOURCE:快Ｖ += LOCAL
SOURCE:痛み += LOCAL:1
SOURCE:屈従 += LOCAL:2

;自慰実行時は経験＋1
IF CHECK_COM_MASTURBATION(SELECTCOM)
	TCVAR:Ｖ経験 += 3
ELSE
	TCVAR:Ｖ経験 += 2
ENDIF

;触手経験フラグ
SIF TEQUIP:触手調教
	TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_12
IF TEQUIP:Ｖ系装着器具
	PRINTFORML %CALLNAME:TARGET%から蠢くバイブをゆっくりと引き抜いた
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF EXP:Ｖ経験 > 200 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%は、膣内の喪失感に耐えかねて涙\@(TEQUIP:口装着器具) ? # と涎\@をこぼし
		ELSEIF EXP:Ｖ経験 > 150 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%の膣内に溜まっていた粘液が掻き出されて滴り、水溜りを作っ
		ELSEIF EXP:Ｖ経験 > 120 && TALENT:恋慕
			PRINTFORM バイブが抜かれた%CALLNAME:TARGET%の肉襞からボタボタと愛液が滴っ
		ELSEIF EXP:Ｖ経験 > 70 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%はもじもじと股を擦り合わせて%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:Ｖ経験 > 40 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%は熱のこもった\@(TEQUIP:口装着器具) ? 視線を向け # 吐息を吐い\@
		ELSEIF EXP:Ｖ経験 > 20
			PRINTFORM %CALLNAME:TARGET%はゆるんだ膣口をひくつかせ
		ELSE
			PRINTFORM %CALLNAME:TARGET%はホッとしたような表情をし
		ENDIF
		PRINTL ている…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:5
		PRINT たっぷりと
	SIF PALAM:潤滑 >= PALAMLV:3
		PRINT 粘液をたたえた
	IF TALENT:小柄体型
		PRINT 幼いワレメに
	ELSE
		PRINT 秘裂に
	ENDIF
	PRINTL バイブの先端をあてがい馴染ませていく…

	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:処女 && TALENT:恋慕 && TALENT:淫乱
			PRINT 自分が処女である事を忘れているかのような笑みを見せ
		ELSEIF TALENT:処女 && TALENT:恋慕
			PRINTFORM 少し寂しそうな目で%CALLNAME:PLAYER%を見
		ELSEIF TALENT:処女 && TALENT:淫乱
			PRINT 処女である事もお構いなしに、腰を揺らしておねだりをし
		ELSEIF TALENT:処女 && TALENT:一線越えない
			PRINT 何かを諦めた表情で目にうっすら涙を浮かべ
		ELSEIF TALENT:処女
			PRINT ガタガタと震え、喪失の恐怖に怯え
		ELSEIF EXP:Ｖ経験 > 200 && TALENT:恋慕
			PRINT 挿入への期待だけでガクガクと腰を震わせてイきそうになっ
		ELSEIF EXP:Ｖ経験 > 150 && TALENT:恋慕
			PRINTFORM とろけきった目で%CALLNAME:PLAYER%を見つめ、自分から陰唇を擦りつけ
		ELSEIF EXP:Ｖ経験 > 120 && TALENT:恋慕
			PRINT 恍惚とした表情で待ちきれないように腰をくねらせ
		ELSEIF EXP:Ｖ経験 > 70 && TALENT:恋慕
			PRINTFORM もの欲しそうに目を潤ませ%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:Ｖ経験 > 40 && TALENT:恋慕
			PRINTFORM バイブの擦れる感触に陶酔しきった\@(TEQUIP:口装着器具) ? 表情を見せ # 吐息をつい\@
		ELSEIF EXP:Ｖ経験 > 0 && TALENT:恋慕
			PRINT バイブの擦れる感触に身をよじらせ愛液を滲ませて
		ELSE
			PRINT バイブの擦れる感触に肉襞をひくつかせ
		ENDIF
		PRINTL ている
		PRINT そんな
	ELSE
		PRINT そして、目覚めていない
	ENDIF
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 濡れそぼった
	PRINTL 性器にバイブを挿し込んだ…
ENDIF
