;=============================================================================
;ニプルキャップ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE15
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:15 > 0
	RETURN 0
;強力アイテム・コマンド無効時はダメ
SIF (FLAG:13 & 2) == 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;触手服が先に責めてるときはダメ
SIF TEQUIP:触手服 & 4
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ｂ系装着器具 == 1
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:ニプルキャップ == 0
	RETURN 0
;調教対象が[小人体型]の場合は主人が[禁断の知識]持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;調教者が助手の場合、技巧3以上じゃないと使えない。
SIF ASSIPLAY && ABL:PLAYER:技巧 < 3
	RETURN 0
;他のＢ系装着具使用中は不可
SIF (TEQUIP:Ｂ系装着器具 && TEQUIP:Ｂ系装着器具 != 1) || TEQUIP:サラシ
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 15) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM15
PRINTL ニプルキャップ

;戦闘判定
CALL COM_TRY_BATTLE, 15
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ニプルキャップ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 70

SOURCE:露出 = 120
SOURCE:逸脱 = 70

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 100
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 300
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 800
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1500
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2300
ELSE
	SOURCE:快Ｂ = 2900
ENDIF

;母乳体質のときは、噴乳中毒を見る
IF TALENT:母乳体質
	IF ABL:噴乳中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:逸脱 , 1.00
	ELSEIF ABL:噴乳中毒 == 1
		SOURCE:中毒充足 += 20
		TIMES SOURCE:快Ｂ , 1.05
		TIMES SOURCE:逸脱 , 1.10
	ELSEIF ABL:噴乳中毒 == 2
		SOURCE:中毒充足 += 80
		TIMES SOURCE:快Ｂ , 1.10
		TIMES SOURCE:逸脱 , 1.15
	ELSEIF ABL:噴乳中毒 == 3
		SOURCE:中毒充足 += 200
		TIMES SOURCE:快Ｂ , 1.15
		TIMES SOURCE:逸脱 , 1.20
	ELSEIF ABL:噴乳中毒 == 4
		SOURCE:中毒充足 += 500
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:逸脱 , 1.60
	ELSE
		SOURCE:中毒充足 += 1000
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:逸脱 , 2.00
	ENDIF
ENDIF

;Ｂ敏感・Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES SOURCE:快Ｂ , 1.50
ELSEIF TALENT:Ｂ鈍感
	TIMES SOURCE:快Ｂ , 0.60
ENDIF
;巨乳・爆乳
IF TALENT:巨乳 || TALENT:爆乳
	TIMES SOURCE:快Ｂ , 1.50
ENDIF
;調教者が［幼児］または［幼稚］または［幼児退行］
SIF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚 || TALENT:PLAYER:幼児退行
	TIMES SOURCE:快Ｂ , 1.20

;ニプルキャップの着脱
IF TEQUIP:Ｂ系装着器具
	TEQUIP:Ｂ系装着器具 = 0
ELSE
	TEQUIP:Ｂ系装着器具 = 1
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;コマンド属性：道具(調教者)
TFLAG:98 = 1
RETURN 1

;-------------------------------------------------
;ニプルキャップ装備中
;-------------------------------------------------
@EQUIP_COM15
IF TEQUIP:触手調教 || TEQUIP:触手服 & 4
	PRINTL ＜触手乳首責め中＞
ELSE
	PRINTL ＜ニプルキャップ装備中＞
ENDIF

DOWNBASE:0 += 5
DOWNBASE:1 += 20

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL = 40
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL = 120
ELSEIF ABL:Ｂ感覚 == 2
	LOCAL = 250
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL = 450
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL = 600
ELSE
	LOCAL = 750
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

SOURCE:快Ｂ += LOCAL
SOURCE:露出 += 50
SOURCE:欲情追加 += 50
;触手経験フラグ
SIF TEQUIP:触手調教
	TFLAG:90 += 1

RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_15
IF TEQUIP:Ｂ系装着器具
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML %CALLNAME:PLAYER%は、\@(TALENT:Ｂ敏感) ? 敏感な # \@%CALLNAME:TARGET%の乳首からクリップローターを取り外した…
	ELSE
		IF MARK:反発刻印 > 1
			PRINTFORML %CALLNAME:PLAYER%が手を伸ばそうとする前に、%CALLNAME:TARGET%はクリップローターを取り外して放り投げた…
		ELSEIF TALENT:相愛 && ASSIPLAY == 0
			PRINTFORML %CALLNAME:PLAYER%は、\@(TALENT:Ｂ敏感) ? 敏感な # \@%CALLNAME:TARGET%の乳首からクリップローターを取り外した
			PRINTFORML %CALLNAME:TARGET%は、次にどこを責めてくれるのかを期待しつつ%CALLNAME:PLAYER%を熱い視線で見つめてきた…
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は、\@(TALENT:Ｂ敏感) ? 敏感な # \@%CALLNAME:TARGET%の乳首からクリップローターを取り外した…
		ENDIF
	ENDIF
ELSE
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML %CALLNAME:PLAYER%は、\@(TALENT:Ｂ敏感) ? 敏感な # \@%CALLNAME:TARGET%の乳首にクリップローターを貼り付けた…
	ELSE
		IF MARK:反発刻印 > 1
			PRINTFORML %CALLNAME:PLAYER%は、嫌がる%CALLNAME:TARGET%を無理やり押さえつけた
			PRINTFORML そして、%CALLNAME:TARGET%の乳首にクリップローターを貼り付けた…
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は、\@(TALENT:Ｂ敏感) ? 敏感な # \@%CALLNAME:TARGET%の乳首にクリップローターを貼り付けた…
		ENDIF
	ENDIF
ENDIF
