;=============================================================================
;騎乗位
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE24
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:24 > 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者が助手でその助手がオトコではない場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && ASSIPLAY && TALENT:PLAYER:オトコ == 0
	RETURN 0
;Ｖ経験が低い場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1
	RETURN 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;Ａ系装着具使用中の場合調教者の技巧4以上必須
SIF (TEQUIP:Ａ系装着器具 || TEQUIP:氷柱) && ABL:PLAYER:技巧 < 4
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;助手が褌着用中の場合技巧4以上必須
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17 && ABL:技巧 < 4
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;人魚はダメ
SIF TALENT:人魚 == 1
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 24) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM24
;………………………………………………
;コマンドの派生判定
;………………………………………………
;御所車派生分岐を追加
;COM_ABLE以降は子宮口攻め分岐から引っ張ってきました
;強制実行時はここは絶賛スルー
IF TFLAG:699 == 0
	CALL COM_ABLE430
	IF RESULT == 1
		IF FLAG:14 & 8
			PRINTL 御所車にする？
			PRINTL [0] - する
			PRINTL [1] - しない
			$INPUT_LOOP
			INPUT
			IF RESULT == 0
				JUMP COM430
			ELSEIF RESULT == 1
				
			ELSE
				CLEARLINE 1
				REUSELASTLINE
				GOTO INPUT_LOOP
			ENDIF
		ELSE
			;PALAM:欲情をみる
			IF PALAM:欲情 < PALAMLV:1
				LOCAL = 1
			ELSEIF PALAM:欲情 < PALAMLV:2
				LOCAL = 2
			ELSEIF PALAM:欲情 < PALAMLV:3
				LOCAL = 3
			ELSEIF PALAM:欲情 < PALAMLV:4
				LOCAL = 4
			ELSE
				LOCAL = 5
			ENDIF
			LOCAL:1 = RAND:11
			LOCAL *= LOCAL:1
			LOCAL:1 = RAND:11
			LOCAL += (LOCAL:1)*ABL:Ｖ感覚
			;LOCALの値は0～100
			;騎乗位だと50以上で御所車
			IF LOCAL >= 50
				JUMP COM430
			ENDIF
		ENDIF
	ENDIF
ENDIF

PRINTL 騎乗位

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 24
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------

;前のプレイが騎乗位かつ調教者の技巧3以上の場合、騎乗Ｇスポ責めまたは騎乗子宮口責めへ派生
;Ｖ感覚が高く欲情しているほど子宮口へ行きやすい
IF FLAG:15 & 2
	IF PREVCOM == 24 && ABL:PLAYER:技巧 > 2 && JUDGE_TRAINER_CONSECUTIVE() == 1
		;ランダム排除時
		IF FLAG:14 & 8
			PRINTL どこを責める？
			PRINTL [0] - Ｇスポット
			PRINTL [1] - 子宮口
			$INPUT_LOOP_1
			INPUT
			IF RESULT == 0
				JUMP COM614
			ELSEIF RESULT == 1
				JUMP COM615
			ELSE
				CLEARLINE 1
				REUSELASTLINE
				GOTO INPUT_LOOP_1
			ENDIF
		ELSE
			;PALAM:欲情をみる
			IF PALAM:欲情 < PALAMLV:1
				LOCAL = 1
			ELSEIF PALAM:欲情 < PALAMLV:2
				LOCAL = 2
			ELSEIF PALAM:欲情 < PALAMLV:3
				LOCAL = 3
			ELSEIF PALAM:欲情 < PALAMLV:4
				LOCAL = 4
			ELSE
				LOCAL = 5
			ENDIF
			LOCAL:1 = RAND:11
			LOCAL *= LOCAL:1
			LOCAL:1 = RAND:11
			LOCAL += (LOCAL:1)*ABL:Ｖ感覚
			;LOCALの値は0～100
			;騎乗位だと50以上で子宮口
			IF LOCAL >= 50
				JUMP COM615
			ELSE
				JUMP COM614
			ENDIF
		ENDIF
	ENDIF
ENDIF

TSTR:0 = 騎乗位
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1
SIF TALENT:処女 && TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:2 = TALENT:処女

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1000
ELSEIF ABL:技巧 == 1
	LOCAL = 1300
ELSEIF ABL:技巧 == 2
	LOCAL = 1700
ELSEIF ABL:技巧 == 3
	LOCAL = 2200
ELSEIF ABL:技巧 == 4
	LOCAL = 3000
ELSE
	LOCAL = 4500
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;欲望をみる
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_2(TARGET, LOCAL)
;欲情をみる
LOCAL = EJACULATIONCHECK_PALAM_13_1(TARGET, LOCAL)
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7
;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

;EXP:Ｖ経験をみる
;Ｖ経験が多くなると少し下がる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES LOCAL , 1.50
	IF ASSIPLAY && TALENT:PLAYER:オトコ == 0
		TCVAR:異常経験 += 3
	ELSE
		TCVAR:異常経験 += 2
	ENDIF
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 1.00
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.90
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.80
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.70
ELSE
	TIMES LOCAL , 0.60
ENDIF

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 60
DOWNBASE:1 += 150

SOURCE:露出 = 900
SOURCE:主導権 = 2500

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*10 + 60

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 200
	SOURCE:達成感 = 50
	SOURCE:逸脱 = 300
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 250
	SOURCE:達成感 = 200
	SOURCE:逸脱 = 100
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 350
	SOURCE:達成感 = 550
	SOURCE:逸脱 = 30
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 450
	SOURCE:達成感 = 900
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 600
	SOURCE:達成感 = 1500
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 750
	SOURCE:達成感 = 2200
	SOURCE:逸脱 = 0
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 20
	SOURCE:情愛 = 25
	TIMES SOURCE:性行動 , 0.50
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 75
	SOURCE:情愛 = 75
	TIMES SOURCE:性行動 , 0.80
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 200
	SOURCE:情愛 = 125
	TIMES SOURCE:性行動 , 1.00
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 500
	SOURCE:情愛 = 175
	TIMES SOURCE:性行動 , 1.20
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 850
	SOURCE:情愛 = 300
	TIMES SOURCE:性行動 , 1.50
ELSE
	SOURCE:快Ｖ = 1100
	SOURCE:情愛 = 425
	TIMES SOURCE:性行動 , 2.00
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 5000
	SOURCE:露出 = 12000
	SOURCE:逸脱 = 120000
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 220
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 30
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 5
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
ELSE
	TIMES SOURCE:快Ｖ , 1.50
ENDIF

;PALAM:潤滑
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 900
	TIMES SOURCE:痛み , 3.00
	SOURCE:露出 += 2000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み += 250
	TIMES SOURCE:痛み , 1.00
	SOURCE:露出 += 400
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:痛み , 0.30
ELSEIF PALAM:潤滑 < PALAMLV:5
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:痛み , 0.10
ENDIF

;調教者が[オトコ]
SIF TALENT:PLAYER:オトコ
	TIMES SOURCE:快Ｖ , 2.50

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:逸脱, 15.00
		SOURCE:反感追加 = 10000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:逸脱, 15.00
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:逸脱, 0.50
		SOURCE:反感追加 = 300
	ELSE
		TIMES SOURCE:逸脱, 0.50
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:反感追加 = 3000
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:情愛 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:情愛 , 1.50
ELSEIF PALAM:欲情 < PALAMLV:5
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:情愛 , 1.80
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:情愛 , 2.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感追加 , 2.00
	SOURCE:屈従 += 700
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感追加 , 1.50
	SOURCE:屈従 += 500
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感追加 , 1.00
	SOURCE:屈従 += 300
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感追加 , 0.80
	SOURCE:屈従 += 100
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感追加 , 0.60
	SOURCE:屈従 += 30
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:反感追加 , 0.30
	SOURCE:屈従 += 0
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:情愛 , 0.60
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:情愛 , 1.40
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:情愛 , 1.60
ENDIF

TCVAR:Ｖ経験 += 1
IF PLAYER == MASTER
	TCVAR:Ｖ調律経験 += 2
	SIF EXIST_BAR(MASTER)
		CFLAG:主人にＶ捧げ回数 += 1
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00
	TIMES SOURCE:達成感 , 5.00
	TIMES SOURCE:屈従 , 1.50

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 300
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1000
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 2200
	ELSE
		SOURCE:中毒充足 = 1000 + (ABL:精液中毒 * 400)
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_24
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7

;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ｖ感覚 >= 3) ? 3 # 2

;奉仕快楽経験入手判定
TFLAG:100 |= 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_24
IF TEQUIP:焦らしプレイ
	SIF PALAM:快Ｃ >= (PALAMLV:4 - 1) ||  PALAM:快Ｖ >= (PALAMLV:4 - 1) ||  PALAM:快Ａ >= (PALAMLV:4 - 1) ||  PALAM:潤滑 >= (PALAMLV:4 - 1)
		PRINT 今にもイきそうになっているにもかかわらず、
	PRINTFORML %CALLNAME:TARGET%は命令を拒否してきた…
ELSE
	PRINTFORML %CALLNAME:TARGET%に上に乗るように命令してみたが拒否された。
	PRINTL もう少し調教が必要なようだ。
ENDIF

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_24
;焦らしプレイ中に自慰系・騎乗位系・助手を犯させる
IF TEQUIP:焦らしプレイ
	IF TFLAG:699 == 0
		SIF PALAM:快Ｃ >= (PALAMLV:4 - 1) ||  PALAM:快Ｖ >= (PALAMLV:4 - 1) ||  PALAM:快Ａ >= (PALAMLV:4 - 1) ||  PALAM:快Ｂ >= (PALAMLV:4 - 1)
			PRINT 今にもイきそうな
		PRINTFORML %CALLNAME:TARGET%の好きにさせてやることにした…
	ELSE
		SIF PALAM:快Ｃ >= (PALAMLV:4 - 1) ||  PALAM:快Ｖ >= (PALAMLV:4 - 1) ||  PALAM:快Ａ >= (PALAMLV:4 - 1) ||  PALAM:快Ｂ >= (PALAMLV:4 - 1)
			PRINT もはや我慢できないといわんばかりに
		PRINTFORML %CALLNAME:TARGET%は勝手に腰を動かし始めた…
	ENDIF
ENDIF

;前回の調教もセックス系調教だった場合で調教対象が勝手にコマンドを実行している場合
IF TFLAG:102 && (PREVCOM >= 20 && PREVCOM <= 24 || (GROUPMATCH(PREVCOM, 614, 615) && GROUPMATCH(PREVCOM:1, 20, 21, 22, 23, 24))) && TFLAG:699 > 0
	PRINTFORML %CALLNAME:PLAYER%が挿入したまま何もしないでいると、まるで満足していないのか%CALLNAME:TARGET%は
	SIF TALENT:淫乱 || TEQUIP:媚薬 == 1
		PRINT 更なる快楽を貪欲に求めて
	SIF TALENT:サド
		PRINTFORM 疲れを訴える%CALLNAME:PLAYER%を無視して
	SIF TALENT:嫉妬
		PRINT 嫉妬の憎悪に狂った様に
	SIF TALENT:狂気 && TEQUIP:口装着器具 == 0
		PRINTL 壊れた様な笑い声を上げ、
	SIF PREVCOM >= 20 && PREVCOM <= 23 || (GROUPMATCH(PREVCOM, 614, 615) && GROUPMATCH(PREVCOM:1, 20, 21, 22, 23))
		PRINTFORM %CALLNAME:PLAYER%を跨るように押し倒して上になり
	PRINTL そのまま自ら再び腰を動かし始めた…
	IF EXP:Ｖ経験 >= EXPLV:3
		PRINTL  
		PRINTFORML 流石の疲れに%CALLNAME:PLAYER%はほとんど動いていないにも拘らず、
		PRINTFORM %CALLNAME:TARGET%は
		SIF TALENT:小人体型
			PRINT 小さな身体で懸命に
		SIF TALENT:巨乳 || TALENT:爆乳
			PRINT 大きく胸を揺らしながら
		PRINTL 男を犯す快楽に没頭し、腰を動かし続けている…
		SIF TFLAG:699 == 3
			PRINTFORML %CALLNAME:TARGET%は完全に肉棒の虜になっており、放って置けば枯れ果てるまで搾り取られそうだ…
	ENDIF
;調教対象が勝手にコマンドを実行している場合
ELSEIF TFLAG:699 > 0
	PRINTFORML %CALLNAME:PLAYER%が何もしないでいると、命令していないのに%CALLNAME:TARGET%は
	SIF TALENT:淫乱 || TEQUIP:媚薬 == 1
		PRINT 激しく肉棒を求めて
	SIF TALENT:サド
		PRINTFORM 嘲笑的な笑み\@(TEQUIP:口装着器具) ? を浮かべ # で口元を歪め\@ながら
	SIF TALENT:嫉妬
		PRINT 嫉妬の憎悪に狂った様に
	SIF TALENT:狂気 && TEQUIP:口装着器具 == 0
		PRINTL 壊れた様な笑い声を上げ、
	PRINTFORM %CALLNAME:PLAYER%を押し倒し、自らペニスを
	IF TALENT:小人体型
		PRINT 幼いワレメに
	ELSEIF PALAM:潤滑 < PALAMLV:2
		PRINT 濡れてもいない秘裂に
	ELSE
		PRINT 秘裂に
	ENDIF
	PRINTL あてがいそのまま埋めていった…
	IF EXP:Ｖ経験 >= EXPLV:3
		PRINTFORML 突然の事に%CALLNAME:PLAYER%はほとんど動いていないにも拘らず、
		PRINTFORM %CALLNAME:TARGET%は
		SIF TALENT:小人体型
			PRINT 小さな身体で懸命に
		SIF TALENT:巨乳 || TALENT:爆乳
			PRINT 大きく胸を揺らしながら
		PRINTL 男を犯す快楽に没頭し、腰を動かし続けている…
	ENDIF
;前回と今回の調教者が同じで、前回の調教が騎乗位であり、抜かないフラグがある場合
ELSEIF TFLAG:102 && (PREVCOM == 24 || (GROUPMATCH(PREVCOM, 614, 615) && PREVCOM:1 == 24)) && JUDGE_TRAINER_CONSECUTIVE() == 1
	PRINTFORML %CALLNAME:TARGET%は挿入されたままのペニスを締め付け、再び腰を動かし始めた…
	SIF TFLAG:101
		TFLAG:101 = 2
;同様に、前回の調教が正常位か後背位である場合
ELSEIF TFLAG:102 && (PREVCOM == 20 || PREVCOM == 21 || (GROUPMATCH(PREVCOM, 614, 615) && GROUPMATCH(PREVCOM:1, 20, 21))) && JUDGE_TRAINER_CONSECUTIVE() == 1
	PRINTFORML 膣内から抜き去ることなく%CALLNAME:TARGET%を抱き上げ、下から腰を突き上げた…
	SIF TFLAG:101
		TFLAG:101 = 2
;同様に、前回の調教が対面座位か背面座位である場合
ELSEIF TFLAG:102 && (PREVCOM == 22 || PREVCOM == 23 || (GROUPMATCH(PREVCOM, 614, 615) && GROUPMATCH(PREVCOM:1, 22, 23))) && JUDGE_TRAINER_CONSECUTIVE() == 1
	PRINTFORML %CALLNAME:PLAYER%は挿入したまま横になり、下から腰を突き上げた…
ELSE
	IF EX:21
		PRINTFORM お預けが堪えたか、%CALLNAME:TARGET%は荒い息をついて%CALLNAME:PLAYER%に圧し掛かっ
	ELSE
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の上にまたがり、ゆっくりと腰を下ろし
	ENDIF
	IF TALENT:小柄体型 && TALENT:処女 == 1
		PRINTL 、男を知らない幼いワレメでペニスを飲み込んでいった…
	ELSEIF TALENT:処女 == 1
		PRINTL 、男を知らない秘裂でペニスを飲み込んでいった…
	ELSE
		PRINTL てきた…
	ENDIF
	IF EXP:Ｖ経験 >= EXPLV:3
		PRINTFORM %CALLNAME:TARGET%は
		SIF TALENT:小柄体型
			PRINT 小さな身体で懸命に
		IF TALENT:巨乳
			PRINT 大きく胸を揺らしながら
		ELSEIF TALENT:爆乳
			PRINT 跳ねる胸に身体ごと揺らされながら
		ENDIF
		SIF EX:21
			PRINT 貪るような激しさで
		PRINTL 自ら腰を動かしている…
	ENDIF
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_24, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:Ｖ感覚
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% 潤滑不足({LOCAL:99})
ENDIF
;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 2
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;処女
IF TALENT:処女 == 1
	RESULTS = %RESULTS% - 
	LOCAL:99 = 20
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:0%({LOCAL:99})
ELSEIF EXP:Ｖ経験 < EXPLV:2
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %EXPNAME:0%不足({LOCAL:99})
ENDIF

;危險日
IF TALENT:危険日 && !(TALENT:恋慕 && ASSIPLAY == 0)
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:510%({LOCAL:99})
ENDIF

;排卵
IF TALENT:排卵 && !(TALENT:恋慕 && ASSIPLAY == 0)
	RESULTS = %RESULTS% - 
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:512%({LOCAL:99})
ENDIF

;生理
IF TALENT:生理
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:511%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;騎乗位では汚れの影響が少なめ
TFLAG:242 /= 3

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 24

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_24
;デフォルト射精箇所タイプを設定
TFLAG:703 = 1
;射精箇所選択の可・不可
TFLAG:704 = 1
