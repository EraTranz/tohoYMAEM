;=============================================================================
;対面座位アナル
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE32
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:32 > 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;調教者にペニスがない、かつ(アイテムあり設定なら)ペニスバンドもないとダメ
SIF EXIST_BAR(PLAYER) == 0 && ITEM:ペニスバンド == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURN 0
;一定のＡ経験が必要
SIF EXP:Ａ経験 < EXPLV:3
	RETURN 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;助手が褌着用中の場合技巧4以上必須
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17 && ABL:技巧 < 4
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;人魚はダメ
SIF TALENT:人魚 == 1
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 32) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM32
;………………………………………………
;コマンドの派生判定
;………………………………………………
;前回と今回の調教者が同じかつ、胸愛撫もしくは対面アナル胸愛撫への場合、対面アナル胸愛撫へ
SIF PREVCOM == 3 || PREVCOM == 619 && JUDGE_TRAINER_CONSECUTIVE() == 1
	JUMP COM619

PRINTL 対面座位アナル

;戦闘判定
CALL COM_TRY_BATTLE, 32
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 対面座位アナル
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 800
ELSEIF ABL:技巧 == 1
	LOCAL = 1000
ELSEIF ABL:技巧 == 2
	LOCAL = 1200
ELSEIF ABL:技巧 == 3
	LOCAL = 1400
ELSEIF ABL:技巧 == 4
	LOCAL = 1600
ELSE
	LOCAL = 1800
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_2(TARGET, LOCAL)
;欲望をみる
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7
;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_2(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 60
DOWNBASE:1 += 80

SOURCE:情愛 = 100
SOURCE:中毒充足 = 100
SOURCE:露出 = 100

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ += 10
	SOURCE:情愛 += 10
	SOURCE:屈従 += 80
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ += 30
	SOURCE:情愛 += 130
	SOURCE:屈従 += 300
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ += 200
	SOURCE:情愛 += 500
	SOURCE:屈従 += 700
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ += 500
	SOURCE:情愛 += 1000
	SOURCE:屈従 += 1500
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ += 900
	SOURCE:情愛 += 1500
	SOURCE:屈従 += 2600
ELSE
	SOURCE:快Ａ += 1400
	SOURCE:情愛 += 2000
	SOURCE:屈従 += 4000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み = 18000
	DOWNBASE:0 += 40
	DOWNBASE:1 += 60
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み = 10000
	DOWNBASE:0 += 30
	DOWNBASE:1 += 50
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み = 4500
	DOWNBASE:0 += 20
	DOWNBASE:1 += 40
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み = 1500
	DOWNBASE:0 += 10
	DOWNBASE:1 += 30
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み = 700
	DOWNBASE:0 += 0
	DOWNBASE:1 += 20
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み = 300
	DOWNBASE:0 += 0
	DOWNBASE:1 += 0
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 50
	SOURCE:達成感 = 10
	SOURCE:恭順追加 = 100
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 150
	SOURCE:達成感 = 50
	SOURCE:恭順追加 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 300
	SOURCE:達成感 = 100
	SOURCE:恭順追加 = 700
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 400
	SOURCE:達成感 = 200
	SOURCE:恭順追加 = 1200
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	SOURCE:恭順追加 = 1800
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 800
	SOURCE:達成感 = 500
	SOURCE:恭順追加 = 2500
	TIMES SOURCE:不潔 , 0.10
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 200
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;調教者が[オトコ]
IF TALENT:PLAYER:オトコ
	TIMES SOURCE:快Ａ , 2.50
;調教者が[ふたなり]
ELSEIF TALENT:PLAYER:ふたなり
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.60
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF EXP:Ａ経験 == 0
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順追加 , 1.10
		SOURCE:反感追加 = 500
	ELSE
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:恭順追加 , 1.10
		SOURCE:反感追加 = 0
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:従順 == 2
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:従順 == 4
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:反感追加 , 0.30
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	SOURCE:情愛 += 100
	SOURCE:液体追加 += 0
	LOCAL:1 = 0
ELSEIF ABL:PLAYER:技巧 == 1
	SOURCE:情愛 += 150
	SOURCE:液体追加 += 50
	LOCAL:1 = 0
ELSEIF ABL:PLAYER:技巧 == 2
	SOURCE:情愛 += 200
	SOURCE:液体追加 += 100
	LOCAL:1 = 0
ELSEIF ABL:PLAYER:技巧 == 3
	SOURCE:情愛 += 300
	SOURCE:液体追加 += 150
	LOCAL:1 = 50
ELSEIF ABL:PLAYER:技巧 == 4
	SOURCE:情愛 += 500
	SOURCE:液体追加 += 250
	LOCAL:1 = 100
ELSE
	SOURCE:情愛 += 800
	SOURCE:液体追加 += 400
	LOCAL:1 = 300
ENDIF

SOURCE:快Ｃ += LOCAL:1

;TALENT:恋慕をみる
IF TALENT:恋慕
	TIMES SOURCE:情愛 , 3.00
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:恭順追加 , 2.00
ENDIF

TCVAR:Ａ経験 += 2
SIF ASSIPLAY == 0 && EXIST_BAR(MASTER)
	CFLAG:主人にＡ捧げ回数 += 2

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	IF ABL:従順 >= 3 && ABL:欲望 >= 3 && CFLAG:9 & 2 && FLAG:10 & 1 && EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の首に手を回して抱きついてきた…
		PRINTL 
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_32
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ａ感覚 >= 3) ? 2 # 1

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_32
;前回と今回の調教者が同じで、前回の調教が対面座位アナルであり、抜かないフラグがある場合
IF TFLAG:102 && PREVCOM == 32 && JUDGE_TRAINER_CONSECUTIVE() == 1
	IF TEQUIP:焦らしプレイ
		LOCAL = 1 + RAND:3
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の繋がったまま\@(TEQUIP:口装着器具) ? でい # 、何度もキスをし\@た…
		IF TEQUIP:口装着器具 == 0
			TCVAR:キス経験 += LOCAL
			TCVAR:PLAYER:キス経験 += LOCAL
		ENDIF
	ELSE
		LOCALS = \@(EQUIP:睡眠薬 || TFLAG:899 > 0) ? に口づけし # とキスをし、舌を絡め \@
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%\@(TEQUIP:口装着器具) ? の # %LOCALS%ながら\@腰を再び揺すり始めた…
		IF TEQUIP:口装着器具 == 0
			TCVAR:キス経験 += 1
			TCVAR:PLAYER:キス経験 += 1
		ENDIF
	ENDIF
;同様に前回の調教が正常位アナル、騎乗位アナルの場合
ELSEIF TFLAG:102 && (PREVCOM == 30 || PREVCOM == 34) && JUDGE_TRAINER_CONSECUTIVE() == 1
	PRINTFORM 腸内から
	;調教者がオトコかふたなりである
	IF EXIST_PENIS(PLAYER)
		PRINTFORM ペニスを
	ELSE
		PRINTFORM ディルドを
	ENDIF
		PRINTFORM 抜き去ることなく%CALLNAME:TARGET%の身体をぎゅっと抱き寄せ
	SIF TEQUIP:焦らしプレイ == 0
		PRINTFORM 、腰を揺すり始め
	PRINTFORML た…
;同様に前回の調教が後背位アナル、背面座位アナルの場合
ELSEIF TFLAG:102 && (PREVCOM == 31 || PREVCOM == 33) && JUDGE_TRAINER_CONSECUTIVE() == 1
	PRINTFORM 挿入したまま身体をこちらに向かせ
	IF PREVCOM == 31
		PRINT て抱き上げ
	ELSE
		PRINT るとぎゅっと抱き寄せ
	ENDIF
	SIF TEQUIP:焦らしプレイ == 0
		PRINTFORM 、腰を揺すり始め
	PRINTFORML た…
ELSE
	PRINTFORM %CALLNAME:TARGET%の身体を抱き上げ、ゆっくりと降ろしながら
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 粘液にまみれた
	PRINTFORML アヌスへと挿入した…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_32
;デフォルト射精箇所タイプを設定
TFLAG:703 = 2
;射精箇所選択の可・不可
TFLAG:704 = 1
