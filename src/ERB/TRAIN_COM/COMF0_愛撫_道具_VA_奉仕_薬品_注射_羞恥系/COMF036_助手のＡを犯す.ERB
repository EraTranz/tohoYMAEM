;=============================================================================
;助手のＡを犯す
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE36
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:36 > 0
	RETURN 0
;助手がいないとダメ
SIF ASSI < 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;主人じゃないとダメ
SIF ASSIPLAY
	RETURN 0
;主人にペニスがない、かつ(アイテムあり設定なら)ペニスバンドもないとダメ
SIF EXIST_BAR(MASTER) == 0 && ITEM:ペニスバンド == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, ASSI) == 0
	RETURN 0
;助手に一定のＡ経験が必要
SIF EXP:ASSI:Ａ経験 < EXPLV:3
	RETURN 0
;対象と助手の相性が良くないとダメ
SIF RELATION:(NO:ASSI) <= 100
	RETURN 0
;睡眠中は不可（あまり意味がないので）
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;縄使用中か従順が4以上ないとダメ
SIF TEQUIP:緊縛関連 == 0 && ABL:従順 < 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;助手が褌着用中の場合主人の技巧4以上必須
SIF ASSI >= 0 && TEQUIP:ASSI:コスチュームプレイ == 17 && ABL:MASTER:技巧 < 4
	RETURN 0
;裸エプロン中はダメ
SIF TEQUIP:コスチュームプレイ == 1
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:クスコ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 36) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM36
PRINTL 助手のＡを犯す

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 36
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 助手のＡを犯す
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:ASSI:技巧 == 0
	LOCAL = 50
ELSEIF ABL:ASSI:技巧 == 1
	LOCAL = 300
ELSEIF ABL:ASSI:技巧 == 2
	LOCAL = 800
ELSEIF ABL:ASSI:技巧 == 3
	LOCAL = 1500
ELSEIF ABL:ASSI:技巧 == 4
	LOCAL = 2000
ELSE
	LOCAL = 3200
ENDIF

;助手の従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(ASSI, LOCAL)
;助手の奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(ASSI, LOCAL)
;助手のＡ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_2_1(ASSI, LOCAL)
;助手のＡ経験をみる
LOCAL = EJACULATIONCHECK_EXP_1_1(ASSI, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 0
DOWNBASE:1 += 300

DOWNBASE:ASSI:0 += 80
DOWNBASE:ASSI:1 += 60

SOURCE:情愛 = 1500
SOURCE:逸脱 = 800

;ABL:欲望をみる
IF ABL:欲望 == 0
	SOURCE:液体追加 = 100
	SOURCE:屈従 = 3200
ELSEIF ABL:欲望 == 1
	SOURCE:液体追加 = 200
	SOURCE:屈従 = 3800
ELSEIF ABL:欲望 == 2
	SOURCE:液体追加 = 350
	SOURCE:屈従 = 4600
ELSEIF ABL:欲望 == 3
	SOURCE:液体追加 = 600
	SOURCE:屈従 = 5400
ELSEIF ABL:欲望 == 4
	SOURCE:液体追加 = 850
	SOURCE:屈従 = 6200
ELSE
	SOURCE:液体追加 = 1200
	SOURCE:屈従 = 7000
ENDIF


;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	TIMES SOURCE:情愛 , 0.10
	TIMES SOURCE:液体追加 , 0.50
ELSEIF ABL:奉仕精神 == 1
	TIMES SOURCE:情愛 , 0.40
	TIMES SOURCE:液体追加 , 0.80
ELSEIF ABL:奉仕精神 == 2
	TIMES SOURCE:情愛 , 0.70
	TIMES SOURCE:液体追加 , 1.00
ELSEIF ABL:奉仕精神 == 3
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:液体追加 , 1.50
ELSEIF ABL:奉仕精神 == 4
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:液体追加 , 2.00
ELSE
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:液体追加 , 2.50
ENDIF
TCVAR:ASSI:Ａ経験 += 1
SIF EXIST_BAR(MASTER)
	CFLAG:ASSI:主人にＡ捧げ回数 += 1

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 1
		TIMES SOURCE:屈従, 1.20
	ELSEIF ABL:精液中毒 == 2
		TIMES SOURCE:屈従, 1.50
	ELSEIF ABL:精液中毒 == 3
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		TIMES SOURCE:屈従, 2.50
	ELSE
		TIMES SOURCE:屈従, 3.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = ASSI
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_36
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
IF EXIST_PENIS(MASTER)
;主人のＰ⇔助手のＡの汚れが移動
	STAIN:MASTER:ペニス |= STAIN:ASSI:アナル
	STAIN:ASSI:アナル |= STAIN:MASTER:ペニス
ENDIF

IF TALENT:MASTER:オトコ == 0 && TALENT:ASSI:オトコ == 0
	TCVAR:ASSI:レズ経験 += 10
ELSEIF TALENT:MASTER:オトコ && TALENT:ASSI:オトコ
	TCVAR:ASSI:ＢＬ経験 += 10
ENDIF

TCVAR:ASSI:主人調教経験 += 1

;奉仕快楽経験入手判定
TFLAG:100 |= 1

RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_36

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_36
;前回の調教が助手のＡを犯すであり、抜かないフラグがある場合
IF TFLAG:102 && PREVCOM == 36
	PRINTFORML %CALLNAME:TARGET%に見せつけながら、%CALLNAME:ASSI%のアナルを犯し続けた…
ELSE
	PRINTFORM %CALLNAME:TARGET%に見せつけながら、\@(MARK:ASSI:反発刻印 > 1) ? 嫌がる # \@%CALLNAME:ASSI%の
	PRINTFORML アナルを\@(MARK:ASSI:反発刻印 > 1) ? 無理やり # \@貫いていった…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_36, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;助手のABL:Ａ感覚
IF ABL:ASSI:Ａ感覚
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:ASSI:Ａ感覚
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% 助手のＡ感覚LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;助手が淫尻
IF TALENT:ASSI:淫尻
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% 助手が淫尻({LOCAL:99})
ENDIF
;助手のABL:露出癖
IF ABL:ASSI:露出癖
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:ASSI:露出癖
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% 助手の露出癖LV{LOCAL:99}({LOCAL:99 * 2})
	LOCAL:99 = 1
ENDIF

;助手が恋慕
IF TALENT:ASSI:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% 助手が恋慕({LOCAL:99})
ENDIF

TFLAG:241 = 40

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_36
;デフォルト射精箇所タイプを設定
TFLAG:703 = 2
;射精箇所選択の可・不可
TFLAG:704 = 0
