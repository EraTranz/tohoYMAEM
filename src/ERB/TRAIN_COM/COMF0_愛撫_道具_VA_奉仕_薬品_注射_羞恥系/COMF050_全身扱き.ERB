;=============================================================================
;全身扱き
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE50
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:50 > 0
	RETURN 0
;調教対象が調教者の体格より充分に小さくないとダメ(禁断の知識は無関係で)
SIF COMMON_BODYSIZE_CHECK2(PLAYER, TARGET) > -2
	RETURN 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;クスコ着けているとダメ
SIF TEQUIP:クスコ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 50) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM50
PRINTL 全身扱き

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 50
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 全身扱き
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 450
ELSEIF ABL:技巧 == 1
	LOCAL = 1000
ELSEIF ABL:技巧 == 2
	LOCAL = 1600
ELSEIF ABL:技巧 == 3
	LOCAL = 2200
ELSEIF ABL:技巧 == 4
	LOCAL = 2700
ELSE
	LOCAL = 3200
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:1 += 100

SOURCE:逸脱 = 400

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*10 + 60

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 20
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 120
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 640
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1200
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2300
ELSE
	SOURCE:快Ｃ = 3400
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 120
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 640
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2300
ELSE
	SOURCE:快Ｂ = 3400
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 250
	SOURCE:達成感 = 50
	TIMES SOURCE:不潔 , 4.00
	SOURCE:屈従 = 40
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 300
	SOURCE:達成感 = 100
	TIMES SOURCE:不潔 , 2.50
	SOURCE:屈従 = 160
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 350
	SOURCE:達成感 = 200
	TIMES SOURCE:不潔 , 1.50
	SOURCE:屈従 = 700
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 400
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 1.00
	SOURCE:屈従 = 1300
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 450
	SOURCE:達成感 = 500
	TIMES SOURCE:不潔 , 0.50
	SOURCE:屈従 = 2200
ELSE
	SOURCE:性行動 = 500
	SOURCE:達成感 = 750
	TIMES SOURCE:不潔 , 0.10
	SOURCE:屈従 = 3100
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
	TIMES SOURCE:屈従 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
	TIMES SOURCE:屈従 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
	TIMES SOURCE:屈従 , 2.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
	TIMES SOURCE:屈従 , 4.00
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES SOURCE:達成感 , 1.80

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 600
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成感 , 5.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_50
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;奴隷の体⇔調教者のＰの汚れが移動
STAIN:手 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:手
STAIN:ペニス |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ペニス
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸
STAIN:足 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:足
STAIN:髪 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:髪

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 4
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 4
ENDIF
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_50
PRINTFORML %CALLNAME:TARGET%に体全体を使って肉棒に奉仕するように命令したが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_50
;未陥落で反発刻印2以上の対象の場合
IF TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0 && MARK:反発刻印 > 1
	PRINTFORML %CALLNAME:TARGET%は露骨に嫌そうな態度を見せつつも、体全体を使って%CALLNAME:PLAYER%の肉棒に奉仕している…
;助手プレイでなくて[恋慕]持ちの対象の場合
ELSEIF ASSIPLAY == 0 && TALENT:恋慕
	PRINTFORML %CALLNAME:TARGET%は愛する%CALLNAME:PLAYER%のために、
	PRINTFORML 体全体を使って%CALLNAME:PLAYER%の肉棒に一生懸命奉仕している…
;恥情が高まっている場合
ELSEIF PALAM:恥情 > PALAMLV:5
	PRINTFORML %CALLNAME:TARGET%は恥じらいに全身を真っ赤に染めつつも、
	PRINTFORML そのかわいらしい体全体を使って%CALLNAME:PLAYER%の肉棒に奉仕している…
;その他の場合
ELSE
	PRINTFORML %CALLNAME:TARGET%は体全体を使って%CALLNAME:PLAYER%の肉棒に奉仕している…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_50, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;全身だから汚れがもろに影響する
TFLAG:242 *= 2

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 34

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_50
;デフォルト射精箇所タイプを設定
TFLAG:703 = 15
;射精箇所選択の可・不可
TFLAG:704 = 0
