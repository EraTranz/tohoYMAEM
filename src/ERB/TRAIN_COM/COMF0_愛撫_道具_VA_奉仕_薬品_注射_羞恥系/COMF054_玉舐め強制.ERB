;=============================================================================
;玉舐め強制
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE54
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:54 > 0
	RETURN 0
IF FLAG:14 & 32
	;ふたたまコマンドを有効にしている場合
	;調教者がオトコか、ふたなりかつ玉ありじゃないとダメ
	SIF TALENT:PLAYER:オトコ == 0 && TALENT:PLAYER:ふたなり != 2
		RETURN 0
ELSE
	;調教者がオトコじゃないとダメ
	SIF TALENT:PLAYER:オトコ == 0
		RETURN 0
ENDIF
;対象が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:猫舌 && (FLAG:14 & 16) == 0
	RETURN 0
;対象が[猫舌]だと技巧4・奉仕精神4・従順4が必要
SIF TALENT:猫舌 && (ABL:技巧 < 4 || ABL:奉仕精神 < 4 || ABL:従順 < 4)
	RETURN 0
;助手調教で[小悪魔][妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)4未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:小悪魔 == 0 && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 4
		RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 54) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM54
PRINTL 玉舐め強制

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 54
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 玉舐め強制
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック（ふたなりの場合）
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 100
ELSEIF ABL:技巧 == 1
	LOCAL = 300
ELSEIF ABL:技巧 == 2
	LOCAL = 500
ELSEIF ABL:技巧 == 3
	LOCAL = 800
ELSEIF ABL:技巧 == 4
	LOCAL = 1200
ELSE
	LOCAL = 1600
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00
	
;TALENT:猫舌をみる
SIF TALENT:猫舌
	TIMES LOCAL , 0.70

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 90

SOURCE:屈従 = 1000
SOURCE:逸脱 = 800

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*50 + 30

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 250
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 350
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 450
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 550
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 650
	SOURCE:達成感 = 1400
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 750
	SOURCE:達成感 = 2000
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:屈従 , 0.80
	TIMES SOURCE:不潔 , 1.20
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:屈従 , 0.90
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:屈従 , 1.00
	TIMES SOURCE:不潔 , 0.90
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:屈従 , 1.20
	TIMES SOURCE:不潔 , 0.80
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:不潔 , 0.60
ELSE
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:不潔 , 0.30
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:情愛 = 20
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:情愛 = 50
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:情愛 = 100
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:情愛 = 200
ELSE
	SOURCE:情愛 = 400
ENDIF

;TALENT:献身的を見る
SIF TALENT:献身的
	TIMES SOURCE:情愛 , 2.00

;TALENT:倒錯的を見る
IF TALENT:倒錯的
	TIMES SOURCE:情愛 , 1.50
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:不潔 , 0.70
	TIMES SOURCE:逸脱 , 0.20
ENDIF

;TALENT:恋慕/妄信/親愛を見る
IF (TALENT:恋慕 || TALENT:妄信 || TALENT:親愛) && ASSIPLAY == 0
	TIMES SOURCE:情愛 , 2.50
	TIMES SOURCE:屈従 , 1.20
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成感 , 2.50
ELSE
	TIMES SOURCE:性行動 , 4.00
	TIMES SOURCE:達成感 , 4.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)
LOCAL:2 = TFLAG:91
;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成感 , 4.50
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 2500
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 5000
		TIMES SOURCE:達成感 , 8.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_54
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;奴隷の口⇔調教者のＰの汚れが移動
STAIN:口 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:口

;奉仕精神ＬＶ２以上、技巧ＬＶ２以上ならＰの汚れをなめ取る
;ただし対象が猫舌の場合、猫フェラパッチ有効でなおかつ
;奉仕精神ＬＶ４以上、技巧ＬＶ５、従順ＬＶ３以上ないとなめ取らない
IF (TALENT:猫舌 == 0 && ABL:奉仕精神 >= 2 && ABL:技巧 >= 2) || (TALENT:猫舌 && FLAG:14 & 16 && ABL:奉仕精神 >= 4 && ABL:技巧 >= 5 && ABL:従順 >=3)
	STAIN:PLAYER:ペニス = 2
	SIF LOCAL:2 >= 1
		TFLAG:10 = 1
ENDIF

;同性ならレズ（ＢＬ）経験が入る
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 3
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 4
ENDIF
;奉仕快楽経験入手判定
TFLAG:100 |= 1
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_54
PRINTFORML %CALLNAME:TARGET%に、%CALLNAME:PLAYER%の玉を舐めるように命じたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_54
;恋慕か妄信か親愛で主人調教のとき
IF (TALENT:恋慕 || TALENT:妄信 || TALENT:親愛) && ASSIPLAY == 0
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%に自らの玉を舐めさせようとペニスを向けると、
	PRINTFORML %CALLNAME:TARGET%は、まるで宝物を扱うかのように優しく%CALLNAME:PLAYER%の玉を舌で舐めまわしはじめた…
;献身的な場合
ELSEIF TALENT:献身的
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%に自らの玉を舐めさせようとすると、
	PRINTFORML %CALLNAME:TARGET%は、丁寧にかつ熱心に玉を舐めまわして%CALLNAME:PLAYER%を愉しませた…
;倒錯的な場合
ELSEIF TALENT:倒錯的
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%に自らの玉を舐めさせた
	PRINTFORML %CALLNAME:TARGET%は、汚いところを舐めさせられて自分自身に少し陶酔しているようだ…
;反発刻印持ちで恋慕がない場合
ELSEIF MARK:反発刻印 && TALENT:恋慕 == 0
	PRINTFORML %CALLNAME:PLAYER%は、嫌がる%CALLNAME:TARGET%に自ら玉を無理やり舐めさせた…
;その他の場合
ELSE
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%に自らの玉を舐めさせた…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_54, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:ＢＬっ気＆TALENT:オトコ
IF TALENT:PLAYER:オトコ && TALENT:オトコ
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓のLOCAL:99の目安(手淫5, フェラ/A愛撫強制10)
	LOCAL:99 = 8
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%同士({LOCAL:99})
	IF ABL:ＢＬっ気
		LOCAL:99 = ABL:ＢＬっ気
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;汚れ無視
IF TALENT:汚れ無視
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:64%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;倒錯的
IF TALENT:倒錯的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:80%({LOCAL:99})
ENDIF
;調教者がサド
IF TALENT:PLAYER:サド
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:81%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 23

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_54
;デフォルト射精箇所タイプを設定
TFLAG:703 = 5
;射精箇所選択の可・不可
TFLAG:704 = 1
