;=============================================================================
;ナイズリ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE56
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:56 > 0
	RETURN 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;対象の性別が♂または[絶壁]でないとダメ
SIF COM_ABLE_CHECK_MALE(TARGET) == 0 && TALENT:絶壁 == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;技巧5以上ないとダメ
SIF ABL:技巧 < 5
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;サラシ装着中はダメ
SIF TEQUIP:サラシ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;ウェディングドレス・ゴスロリ着用中の場合技巧4以上必須
SIF (TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20) && ABL:技巧 < 4
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 56) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM56
PRINTL ナイズリ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 56
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = ナイズリ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 750
ELSEIF ABL:技巧 == 1
	LOCAL = 1050
ELSEIF ABL:技巧 == 2
	LOCAL = 1450
ELSEIF ABL:技巧 == 3
	LOCAL = 2000
ELSEIF ABL:技巧 == 4
	LOCAL = 2500
ELSE
	LOCAL = 3000
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_2(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF TALENT:PLAYER:オトコ || TALENT:PLAYER:ふたなり
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 20
DOWNBASE:1 += 250

SOURCE:主導権 = 800
SOURCE:屈従 = 2000
SOURCE:逸脱 = 1200

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 336
	SOURCE:達成感 = 120
	SOURCE:不潔 = 400
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 400
	SOURCE:達成感 = 240
	SOURCE:不潔 = 300
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 464
	SOURCE:達成感 = 480
	SOURCE:不潔 = 150
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 528
	SOURCE:達成感 = 720
	SOURCE:不潔 = 50
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 592
	SOURCE:達成感 = 1200
	SOURCE:不潔 = 20
ELSE
	SOURCE:性行動 = 656
	SOURCE:達成感 = 1760
	SOURCE:不潔 = 0
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL:1 = 80
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:1 = 160
ELSEIF ABL:Ｂ感覚 == 2
	LOCAL:1 = 320
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:1 = 640
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:1 = 960
ELSE
	LOCAL:1 = 1200
ENDIF

;Ｂ敏感、Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:1 , 0.70
ENDIF
;淫乳
IF TALENT:淫乳
	TIMES LOCAL:1 , 1.20
ENDIF

SOURCE:快Ｂ += LOCAL:1
;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)
LOCAL:2 = TFLAG:91
;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 6.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 4.50
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 3.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成感 , 9.00
		TIMES SOURCE:屈従, 2.00
	ELSE
		SOURCE:中毒充足 = 12000
		TIMES SOURCE:達成感 , 15.00
		TIMES SOURCE:屈従, 1.50
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_56
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;奴隷の胸⇔調教者のＰの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:50 += 1

;奉仕快楽経験入手判定
TFLAG:100 |= 1
;調教対象のパイズリ実行回数
CFLAG:パイズリした回数 += 1
;調教者のパイズリ堪能回数
CFLAG:PLAYER:パイズリされた回数 += 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_56
IF ASSIPLAY
	PRINTFORML %CALLNAME:TARGET%に%CALLNAME:PLAYER%のペニスを胸で扱かせて奉仕するように命令してみたが拒否された。
ELSE
	PRINTFORML %CALLNAME:TARGET%に自分のペニスを胸で扱かせて奉仕するように命令してみたが拒否された。
ENDIF
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_56
;前回もナイズリだった場合
IF PREVCOM == 56
	;対象が勝手にした場合
	IF TFLAG:699 > 0
		PRINTFORML %CALLNAME:TARGET%は、相変わらずナイズリを続けている
		PRINTL どうやら、やめられないようだ…
	ELSE
		PRINTFORM %CALLNAME:TARGET%は、引き続き
		IF TALENT:オトコ
			PRINT 厚い胸板で懸命にペニスを
		ELSE
			PRINT 薄い胸で懸命にペニスを
		ENDIF
		PRINTL 擦り続けている…
	ENDIF
ELSE
	;対象が勝手にした場合
	IF TFLAG:699 > 0
		PRINTFORM %CALLNAME:MASTER%が何もしないでいると、命令していないのに%CALLNAME:TARGET%は自分から
		SIF TALENT:淫乱 || TEQUIP:媚薬 == 1
			PRINT 我慢できないと言った表情で
		SIF TALENT:サド
			PRINTFORM 煽るような視線を%CALLNAME:MASTER%に送ると
		SIF TALENT:嫉妬
			PRINT 嫉妬の念をぶつける様に
		SIF TALENT:狂気 && TEQUIP:口装着器具 == 0
			PRINT 壊れた様な笑い声を上げ、
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:恋慕
			PRINT うっとりとした表情をしながら、
		ELSEIF TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0 && MARK:反発刻印 > 1
			PRINT いかにも嫌そうな表情を見せながら
		ENDIF
	ENDIF
	IF TALENT:オトコ
		PRINT 懸命にペニスに厚い胸板を
	ELSE
		PRINT 懸命にペニスに薄い胸を
	ENDIF
	PRINTFORML 押しあて、擦り付けている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_56, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;オトコ
IF TALENT:オトコ
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%({LOCAL:99})
ENDIF
;オトコ同士の場合はさらにマイナス(ただし、BLっ気あればむしろプラス)
IF TALENT:PLAYER:オトコ && TALENT:オトコ
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓のLOCAL:99の目安(手淫5, フェラ/A愛撫強制10)
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%同士({LOCAL:99})
	IF ABL:ＢＬっ気
		LOCAL:99 = ABL:ＢＬっ気
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF
;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;貧乳
IF TALENT:貧乳
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:111%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;淫乳
IF TALENT:淫乳
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:174%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	PRINTFORM ({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 35

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_56
;デフォルト射精箇所タイプを設定
TFLAG:703 = 6
;射精箇所選択の可・不可
TFLAG:704 = 1

