;=============================================================================
;座薬(膣)
;=============================================================================
;薬毒耐性持ちだと効果が減弱(挿入時は除く)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE64
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:64 > 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:坐剤 == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;処女はダメ
SIF TALENT:処女 == 1
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 64) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM64
IF TEQUIP:坐剤 & 2
	PRINTL 座薬(膣)追加挿入

	;戦闘判定
	CALL COM_TRY_BATTLE, 64
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 座薬(膣)追加挿入
ELSE
	PRINTL 座薬(膣)挿入

	;戦闘判定
	CALL COM_TRY_BATTLE, 64
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 座薬(膣)挿入
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;調合知識があれば消費を抑える
IF TALENT:MASTER:調合知識 || (ASSI >= 0 && TALENT:ASSI:調合知識)
	DOWNBASE:0 += 6
	DOWNBASE:1 += 22
ELSE
	DOWNBASE:0 += 11
	DOWNBASE:1 += 60
ENDIF

SOURCE:露出 = 350
SOURCE:逸脱 = 225

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:屈従 = 50
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 50
	SOURCE:屈従 = 75
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 100
	SOURCE:屈従 = 150
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 250
	SOURCE:屈従 = 300
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 400
	SOURCE:屈従 = 500
ELSE
	SOURCE:快Ｖ = 750
	SOURCE:屈従 = 800
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	SOURCE:痛み = 500
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:屈従 , 0.20
ELSEIF EXP:Ｖ経験 < EXPLV:2
	SOURCE:痛み = 400
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:屈従 , 0.50
ELSEIF EXP:Ｖ経験 < EXPLV:3
	SOURCE:痛み = 300
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF EXP:Ｖ経験 < EXPLV:4
	SOURCE:痛み = 200
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:5
	SOURCE:痛み = 100
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:屈従 , 1.60
ELSE
	SOURCE:痛み = 50
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:屈従 , 1.80
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	TIMES SOURCE:痛み , 3.00
	TIMES SOURCE:屈従 , 0.10
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:痛み , 2.00
	TIMES SOURCE:屈従 , 0.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:痛み , 0.10
	TIMES SOURCE:屈従 , 2.00
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.30
	TIMES SOURCE:屈従 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:屈従 , 1.30
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:屈従 , 1.60
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ敏感
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ鈍感
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念（ただし処女にはできないようにするので死にIF）
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

TEQUIP:坐剤 |= 2

TCVAR:Ｖ経験 += 2

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;坐剤1つ消費
CALL USE_PLURAL_ITEM, 63, 1
RETURN 1

;-------------------------------------------------
;膣座薬効果発揮中
;-------------------------------------------------
@EQUIP_COM64
PRINTL ＜膣座薬効果発揮中＞

DOWNBASE:0 += 55
DOWNBASE:1 += 33

LOCAL:1 = 300
LOCAL:2 = 100

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 5
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 10
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 20
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 50
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 100
ELSE
	LOCAL = 180
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ敏感
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:Ｖ鈍感
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF
;薬毒耐性
IF TALENT:薬毒耐性
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:2 , 1.50
ENDIF

SOURCE:快Ｖ += LOCAL
SOURCE:屈従 += LOCAL:1
SOURCE:逸脱 += LOCAL:2

TCVAR:Ｖ経験 += 1

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_64

IF TEQUIP:坐剤 & 2
	PRINTFORML 座薬を押し込まれ、緩んでしまった%CALLNAME:TARGET%の膣に
	PRINTFORML %CALLNAME:PLAYER%はさらに新たな座薬を潜り込ませた
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣に弾丸のような形の坐剤を捻じ込んだ
ENDIF

IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORM %CALLNAME:TARGET%は
	IF EXP:Ｖ経験 < 20
		PRINTL 恐怖に顔を歪め、無駄と知りつつ
		PRINTFORM なんとか%CALLNAME:PLAYER%の手から逃れようと暴れ
	ELSEIF EXP:Ｖ経験 < 40
		PRINT 膣より侵入してくる異物感に屈辱と嫌悪の表情をし
	ELSEIF EXP:Ｖ経験 < 75
		PRINTL 嫌がっていたものの、たいした抵抗もせず
		PRINT おとなしく座薬を挿入され
	ELSEIF EXP:Ｖ経験 < 100
		PRINT 膣を通ったときの刺激が堪らないのか身体を震わせ
	ELSEIF EXP:Ｖ経験 < 140
		PRINT 座薬を入れられる感覚が気に入ったのか表情が緩みきっ
	ELSEIF EXP:Ｖ経験 < 180
		PRINT 薬を挿入した後も腰を振ってさらなる快楽を引き出そうとし
	ELSE
		PRINTL それだけで満足できないのか薬をさらに奥に押し込もうと
		PRINT 自らの指を膣の中に突っ込んで
	ENDIF
	PRINTL ている…
ENDIF
