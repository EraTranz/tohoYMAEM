;=============================================================================
;アナル注射
;=============================================================================
;アナルは鈍感なので痛みも少ないですが、Ａ経験は増えないので
;そういった意味では一番使えないかもしれません
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE82
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:82 > 0
	RETURN 0
;強力アイテム・コマンド無効時はダメ
SIF (FLAG:13 & 2) == 0
	RETURN 0
;注射系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 64) == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;助手調教の場合、技巧4以上か[針さばき]じゃないと使えない
SIF ASSIPLAY && ABL:PLAYER:技巧 < 4 && TALENT:PLAYER:針さばき == 0
	RETURN 0
;注射セットと媚薬を持っていないとダメ
SIF (ITEM:注射セット == 0 || ITEM:媚薬 == 0)
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;ウェディングドレス・ゴスロリ着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20
	RETURN 0
;裸エプロン、スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 1 || TEQUIP:コスチュームプレイ == 2
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 82) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM82
PRINTL アナル注射

;戦闘判定
CALL COM_TRY_BATTLE, 82
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 =アナル注射
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;調合知識があれば消費を抑える
LOCAL:3 = (TALENT:MASTER:調合知識) ? 1 # 0
SIF ASSI >= 0 && TALENT:ASSI:調合知識
	LOCAL:3 = 1

;調教対象に媚薬中毒があれば消費を抑える
IF LOCAL:3
	LOCAL:1 = (TALENT:媚薬中毒) ? 250 # 500
	LOCAL:2 = (TALENT:媚薬中毒) ? 100 # 200
ELSE
	LOCAL:1 = (TALENT:媚薬中毒) ? 350 # 700
	LOCAL:2 = (TALENT:媚薬中毒) ? 150 # 300
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2

SOURCE:快Ａ = 300
SOURCE:中毒充足 = 700
SOURCE:薬物浸潤 = 3300
SOURCE:逸脱 = 4000

;触手でない場合は媚薬を1個使用
IF TEQUIP:触手調教
	TFLAG:90 = 1
ELSE
	CALL USE_PLURAL_ITEM, 61, 1
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ａ , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ａ , 1.80
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ａ , 1.50
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ａ , 1.20
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ａ , 1.00
ELSE
	TIMES SOURCE:快Ａ , 0.80
ENDIF

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	TIMES SOURCE:快Ａ , 0.50
ELSEIF ABL:Ａ感覚 == 1
	TIMES SOURCE:快Ａ , 1.00
ELSEIF ABL:Ａ感覚 == 2
	TIMES SOURCE:快Ａ , 1.50
ELSEIF ABL:Ａ感覚 == 3
	TIMES SOURCE:快Ａ , 2.00
	EX:2 += RAND:2
ELSEIF ABL:Ａ感覚 == 4
	TIMES SOURCE:快Ａ , 2.50
	EX:2 += 1
ELSE
	TIMES SOURCE:快Ａ , 3.00
	EX:2 += 2
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:中毒充足 , 0.80
	TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:中毒充足 , 1.10
	TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:中毒充足 , 1.30
	TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:中毒充足 , 1.50
	TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:中毒充足 , 1.70
	TIMES DOWNBASE:0 , 0.80
ELSE
	TIMES SOURCE:中毒充足 , 2.10
	TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 2700
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 3000
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 3300
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 3700
ELSE
	SOURCE:痛み = 4000
ENDIF

;媚薬効果を発揮
TEQUIP:媚薬 = 1

;反抗的
SIF TALENT:反抗的
	TIMES SOURCE:痛み , 1.10
;抵抗
SIF TALENT:抵抗
	TIMES SOURCE:痛み , 1.10
;媚薬中毒
IF TALENT:媚薬中毒
	SOURCE:中毒充足 += 500
	TIMES SOURCE:快Ａ , 1.50
	TIMES SOURCE:逸脱 , 1.50
	SOURCE:媚薬浸潤 = 300
ELSE
	SOURCE:媚薬浸潤 = 2 + RAND:5
ENDIF
;Ａ敏感
IF TALENT:Ａ敏感
	IF ABL:Ａ感覚 <= 3
		TIMES SOURCE:快Ａ , 1.80
	ELSEIF ABL:Ａ感覚 == 4
		TIMES SOURCE:快Ａ , 2.00
		EX:2 += RAND:2
	ELSE
		TIMES SOURCE:快Ａ , 2.20
		EX:2 += RAND:3
	ENDIF
;Ａ鈍感
ELSEIF TALENT:Ａ鈍感
	IF ABL:Ａ感覚 <= 3
		TIMES SOURCE:快Ａ , 0.70
		EX:2 -= (EX:2 > 1) ? 1 # 0
	ELSEIF ABL:Ａ感覚 == 4
		TIMES SOURCE:快Ａ , 0.75
		EX:2 -= (EX:2 > 1) ? 1 # 0
	ELSE
		TIMES SOURCE:快Ａ , 0.80
	ENDIF
ENDIF

;触手補正
IF TEQUIP:触手調教
	TIMES SOURCE:痛み , 0.30
	TIMES SOURCE:逸脱 , 0.80
	LOCAL:1 -= 200
	DOWNBASE:0 -= LOCAL:1
	DOWNBASE:1 -= 400
ENDIF

;調教者に針さばき
SIF TALENT:PLAYER:針さばき
	TIMES SOURCE:痛み , 0.90

;鈴仙か永琳による使用
IF TEQUIP:触手調教 == 0
	SIF NO:PLAYER == 1028
		TIMES SOURCE:痛み , 0.70
	SIF NO:PLAYER == 1029
		TIMES SOURCE:痛み , 0.50
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;薬物経験
TCVAR:薬物経験 += 2
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_82
PRINTFORM %CALLNAME:TARGET%の
SIF TALENT:Ａ敏感
	PRINT 敏感な
IF ABL:Ａ感覚 >= 4
	PRINT 押し広げられた腸壁に
ELSE
	PRINT アナルの入り口に
ENDIF
PRINTL 注射針を突き立てた

PRINTFORM %CALLNAME:TARGET%は
SIF TFLAG:899 > 0
	PRINT 失神しているにも拘らず、
IF ABL:Ａ感覚 >= 5
	PRINT 体が震えるだけで断続的に絶頂を迎え続けさせられて、
ELSEIF ABL:Ａ感覚 >= 3
	PRINT 敏感な性感帯に変えられたアナルの快楽に打ちのめされて、
ELSE
	PRINT 入り口から腹の奥へと流れていく様な、甘い痺れの奔流に
ENDIF
IF PALAM:潤滑 >= PALAMLV:5 || TALENT:Ｃ敏感
	PRINT お漏らしのように愛液と腸液を溢れさせながら
ELSEIF PALAM:潤滑 >= PALAMLV:3
	PRINT 舌を突き出して目を上向かせながら
ELSE
	PRINT こみ上げて来るどす黒い愉悦に
ENDIF
SIF TFLAG:899 > 0
	PRINT 無意識のうちに
IF PALAM:欲情 >= PALAMLV:5
	PRINT 下品な呻き声を上げて痙攣し
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINT 獣じみた声を上げてのた打ち回っ
ELSE
	PRINT ガチガチと歯を鳴らしながら、悶え
ENDIF
PRINTL ている…
