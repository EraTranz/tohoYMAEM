;=============================================================================
;バスト注射
;=============================================================================
;噴乳どうしようか考え中。
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE83
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:83 > 0
	RETURN 0
;強力アイテム・コマンド無効時はダメ
SIF (FLAG:13 & 2) == 0
	RETURN 0
;注射系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 64) == 0
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;助手調教の場合、技巧4以上か[針さばき]じゃないと使えない
SIF ASSIPLAY && ABL:PLAYER:技巧 < 4 && TALENT:PLAYER:針さばき == 0
	RETURN 0
;注射セットと媚薬を持っていないとダメ
SIF (ITEM:注射セット == 0 || ITEM:媚薬 == 0)
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;ウェディングドレス・ゴスロリ着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20
	RETURN 0
;裸エプロン、スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 1 || TEQUIP:コスチュームプレイ == 2
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 83) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM83
PRINTL バスト注射

;戦闘判定
CALL COM_TRY_BATTLE, 83
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = バスト注射
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;調合知識があれば消費を抑える
LOCAL:3 = (TALENT:MASTER:調合知識) ? 1 # 0
SIF ASSI >= 0 && TALENT:ASSI:調合知識
	LOCAL:3 = 1

;調教対象に媚薬中毒があれば消費を抑える
IF LOCAL:3
	LOCAL:1 = (TALENT:媚薬中毒) ? 375 # 750
	LOCAL:2 = (TALENT:媚薬中毒) ? 300 # 600
ELSE
	LOCAL:1 = (TALENT:媚薬中毒) ? 500 # 1000
	LOCAL:2 = (TALENT:媚薬中毒) ? 400 # 800
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2

SOURCE:快Ｂ = 500
SOURCE:中毒充足 = 500
SOURCE:薬物浸潤 = 2600
SOURCE:逸脱 = 3000

;触手でない場合は媚薬を1個使用
IF TEQUIP:触手調教
	TFLAG:90 = 1
ELSE
	CALL USE_PLURAL_ITEM, 61, 1
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:逸脱 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:逸脱 , 1.80
ELSEIF ABL:従順 == 2
	TIMES SOURCE:逸脱 , 1.50
ELSEIF ABL:従順 == 3
	TIMES SOURCE:逸脱 , 1.20
ELSEIF ABL:従順 == 4
	TIMES SOURCE:逸脱 , 1.00
ELSE
	TIMES SOURCE:逸脱 , 0.80
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	TIMES SOURCE:快Ｂ , 0.50
ELSEIF ABL:Ｂ感覚 == 1
	TIMES SOURCE:快Ｂ , 1.00
ELSEIF ABL:Ｂ感覚 == 2
	TIMES SOURCE:快Ｂ , 1.50
ELSEIF ABL:Ｂ感覚 == 3
	TIMES SOURCE:快Ｂ , 2.00
	EX:3 += RAND:2
ELSEIF ABL:Ｂ感覚 == 4
	TIMES SOURCE:快Ｂ , 2.50
	EX:3 += 1
ELSE
	TIMES SOURCE:快Ｂ , 3.00
	EX:3 += 2
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:中毒充足 , 0.80
	TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:中毒充足 , 1.10
	TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:中毒充足 , 1.30
	TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:中毒充足 , 1.50
	TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:中毒充足 , 1.70
	TIMES DOWNBASE:0 , 0.80
ELSE
	TIMES SOURCE:中毒充足 , 2.10
	TIMES DOWNBASE:0 , 0.70
ENDIF

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 3000
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 3300
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 3600
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 4000
ELSE
	SOURCE:痛み = 4500
ENDIF

;媚薬効果を発揮
TEQUIP:媚薬 = 1

;反抗的
SIF TALENT:反抗的
	TIMES SOURCE:痛み , 1.10
;抵抗
SIF TALENT:抵抗
	TIMES SOURCE:痛み , 1.10
;媚薬中毒
IF TALENT:媚薬中毒
	SOURCE:中毒充足 += 500
	TIMES SOURCE:快Ｂ , 1.50
	TIMES SOURCE:逸脱 , 1.50
	SOURCE:媚薬浸潤 = 300
ELSE
	SOURCE:媚薬浸潤 = 2 + RAND:5
ENDIF
;Ｂ敏感
IF TALENT:Ｂ敏感
	IF ABL:Ｂ感覚 <= 3
		TIMES SOURCE:快Ｂ , 1.80
	ELSEIF ABL:Ｂ感覚 == 4
		TIMES SOURCE:快Ｂ , 2.00
		EX:3 += RAND:2
	ELSE
		TIMES SOURCE:快Ｂ , 2.20
		EX:3 += RAND:3
	ENDIF
;Ｂ鈍感
ELSEIF TALENT:Ｂ鈍感
	IF ABL:Ｂ感覚 <= 3
		TIMES SOURCE:快Ｂ , 0.70
		EX:3 -= (EX:3 > 1) ? 1 # 0
	ELSEIF ABL:Ｂ感覚 == 4
		TIMES SOURCE:快Ｂ , 0.75
		EX:3 -= (EX:3 > 1) ? 1 # 0
	ELSE
		TIMES SOURCE:快Ｂ , 0.80
	ENDIF
ENDIF

;触手補正
IF TEQUIP:触手調教
	TIMES SOURCE:痛み , 0.30
	TIMES SOURCE:逸脱 , 0.80
	LOCAL:1 -= 300
	DOWNBASE:0 -= LOCAL:1
	DOWNBASE:1 -= 400
ENDIF

;調教者に針さばき
SIF TALENT:PLAYER:針さばき
	TIMES SOURCE:痛み , 0.90
;調教者に幼稚
SIF TALENT:PLAYER:幼稚
	TIMES SOURCE:痛み , 1.20

;鈴仙か永琳による使用
IF TEQUIP:触手調教 == 0
	SIF NO:PLAYER == 1028
		TIMES SOURCE:痛み , 0.70
	SIF NO:PLAYER == 1029
		TIMES SOURCE:痛み , 0.50
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;薬物経験
TCVAR:薬物経験 += 2

;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_83
PRINTFORM %CALLNAME:TARGET%の
SIF TALENT:Ｂ敏感
	PRINT 敏感な
IF TALENT:絶壁 || TALENT:貧乳
	PRINT 殆ど膨らみの感じられない
ELSEIF TALENT:巨乳 || TALENT:爆乳
	PRINT 大きく揺れる
ENDIF
IF ABL:Ｂ感覚 >= 4
	PRINT 乳首の先端に
ELSE
	PRINT 真っ白な胸に
ENDIF
PRINTL 注射針を突き立てた
PRINTFORM %CALLNAME:TARGET%は
SIF TFLAG:899 > 0
	PRINT 失神しているにも拘らず、
IF ABL:Ｂ感覚 >= 5
	PRINT 頭の中まで爛れるような激しい快楽に、
ELSEIF ABL:Ｂ感覚 >= 3 && ABL:Ｂ感覚 <= 4
	PRINT 注入された媚薬によって徐々に広がってくる愉悦に、
ELSE
	PRINT 鋭い痛みと、こみ上げてくるどうしようも無い熱さに、
ENDIF
SIF TFLAG:899 > 0
	PRINT 無意識のうちに
IF TALENT:母乳体質
	PRINT 大量の母乳を噴出しながら
ELSE
	PRINT 口をパクパクさせながら
ENDIF
IF PALAM:欲情 >= PALAMLV:5 && TEQUIP:緊縛関連 == 0
	PRINT 涎を垂らして、自ら弄り回し
ELSEIF PALAM:欲情 >= PALAMLV:3 && PALAM:欲情 < PALAMLV:5
	PRINT 下半身から崩れ落ちて腰を振り乱し
ELSE
	PRINT 体を弛緩させたまま震え
ENDIF
PRINTL ている…
