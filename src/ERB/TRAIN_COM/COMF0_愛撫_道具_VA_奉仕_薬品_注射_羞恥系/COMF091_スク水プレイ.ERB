;=============================================================================
;スク水プレイ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE91
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:91 > 0
	RETURN 0
;羞恥プレイ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 512) == 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 1
;アイテム｢スクール水着｣を持っていないとダメ
SIF ITEM:スクール水着 == 0
	RETURN 0
;[オトコ]の場合、陥落していないとダメ
SIF TALENT:オトコ && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
	RETURN 0
;従順が3以上ないとダメ
SIF ABL:従順 < 3
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;緊縛中はダメ
SIF TEQUIP:緊縛関連
	RETURN 0
;アイマスク装着中はダメ
SIF TEQUIP:目装着器具
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;お医者さんプレイ中はダメ
SIF TEQUIP:お医者さんプレイ
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;裸エプロン中はダメ
SIF TEQUIP:コスチュームプレイ == 1
	RETURN 0
;コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ > 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 91) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM91
PRINTL スク水プレイ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 91
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = スク水プレイ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 40

LOCAL:1 = 500
LOCAL:2 = 500

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL:1 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL:1 , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL:1 , 1.10
ELSE
	TIMES LOCAL:1 , 1.20
ENDIF
;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:中毒充足 += 0
	SOURCE:液体追加 += 0
	TIMES LOCAL:1, 1.00
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 += 50
	SOURCE:液体追加 += 50
	TIMES LOCAL:1, 1.20
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 += 100
	SOURCE:液体追加 += 100
	TIMES LOCAL:1, 1.40
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 += 200
	SOURCE:液体追加 += 200
	TIMES LOCAL:1, 1.60
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 += 400
	SOURCE:液体追加 += 400
	TIMES LOCAL:1, 2.00
ELSE
	SOURCE:中毒充足 += 800
	SOURCE:液体追加 += 800
	TIMES LOCAL:1, 3.00
ENDIF
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 2.00
ELSE
	TIMES LOCAL:1 , 3.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:2 , 2.00
;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES LOCAL:1 , 1.50
;解放
SIF TALENT:解放
	TIMES LOCAL:1 , 1.50
;恥じらい
SIF TALENT:恥じらい
	TIMES LOCAL:2 , 2.00
;巨乳
IF TALENT:巨乳
	SOURCE:痛み = 500
;爆乳
ELSEIF TALENT:爆乳
	SOURCE:痛み = 800
ENDIF
;小柄体型
IF TALENT:小柄体型
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ENDIF
;幼児、幼稚、幼児退行
SIF TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行
	TIMES LOCAL:1 , 2.00

SOURCE:露出 += LOCAL:1
SOURCE:屈従 += LOCAL:1 /2
SOURCE:逸脱 += LOCAL:2

;オトコ
IF TALENT:オトコ
	SOURCE:痛み += 1600
	SOURCE:羞恥扇情 += LOCAL:1 /2
	TIMES SOURCE:逸脱 , 2.50
;絶壁
ELSEIF TALENT:絶壁
	SOURCE:羞恥扇情 += LOCAL:1 /4
;貧乳
ELSEIF TALENT:貧乳
	SOURCE:羞恥扇情 += LOCAL:1 /6
;普通以上のおっぱいの女
ELSE
	SOURCE:羞恥扇情 += LOCAL:1 /8
ENDIF

SOURCE:トラウマ += LOCAL:2 /5
;幼児、幼稚、幼児退行
SIF TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行
	SOURCE:トラウマ += SOURCE:逸脱/2

;スク水の着脱
IF TEQUIP:コスチュームプレイ == 2
	TEQUIP:コスチュームプレイ = 0
ELSE
	TEQUIP:コスチュームプレイ = 2
	;調教対象のコスプレ経験
	CFLAG:コスプレ実行回数 += 1
ENDIF
RETURN 1

;-------------------------------------------------
;スク水プレイ中
;-------------------------------------------------
@EQUIP_COM91
PRINTL ＜スク水プレイ中＞

DOWNBASE:0 += 0
DOWNBASE:1 += 20

LOCAL = 500
LOCAL:1 = 500

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF
;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:中毒充足 += 0
	SOURCE:液体追加 += 0
	TIMES LOCAL, 1.00
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 += 50
	SOURCE:液体追加 += 50
	TIMES LOCAL, 1.20
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 += 100
	SOURCE:液体追加 += 100
	TIMES LOCAL, 1.40
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 += 200
	SOURCE:液体追加 += 200
	TIMES LOCAL, 1.60
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 += 400
	SOURCE:液体追加 += 400
	TIMES LOCAL, 2.00
ELSE
	SOURCE:中毒充足 += 800
	SOURCE:液体追加 += 800
	TIMES LOCAL, 3.00
ENDIF
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 3.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 2.00
;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES LOCAL , 1.50
;解放
SIF TALENT:解放
	TIMES LOCAL , 1.50
;恥じらい
SIF TALENT:恥じらい
	TIMES LOCAL:1 , 2.00
;巨乳
IF TALENT:巨乳
	SOURCE:痛み += 250
;爆乳
ELSEIF TALENT:爆乳
	SOURCE:痛み += 400
ENDIF
;小柄体型
IF TALENT:小柄体型
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
ENDIF
;幼児、幼稚、幼児退行
SIF TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行
	TIMES LOCAL , 2.00

SOURCE:露出 += LOCAL
SOURCE:屈従 += LOCAL/2
SOURCE:逸脱 += LOCAL:1

;オトコ
IF TALENT:オトコ
	SOURCE:痛み += 800
	SOURCE:羞恥扇情 += LOCAL/2
	TIMES SOURCE:逸脱 , 2.50
;絶壁
ELSEIF TALENT:絶壁
	SOURCE:羞恥扇情 += LOCAL/4
;貧乳
ELSEIF TALENT:貧乳
	SOURCE:羞恥扇情 += LOCAL/6
ELSE
	SOURCE:羞恥扇情 += LOCAL/8
ENDIF

SOURCE:トラウマ += LOCAL:1 /5
;幼児、幼稚、幼児退行
SIF TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行
	SOURCE:トラウマ += LOCAL:1 /20

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;露出快楽経験入手判定
TFLAG:100 |= 2
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:露出癖 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_91
IF TEQUIP:コスチュームプレイ == 2
	PRINTFORML %CALLNAME:TARGET%に着ているスクール水着を脱ぐように命じたが拒否された。
	PRINTL もう少し調教が必要なようだ。
ELSE
	PRINTFORML %CALLNAME:TARGET%にスクール水着を着けるように命じたが拒否された。
	PRINTL もう少し調教が必要なようだ。
ENDIF

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_91
IF TEQUIP:コスチュームプレイ == 2
	IF MARK:反発刻印 > 1
		PRINTFORML %CALLNAME:PLAYER%が近づく前に、%CALLNAME:TARGET%は着けているスクール水着を脱ぎ捨ててしまった……
	ELSEIF TALENT:相愛 && ASSIPLAY == 0
		PRINTFORML %CALLNAME:PLAYER%が、%CALLNAME:TARGET%が着けているスクール水着を脱がせていくと、
		PRINTFORML %CALLNAME:TARGET%は幾分赤い顔をしつつ、その肢体を押し付けてきた…
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%が着けているスクール水着を脱がせた……
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%にスクール水着を着けるよう命じた
	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:恥じらい
		PRINTFORML 顔を真っ赤にしつつ%CALLNAME:PLAYER%の命令を実行している……
	ELSEIF TALENT:絶壁 || TALENT:貧乳
		PRINTL 水着を着けたまではよかったものの、
		PRINTL 自分の胸の貧相さを意識しているのかしきりに胸を隠す動作を繰り返している……
	ELSEIF TALENT:巨乳 || TALENT:爆乳
		PRINTL 水着を着けたまではよかったものの、
		PRINTL 胸回りが窮屈に感じるのか胸元に手を伸ばしては布地を引っ張る動作を頻繁に繰り返している……
	ELSEIF TALENT:幼児 || TALENT:幼稚
		PRINTL 水着を着けると子供のようにはしゃぎ始めた
	ELSEIF TALENT:倒錯的 || TALENT:淫乱
		PRINTL 一瞬躊躇するそぶりを見せたものの、
		PRINTL 早くも新たな快感に目覚めたのかいそいそと着替え始めた……
	ELSEIF TALENT:相愛 || TALENT:隷属
		PRINTFORML %CALLNAME:MASTER%が全身を堪能できるようにくるりと回りながら水着を着けていった……
	ELSEIF MARK:反発刻印 > 1
		PRINTL 露骨に嫌悪感を浮かべつつも命令通り水着を着けていく……
	ELSE
		PRINTL 心底呆れたような表情を浮かべながらも命令通り水着を着けていく……
	ENDIF
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_91, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:露出癖
IF ABL:露出癖
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:露出癖
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:自慰中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:30%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;自制心
IF TALENT:自制心
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;恥薄い
ELSEIF TALENT:恥薄い
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:60%({LOCAL:99})
ENDIF
;快感に素直
IF TALENT:快感に素直
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;快感の否定
ELSEIF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;オトコ
IF TALENT:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 15
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;難易度上昇
;ビデオまたは野外で+10、羞恥プレイ中で+7、Ｃ系道具またはカテーテル系装着で＋3、
;バイブ系やアナル系の道具で+5、拡張系道具装着でさらに＋3
TFLAG:241 = 27
SIF TEQUIP:ビデオ撮影 || TEQUIP:野外プレイ
	TFLAG:241 += 10
SIF TEQUIP:羞恥プレイ
	TFLAG:241 += 7
SIF TEQUIP:Ｃ系装着器具 || TEQUIP:Ｕ系装着器具
	TFLAG:241 += 3
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	TFLAG:241 += 5
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	TFLAG:241 += 5
SIF TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 5 || TEQUIP:Ｖ系装着器具 == 6 || TEQUIP:Ｖ系装着器具 == 8 || TEQUIP:Ａ系装着器具 == 2 || TEQUIP:Ａ系装着器具 == 5 || TEQUIP:Ａ系装着器具 == 6 || TEQUIP:Ａ系装着器具 == 8 || TEQUIP:氷柱 > 2
	TFLAG:241 += 3
