;=============================================================================
;ビデオカメラ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE100
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:100 > 0
	RETURN 0
;強力アイテム・コマンド無効時はダメ
SIF (FLAG:13 & 2) == 0
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:ビデオ撮影
	RETURN 1
;助手が使う場合、「あなた」か、[家電使役知識]か[工作名人]か[報道者]のいずれかを取得している、撮影経験＋工作経験＋教育経験の合計が200以上なら問題なし
IF ASSIPLAY && NO:PLAYER != 1000 && TALENT:PLAYER:家電使役知識 == 0 && TALENT:PLAYER:工作名人 == 0 && TALENT:PLAYER:報道者 == 0 && (EXP:PLAYER:撮影経験 + EXP:PLAYER:工作経験 + EXP:PLAYER:教育経験) < 200
	;主人が「あなた」か、[家電使役知識]か[工作名人]か[報道者]のいずれかを取得している、撮影経験＋工作経験＋教育経験の合計が200以上なら助手に使い方を教えられる
	SIF NO:MASTER != 1000 && TALENT:MASTER:家電使役知識 == 0 && TALENT:MASTER:工作名人 == 0 && TALENT:MASTER:報道者 == 0 && (EXP:MASTER:撮影経験 + EXP:MASTER:工作経験 + EXP:MASTER:教育経験) < 200
		RETURN 0
	;この場合、技巧が3以上必要
	SIF ABL:PLAYER:技巧 < 3
		RETURN 0
ENDIF
;アイテム｢ビデオカメラ｣と｢ビデオテープ｣を持っていないとダメ
SIF (ITEM:ビデオカメラ == 0 || ITEM:ビデオテープ == 0)
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 100) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM100
PRINTL ビデオカメラ

;戦闘判定
CALL COM_TRY_BATTLE, 100
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ビデオカメラ
PRINTFORML ★★★ビデオ撮影を\@(TEQUIP:ビデオ撮影) ? 終了 # 開始\@します★★★

;ビデオ撮影がされていれば終了させる
IF TEQUIP:ビデオ撮影
	TEQUIP:ビデオ撮影 = 0
	;テープは終了時に数を減らす
	CALL USE_PLURAL_ITEM, 75, 1
;ビデオ撮影してなければ初期化して開始
ELSE
	DOWNBASE:1 += 10
	TEQUIP:ビデオ撮影 = 1
	TFLAG:121 = 0
	;カウントは10回とします
	REPEAT 10
		FLAG:(COUNT+40) = 0
	REND
	;コンフィグで撮影拡張の機能を有効にしているかどうかで変化
	IF FLAG:15 & 128 && ASSI >= 0
		CALL SET_VIDEO
	ELSE
		FLAG:61 = 0
	ENDIF
	TFLAG:140 |= 128
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM
RETURN 1

;-------------------------------------------------
;撮影者設定(撮影拡張有効時限定)
;-------------------------------------------------
@SET_VIDEO
PRINTL 撮影者を選択してください。途中変更はできません
PRINTL （撮影者を変更する場合、一度撮影を終了してください）
PRINTL 
PRINTFORML [0] %CALLNAME:MASTER%
PRINTFORML [1] %CALLNAME:ASSI%
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 0
	FLAG:61 = 1
	PRINTFORML 撮影者は %CALLNAME:MASTER% です
ELSEIF RESULT == 1
	FLAG:61 = 2
	PRINTFORML 撮影者は %CALLNAME:ASSI% です
ELSE
	CLEARLINE 1
	REUSELASTLINE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;ビデオ撮影中
;-------------------------------------------------
@EQUIP_COM100
;カウントは10回とします
LOCAL:5 = 10

IF TFLAG:121 == 0
;初回はカウントに入れない
	TFLAG:121 += 1
ELSEIF TFLAG:121 > 0 && TFLAG:121 <= LOCAL:5
	LOCAL = TFLAG:121 + 39
	IF TFLAG:122 >= 1
		FLAG:LOCAL = TFLAG:122
		TFLAG:122 = 0
	ELSE
		FLAG:LOCAL = SELECTCOM
	ENDIF
	;TODO 装備状態や助手による調教の記録をどうするか？
	PRINTFORML ＜ビデオ撮影中{TFLAG:121}/{LOCAL:5}＞
	TFLAG:121 += 1

	;パラメータ変化
	;TODO バランス調整(ビデオによるパラメータ変化は考慮しないか？)
	DOWNBASE:0 += 0
	DOWNBASE:1 += 50

	;撮影者は気を使う
	IF FLAG:61 == 1
		DOWNBASE:MASTER:1 += 5
	ELSEIF FLAG:61 == 2
		DOWNBASE:ASSI:1 += 5
	ENDIF

	LOCAL:1 = 370
	LOCAL:2 = 1750
	LOCAL:3 = 700

	;PALAM:欲情をみる
	IF PALAM:欲情 < PALAMLV:1
		TIMES LOCAL:1 , 0.80
	ELSEIF PALAM:欲情 < PALAMLV:2
		TIMES LOCAL:1 , 0.90
	ELSEIF PALAM:欲情 < PALAMLV:3
		TIMES LOCAL:1 , 1.00
	ELSEIF PALAM:欲情 < PALAMLV:4
		TIMES LOCAL:1 , 1.10
	ELSE
		TIMES LOCAL:1 , 1.20
	ENDIF

	;ABL:従順をみる
	IF ABL:従順 == 0
		TIMES LOCAL:1 , 0.40
	ELSEIF ABL:従順 == 1
		TIMES LOCAL:1 , 0.60
	ELSEIF ABL:従順 == 2
		TIMES LOCAL:1 , 0.80
	ELSEIF ABL:従順 == 3
		TIMES LOCAL:1 , 1.00
	ELSEIF ABL:従順 == 4
		TIMES LOCAL:1 , 1.10
	ELSE
		TIMES LOCAL:1 , 1.20
	ENDIF

	;ABL:露出癖をみる
	IF ABL:露出癖 == 0
		TIMES LOCAL:1 , 0.80
		TIMES LOCAL:3 , 2.00
	ELSEIF ABL:露出癖 == 1
		TIMES LOCAL:1 , 1.00
		TIMES LOCAL:3 , 1.70
	ELSEIF ABL:露出癖 == 2
		TIMES LOCAL:1 , 1.30
		TIMES LOCAL:3 , 1.40
	ELSEIF ABL:露出癖 == 3
		TIMES LOCAL:1 , 1.60
		TIMES LOCAL:3 , 1.00
	ELSEIF ABL:露出癖 == 4
		TIMES LOCAL:1 , 2.00
		TIMES LOCAL:3 , 0.80
	ELSE
		TIMES LOCAL:1 , 3.00
		TIMES LOCAL:3 , 0.60
	ENDIF

	;倒錯的
	SIF TALENT:倒錯的
		TIMES LOCAL:1 , 2.00
	;目立ちたがり
	SIF TALENT:目立ちたがり
		TIMES LOCAL:1 , 1.50
	;臆病
	SIF TALENT:臆病
		TIMES LOCAL:3 , 1.70

	SOURCE:欲情追加 += LOCAL:1
	SOURCE:液体追加 += LOCAL:1
	SOURCE:露出 += LOCAL:2
	SOURCE:トラウマ += LOCAL:3
	SOURCE:逸脱 += LOCAL:3
ENDIF

;テープ残量がなくなると強制的にEQUIPフラグを外す
IF TFLAG:121 <= 0 || TFLAG:121 > LOCAL:5
	TEQUIP:ビデオ撮影 = 0
	CALL USE_PLURAL_ITEM, 75, 1
	PRINTL ＜テープの残量がなくなりました。ビデオ撮影を終了します＞
ENDIF

;露出快楽経験入手判定
TFLAG:100 |= 2
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:露出癖 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_100
;撮影開始
IF TEQUIP:ビデオ撮影
	;ビデオ拡張OFF
	IF FLAG:61 == 0
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の恥ずかしい姿をビデオとして撮っておくことにした…
	;撮影者が主人
	ELSEIF FLAG:61 == 1
		PRINTFORML %CALLNAME:MASTER%は自ら%CALLNAME:TARGET%への調教風景をビデオに撮ることにした…
	;撮影者が助手
	ELSE
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ASSI%に、%CALLNAME:TARGET%への調教風景をビデオにしっかり撮るように命令した…
	ENDIF
;撮影終了
ELSE
	;ビデオ拡張OFF
	IF FLAG:61 == 0
		PRINTFORML %CALLNAME:PLAYER%はビデオ撮影をやめた…
		;撮影者が主人だった
	ELSEIF FLAG:61 == 1
		PRINTFORML %CALLNAME:MASTER%はビデオ撮影をやめることにした…
	;撮影者が助手だった
	ELSE
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ASSI%にビデオ撮影をやめるように命じた…
	ENDIF
ENDIF
