;=============================================================================
;自慰見せつけ
;=============================================================================
;殆ど手淫に手加えただけだよ！
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE115
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:115 > 0
	RETURN 0
;羞恥プレイ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 512) == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;助手が褌着用中の場合助手に技巧4以上必須
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17 && ABL:ASSI:技巧 < 4
	RETURN 0
;眼隠しと耳栓両方つけていた場合は意味がないので実行できない
SIF TEQUIP:目装着器具 && TEQUIP:耳装着器具
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 115) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM115
;………………………………………………
;コマンドの派生判定
;………………………………………………
;前回と今回の調教者同じかつ、前回が自慰もしくは相互自慰の場合、相互自慰へ
IF (PREVCOM == 7 || PREVCOM == 628) && JUDGE_TRAINER_CONSECUTIVE() == 1
	SIF EXCOM_ABLE628() == 1
		JUMP COM628
ENDIF

PRINTL 自慰見せつけ

;戦闘判定
CALL COM_TRY_BATTLE, 115
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 自慰見せつけ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL =0

;プレイヤーの技巧をみる
IF ABL:PLAYER:技巧 == 0
	LOCAL = 800
ELSEIF ABL:PLAYER:技巧 == 1
	LOCAL = 1600
ELSEIF ABL:PLAYER:技巧 == 2
	LOCAL = 2400
ELSEIF ABL:PLAYER:技巧 == 3
	LOCAL = 3200
ELSEIF ABL:PLAYER:技巧 == 4
	LOCAL = 4000
ELSE
	LOCAL = 4800
ENDIF

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_2(PLAYER, LOCAL)

;Ｃ感覚の上げられない主人かつオトコの場合2倍(ということにしておく＆それでも10000には届かないんだぜウフフ)
SIF ASSIPLAY == 0 && TALENT:PLAYER:オトコ
	TIMES LOCAL , 2.00

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 10

SOURCE:露出 = 500
SOURCE:不潔 = 100
SOURCE:逸脱 = 400

;ABL:自慰中毒をみる
IF ABL:自慰中毒 == 0
	SOURCE:性行動 = 25
	SOURCE:屈従 = 100
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:自慰中毒 == 1
	SOURCE:性行動 = 50
	SOURCE:屈従 = 80
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:自慰中毒 == 2
	SOURCE:性行動 = 75
	SOURCE:屈従 = 70
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:自慰中毒 == 3
	SOURCE:性行動 = 100
	SOURCE:屈従 = 60
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:自慰中毒 == 4
	SOURCE:性行動 = 125
	SOURCE:屈従 = 50
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 150
	SOURCE:屈従 = 0
	TIMES SOURCE:不潔 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:欲情追加 = 25
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:欲情追加 = 50
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:欲情追加 = 100
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:欲情追加 = 200
ELSE
	SOURCE:欲情追加 = 400
ENDIF

;-------------------------------------------------
;読心の場合エロい気分が移るのか見せつけ効果アップ（2倍）
;-------------------------------------------------
;キャラ専用コマンドがオンの場合、対象が[恋慕]と心の目を閉ざしていない[覚]持ちだと変化
IF TALENT:覚 == 1 && TALENT:恋慕 && (FLAG:15 & 16384)
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:欲情追加 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 600
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_115
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;調教者の指⇔調教者のＰの汚れが移動
IF EXIST_PENIS(PLAYER)
	STAIN:PLAYER:手 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:PLAYER:手
;調教者の指⇔調教者のＶの汚れが移動
ELSE
	STAIN:PLAYER:ヴァギナ |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:PLAYER:ヴァギナ
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2

;自慰経験
TCVAR:PLAYER:自慰経験 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_115
;恋慕さとりんに見せ付けるとさとりんも大興奮
IF TALENT:覚 == 1 && TALENT:恋慕 && (FLAG:15 & 16384)
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 自慰行為で盛り上がる%CALLNAME:PLAYER%の欲情に当てられて%CALLNAME:TARGET%もさらに激しく欲情していくのであった…
;反発Lv2以上持ちだが欲情が跳ね上がってる[解放][快感に素直]を持たない非陥落奴隷相手に
ELSEIF MARK:反発刻印 > 1 && PALAM:欲情 >= PALAMLV:3 && TALENT:解放 == 0 && TALENT:快感に素直 == 0 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 自慰行為で盛り上がる%CALLNAME:PLAYER%を、苦々しく見ている%CALLNAME:TARGET%であったが、
	PRINTL その身体が激しく欲情していることは隠しようがなかった…
;自慰中毒持ちで[自慰しやすい]があり恐怖刻印がない場合
ELSEIF ABL:自慰中毒 > 0 && TALENT:自慰しやすい && MARK:恐怖刻印 <= 0
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML %CALLNAME:TARGET%は、自慰行為で盛り上がる%CALLNAME:PLAYER%を羨ましそうにみていた…
;[精神崩壊]している場合
ELSEIF TALENT:精神崩壊
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML しかし、自慰行為で盛り上がる%CALLNAME:PLAYER%に%CALLNAME:TARGET%は興味はなさそうだった…
;調教者が対象の実の親である場合（二世代以上前は考慮しない）
ELSEIF (NO:TARGET == 2048 || NO:TARGET == 2049) && (CFLAG:3105 == CFLAG:PLAYER:3109 || CFLAG:3107 == CFLAG:PLAYER:3109)
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 実の親の自慰行為を見せ付けられて、%CALLNAME:TARGET%は深い衝撃を受けつつも性的興奮を感じているようだった…
;その他
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_115
;デフォルト射精箇所タイプを設定
TFLAG:703 = 4
;射精箇所選択の可・不可
TFLAG:704 = 1
