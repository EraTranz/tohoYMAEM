;=============================================================================
;二人自慰見せつけ
;=============================================================================
;自慰見せつけダブル版
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE116
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:116 > 0
	RETURN 0
;羞恥プレイ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 512) == 0
	RETURN 0
;助手がいないとダメ
SIF ASSI < 0
	RETURN 0
;調教者が主人でないとダメ
SIF ASSIPLAY
	RETURN 0
;野外もしくは公開自慰を行ったことがない場合の野外およびビデオ撮影中は、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:ASSI:21 & 1) == 0 && (TEQUIP:ビデオ撮影 || TEQUIP:野外プレイ)
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;助手が褌着用中の場合助手に技巧4以上必須
SIF ASSI >= 0 && TEQUIP:ASSI:コスチュームプレイ == 17 && ABL:ASSI:技巧 < 4
	RETURN 0
;眼隠しと耳栓両方つけていた場合は意味がないので実行できない
SIF TEQUIP:目装着器具 && TEQUIP:耳装着器具
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 116) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM116
;特定の主人と助手の組み合わせを調べる
CALL JUDGE_TRAIN_PAIR, MASTER, ASSI
IF TFLAG:175 == 1
	LOCALS = 紅魔姉妹競演による
ELSEIF TFLAG:175 == 2
	LOCALS = 腋巫女二人による神聖な
ELSEIF TFLAG:175 == 3
	LOCALS = 秋の実りを祝う二柱の
ELSEIF TFLAG:175 == 4
	LOCALS = 月の姉妹による
ELSEIF TFLAG:175 == 5
	LOCALS = 秘封倶楽部版
ELSEIF TFLAG:175 == 6
	LOCALS = 春告精、Ｗリリーによる
ELSEIF TFLAG:175 == 7
	LOCALS = 覚り姉妹によるいやらしい
ELSEIF TFLAG:175 == 8
	LOCALS = 夢幻姉妹による幻想的な
ELSEIF TFLAG:175 == 9
	LOCALS = ゆゆゆか競演による
ELSEIF TFLAG:175 == 10 || TFLAG:175 == 11 || TFLAG:175 == 12
	LOCALS = 騒霊姉妹による
ELSE
	LOCALS = 
ENDIF

PRINTFORML %LOCALS%二人自慰見せつけ

;戦闘判定
CALL COM_TRY_BATTLE, 116
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = %LOCALS%二人自慰見せつけ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
LOCAL = 0

;主人の技巧をみる
IF ABL:MASTER:技巧 == 0
	LOCAL = 800
ELSEIF ABL:MASTER:技巧 == 1
	LOCAL = 1600
ELSEIF ABL:MASTER:技巧 == 2
	LOCAL = 2400
ELSEIF ABL:MASTER:技巧 == 3
	LOCAL = 3200
ELSEIF ABL:MASTER:技巧 == 4
	LOCAL = 4000
ELSE
	LOCAL = 4800
ENDIF

;主人のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_2(MASTER, LOCAL)

SIF EXIST_PENIS(MASTER)
	BASE:MASTER:射精 += LOCAL

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
LOCAL:1 = 0

;助手の技巧をみる
IF ABL:ASSI:技巧 == 0
	LOCAL:1 = 800
ELSEIF ABL:ASSI:技巧 == 1
	LOCAL:1 = 1600
ELSEIF ABL:ASSI:技巧 == 2
	LOCAL:1 = 2400
ELSEIF ABL:ASSI:技巧 == 3
	LOCAL:1 = 3200
ELSEIF ABL:ASSI:技巧 == 4
	LOCAL:1 = 4000
ELSE
	LOCAL:1 = 4800
ENDIF

;助手のＣ感覚をみる
LOCAL:1 = EJACULATIONCHECK_ABL_0_2(ASSI, LOCAL:1)

SIF EXIST_PENIS(ASSI)
	BASE:ASSI:射精 += LOCAL:1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 15

SOURCE:露出 = 500
SOURCE:不潔 = 100
SOURCE:逸脱 = 600

;ABL:自慰中毒をみる
IF ABL:自慰中毒 == 0
	SOURCE:性行動 = 50
	SOURCE:屈従 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:自慰中毒 == 1
	SOURCE:性行動 = 100
	SOURCE:屈従 = 120
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:自慰中毒 == 2
	SOURCE:性行動 = 150
	SOURCE:屈従 = 105
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:自慰中毒 == 3
	SOURCE:性行動 = 200
	SOURCE:屈従 = 90
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:自慰中毒 == 4
	SOURCE:性行動 = 250
	SOURCE:屈従 = 75
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 300
	SOURCE:屈従 = 0
	TIMES SOURCE:不潔 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:欲情追加 = 37
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:欲情追加 = 75
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:欲情追加 = 150
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:欲情追加 = 300
ELSE
	SOURCE:欲情追加 = 600
ENDIF

;-------------------------------------------------
;読心の場合エロい気分が移るのか見せつけ効果アップ（1.8倍）
;-------------------------------------------------
;キャラ専用コマンドがオンの場合、対象が[恋慕]と心の目を閉ざしていない[覚]持ちだと変化
IF TALENT:覚 == 1 && TALENT:恋慕 && (FLAG:15 & 16384)
	TIMES SOURCE:性行動 , 1.80
	TIMES SOURCE:欲情追加 , 1.80
ENDIF

;-------------------------------------------------
;射精チェック（主人）
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(MASTER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 600
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = MASTER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_116
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;主人の指⇔主人のＰの汚れが移動
IF EXIST_PENIS(MASTER)
	STAIN:MASTER:手 |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:MASTER:手
;主人の指⇔主人のＶの汚れが移動
ELSE
	STAIN:MASTER:ヴァギナ |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:MASTER:ヴァギナ
ENDIF

;-------------------------------------------------
;射精チェック２（助手）
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(ASSI)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 += 200
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 += 600
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 += 1500
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 += 3000
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 += 6000
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = ASSI
	;射精された人物を設定
	TFLAG:94 = TARGET	
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_116_2
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;助手の指⇔助手のＰの汚れが移動
IF EXIST_PENIS(ASSI)
	STAIN:ASSI:手 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:ASSI:手
;助手の指⇔助手のＶの汚れが移動
ELSE
	STAIN:ASSI:ヴァギナ |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:ASSI:ヴァギナ
ENDIF

;主人か助手かふたなり
SIF TALENT:MASTER:ふたなり || TALENT:ASSI:ふたなり
	SOURCE:屈従 /= 2
;主人も助手もふたなり
SIF TALENT:MASTER:ふたなり && TALENT:ASSI:ふたなり
	SOURCE:屈従 /= 2

IF TALENT:オトコ == 0 && TALENT:MASTER:オトコ == 0 && TALENT:ASSI:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:MASTER:オトコ && TALENT:ASSI:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;自慰経験
TCVAR:MASTER:自慰経験 += 1
TCVAR:ASSI:自慰経験 += 1
;助手のみ野外か公開で初オナニーならば…、異常経験を獲得
IF (TEQUIP:ビデオ撮影 || TEQUIP:野外プレイ) && (CFLAG:ASSI:21 & 1) == 0
	TCVAR:ASSI:異常経験 += 1
	CFLAG:ASSI:21 |= 4
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_116
;恋慕さとりんに見せ付けるとさとりんも大興奮
IF TALENT:覚 == 1 && TALENT:恋慕 && (FLAG:15 & 16384)
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 自慰行為で盛り上がる二人の欲情に当てられて%CALLNAME:TARGET%もさらに激しく欲情していくのであった…
;反発Lv2以上持ちだが欲情が跳ね上がってる[解放][快感に素直]を持たない非陥落奴隷相手に
ELSEIF MARK:反発刻印 > 1 && PALAM:欲情 >= PALAMLV:3 && TALENT:解放 == 0 && TALENT:快感に素直 == 0 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 自慰行為で盛り上がる二人を、苦々しく見ている%CALLNAME:TARGET%であったが、
	PRINTL その身体が激しく欲情していることは隠しようがなかった…
;自慰中毒持ちで[自慰しやすい]があり恐怖刻印がない場合
ELSEIF ABL:自慰中毒 > 0 && TALENT:自慰しやすい && MARK:恐怖刻印 <= 0
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML %CALLNAME:TARGET%は、自慰行為で盛り上がる二人を羨ましそうにみていた…
;[精神崩壊]している場合
ELSEIF TALENT:精神崩壊
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML しかし、自慰行為で盛り上がる二人に%CALLNAME:TARGET%は興味はなさそうだった…
;主人か助手が対象の実の親である場合（二世代以上前は考慮しない）
ELSEIF (NO:TARGET == 2048 || NO:TARGET == 2049) && (CFLAG:3105 == CFLAG:MASTER:3109 || CFLAG:3105 == CFLAG:ASSI:3109 || CFLAG:3107 == CFLAG:MASTER:3109 || CFLAG:3107 == CFLAG:ASSI:3109)
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:PLAYER%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った
	PRINTFORML 実の親の自慰行為を見せ付けられて、%CALLNAME:TARGET%は深い衝撃を受けつつも性的興奮を感じているようだった…
;その他
ELSE
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%はそれぞれ%CALLNAME:TARGET%に見せ付けるように目の前で自慰を行った…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_116
;デフォルト射精箇所タイプを設定
TFLAG:703 = 4
;射精箇所選択の可・不可
TFLAG:704 = 1

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_116_2
;デフォルト射精箇所タイプを設定
TFLAG:703 = 4
;射精箇所選択の可・不可
TFLAG:704 = 1

