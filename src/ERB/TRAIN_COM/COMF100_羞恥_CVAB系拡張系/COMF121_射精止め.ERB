;=============================================================================
;射精止め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE121
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:121 > 0
	RETURN 0
;Ｃ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 1) == 0
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:リボン == 0
	RETURN 0
;調教対象にペニスがないとダメ
SIF EXIST_PENIS(TARGET) == 0
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:Ｃ系装着器具 || TEQUIP:コンドーム
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場プレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 121) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM121
PRINTL 射精止め

;戦闘判定
CALL COM_TRY_BATTLE, 121
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 射精止め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;リボンの着脱
IF TEQUIP:射精止め
	;せき止めた精液が射精ゲージに加算される
	BASE:射精 += EX:40 * MAXBASE:2
	;オトコの場合せき止めた尿も放尿ゲージに加算される(ふたなりは女の方から放尿できる)
	SIF TALENT:オトコ
		BASE:尿意 += EX:43 * MAXBASE:4
	TEQUIP:射精止め = 0
ELSE
	TCVAR:緊縛経験 += 1
	TEQUIP:射精止め = 1
ENDIF

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:快Ｃ = 50
SOURCE:露出 = 100
SOURCE:痛み = 100
SOURCE:屈従 = 250
SOURCE:逸脱 = 500

;PALAM:Ｃ感覚をみる
IF PALAM:快Ｃ < PALAMLV:1
	SOURCE:痛み += 100
ELSEIF PALAM:快Ｃ < PALAMLV:2
	SOURCE:痛み += 110
ELSEIF PALAM:快Ｃ < PALAMLV:3
	SOURCE:痛み += 120
ELSEIF PALAM:快Ｃ < PALAMLV:4
	SOURCE:痛み += 130
ELSE
	SOURCE:痛み += 140
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:欲情追加 = 100
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:欲情追加 = 110
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:欲情追加 = 120
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:欲情追加 = 130
ELSE
	SOURCE:欲情追加 = 140
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:欲情追加 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:欲情追加 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:欲情追加 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:欲情追加 , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:欲情追加 , 2.00
ELSE
	TIMES SOURCE:欲情追加 , 3.00
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES SOURCE:欲情追加 , 2.00
;調教者がサド、ドＳ、サドっ気3以上
SIF TALENT:PLAYER:サド || TALENT:PLAYER:ドＳ || ABL:PLAYER:サドっ気 > 2
	TIMES SOURCE:欲情追加 , 1.20
RETURN 1

;--------------------------------------------------
;射精止め
;--------------------------------------------------
@EQUIP_COM121
PRINTL ＜射精抑制中＞

DOWNBASE:0 += 5
DOWNBASE:1 += 15

SOURCE:痛み += 20

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_121
IF TEQUIP:射精止め
	PRINTFORML %CALLNAME:TARGET%を戒めるリボンをするりと解いてやった…
ELSE
	PRINTFORML %CALLNAME:TARGET%のペニスの根元に、フリルのついた可愛いリボンをきつく結びつけた
	PRINTL 血流が集まり締め付けられ、亀頭が赤黒く膨張している…
ENDIF
