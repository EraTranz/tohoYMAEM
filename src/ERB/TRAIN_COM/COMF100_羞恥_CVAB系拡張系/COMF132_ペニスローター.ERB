;=============================================================================
;ペニスローター
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE132
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:132 > 0
	RETURN 0
;Ｃ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 1) == 0
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:ローター == 0
	RETURN 0
;調教対象にペニスがないとダメ
SIF EXIST_BAR(TARGET) == 0
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:Ｃ系装着器具 || TEQUIP:コンドーム
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 132) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM132
PRINTL ペニスローター

;戦闘判定
CALL COM_TRY_BATTLE, 132
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ペニスローター
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 10
DOWNBASE:1 += 90

SOURCE:露出 = 280
SOURCE:逸脱 = 70

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 100
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 200
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 4500
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 800
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1200
ELSE
	SOURCE:快Ｃ = 1500
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;コマンド属性:道具(調教者)
TFLAG:98 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_132
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%のペニスにローターで刺激を与えている…
