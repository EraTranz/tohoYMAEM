;=============================================================================
;割礼
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE136
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:136 > 0
	RETURN 0
;Ｃ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 1) == 0
	RETURN 0
;異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4
	RETURN 0
;拷問系、精神操作系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16) == 0
	RETURN 0
;割礼状態ではダメ
SIF CFLAG:37 & 16
	RETURN 0
;オトコまたはふたなりだとダメ
SIF EXIST_PENIS(TARGET)
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:Ｃ系装着器具 || TEQUIP:コンドーム
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場プレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;裸エプロン、スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 1 || TEQUIP:コスチュームプレイ == 2
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 136) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM136
PRINTL 割礼

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 136
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 割礼
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;一度実行したことがあるか
IF (CFLAG:20 & 128) == 0
	DOWNBASE:0 += 90
	DOWNBASE:1 += 70
ELSE
	DOWNBASE:0 += 60
	DOWNBASE:1 += 40
ENDIF

SOURCE:中毒充足 = 800
SOURCE:逸脱 = 2000

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 2200
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 3000
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 4000
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 5000
ELSE
	SOURCE:痛み = 7000
ENDIF

;PALAM:恐怖をみる
IF PALAM:恐怖 < PALAMLV:1
	SOURCE:トラウマ = 900
	SOURCE:逸脱 += 600
ELSEIF PALAM:恐怖 < PALAMLV:2
	SOURCE:トラウマ = 1600
	SOURCE:逸脱 += 1200
ELSEIF PALAM:恐怖 < PALAMLV:3
	SOURCE:トラウマ = 2300
	SOURCE:逸脱 += 1800
ELSEIF PALAM:恐怖 < PALAMLV:4
	SOURCE:トラウマ = 3000
	SOURCE:逸脱 += 2400
ELSE
	SOURCE:トラウマ = 3700
	SOURCE:逸脱 += 3000
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	SOURCE:達成感 = 130
	TIMES SOURCE:中毒充足 , 0.80
	TIMES DOWNBASE:0 , 1.20
ELSEIF ABL:マゾっ気 == 1
	SOURCE:達成感 = 250
	TIMES SOURCE:中毒充足 , 1.10
	TIMES DOWNBASE:0 , 1.10
ELSEIF ABL:マゾっ気 == 2
	SOURCE:達成感 = 500
	TIMES SOURCE:中毒充足 , 1.30
	TIMES DOWNBASE:0 , 1.00
ELSEIF ABL:マゾっ気 == 3
	SOURCE:達成感 = 800
	TIMES SOURCE:中毒充足 , 1.50
	TIMES DOWNBASE:0 , 0.90
ELSEIF ABL:マゾっ気 == 4
	SOURCE:達成感 = 1400
	TIMES SOURCE:中毒充足 , 1.70
	TIMES DOWNBASE:0 , 0.80
ELSE
	SOURCE:達成感 = 2000
	TIMES SOURCE:中毒充足 , 2.10
	TIMES DOWNBASE:0 , 0.70
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:痛み , 2.00
	TIMES SOURCE:屈従 , 0.50
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 0.80
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:屈従 , 1.00
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:屈従 , 1.50
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:屈従 , 2.00
ELSE
	TIMES SOURCE:痛み , 0.50
	TIMES SOURCE:屈従 , 2.50
ENDIF

;調教者の指⇔奴隷のＰの汚れが移動
IF EXIST_BAR(TARGET)
	STAIN:PLAYER:ペニス |= STAIN:手
	STAIN:ペニス |= STAIN:PLAYER:手
;奴隷の指⇔調教者のＶの汚れが移動
ELSE
	STAIN:PLAYER:ヴァギナ |= STAIN:ペニス
	STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2

;割礼状態
CFLAG:37 |= 16

;初体験
IF (CFLAG:20 & 128) == 0
	TCVAR:異常経験 += 1
	;初体験判定フラグを使用してみる
	CFLAG:20 |= 128
ENDIF
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_136
PRINTFORM %CALLNAME:TARGET%の
IF EXIST_PENIS(TARGET)
	PRINT ペニス
ELSE
	SIF TALENT:肥大陰核
		PRINT 巨大な
	PRINT クリトリス
ENDIF
PRINT の皮を
IF ASSIPLAY
	PRINTFORML %CALLNAME:PLAYER%に切除させようとしたが、
ELSE
	PRINTFORML %CALLNAME:MASTER%が自ら切除しようとしたが、
ENDIF
PRINTFORM %CALLNAME:TARGET%に
IF TFLAG:241 - TFLAG:240 > 20
	PRINTL 激しく抵抗されてしまった。
ELSE
	PRINTL 拒否されてしまった。
ENDIF
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_136
PRINTFORM %CALLNAME:PLAYER%は
SIF PALAM:快Ｃ >= PALAMLV:3
	PRINT 膨張しているにも関わらず
PRINTFORM %CALLNAME:TARGET%の
IF EXIST_PENIS(TARGET)
	PRINT ペニスの
ELSE
	SIF TALENT:肥大陰核
		PRINT 巨大な
	PRINT クリトリスを覆っている
ENDIF
PRINTL 皮を切除した
SIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML %CALLNAME:TARGET%は激しい痛みに声も出ないようだ……

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_136, ARG
LOCAL:99 = ARG
;ABL:Ｃ感覚
IF ABL:Ｃ感覚
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:Ｃ感覚
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:0%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:露出癖
IF ABL:露出癖
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:露出癖
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;苦痛刻印
IF MARK:苦痛刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:苦痛刻印
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:0%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% 潤滑不足({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF


;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 2
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;処女
IF TALENT:処女 == 1
	RESULTS = %RESULTS% - 
	LOCAL:99 = 20
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:0%({LOCAL:99})
ELSEIF EXP:Ｖ経験 < EXPLV:2
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %EXPNAME:0%不足({LOCAL:99})
ENDIF

;一度実行したことがあるか
IF (CFLAG:20 & 128) == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 20
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% 割礼初体験({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	RESULTS = %RESULTS% - 
	LOCAL:99 = 4
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

TFLAG:241 = 28
