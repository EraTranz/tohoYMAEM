;=============================================================================
;尿道挿入
;=============================================================================
;正常位を魔改造という名の改悪という元パッチの仕様を
;さらに魔改造したので元の面影がないorz
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE146
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:146 > 0
	RETURN 0
;Ｃ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 1) == 0
	RETURN 0
;尿道開発経験が5以下の場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:尿道開発経験 <= 5
	RETURN 0
;調教者にペニスがない、かつペニスバンドもないとダメ
SIF EXIST_BAR(PLAYER) == 0 && ITEM:ペニスバンド == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;対象が[処女]
IF TALENT:処女 == 1
	;対象の処女の判定
	SIF COM_ABLE_VIRGIN(TARGET, PLAYER) == 0
		RETURN 0
ELSE
	;対象の《潤滑》は大丈夫かチェック
	SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
		RETURN 0
ENDIF
;[オトコ]の場合、技巧3以上じゃないと使えない
SIF TALENT:オトコ && ABL:PLAYER:技巧 < 3
	RETURN 0
;助手調教の場合、技巧4以上じゃないと使えない。
SIF ASSIPLAY && ABL:PLAYER:技巧 < 4
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;体力が1000未満ではダメ
SIF BASE:体力 < 1000 && !(FLAG:16 & 32)
	RETURN 0
;気力が500未満の時はダメ
SIF BASE:気力 < 500 && !(FLAG:16 & 32)
	RETURN 0
;Ｕ拡張経験少ないとダメ
SIF EXP:Ｕ拡張経験 < 5
	RETURN 0
;クスコ使用中は不可
SIF TEQUIP:クスコ
	RETURN 0
;Ｕ系装着具使用中は不可
SIF TEQUIP:Ｕ系装着器具
	RETURN 0
;オトコの場合、Ｃ系装着具使用中は不可
SIF (TEQUIP:Ｃ系装着器具 || TEQUIP:コンドーム) && TALENT:オトコ
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 146) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM146
PRINTL 尿道挿入

;戦闘判定
CALL COM_TRY_BATTLE, 146
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 尿道挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1500
ELSEIF ABL:技巧 == 1
	LOCAL = 1600
ELSEIF ABL:技巧 == 2
	LOCAL = 1800
ELSEIF ABL:技巧 == 3
	LOCAL = 2000
ELSEIF ABL:技巧 == 4
	LOCAL = 2400
ELSE
	LOCAL = 3000
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;欲望をみる
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7
;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL:1 = 240

;EXP:Ｕ拡張経験をみる
IF EXP:Ｕ拡張経験 < EXPLV:1
	TIMES LOCAL:1, 3.00
	SOURCE:痛み = 5500
ELSEIF EXP:Ｕ拡張経験 < EXPLV:2
	TIMES LOCAL:1, 2.50
	SOURCE:痛み = 300
ELSEIF EXP:Ｕ拡張経験 < EXPLV:3
	TIMES LOCAL:1, 2.00
	SOURCE:痛み = 50
ELSEIF EXP:Ｕ拡張経験 < EXPLV:4
	TIMES LOCAL:1, 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ｕ拡張経験 < EXPLV:5
	TIMES LOCAL:1, 0.80
	SOURCE:痛み = 0
ELSE
	TIMES LOCAL:1, 0.60
	SOURCE:痛み = 0
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1, 2.00
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1, 0.60
;小柄体形
SIF TALENT:小柄体型
	TIMES LOCAL:1, 2.00

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 * 2

SOURCE:露出 = 800
SOURCE:逸脱 = 2000

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 80
	SOURCE:情愛 = 150
	SOURCE:屈従 = 300
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 250
	SOURCE:情愛 = 250
	SOURCE:屈従 = 800
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 600
	SOURCE:情愛 = 350
	SOURCE:屈従 = 1400
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1000
	SOURCE:情愛 = 500
	SOURCE:屈従 = 1800
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1300
	SOURCE:情愛 = 700
	SOURCE:屈従 = 2100
ELSE
	SOURCE:快Ｃ = 1700
	SOURCE:情愛 = 1000
	SOURCE:屈従 = 2400
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.40
	SOURCE:痛み += 1600
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.80
	SOURCE:痛み += 1000
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	SOURCE:痛み += 600
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.40
	SOURCE:痛み += 250
ELSE
	TIMES SOURCE:快Ｃ , 1.80
	SOURCE:痛み += 120
ENDIF

;EXP:尿道開発経験をみる
IF EXP:尿道開発経験 < EXPLV:1
	TIMES SOURCE:快Ｃ , 0.20
	SOURCE:痛み += 8000
ELSEIF EXP:尿道開発経験 < EXPLV:2
	TIMES SOURCE:快Ｃ , 0.60
	SOURCE:痛み += 3600
ELSEIF EXP:尿道開発経験 < EXPLV:3
	TIMES SOURCE:快Ｃ , 1.00
	SOURCE:痛み += 1200
ELSEIF EXP:尿道開発経験 < EXPLV:4
	TIMES SOURCE:快Ｃ , 1.20
	SOURCE:痛み += 300
ELSEIF EXP:尿道開発経験 < EXPLV:5
	TIMES SOURCE:快Ｃ , 1.40
	SOURCE:痛み += 30
ELSE
	TIMES SOURCE:快Ｃ , 1.60
	SOURCE:痛み += 0
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｃ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｃ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｃ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｃ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｃ , 1.20
ELSE
	TIMES SOURCE:快Ｃ , 1.30
ENDIF

;オトコ
SIF TALENT:オトコ
	TIMES SOURCE:痛み , 4.50
;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00
;尿道狂
SIF TALENT:尿道狂
	TIMES SOURCE:快Ｃ , 1.20

;Ｃ敏感、鈍感をみる
;快Ｃ自体のチェックは後でまとめてやる
IF TALENT:Ｃ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｃ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

SIF EXP:尿道開発経験 <= 5
	TCVAR:異常経験 += 1

;放尿レベル増加
CFLAG:放尿レベル += 3
BASE:尿意 = 0
TCVAR:尿道開発経験 += 3
TCVAR:Ｕ拡張経験 += 2

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	IF ABL:従順 >= 3 && ABL:欲望 >= 3 && CFLAG:9 & 2 && FLAG:10 & 1 && EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に足を絡め腰を押し付けてきた…
		PRINTFORML 
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_146
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
IF TALENT:オトコ == 0
	;奴隷のＶ⇔調教者のＰの汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:ヴァギナ
ELSE
	;奴隷のＰ⇔調教者のＰの汚れが移動
	STAIN:ペニス |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:ペニス
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ｖ感覚 >= 3) ? 2 # 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_146
PRINTFORM %CALLNAME:PLAYER%は
;焦らしプレイ
IF TEQUIP:焦らしプレイ
	PRINTFORM %CALLNAME:TARGET%の
	SIF EX:20
		PRINT ガクガク暴れる
	PRINT 腰を押さえつけ、尿道に
	IF EXIST_PENIS(PLAYER)
		PRINTL ペニスを挿入したまま小さくグラインドさせた…
	ELSE
		PRINTL ペニスバンドの先端を軽く挿入した…
	ENDIF
;調教者が[オトコ]か[ふたなり]である
ELSEIF EXIST_PENIS(PLAYER)
	PRINTFORML %CALLNAME:TARGET%の尿道内の感触を味わいつつ、奥までペニスを突き入れた…
;ペニスバンドを使用している場合
ELSE
	PRINTFORML %CALLNAME:TARGET%の尿道内にペニスバンドの突起部を挿入して刺激を与え続けた…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_146
;デフォルト射精箇所タイプを設定
TFLAG:703 = 15
;射精箇所選択の可・不可
TFLAG:704 = 0
