;=============================================================================
;クスコ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE150
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:150 > 0
	RETURN 0
;Ｖ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2) == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:クスコ
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:クスコ == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;処女だとダメ
SIF TALENT:処女 == 1
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
		RETURN 0
;調教象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;他のＶ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 150) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM150
PRINTL クスコ

;戦闘判定
CALL COM_TRY_BATTLE, 150
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = クスコ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 50
DOWNBASE:1 += 100

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 40
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 130
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 300
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 500
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 800
ELSE
	SOURCE:快Ｖ = 1200
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:露出 = 300
	SOURCE:液体追加 = 0
	SOURCE:屈従 = 600
	SOURCE:中毒充足 = 0
	SOURCE:逸脱 = 800
ELSEIF ABL:露出癖 == 1
	SOURCE:露出 = 500
	SOURCE:液体追加 = 150
	SOURCE:屈従 = 500
	SOURCE:中毒充足 = 150
	SOURCE:逸脱 = 200
ELSEIF ABL:露出癖 == 2
	SOURCE:露出 = 500
	SOURCE:液体追加 = 800
	SOURCE:屈従 = 400
	SOURCE:中毒充足 = 300
ELSEIF ABL:露出癖 == 3
	SOURCE:露出 = 500
	SOURCE:液体追加 = 600
	SOURCE:屈従 = 300
	SOURCE:中毒充足 = 600
ELSEIF ABL:露出癖 == 4
	SOURCE:露出 = 1000
	SOURCE:液体追加 = 1000
	SOURCE:屈従 = 200
	SOURCE:中毒充足 = 1000
ELSE
	SOURCE:露出 = 1300
	SOURCE:液体追加 = 1800
	SOURCE:屈従 = 100
	SOURCE:中毒充足 = 1800
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:露出 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:露出 , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:露出 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:露出 , 1.50
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:露出 , 1.70
ELSE
	TIMES SOURCE:露出 , 2.10
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 6500
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 2000
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 500
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 10
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 4.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 400
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.10
ELSE
	TIMES SOURCE:快Ｖ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.30
ENDIF

;ビデオカメラ
SIF TEQUIP:ビデオ撮影
	TIMES SOURCE:露出 , 1.20
;羞恥プレイ
SIF TEQUIP:羞恥プレイ
	TIMES SOURCE:露出 , 1.20

;貞操観念
SIF TALENT:貞操観念
	TIMES SOURCE:逸脱 , 4.00
;倒錯的
IF TALENT:倒錯的
	TIMES SOURCE:露出 , 1.50
	TIMES SOURCE:逸脱 , 1.20
ENDIF
;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.50

;鈴仙か永琳による使用
IF ASSI >= 0 && ASSIPLAY
	SIF NO:ASSI == 1028
		TIMES SOURCE:痛み , 0.70
	SIF NO:ASSI == 1029
		TIMES SOURCE:痛み , 0.50
ENDIF

TCVAR:Ｖ経験 += 3

;コマンド属性：道具(調教者)
TFLAG:98 = 1

;クスコの着脱
IF TEQUIP:クスコ
	TEQUIP:クスコ = 0
ELSE
	TEQUIP:クスコ = 1
ENDIF
;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;クスコ挿入中
;--------------------------------------------------
@EQUIP_COM150
PRINTL ＜クスコ挿入中＞

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 10
DOWNBASE:1 += 50

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 0
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 50
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 100
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 150
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 200
ELSE
	LOCAL = 300
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	LOCAL:1 = 300
	LOCAL:3 = 1000
	SOURCE:屈従 += 600
ELSEIF ABL:露出癖 == 1
	LOCAL:1 = 500
	LOCAL:3 = 500
	SOURCE:屈従 += 500
ELSEIF ABL:露出癖 == 2
	LOCAL:1 = 500
	LOCAL:3 = 150
	SOURCE:屈従 += 400
	SOURCE:中毒充足 += 300
ELSEIF ABL:露出癖 == 3
	LOCAL:1 = 500
	LOCAL:3 = 50
	SOURCE:屈従 += 300
	SOURCE:中毒充足 += 600
ELSEIF ABL:露出癖 == 4
	LOCAL:1 = 1000
	LOCAL:3 = 0
	SOURCE:屈従 += 200
	SOURCE:中毒充足 += 1000
ELSE
	LOCAL:1 = 1300
	LOCAL:3 = 0
	SOURCE:屈従 += 100
	SOURCE:中毒充足 += 1800
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 0.00
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.50
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 1.70
ELSE
	TIMES LOCAL:1 , 2.10
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:2 = 150
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 = 20
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.10
	LOCAL:2 += 400
	TIMES LOCAL:2 , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.40
	LOCAL:2 += 150
	TIMES LOCAL:2 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:2 , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:2 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:2 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;プライド高い
SIF TALENT:プライド高い
	TIMES LOCAL:3 , 1.20
;無関心
IF TALENT:無関心
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:3 , 0.80
ENDIF
;貞操観念
SIF TALENT:貞操観念
	TIMES LOCAL:3 , 2.00
;倒錯的
IF TALENT:倒錯的
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:3 , 1.20
ENDIF
;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:2 , 1.50

;ビデオカメラ
SIF TEQUIP:ビデオ撮影
	TIMES LOCAL:1 , 1.20
;羞恥プレイ
SIF TEQUIP:羞恥プレイ
	TIMES LOCAL:1 , 1.20

SOURCE:快Ｖ += LOCAL
SOURCE:露出 += LOCAL:1
SOURCE:痛み += LOCAL:2
SOURCE:逸脱 += LOCAL:3

TCVAR:Ｖ経験 += 2

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;露出快楽経験入手判定
TFLAG:100 |= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_150
IF EQUIP:睡眠薬 || TFLAG:899 > 0
	IF TEQUIP:クスコ
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の膣からクスコを引き抜いた…
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は、
		SIF TEQUIP:ビデオ撮影
			PRINT ビデオカメラの前で
		PRINTFORML %CALLNAME:TARGET%の膣にクスコを挿入して割り開いた
		IF PALAM:潤滑 >= PALAMLV:5
			PRINTFORML 広げられた秘肉から大量の白濁した蜜が、とろとろと床に流れ出した…
		ELSEIF PALAM:潤滑 >= PALAMLV:3
			PRINTFORML 秘肉からは粘液が、ねっとりとクスコに垂れ落ちている…
		ELSE
			RETURN 1
		ENDIF
	ENDIF
ELSE
	IF TEQUIP:クスコ
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の膣からクスコを引き抜いた…
		IF EXP:Ｖ経験 >= 200 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%は広がったまま大量の粘液を溢れさせる秘裂を、恍惚とした表情で見つめ
		ELSEIF EXP:Ｖ経験 >= 71
			PRINTFORM %CALLNAME:TARGET%の膣孔は中々元に戻らずに、愛液を垂れ流し
		ELSEIF EXP:Ｖ経験 >= 21
			PRINTFORM %CALLNAME:TARGET%は顔を紅潮させ、未だに残る異物感に震え
		ELSE
			PRINTFORM %CALLNAME:TARGET%は羞恥と圧迫感から開放されて、安堵の表情を浮かべ
		ENDIF
		PRINTL ている…
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は、
		SIF TEQUIP:ビデオ撮影
			PRINT ビデオカメラの前で
		SIF TEQUIP:羞恥プレイ
			PRINT 鏡に映る膣内を自身に見せつけるように
		PRINTFORML %CALLNAME:TARGET%の膣にクスコを挿入して割り開いた
		IF PALAM:潤滑 >= PALAMLV:5
			PRINTFORML 広げられた秘肉から大量の白濁した蜜が、とろとろと床に流れ出した…
		ELSEIF PALAM:潤滑 >= PALAMLV:3
			PRINTFORML 秘肉からは粘液が、ねっとりとクスコに垂れ落ちている…
		ELSE
			RETURN 1
		ENDIF

		PRINTFORM %CALLNAME:TARGET%は
		IF EXP:Ｖ経験 >= 200 && (TALENT:恋慕 || TALENT:服従 || TALENT:壊造人格 || TALENT:淫乱)
			PRINTFORM %CALLNAME:PLAYER%の方に腰を突き上げて、しきりにもっと押し広げて欲しいとねだっ
		ELSEIF EXP:Ｖ経験 >= 71 && (TALENT:恋慕 || TALENT:服従 || TALENT:壊造人格 || TALENT:淫乱)
			PRINTFORM ゆっくりと広げていく%CALLNAME:PLAYER%の手を、涎を垂らしながら見つめ
		ELSEIF EXP:Ｖ経験 >= 71
			PRINTFORM 何かを諦めたような表情でゆっくりと広げていく%CALLNAME:PLAYER%の手を見つめ
		ELSEIF EXP:Ｖ経験 >= 21
			PRINT 膣内のひんやりとした異物感に何とも言えない表情をし
		ELSE
			PRINT 体内から引き裂かれるような感覚に、呻き声を上げ
		ENDIF
		PRINTL ている…
	ENDIF
ENDIF
