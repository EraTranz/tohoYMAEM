;=============================================================================
;愛液採集器
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE153
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:153 > 0
	RETURN 0
;強力アイテム・コマンド無効時はダメ
SIF (FLAG:13 & 2) == 0
	RETURN 0
;触手服が先に責めてるときはダメ
SIF TEQUIP:触手服 & 8
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ｖ系装着器具 == 7
	RETURN 1
;Ｖ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2) == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;対象のＶ拡張経験が0のとき異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ拡張経験 == 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:愛液採集器 == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;処女だとダメ
SIF TALENT:処女 == 1
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;他のＶ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:クスコ
	RETURN 0
;Ｖ調律経験が10未満だとダメ
SIF EXP:Ｖ調律経験 < 10
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 153) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM153
PRINTL 愛液採集器

;戦闘判定
CALL COM_TRY_BATTLE, 153
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 愛液採集器
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 65
DOWNBASE:1 += 110

SOURCE:露出 = 180
SOURCE:達成感 = 180
SOURCE:欲情追加 = 100
SOURCE:痛み = 250
SOURCE:屈従 = 80
SOURCE:中毒充足 = 300
SOURCE:逸脱 = 350

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL:1 = 200
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL:1 = 400
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL:1 = 900
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL:1 = 1600
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL:1 = 2400
ELSE
	LOCAL:1 = 3000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 <= EXPLV:0
	TIMES LOCAL:1 , 0.20
	SOURCE:痛み += 5500
ELSEIF EXP:Ｖ経験 <= EXPLV:1
	TIMES LOCAL:1 , 0.60
	SOURCE:痛み += 300
ELSEIF EXP:Ｖ経験 <= EXPLV:2
	TIMES LOCAL:1 , 1.00
	SOURCE:痛み += 50
ELSEIF EXP:Ｖ経験 <= EXPLV:3
	TIMES LOCAL:1 , 1.20
	SOURCE:痛み += 10
ELSEIF EXP:Ｖ経験 <= EXPLV:4
	TIMES LOCAL:1 , 1.40
ELSE
	TIMES LOCAL:1 , 1.60
ENDIF

;欲望をみる
IF ABL:欲望 == 0
	SOURCE:中毒充足 += 0
	TIMES LOCAL:1 , 1.00
	TIMES SOURCE:逸脱 , 1.00
ELSEIF ABL:欲望 == 1
	SOURCE:中毒充足 += 60
	TIMES LOCAL:1 , 1.10
	TIMES SOURCE:逸脱 , 1.10
ELSEIF ABL:欲望 == 2
	SOURCE:中毒充足 += 120
	TIMES LOCAL:1 , 1.15
	TIMES SOURCE:逸脱 , 1.25
ELSEIF ABL:欲望 == 3
	SOURCE:中毒充足 += 240
	TIMES LOCAL:1 , 1.25
	TIMES SOURCE:逸脱 , 1.50
ELSEIF ABL:欲望 == 4
	SOURCE:中毒充足 += 480
	TIMES LOCAL:1 , 1.30
	TIMES SOURCE:逸脱 , 1.75
ELSE
	SOURCE:中毒充足 += 960
	TIMES LOCAL:1 , 1.50
	TIMES SOURCE:逸脱 , 2.00
ENDIF

;Ｖ敏感とＶ鈍感
IF TALENT:Ｖ敏感
	TIMES LOCAL:1 , 1.50
ELSEIF TALENT:Ｖ鈍感
	TIMES LOCAL:1 , 0.60
ENDIF
;淫壷
SIF TALENT:淫壷
	TIMES LOCAL:1 , 1.20

SOURCE:快Ｖ += LOCAL:1

;ただし処女には使えないけど
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;愛液採集器の着脱
IF TEQUIP:Ｖ系装着器具 == 7
	TEQUIP:Ｖ系装着器具 = 0
	TCVAR:Ｖ経験 += 2
	TCVAR:Ｖ調律経験 += 2
ELSE
	TCVAR:Ｖ経験 += 1
	SIF EXP:Ｖ拡張経験 == 0 
		TCVAR:異常経験 += 1
	TCVAR:Ｖ拡張経験 += 1
	TCVAR:Ｖ調律経験 += 1
	TEQUIP:Ｖ系装着器具 = 7
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;コマンド属性：道具(調教者)
TFLAG:98 = 1
RETURN 1

;-------------------------------------------------
;愛液採集器装備中
;-------------------------------------------------
@EQUIP_COM153
IF TEQUIP:触手調教 || TEQUIP:触手服 & 8
	PRINTL ＜触手愛液採集中＞
ELSE
	PRINTL ＜愛液採集器装備中＞
ENDIF

DOWNBASE:0 += 25
DOWNBASE:1 += 25

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 40
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 120
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 250
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 450
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 600
ELSE
	LOCAL = 750
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 <= EXPLV:0
	TIMES LOCAL , 0.20
	SOURCE:痛み += 5500
ELSEIF EXP:Ｖ経験 <= EXPLV:1
	TIMES LOCAL , 0.60
	SOURCE:痛み += 300
ELSEIF EXP:Ｖ経験 <= EXPLV:2
	TIMES LOCAL , 1.00
	SOURCE:痛み += 50
ELSEIF EXP:Ｖ経験 <= EXPLV:3
	TIMES LOCAL , 1.20
	SOURCE:痛み += 10
ELSEIF EXP:Ｖ経験 <= EXPLV:4
	TIMES LOCAL , 1.40
ELSE
	TIMES LOCAL , 1.60
ENDIF

;Ｖ敏感とＶ鈍感
IF TALENT:Ｖ敏感
	TIMES LOCAL , 1.40
ELSEIF TALENT:Ｖ鈍感
	TIMES LOCAL , 0.60
ENDIF

SOURCE:快Ｖ += LOCAL
SOURCE:露出 += 45
SOURCE:達成感 += 95
SOURCE:欲情追加 += 75
SOURCE:痛み += 90
SOURCE:屈従 += 5
SOURCE:中毒充足 += 100
SOURCE:逸脱 += 45

TCVAR:Ｖ経験 += 1

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;潤滑と欲情が一定以上ならば愛液が溜まる
SIF PALAM:潤滑 >= 30000 && PALAM:欲情 >= 50000
	TFLAG:129 += 1
;触手経験フラグ
SIF TEQUIP:触手調教
	TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_153
IF TEQUIP:Ｖ系装着器具 == 7
	IF MARK:反発刻印 > 1 && EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORML %CALLNAME:PLAYER%が手を伸ばそうとする前に、%CALLNAME:TARGET%はヴァギナから愛液採集器を取り外して放り投げた…
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%のヴァギナから愛液採集器を取り外した…
	ENDIF
ELSE
	IF MARK:反発刻印 > 1 && EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORML %CALLNAME:PLAYER%は、嫌がる%CALLNAME:TARGET%を無理やり押さえつけた
		PRINTFORM そして、%CALLNAME:TARGET%の
		SIF PALAM:潤滑 >= PALAMLV:4
			PRINT 濡れそぼった
		PRINTL ヴァギナに愛液を採集する器を取り付けた…
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の
		SIF PALAM:潤滑 >= PALAMLV:4
			PRINT 濡れそぼった
		PRINTL ヴァギナに愛液を採集する器を取り付けた…
	ENDIF
ENDIF
