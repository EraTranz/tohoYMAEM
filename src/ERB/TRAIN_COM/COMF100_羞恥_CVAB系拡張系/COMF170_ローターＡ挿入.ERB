;=============================================================================
;ローターＡ挿入
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE170
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:170 > 0
	RETURN 0
;Ａ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4) == 0
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ａ系装着器具 == 3
	RETURN 1
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:ローター == 0
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURN 0
;他のＡ系装着具使用中は不可
SIF (TEQUIP:Ａ系装着器具 && TEQUIP:Ａ系装着器具 != 3) || TEQUIP:氷柱
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 170) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM170
PRINTL ローターＡ挿入

;戦闘判定
CALL COM_TRY_BATTLE, 170
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ローターＡ挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 35
DOWNBASE:1 += 135

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 30
	SOURCE:屈従 = 150
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 50
	SOURCE:屈従 = 400
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 90
	SOURCE:屈従 = 700
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 160
	SOURCE:屈従 = 900
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 200
	SOURCE:屈従 = 1050
ELSE
	SOURCE:快Ａ = 250
	SOURCE:屈従 = 1200
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 1.50
	SOURCE:痛み = 200
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 1.20
	SOURCE:痛み = 300
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み = 50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ａ , 1.10
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 500
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.70
	SOURCE:痛み += 400
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 0.90
	SOURCE:痛み += 300
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 120
ELSE
	TIMES SOURCE:快Ａ , 1.10
	SOURCE:痛み += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.00
ELSE
	TIMES SOURCE:快Ａ , 1.00
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ａ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ａ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ａ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ａ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ａ , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 1.50

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;奴隷のＡ⇔調教者の手の汚れが移動
STAIN:アナル |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:アナル

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

;コマンド属性：道具(調教者)
TFLAG:98 = 1

;アナルローターの着脱
IF TEQUIP:Ａ系装着器具 == 3
	TEQUIP:Ａ系装着器具 = 0
ELSE
	TEQUIP:Ａ系装着器具 = 3
ENDIF
;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;ローターＡ挿入中
;-------------------------------------------------
@EQUIP_COM170
PRINTL ＜ローターＡ挿入中＞

DOWNBASE:0 += 30
DOWNBASE:1 += 40

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	LOCAL = 20
	LOCAL:1 = 150
ELSEIF ABL:Ａ感覚 == 1
	LOCAL = 40
	LOCAL:1 = 400
ELSEIF ABL:Ａ感覚 == 2
	LOCAL = 80
	LOCAL:1 = 700
ELSEIF ABL:Ａ感覚 == 3
	LOCAL = 130
	LOCAL:1 = 900
ELSEIF ABL:Ａ感覚 == 4
	LOCAL = 170
	LOCAL:1 = 1050
ELSE
	LOCAL = 200
	LOCAL:1 = 1200
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES LOCAL , 0.50
	LOCAL:2 = 400
ELSEIF EXP:Ａ経験 < EXPLV:2
	LOCAL:2 = 60
ELSEIF EXP:Ａ経験 < EXPLV:3
	LOCAL:2 = 30
ELSEIF EXP:Ａ経験 < EXPLV:4
	LOCAL:2 = 0
ELSE
	TIMES LOCAL , 1.10
	LOCAL:2 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
	LOCAL:2 += 100
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.80
	LOCAL:2 += 40
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:2 , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:2 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:2 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.00
ELSE
	TIMES LOCAL , 1.00
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:2 , 1.20

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:1 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES LOCAL:2 , 0.60
	TIMES LOCAL:1 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	LOCAL:1 /= 3

SOURCE:快Ａ += LOCAL
SOURCE:痛み += LOCAL:2
SOURCE:屈従 += LOCAL:1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_170
IF TEQUIP:Ａ系装着器具
	PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%のアヌスから高速で振動するローターをゆっくりと引き抜いた。
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF EXP:Ａ経験 > 180
			PRINT 口から涎をたらしながら、お尻を広げ続け
		ELSEIF EXP:Ａ経験 > 140
			PRINT 潤んだ瞳で懇願するようにお尻を振っ
		ELSEIF EXP:Ａ経験 > 100
			PRINT 物足りなさそうにお尻を突き出し
		ELSEIF EXP:Ａ経験 > 75
			PRINT 少し名残惜しそうにお尻を揺らし
		ELSEIF EXP:Ａ経験 > 40
			PRINT お尻をもじもじさせつつも、どこかほっとした表情をうかべ
		ELSE
			PRINT 刺激に辛そうな表情で耐え
		ENDIF
		PRINTFORML ている…
	ELSE
		PRINTFORML ローターが抜かれた後の%CALLNAME:TARGET%の菊穴がゆっくりと閉じていった…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT いやらしい粘液をたたえた
	PRINTL 菊穴にローターをあてがった…
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF EXP:Ａ経験 > 180
			PRINT とろけきった表情で、自らのお尻を広げ
		ELSEIF EXP:Ａ経験 > 140
			PRINT 挿入を待ち焦がれるように腰をくねらせ
		ELSEIF EXP:Ａ経験 > 100
			PRINT 頬を染め、期待にお尻を揺らめかせ
		ELSEIF EXP:Ａ経験 > 75
			PRINT すぼまりに与えられるローターの刺激に身体を震わせ
		ELSEIF EXP:Ａ経験 > 40
			PRINT 期待と否定の入り混じったような複雑な表情をし
		ELSEIF EXP:Ａ経験 > 20
			PRINT 恥じらいに染まった顔を伏せ
		ELSE
			PRINT 不浄の穴を犯される恥辱と屈辱感にまみれた表情をし
		ENDIF
		PRINTL ている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%の菊穴があてがわれたローターに反応するかのごとく妖しく蠢きはじめている…
	ENDIF

	PRINTFORM その反応を楽しみながら%CALLNAME:PLAYER%は、%CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 粘液にまみれた
	PRINTL アヌスにローターを挿し込んだ…
ENDIF
