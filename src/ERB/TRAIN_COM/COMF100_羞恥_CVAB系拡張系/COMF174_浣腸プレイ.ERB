;=============================================================================
;浣腸プレイ
;=============================================================================
;TFLAG:99 = 口上分岐用(1=通常の浣腸or浣腸プラグ引き抜き 2=媚薬浣腸 3=利尿剤浣腸 4=賢者の血浣腸 5=興奮剤浣腸 6=感覚増幅薬浣腸)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE174
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:174 > 0
	RETURN 0
;Ａ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4) == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手服が先に責めてるときはダメ
SIF TEQUIP:触手服 & 16
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ａ系装着器具 == 7
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:浣腸器具一式 == 0
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;Ａ経験が100未満じゃできない
SIF EXP:Ａ経験 < 100
	RETURN 0
;露出癖、欲望、従順の合計が10ないとできない
SIF ABL:従順 + ABL:欲望 + ABL:露出癖 < 10
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:クスコ
	RETURN 0
;他のＡ系装着具使用中は不可
SIF (TEQUIP:Ａ系装着器具 && TEQUIP:Ａ系装着器具 != 7) || TEQUIP:氷柱
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 174) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM174
VARSET LOCAL, 0

TFLAG:177 = KANTYO_BLEND_CHECK(TARGET)
IF TEQUIP:Ａ系装着器具 == 7
	PRINTL 浣腸プラグ引き抜き

	;戦闘判定
	CALL COM_TRY_BATTLE, 174
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 浣腸プラグ引き抜き
	TFLAG:99 = 1
ELSEIF TFLAG:177 <= 0
	PRINTL 浣腸プレイ

	;戦闘判定
	CALL COM_TRY_BATTLE, 174
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 浣腸プレイ
	TFLAG:99 = 1
ELSE
	VARSET LOCALS,""
	SPLIT "ノーマル,媚薬,利尿剤,賢者の血,興奮剤,感覚増幅薬",",",LOCALS

	PRINTL 何を浣腸液にブレンドする？
	PRINTLC [ 0] 何もブレンドしない
	SIF TFLAG:177 & 1
		PRINTLC [ 1] 媚薬
	SIF TFLAG:177 & 2
		PRINTLC [ 2] 利尿剤
	SIF TFLAG:177 & 4
		PRINTLC [ 3] 賢者の血
	SIF TFLAG:177 & 8
		PRINTLC [ 4] 興奮剤
	SIF TFLAG:177 & 16
		PRINTLC [ 5] 感覚増幅薬
	$INPUT_LOOP
	INPUT
	IF RESULT < 0 || RESULT > 5 || (RESULT > 0 && GETBIT(TFLAG:177, RESULT - 1) == 0)
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP
	ENDIF

	TFLAG:99 = RESULT + 1
	PRINTFORML %LOCALS:RESULT%浣腸
	TSTR:0 = %LOCALS:RESULT%浣腸
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;調合知識があれば薬物関連の消費を抑える
LOCAL:1 = (TALENT:MASTER:調合知識) ? 1 # 0
SIF ASSI >= 0 && TALENT:ASSI:調合知識
	LOCAL:1 += 1

;媚薬の場合
IF TFLAG:99 == 2
	LOCAL:2 = (LOCAL:1) ? 150 # 300
	LOCAL:2 = (LOCAL:1 >= 2) ? 75 # LOCAL:2
	
	;調教対象に媚薬中毒があれば消費を抑える
	SIF TALENT:媚薬中毒
		TIMES LOCAL:2 , 0.50
	SIF TEQUIP:YASAIモードで使用 == 2 && TALENT:PLAYER:野菜の錬金術師 && PALAM:潤滑 >= 5000 && TEQUIP:触手調教 == 0
		TIMES LOCAL:2 , 0.10

	LOCAL:3 = LOCAL:2

	;媚薬由来ソース処理
	SOURCE:欲情追加 = 5000
	SOURCE:薬物浸潤 = 2000
	SOURCE:逸脱 = 2000
	
	;TALENT:媚薬中毒をみる
	IF TALENT:媚薬中毒
		SOURCE:中毒充足 = 500
		TIMES SOURCE:欲情追加 , 1.50
		TIMES SOURCE:逸脱 , 1.50
		SOURCE:媚薬浸潤 = 150
	ELSE
		SOURCE:媚薬浸潤 = 1 + RAND:3
	ENDIF

	;YASAIモード処理or媚薬消費
	IF TEQUIP:YASAIモードで使用 == 2 && TALENT:PLAYER:野菜の錬金術師 && PALAM:潤滑 >= 5000
		SOURCE:欲情追加 += PALAM:潤滑
		PALAM:潤滑 = 0
		SOURCE:逸脱 = 0
	ELSE
		;媚薬1つ消費
		CALL USE_PLURAL_ITEM, 69, 1
	ENDIF

	;媚薬関連のTEQUIPと経験処理
	TEQUIP:媚薬 = 1
	TCVAR:薬物経験 += 2
;利尿剤の場合
ELSEIF TFLAG:99 == 3
	LOCAL:2 = (LOCAL:1) ? 50 # 120
	LOCAL:3 = LOCAL:2

	;利尿剤1つ消費
	CALL USE_PLURAL_ITEM, 62, 1

	;利尿剤関連ソース処理
	SOURCE:薬物浸潤 = 100
	SOURCE:逸脱 = 2000
	SOURCE:反感追加 = 150

	;利尿剤関連のTEQUIPと経験処理
	TEQUIP:利尿剤 = 1
	CALL CALC_GAUGE4_SLAVE, TARGET, 1
	TCVAR:薬物経験 += 1
;賢者の血の場合
ELSEIF TFLAG:99 == 4
	LOCAL:2 = 20
	LOCAL:3 = 100

	;賢者の血1つ消費
	CALL USE_PLURAL_ITEM, 66, 1

	;賢者の血関連ソース処理
	SOURCE:薬物浸潤 = 2500
	SOURCE:逸脱 = 3000

	;賢者の血関連のTEQUIPと経験処理
	TEQUIP:賢者の血 = 8
	TCVAR:薬物経験 += 3
;興奮剤の場合
ELSEIF TFLAG:99 == 5
	LOCAL:2 = 20
	LOCAL:3 = 90

	;興奮剤1つ消費
	CALL USE_PLURAL_ITEM, 69, 1

	;興奮剤関連ソース処理
	SOURCE:薬物浸潤 = 3800
	SOURCE:逸脱 = 3200

	;興奮剤関連のTEQUIPと経験処理
	TEQUIP:興奮剤 = 10
	TCVAR:薬物経験 += 5
;感覚増幅薬の場合
ELSEIF TFLAG:99 == 6
	LOCAL:2 = 20
	LOCAL:3 = 100

	;感覚増幅薬1つ消費
	CALL USE_PLURAL_ITEM, 70, 1

	;感覚増幅薬関連ソース処理
	SOURCE:薬物浸潤 = 3300
	SOURCE:逸脱 = 3100

	;感覚増幅薬関連のTEQUIPと経験処理
	TEQUIP:7 = 8
	TCVAR:薬物経験 += 4
ENDIF

DOWNBASE:0 += LOCAL:2 + 60 
DOWNBASE:1 += LOCAL:3 + 150

SOURCE:中毒充足 += 600

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 80
	SOURCE:屈従 = 300
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 250
	SOURCE:屈従 = 800
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 600
	SOURCE:屈従 = 1400
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:屈従 = 1800
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1300
	SOURCE:屈従 = 2100
ELSE
	SOURCE:快Ａ = 1700
	SOURCE:屈従 = 2400
ENDIF

;ABL:排泄中毒をみる
IF ABL:排泄中毒 == 0
	SOURCE:痛み = 2000
	SOURCE:屈従 += 200
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 2000
ELSEIF ABL:排泄中毒 == 1
	SOURCE:痛み = 1600
	SOURCE:屈従 += 500
	SOURCE:不潔 += 2000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 1000
ELSEIF ABL:排泄中毒 == 2
	SOURCE:痛み = 1200
	SOURCE:屈従 += 800
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 500
ELSEIF ABL:排泄中毒 == 3
	SOURCE:痛み = 800
	SOURCE:屈従 += 1200
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 100
ELSEIF ABL:排泄中毒 == 4
	SOURCE:痛み = 600
	SOURCE:屈従 += 1500
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 0
ELSE
	SOURCE:痛み = 400
	SOURCE:屈従 += 2000
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 800
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 500
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 300
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 120
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.10
ELSE
	TIMES SOURCE:快Ａ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ａ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ａ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ａ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ａ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ａ , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.30
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;浣腸プラグの着脱
IF TEQUIP:Ａ系装着器具 == 7
	TEQUIP:Ａ系装着器具 = 0
	IF TFLAG:172 > 20
		LOCAL = 5
	ELSEIF TFLAG:172 > 10
		LOCAL = 3
	ELSEIF TFLAG:172 > 5
		LOCAL = 2
	ELSE
		LOCAL = 1
	ENDIF
ELSE
	TEQUIP:Ａ系装着器具 = 7
	IF TEQUIP:触手調教 || TEQUIP:触手服 & 16
		TEQUIP:Ａ系装着器具 = 7
		STAIN:アナル |= 2
		STAIN:アナル |= 4
		STAIN:アナル |= 64
	ENDIF
	LOCAL = 0
ENDIF

TCVAR:Ａ経験 += 5
IF LOCAL > 0
	TCVAR:調教排泄経験 += LOCAL
	IF (CFLAG:20 & 2) == 0
		TCVAR:異常経験 += 1
		CFLAG:20 |= 2
	ENDIF
	TFLAG:172 = 0
	TFLAG:140 |= 256
ENDIF

;コマンド属性：道具(調教者)
TFLAG:98 = 1
RETURN 1

;--------------------------------------------------
;浣腸＋アナルプラグ挿入中
;--------------------------------------------------
@EQUIP_COM174
IF TEQUIP:触手調教 || TEQUIP:触手服 & 16
	PRINTL ＜触手浣腸中＞
ELSE
	PRINTL ＜浣腸＋アナルプラグ挿入中＞
ENDIF

DOWNBASE:0 += 100
DOWNBASE:1 += 80

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	LOCAL = 80
	LOCAL:1 = 300
ELSEIF ABL:Ａ感覚 == 1
	LOCAL = 250
	LOCAL:1 = 800
ELSEIF ABL:Ａ感覚 == 2
	LOCAL = 600
	LOCAL:1 = 1400
ELSEIF ABL:Ａ感覚 == 3
	LOCAL = 1000
	LOCAL:1 = 1800
ELSEIF ABL:Ａ感覚 == 4
	LOCAL = 1300
	LOCAL:1 = 2100
ELSE
	LOCAL = 1700
	LOCAL:1 = 2400
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES LOCAL , 0.50
	LOCAL:2 = 2000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.00
	LOCAL:2 = 300
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES LOCAL , 1.10
	LOCAL:2 = 50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 10
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
	LOCAL:2 += 800
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.80
	LOCAL:2 += 500
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 += 300
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	LOCAL:2 += 120
ELSE
	TIMES LOCAL , 1.80
	LOCAL:2 += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES LOCAL:2 , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES LOCAL , 1.50
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 0.60
	TIMES LOCAL:2 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	LOCAL:1 /= 3

SOURCE:快Ａ += LOCAL
SOURCE:屈従 += LOCAL:1
SOURCE:痛み += LOCAL:2
SOURCE:逸脱 += LOCAL:1

CALL DISPLAY_EQUIP_MESSAGE_COM, 174

TCVAR:Ａ経験 += 3
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
;触手経験フラグ
SIF TEQUIP:触手調教
	TFLAG:90 += 1
TFLAG:172 += 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_174
IF TEQUIP:Ａ系装着器具 == 7
	PRINTFORML %CALLNAME:TARGET%は肛門栓を抜かれ、腸内の汚物を肛門から撒き散らした
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF ABL:マゾっ気 == 0
			PRINTFORML %CALLNAME:TARGET%は苦悶と恥辱の表情を浮かべている
		ELSEIF ABL:マゾっ気 == 1
			PRINTFORML %CALLNAME:TARGET%は脂汗を浮かべ、いまだ下腹に残る排泄感に堪えている
		ELSEIF ABL:マゾっ気 == 2
			PRINTFORML %CALLNAME:TARGET%は恥ずかしがりながらも、開放感にほっとした表情を浮かべている
		ELSEIF ABL:マゾっ気 == 3
			PRINTFORML %CALLNAME:TARGET%は、肛門を通り過ぎる排泄物の感覚に時折艶っぽい喘ぎ声を上げている
		ELSEIF ABL:マゾっ気 == 4
			PRINTFORML %CALLNAME:TARGET%は、うっとりした表情を浮かべ、排泄感を愉しんでいる
		ELSE
			PRINTFORML %CALLNAME:TARGET%は、排泄感と恥辱を心ゆくまで味わっている
			PRINTFORML %CALLNAME:TARGET%の表情は快感にだらしなく緩み、肛門から汚物をひりだすと同時に、膣口からも愛液をボタボタと滴らせている
		ENDIF
	ENDIF
ELSE
	SELECTCASE TFLAG:99
		CASE 1
			PRINTFORML %CALLNAME:TARGET%の菊穴に浣腸液を流し込み、栓をした
		CASE 2
			PRINTFORML %CALLNAME:TARGET%の菊穴に媚薬と浣腸液を流し込み、栓をした
		CASE 3
			PRINTFORML %CALLNAME:TARGET%の菊穴に利尿剤と浣腸液を流し込み、栓をした
		CASE 4
			PRINTFORML %CALLNAME:TARGET%の菊穴に賢者の血と浣腸液を流し込み、栓をした
		CASE 5
			PRINTFORML %CALLNAME:TARGET%の菊穴に興奮剤と浣腸液を流し込み、栓をした
		CASE 6
			PRINTFORML %CALLNAME:TARGET%の菊穴に感覚増幅薬と浣腸液を流し込み、栓をした
	ENDSELECT
ENDIF

;--------------------------------------------------
;装着時メッセージ
;--------------------------------------------------
@SYSTXT_EQUIP_MESSAGE_COM_174
RESETCOLOR
PRINTFORML %CALLNAME:TARGET%は大量に浣腸液を注がれ、肛門に栓をされたまま攻め続けられている

IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORM %CALLNAME:TARGET%は
	IF ABL:マゾっ気 == 0
		PRINTL 苦悶の表情を浮かべている
	ELSEIF ABL:マゾっ気 == 1
		PRINTL 脂汗を浮かべ、排泄感に堪えている
	ELSEIF ABL:マゾっ気 == 2
		PRINTL 苦しそうな表情をしながらも、顔を紅潮させもじもじとお尻を振っている
	ELSEIF ABL:マゾっ気 == 3
		PRINTL 排泄感に苛まれながらも、時折恍惚とした表情を浮かべている
	ELSEIF ABL:マゾっ気 == 4
		PRINTL うっとりした表情を浮かべ、排泄感と腹痛を愉しんでいる
	ELSE
		PRINTL 、排泄感を心ゆくまで味わっている。
		PRINTFORML %CALLNAME:TARGET%の表情は快感にだらしなく緩み、\@(TALENT:オトコ) ? 口からは # その口と膣口からは\@ポタポタと涎を滴らせている
	ENDIF
ENDIF
SETCOLOR COLOR("P-RED")

;--------------------------------------------------
;浣腸プレイブレンド可否判定
;--------------------------------------------------
@KANTYO_BLEND_CHECK(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:薬毒耐性 == 0 && ITEM:媚薬 && TEQUIP:ARG:媚薬 == 0 && EQUIP:ARG:睡眠薬 == 0
	LOCAL |= 1
SIF TALENT:ARG:薬毒耐性 == 0 && ITEM:利尿剤 && TEQUIP:ARG:利尿剤 == 0 && EQUIP:ARG:睡眠薬 == 0 && TEQUIP:ARG:コスチュームプレイ != 1
	LOCAL |= 2
SIF FLAG:15 & 32 && TALENT:ARG:薬毒耐性 == 0 && ITEM:賢者の血 && TEQUIP:ARG:賢者の血 == 0 && TEQUIP:ARG:産卵促進剤 == 0 && EQUIP:ARG:睡眠薬 == 0
	LOCAL |= 4
SIF FLAG:15 & 32 && TALENT:ARG:薬毒耐性 == 0 && ITEM:興奮剤 && TEQUIP:ARG:興奮剤 == 0 && TEQUIP:ARG:産卵促進剤 == 0 & EQUIP:ARG:睡眠薬 == 0
	LOCAL |= 8
SIF FLAG:15 & 32 && TALENT:ARG:薬毒耐性 == 0 && ITEM:感覚増幅薬 && TEQUIP:ARG:7 == 0 && TEQUIP:ARG:産卵促進剤 == 0 && EQUIP:ARG:睡眠薬 == 0 && EXP:ARG:薬物経験 >= EXPLV:3
	LOCAL |= 16

RETURNF LOCAL
