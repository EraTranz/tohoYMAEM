;=============================================================================
;尻素股
;=============================================================================
;騎乗位の体位から尻の割れ目で擦らせ女性がおちんちんの裏筋に手を添えてやる。尻扱きと似ているようでちょっと違う
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE183
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:183 > 0
	RETURN 0
;Ａ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4) == 0
	RETURN 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 183) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM183
PRINTL 尻素股

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 183
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 尻素股
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 400
ELSEIF ABL:技巧 == 1
	LOCAL = 880
ELSEIF ABL:技巧 == 2
	LOCAL = 1600
ELSEIF ABL:技巧 == 3
	LOCAL = 2400
ELSEIF ABL:技巧 == 4
	LOCAL = 3120
ELSE
	LOCAL = 3680
ENDIF

;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_3(TARGET, LOCAL)
;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 50
DOWNBASE:1 += 135

SOURCE:露出 = 900
SOURCE:主導権 = 2000
SOURCE:屈従 = 600
SOURCE:逸脱 = 450

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*10 + 60

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 200
	SOURCE:達成感 = 50
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 220
	SOURCE:達成感 = 90
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 300
	SOURCE:達成感 = 125
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 425
	SOURCE:達成感 = 225
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 500
	SOURCE:達成感 = 360
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 650
	SOURCE:達成感 = 550
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.70
	TIMES SOURCE:達成感 , 0.70
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.90
	TIMES SOURCE:達成感 , 0.90
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.40
	TIMES SOURCE:達成感 , 1.40
ELSE
	TIMES SOURCE:性行動 , 1.60
	TIMES SOURCE:達成感 , 1.60
ENDIF

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:屈従 += 1800
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 37
	SOURCE:屈従 += 1850
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 150
	SOURCE:屈従 += 1900
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 350
	SOURCE:屈従 += 2150
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 550
	SOURCE:屈従 += 2500
ELSE
	SOURCE:快Ａ = 750
	SOURCE:屈従 += 3000
ENDIF

;PALAM:潤滑
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.30
	TIMES SOURCE:性行動 , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:性行動 , 0.80
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:性行動 , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.50
	TIMES SOURCE:性行動 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:5
	TIMES SOURCE:快Ａ , 2.00
	TIMES SOURCE:性行動 , 1.40
ELSE
	TIMES SOURCE:快Ａ , 2.50
	TIMES SOURCE:性行動 , 1.60
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 2.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 1.50
		TIMES SOURCE:屈従, 1.40
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 400
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 0.80
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 700
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 0.50
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 1000
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 0.20
	ELSE
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成感 , 5.00
		TIMES SOURCE:屈従, 0.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_183
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF

;SIF PLAYER == MASTER
;	TCVAR:Ｃ調律経験 += 1

;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += 1
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_183
PRINTFORML %CALLNAME:TARGET%に騎乗位の体勢から尻の割れ目に挟むんで奉仕をするように命令したが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_183
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%に跨り、ペニスをお尻の割れ目にはさみ、
IF TALENT:恋慕 && TALENT:淫乱 && PALAM:欲情 > PALAMLV:5
	PRINT 快楽に溺れきった表情を浮かべながら
ELSEIF TALENT:恋慕 && PALAM:欲情 > PALAMLV:5
	PRINT 今にもとろけそうな表情を浮かべながら
ELSEIF TALENT:淫乱 && PALAM:欲情 > PALAMLV:5
	PRINT 待ち望んでいたという様子で
ELSEIF PALAM:欲情 > PALAMLV:4
	PRINT 口の端からよだれをこぼしながら
ELSEIF TALENT:恋慕 && TALENT:淫乱
	PRINT 陶酔しきった瞳をしながら
ELSEIF TALENT:恋慕
	PRINT 幸せそうな表情を浮かべながら
ELSEIF TALENT:淫乱
	PRINT 淫猥で挑発的な振動をさせながら
ELSEIF TALENT:恥薄い
	PRINT 大胆に腰を動かしながら
ELSEIF TALENT:感情乏しい
	PRINT うっすらと上気した顔で
ELSE
	PRINT 恥ずかしそうに
ENDIF
PRINTL ペニスの裏側に手を添えて、自らのお尻のワレメに擦りつけている…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_183, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 2
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 2})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 4
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 4
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)
;尻素股では汚れの影響が少なめ
TFLAG:242 /= 3

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 22

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_183
;デフォルト射精箇所タイプを設定
TFLAG:703 = 12
;射精箇所選択の可・不可
TFLAG:704 = 0
