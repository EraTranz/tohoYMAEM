;=============================================================================
;乳腺開発
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE192
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:192 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;一度も乳腺開発で母乳体質になったことが無い場合、
;異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:21 & 16) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;母乳体質じゃダメ
SIF TALENT:母乳体質
	RETURN 0
;調教者の技巧が4以上じゃないとダメ
SIF ABL:PLAYER:技巧 < 4
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;褌と裸Ｙシャツ以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 11 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 192) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM192
PRINTL 乳腺開発

;戦闘判定
CALL COM_TRY_BATTLE, 192
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 乳腺開発

DOWNBASE:0 += 500
DOWNBASE:1 += 3000

IF ABL:PLAYER:技巧 == 4
	SOURCE:痛み = 5000
ELSEIF ABL:PLAYER:技巧 >= 5
	SOURCE:痛み = 2000
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	SOURCE:逸脱 = 10000
ELSEIF ABL:従順 == 1
	SOURCE:逸脱 = 8000
ELSEIF ABL:従順 == 2
	SOURCE:逸脱 = 6000
ELSEIF ABL:従順 == 3
	SOURCE:逸脱 = 4000
ELSEIF ABL:従順 == 4
	SOURCE:逸脱 = 2000
ELSE
	SOURCE:逸脱 = 500
ENDIF

;淫乳
SIF TALENT:淫乳
	SOURCE:欲情追加 = 300

IF ABL:PLAYER:技巧 >= 5 || ABL:従順 >= 5
	IF TALENT:淫乳
		LOCAL:1 = RAND:2
	ELSEIF TALENT:Ｂ敏感
		LOCAL:1 = RAND:4
	ELSEIF TALENT:Ｂ鈍感
		LOCAL:1 = RAND:8
	ELSE
		LOCAL:1 = RAND:6
	ENDIF
ELSE
	IF TALENT:淫乳
		LOCAL:1 = RAND:4
	ELSEIF TALENT:Ｂ敏感
		LOCAL:1 = RAND:8
	ELSEIF TALENT:Ｂ鈍感
		LOCAL:1 = RAND:16
	ELSE
		LOCAL:1 = RAND:12
	ENDIF
ENDIF
SIF ITEM:針 && (TALENT:PLAYER:サド || TALENT:PLAYER:ドＳ || ABL:PLAYER:サドっ気 > 2) && LOCAL:1 > 0
	LOCAL:1 -= 1
;口上用
TFLAG:99 = LOCAL:1
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

IF LOCAL:1 == 0 && TALENT:絶壁 == 0
	TALENT:母乳体質 = 1
	CALL CALC_GAUGE3_SLAVE, TARGET, 1
	IF (CFLAG:21 & 16) == 0
		TCVAR:異常経験 += 1
		CFLAG:21 |= 16
	ENDIF
ELSEIF LOCAL:1 > 0
	TCVAR:乳腺開発経験 += LOCAL:1
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_192
PRINTFORM %CALLNAME:PLAYER%は
SIF ITEM:針 && (TALENT:PLAYER:サド || TALENT:PLAYER:ドＳ || ABL:PLAYER:サドっ気 > 2)
	PRINTFORM %ITEMNAME:33%を用いて
PRINTFORML %CALLNAME:TARGET%の乳腺に性的な開発を施した
IF TALENT:絶壁
	PRINTFORML しかし%CALLNAME:TARGET%の乳腺を開発するには今の胸の大きさのままでは少し無理があるようだ…
ELSEIF TFLAG:99 == 0
	PRINTFORML ツボに刺激を受けているうちに%CALLNAME:TARGET%の乳腺から母乳が垂れ出るようになった…
ELSE
	PRINTFORML 技巧が足りないのか、%CALLNAME:TARGET%の乳腺を開発しきれなかった…
ENDIF
