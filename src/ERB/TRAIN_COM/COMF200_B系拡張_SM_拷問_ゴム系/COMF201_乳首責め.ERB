;=============================================================================
;乳首責め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE201
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:201 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;調教者がウェディングドレス・ゴスロリ着用中の場合調教者に技巧4以上必須
SIF (TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20) && ABL:PLAYER:技巧 < 4
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 201) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM201
PRINTL 乳首責め

;戦闘判定
CALL COM_TRY_BATTLE, 201
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 乳首責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 10
DOWNBASE:1 += 80

SOURCE:性行動 = 60
SOURCE:不潔 = 80
SOURCE:露出 = 140

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 60
	SOURCE:情愛 = 30
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 300
	SOURCE:情愛 = 60
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 600
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1400
	SOURCE:情愛 = 120
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2200
	SOURCE:情愛 = 140
ELSE
	SOURCE:快Ｂ = 3000
	SOURCE:情愛 = 300
ENDIF

IF TALENT:母乳体質 == 0
	TIMES SOURCE:快Ｂ , 0.90
	TIMES SOURCE:情愛 , 0.90
;ABL:噴乳中毒をみる
ELSEIF ABL:噴乳中毒 == 0
	TIMES SOURCE:快Ｂ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:噴乳中毒 == 1
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:噴乳中毒 == 2
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:噴乳中毒 == 3
	TIMES SOURCE:快Ｂ , 1.40
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:噴乳中毒 == 4
	TIMES SOURCE:快Ｂ , 1.40
	TIMES SOURCE:情愛 , 1.40
ELSE
	TIMES SOURCE:快Ｂ , 1.60
	TIMES SOURCE:情愛 , 1.60
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.40
ENDIF

;調教者が［幼児］、［幼稚］
IF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF
;調教者が［幼児退行］
SIF TALENT:PLAYER:幼児退行
	TIMES SOURCE:情愛 , 1.20

;奴隷のＢ⇔調教者の指の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_201
PRINTFORM %CALLNAME:TARGET%の
IF TALENT:絶壁
	PRINT ほぼ無に等しい
ELSEIF TALENT:小柄体型 && TALENT:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:爆乳
	PRINT 手に余るほど大きな
ENDIF
IF TEQUIP:焦らしプレイ
	PRINT 胸の
	SIF EX:23
		PRINT コリコリにしこった
	PRINTL 先端を、指の腹でそっとくすぐった…
ELSEIF PALAM:欲情 > PALAMLV:3
	PRINTL 胸の先端の、すっかり硬くなった突起をつまみあげた…
ELSE
	PRINTL 胸の先端の、まだ柔らかい突起をつまみあげた…
ENDIF
