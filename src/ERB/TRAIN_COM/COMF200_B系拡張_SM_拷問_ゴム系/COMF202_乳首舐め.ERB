;=============================================================================
;乳首舐め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE202
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:202 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;調教者が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:PLAYER:猫舌 && (FLAG:14 & 16) == 0
	RETURN 0
;調教者が[猫舌]だと調教者に技巧4必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 4
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;調教者がウェディングドレス・ゴスロリ着用中の場合調教者に技巧4以上必須
SIF (TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20) && ABL:PLAYER:技巧 < 4
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 202) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM202
;………………………………………………
;コマンドの派生判定
;………………………………………………
;連続で使うと少しだけ性能が高くなった乳首甘噛みに変化。
SIF PREVCOM == 202
	JUMP COM627

PRINTL 乳首舐め

;戦闘判定
CALL COM_TRY_BATTLE, 202
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 乳首舐め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:露出 = 140
SOURCE:性行動 = 60
SOURCE:達成感 = 40
SOURCE:不潔 = 80

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 400
	SOURCE:情愛 = 160
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
	SOURCE:情愛 = 200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1400
	SOURCE:情愛 = 230
ELSE
	SOURCE:快Ｂ = 2000
	SOURCE:情愛 = 250
ENDIF

IF TALENT:母乳体質
	;ABL:噴乳中毒をみる
	IF ABL:噴乳中毒 == 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:噴乳中毒 == 1
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:噴乳中毒 == 2
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:情愛 , 1.20
	ELSEIF ABL:噴乳中毒 == 3
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:情愛 , 1.20
	ELSEIF ABL:噴乳中毒 == 4
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:情愛 , 1.40
	ELSE
		TIMES SOURCE:快Ｂ , 1.60
		TIMES SOURCE:情愛 , 1.60
	ENDIF
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:性行動 , 0.60
	TIMES SOURCE:不潔 , 1.40
	TIMES SOURCE:快Ｂ , 0.50
ELSEIF ABL:従順 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
	TIMES SOURCE:不潔 , 1.20
	TIMES SOURCE:快Ｂ , 0.70
ELSEIF ABL:従順 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
	TIMES SOURCE:不潔 , 1.00
	TIMES SOURCE:快Ｂ , 0.80
ELSEIF ABL:従順 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.00
ELSEIF ABL:従順 == 4
	TIMES SOURCE:性行動 , 1.60
	TIMES SOURCE:達成感 , 1.60
	TIMES SOURCE:不潔 , 0.40
	TIMES SOURCE:快Ｂ , 1.20
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
	TIMES SOURCE:不潔 , 0.00
	TIMES SOURCE:快Ｂ , 1.50
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.40
ENDIF

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｂ , 1.40
ENDIF
;調教者が［幼児］、［幼稚］
IF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF
;調教者が［幼児退行］
SIF TALENT:PLAYER:幼児退行
	TIMES SOURCE:情愛 , 1.20

;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快Ｂ , 0.70

;奴隷のＢ⇔調教者の口の汚れが移動
STAIN:胸 |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_202
PRINTFORM %CALLNAME:TARGET%の
IF TALENT:絶壁
	PRINT ほぼ無に等しい
ELSEIF TALENT:小柄体型 && TALENT:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:爆乳
	PRINT 手に余るほど大きな
ENDIF
PRINT 胸の先端の、
IF PALAM:欲情 > PALAMLV:3
	PRINT すっかり硬くなった突起
ELSE
	PRINT まだ柔らかい突起
ENDIF
IF TEQUIP:焦らしプレイ
	PRINTL を舌先で弾いてくすぐった…
ELSEIF TALENT:PLAYER:猫舌
	PRINTL に舌の棘を押し当てた…
ELSE
	PRINTL を舐めあげた…
ENDIF
