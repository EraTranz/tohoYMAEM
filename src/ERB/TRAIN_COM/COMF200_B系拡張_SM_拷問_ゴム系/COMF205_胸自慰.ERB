;=============================================================================
;胸自慰
;=============================================================================
;ビデオ撮影の時に内容変化
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE205
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:205 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;公開自慰を行ったことがない場合のビデオ撮影中は、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:21 & 1) == 0 && TEQUIP:ビデオ撮影
	RETURN 0
;助手調教で[小悪魔][妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)4未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:小悪魔 == 0 && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 4
		RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;ウェディングドレス・ゴスロリ着用中の場合技巧4以上必須
SIF (TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20) && ABL:技巧 < 4
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 205) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM205
;………………………………………………
;コマンドの派生判定
;………………………………………………
;前コマンドが挿入関連の場合騎乗乳房自慰にジャンプ
IF (PREVCOM == 20 || PREVCOM == 21 || PREVCOM == 23 || PREVCOM == 24 || PREVCOM == 25 || PREVCOM == 613 || PREVCOM == 614 || PREVCOM == 615) && TFLAG:102
	SIF EXCOM_ABLE613() == 1
		JUMP COM613
ENDIF

LOCALS = 
;ビデオ撮影中なら公開オナニー
SIF TEQUIP:ビデオ撮影
	LOCALS = %LOCALS%公開
LOCALS = %LOCALS%胸自慰
PRINTFORML %LOCALS%

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 205
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = %LOCALS%
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 5
DOWNBASE:1 += 50

SOURCE:逸脱 = 400

;お風呂場プレイのとき
IF TEQUIP:お風呂場プレイ
	SOURCE:液体追加 = 50
	SOURCE:欲情追加 = 100
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:露出 = 2000
	SOURCE:屈従 = 500
	SOURCE:快Ｂ = 30
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:露出 = 2300
	SOURCE:屈従 = 800
	SOURCE:快Ｂ = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:露出 = 2600
	SOURCE:屈従 = 1200
	SOURCE:快Ｂ = 600
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:露出 = 2900
	SOURCE:屈従 = 1900
	SOURCE:快Ｂ = 1400
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:露出 = 3200
	SOURCE:屈従 = 2500
	SOURCE:快Ｂ = 2200
ELSE
	SOURCE:露出 = 3500
	SOURCE:屈従 = 3000
	SOURCE:快Ｂ = 3200
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	SOURCE:性行動 = 100
	TIMES SOURCE:快Ｂ , 0.30
ELSEIF ABL:技巧 == 1
	SOURCE:性行動 = 160
	TIMES SOURCE:快Ｂ , 0.70
ELSEIF ABL:技巧 == 2
	SOURCE:性行動 = 220
	TIMES SOURCE:快Ｂ , 1.00
ELSEIF ABL:技巧 == 3
	SOURCE:性行動 = 280
	TIMES SOURCE:快Ｂ , 1.20
ELSEIF ABL:技巧 == 4
	SOURCE:性行動 = 340
	TIMES SOURCE:快Ｂ , 1.40
ELSE
	SOURCE:性行動 = 400
	TIMES SOURCE:快Ｂ , 1.60
ENDIF

;ABL:自慰中毒をみる
IF ABL:自慰中毒 == 0
	SOURCE:中毒充足 = 0
	TIMES SOURCE:快Ｂ , 1.00
ELSEIF ABL:自慰中毒 == 1
	SOURCE:中毒充足 = 100
	TIMES SOURCE:快Ｂ , 1.10
ELSEIF ABL:自慰中毒 == 2
	SOURCE:中毒充足 = 300
	TIMES SOURCE:快Ｂ , 1.20
ELSEIF ABL:自慰中毒 == 3
	SOURCE:中毒充足 = 800
	TIMES SOURCE:快Ｂ , 1.30
ELSEIF ABL:自慰中毒 == 4
	SOURCE:中毒充足 = 1500
	TIMES SOURCE:快Ｂ , 1.50
ELSE
	SOURCE:中毒充足 = 2500
	TIMES SOURCE:快Ｂ , 1.70
ENDIF

;公開のときは、ABL:露出癖をみる
IF TEQUIP:ビデオ撮影
	IF ABL:露出癖 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:露出, 1.00
		TIMES SOURCE:快Ｂ , 1.00
	ELSEIF ABL:露出癖 == 1
		SOURCE:中毒充足 += 100
		TIMES SOURCE:露出, 1.20
		TIMES SOURCE:快Ｂ , 1.20
	ELSEIF ABL:露出癖 == 2
		SOURCE:中毒充足 += 300
		TIMES SOURCE:露出, 1.40
		TIMES SOURCE:快Ｂ , 1.40
	ELSEIF ABL:露出癖 == 3
		SOURCE:中毒充足 += 800
		TIMES SOURCE:露出, 1.60
		TIMES SOURCE:快Ｂ , 1.60
	ELSEIF ABL:露出癖 == 4
		SOURCE:中毒充足 += 1500
		TIMES SOURCE:露出, 2.00
		TIMES SOURCE:快Ｂ , 1.80
	ELSE
		SOURCE:中毒充足 += 2500
		TIMES SOURCE:露出, 3.00
		TIMES SOURCE:快Ｂ , 2.00
	ENDIF
ENDIF

IF TALENT:母乳体質
	;ABL:噴乳中毒をみる
	IF ABL:噴乳中毒 == 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:中毒充足 , 1.00
	ELSEIF ABL:噴乳中毒 == 1
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:中毒充足 , 1.00
	ELSEIF ABL:噴乳中毒 == 2
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:中毒充足 , 1.20
	ELSEIF ABL:噴乳中毒 == 3
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:中毒充足 , 1.20
	ELSEIF ABL:噴乳中毒 == 4
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:中毒充足 , 1.40
	ELSE
		TIMES SOURCE:快Ｂ , 1.60
		TIMES SOURCE:中毒充足 , 1.60
	ENDIF
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:性行動 , 1.40
	TIMES SOURCE:中毒充足 , 1.40
	TIMES SOURCE:快Ｂ , 1.40
ENDIF

;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオ撮影
	TCVAR:自慰経験 += 2
	TCVAR:調教自慰経験 += 2
ELSE
	TCVAR:自慰経験 += 1
	TCVAR:調教自慰経験 += 1
ENDIF
IF TEQUIP:ビデオ撮影 && (CFLAG:21 & 1) == 0
	TCVAR:異常経験 += 1
	CFLAG:21 |= 1
ENDIF

;奴隷の指⇔奴隷のＢの汚れが移動
STAIN:手 |= STAIN:胸
STAIN:胸 |= STAIN:手

;欲望or自慰中毒が３以上かつ貧乳、絶壁、猫舌じゃないと口も使う
IF TALENT:猫舌 == 0 && TALENT:絶壁 == 0 && TALENT:貧乳 == 0 && (ABL:欲望 >= 3 || ABL:自慰中毒 >= 3 )
	TIMES SOURCE:快Ｂ , 1.10
	TIMES SOURCE:中毒充足 , 1.10
	;対象のTALENT:舌使いを見る
	SIF TALENT:舌使い
		TIMES SOURCE:快Ｂ , 1.20
	STAIN:口 |= STAIN:胸
	STAIN:胸 |= STAIN:口
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 3
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 3
ENDIF

;露出快楽経験入手判定
TFLAG:100 |= 2

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_205
IF TEQUIP:焦らしプレイ
	SIF PALAM:快Ｃ >= (PALAMLV:4 - 1) ||  PALAM:快Ｖ >= (PALAMLV:4 - 1) ||  PALAM:快Ａ >= (PALAMLV:4 - 1) ||  PALAM:潤滑 >= (PALAMLV:4 - 1)
		PRINT 今にもイきそうになっているにもかかわらず、
	PRINTFORML %CALLNAME:TARGET%は命令を拒否してきた…
ELSE
	PRINTFORML %CALLNAME:TARGET%に胸自慰を命令してみたが拒否された。もう少し躾をする必要があるようだ。
ENDIF

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_205
;焦らしプレイ中に自慰系・騎乗位系・助手を犯させる
IF TEQUIP:焦らしプレイ
	SIF PALAM:快Ｃ >= (PALAMLV:4 - 1) ||  PALAM:快Ｖ >= (PALAMLV:4 - 1) ||  PALAM:快Ａ >= (PALAMLV:4 - 1) ||  PALAM:潤滑 >= (PALAMLV:4 - 1)
		PRINT 今にもイきそうな
	PRINTFORML %CALLNAME:TARGET%の好きにさせてやることにした…
ENDIF
IF TFLAG:699 > 0
	PRINTFORML %CALLNAME:MASTER%が何もしないでいると、命令していないのに%CALLNAME:TARGET%は
	SIF TALENT:淫乱 || TEQUIP:媚薬 == 1
		PRINT 我慢できないと言った表情で
	SIF TALENT:サド
		PRINTFORM 煽るような視線を%CALLNAME:MASTER%に送ると
	SIF TALENT:嫉妬
		PRINT 嫉妬の念をぶつける様に
	SIF TALENT:狂気 && TEQUIP:口装着器具 == 0
		PRINT 壊れた様な笑い声を上げ、
	IF TALENT:絶壁
		PRINT ほぼ無に等しい
	ELSEIF TALENT:貧乳
		PRINT その控えめな
	ELSEIF TALENT:巨乳
		PRINT その大きな
	ELSEIF TALENT:爆乳
		PRINT その巨大な
	ENDIF
	SIF TALENT:母乳体質
		PRINT 母乳が滴る
	PRINTL 胸を自らの指で弄りながら一人で勝手に快感に浸っている…
ELSE
	SIF EX:23
		PRINTFORM お預けされている%CALLNAME:TARGET%に自慰を許すとだらしな\@(TEQUIP:口装着器具) ? い表情を見せつつ # く唇を歪め\@、
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:ビデオ撮影 && ABL:露出癖 > 3
		PRINTL カメラに見せつけるようにして、
	ELSEIF TEQUIP:ビデオ撮影
		PRINTL カメラの前で、
	ELSEIF ABL:露出癖 > 3
		PRINTL 見せつけるようにして、
	ENDIF
	IF TALENT:絶壁
		PRINT ほぼ無に等しい
	ELSEIF TALENT:貧乳
		PRINT その控えめな
	ELSEIF TALENT:巨乳
		PRINT その大きな
	ELSEIF TALENT:爆乳
		PRINT その巨大な
	ENDIF
	SIF TALENT:母乳体質
		PRINT 母乳が滴る
	PRINTL 胸を自らの指で弄りながら快感を味わっている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_205, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:露出癖
IF ABL:露出癖
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:露出癖
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:14%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:自慰中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:30%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2 * 3
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;自制心
IF TALENT:自制心
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:20%({LOCAL:99})
ENDIF
;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
;恥薄い
ELSEIF TALENT:恥薄い
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:36%({LOCAL:99})
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:60%({LOCAL:99})
ENDIF
;快感に素直
IF TALENT:快感に素直
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:70%({LOCAL:99})
;快感の否定
ELSEIF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 5
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ITEMNAME:61%({LOCAL:99})
ENDIF

;難易度上昇
;公開で+10
TFLAG:241 = 33
SIF TEQUIP:ビデオ撮影
	TFLAG:241 += 10
