;=============================================================================
;乳ファック
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE207
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:207 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;褌と裸Ｙシャツ以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 11 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 207) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM207
PRINTL 乳ファック

;戦闘判定
CALL COM_TRY_BATTLE, 207
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 乳ファック
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1350
ELSEIF ABL:技巧 == 1
	LOCAL = 1890
ELSEIF ABL:技巧 == 2
	LOCAL = 2610
ELSEIF ABL:技巧 == 3
	LOCAL = 3600
ELSEIF ABL:技巧 == 4
	LOCAL = 4500
ELSE
	LOCAL = 5400
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_3(TARGET, LOCAL)
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7
;調教対象のバストサイズを見る
IF TALENT:絶壁
	TIMES LOCAL , 0.75
ELSEIF TALENT:貧乳
	TIMES LOCAL , 0.90
ELSEIF TALENT:巨乳
	TIMES LOCAL , 1.20
ELSEIF TALENT:爆乳
	TIMES LOCAL , 1.35
ENDIF

;ABL:調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 60
DOWNBASE:1 += 100

SOURCE:不潔 = 1000
SOURCE:露出 = 200
SOURCE:逸脱 = 500

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 10
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 40
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 300
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1700
ELSE
	SOURCE:快Ｂ = 2500
ENDIF

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 250
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 400
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 800
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 1050
ELSE
	SOURCE:痛み = 1200
ENDIF

;母乳体質のときは、ABL:噴乳中毒をみる
IF TALENT:母乳体質
	IF ABL:噴乳中毒 == 0
		LOCAL:1 += 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:逸脱, 1.00
	ELSEIF ABL:噴乳中毒 == 1
		LOCAL:1 += 50
		TIMES SOURCE:快Ｂ , 1.05
		TIMES SOURCE:逸脱, 1.10
	ELSEIF ABL:噴乳中毒 == 2
		LOCAL:1 += 200
		TIMES SOURCE:快Ｂ , 1.10
		TIMES SOURCE:逸脱, 1.20
	ELSEIF ABL:噴乳中毒 == 3
		LOCAL:1 += 600
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:逸脱, 1.30
	ELSEIF ABL:噴乳中毒 == 4
		LOCAL:1 += 1200
		TIMES SOURCE:快Ｂ , 1.25
		TIMES SOURCE:逸脱, 1.80
	ELSE
		LOCAL:1 += 2000
		TIMES SOURCE:快Ｂ , 1.50
		TIMES SOURCE:逸脱, 2.50
	ENDIF
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

IF TFLAG:91
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 1.25
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 300
		TIMES SOURCE:達成感 , 1.75
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1000
		TIMES SOURCE:達成感 , 3.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 2300
		TIMES SOURCE:達成感 , 5.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 4800
		TIMES SOURCE:達成感 , 7.50
	ELSE
		SOURCE:中毒充足 = 8000
		TIMES SOURCE:達成感 , 10.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_207
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF
SOURCE:中毒充足 += LOCAL:1

;奴隷のＢ⇔調教者のＰの汚れが移動
STAIN:胸 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_207
PRINTFORM %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の
IF TALENT:貧乳
	PRINT 薄い胸のかすかな膨らみに擦り付けるように
ELSEIF TALENT:絶壁
	PRINT 殆ど平らな胸に擦り回すように
ELSEIF TALENT:巨乳
	PRINT 豊満な胸に埋もれさせ突き刺すように
ELSEIF TALENT:爆乳
	PRINT 豊か過ぎる胸に凹ませ埋め込むように
ELSE
	PRINT 胸で
ENDIF
PRINTL 強引にペニスを扱いた…

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_207
;デフォルト射精箇所タイプを設定
TFLAG:703 = 6
;射精箇所選択の可・不可
TFLAG:704 = 1
