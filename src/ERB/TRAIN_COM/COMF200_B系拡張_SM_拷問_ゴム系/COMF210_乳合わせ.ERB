;=============================================================================
;乳合わせ
;=============================================================================
;調教者と対象が乳首を合わせるプレイ
;見た目的に眼福だが効果的にはしょぼい
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE210
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:210 > 0
	RETURN 0
;Ｂ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;調教者の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(PLAYER) == 0
	RETURN 0
;調教対象と調教者の体格差が違いすぎるとダメ(禁断の知識は無関係で)
SIF ABS(COMMON_BODYSIZE_CHECK2(PLAYER, TARGET)) > 1
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;助手がウェディングドレス・ゴスロリ着用中の場合助手に技巧4以上必須
SIF ASSI >= 0 && ASSIPLAY && (TEQUIP:ASSI:コスチュームプレイ == 19 || TEQUIP:ASSI:コスチュームプレイ == 20) && ABL:ASSI:技巧 < 4
	RETURN 0
;調教者がウェディングドレス・ゴスロリ着用中の場合調教者に技巧4以上必須
SIF (TEQUIP:コスチュームプレイ == 19 || TEQUIP:コスチュームプレイ == 20) && ABL:PLAYER:技巧 < 4
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 210) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM210
PRINTL 乳合わせ

;戦闘判定
CALL COM_TRY_BATTLE, 210
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 乳合わせ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 5
DOWNBASE:1 += 60

SOURCE:性行動 = 60
SOURCE:不潔 = 80
SOURCE:露出 = 120

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 10
	SOURCE:情愛 = 30
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 50
	SOURCE:情愛 = 60
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 200
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 400
	SOURCE:情愛 = 120
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 700
	SOURCE:情愛 = 140
ELSE
	SOURCE:快Ｂ = 1000
	SOURCE:情愛 = 300
ENDIF

IF TALENT:母乳体質
	;ABL:噴乳中毒をみる
	IF ABL:噴乳中毒 == 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:噴乳中毒 == 1
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:噴乳中毒 == 2
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:情愛 , 1.20
	ELSEIF ABL:噴乳中毒 == 3
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:情愛 , 1.20
	ELSEIF ABL:噴乳中毒 == 4
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:情愛 , 1.40
	ELSE
		TIMES SOURCE:快Ｂ , 1.60
		TIMES SOURCE:情愛 , 1.60
	ENDIF
ENDIF

;ABL:調教者のＢ感覚をみる
IF ABL:PLAYER:Ｂ感覚 == 0
	SOURCE:快Ｂ += 10
ELSEIF ABL:PLAYER:Ｂ感覚 == 1
	SOURCE:快Ｂ += 50
ELSEIF ABL:PLAYER:Ｂ感覚 == 2
	SOURCE:快Ｂ += 200
ELSEIF ABL:PLAYER:Ｂ感覚 == 3
	SOURCE:快Ｂ += 400
ELSEIF ABL:PLAYER:Ｂ感覚 == 4
	SOURCE:快Ｂ += 700
ELSE
	SOURCE:快Ｂ += 1000
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.20
ENDIF

;調教者が淫乳
IF TALENT:PLAYER:淫乳
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.20
ENDIF


;奴隷のＢ⇔調教者のＢの汚れが移動
STAIN:胸 |= STAIN:PLAYER:胸
STAIN:PLAYER:胸 |= STAIN:胸

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_210
PRINTFORM %CALLNAME:PLAYER%は
IF TALENT:PLAYER:絶壁
	PRINT ほぼ無に等しい
ELSEIF TALENT:PLAYER:小柄体型 && TALENT:PLAYER:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:PLAYER:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:PLAYER:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:PLAYER:爆乳
	PRINT 手に余るほど大きな
ENDIF
PRINTL 胸の先端の、まだ柔らかい突起と、
PRINTFORM %CALLNAME:TARGET%の
IF TALENT:絶壁
	PRINT ほぼ無に等しい
ELSEIF TALENT:小柄体型 && TALENT:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:爆乳
	PRINT 手に余るほど大きな
ENDIF
IF TEQUIP:焦らしプレイ
	PRINT 胸の
	SIF EX:23
		PRINT コリコリにしこった
	PRINTL 先端とを、くっつけた…
ELSEIF PALAM:欲情 > PALAMLV:3
	PRINTL 胸の先端の、すっかり硬くなった突起とをくっつけ少し押したりしてみた…
ELSE
	PRINTL 胸の先端の、まだ柔らかい突起をくっつけ少し押したりしてみた…
ENDIF
