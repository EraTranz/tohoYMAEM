;=============================================================================
;スパンキング
;=============================================================================
;実際には苦痛があるためもっと体力気力をもっていかれる
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE220
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:220 > 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 0) == 0
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 220) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM220
IF NO:TARGET == 1011
	PRINTL お尻ペンペン

	;戦闘判定
	CALL COM_TRY_BATTLE, 220
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = お尻ペンペン
ELSE
	PRINTL スパンキング

	;戦闘判定
	CALL COM_TRY_BATTLE, 220
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = スパンキング
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 80
DOWNBASE:1 += 40

SOURCE:露出 = 200
SOURCE:逸脱 = 500

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 300
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 500
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 800
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 1200
ELSE
	SOURCE:痛み = 1800
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 1
	TFLAG:50 += 1
;ＳＭ教育経験フラグ
SIF ASSIPLAY
	TFLAG:55 = 1
RETURN 1
;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_220

IF EQUIP:睡眠薬 || TFLAG:899 > 0
	PRINTFORML %CALLNAME:TARGET%の\@(TALENT:オトコ == 0 || TALENT:幼児) ? かわいらしい # \@尻を平手で強く叩いた
ELSE
	;反抗的か抵抗を持ち陥落していない場合
	IF (TALENT:反抗的 || TALENT:抵抗) && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
		PRINTFORML 激しく嫌がる%CALLNAME:TARGET%の\@(TALENT:オトコ == 0 || TALENT:幼児) ? かわいらしい # \@尻を強引に露出させ、平手で強く叩いた
	;臆病持ちか恐怖刻印2以上で気丈と感情乏しいを持たず、陥落していない場合
	ELSEIF (TALENT:臆病 || MARK:恐怖刻印 >= 2) && TALENT:気丈 == 0 && TALENT:感情乏しい == 0 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
		PRINTFORML 恐怖に怯え身体を縮め込もうとする%CALLNAME:TARGET%を押さえつけ、その\@(TALENT:オトコ == 0 || TALENT:幼児) ? かわいらしい # \@尻を平手で強く叩いた
	ELSE
		PRINTFORML %CALLNAME:TARGET%の\@(TALENT:オトコ == 0 || TALENT:幼児) ? かわいらしい # \@尻を平手で強く叩いた
	ENDIF
ENDIF

PRINTFORM 何度も何度も叩かれている内に、%CALLNAME:TARGET%の
IF ABL:マゾっ気 >= 2 && TALENT:オトコ == 0
	PRINTL 股間は自身の愛液で濡れはじめていた…
ELSE
	PRINTL 尻は真っ赤に腫れていった…
ENDIF
