;=============================================================================
;ボールギャグ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE226
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:226 > 0
	RETURN 0
;触手口辱中、強制開口器装着中はダメ
SIF TEQUIP:口装着器具 && TEQUIP:口装着器具 != 1
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:口装着器具 == 1
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:ボールギャグ == 0
	RETURN 0
;助手調教の場合、技巧3以上じゃないと使えない
SIF ASSIPLAY && ABL:PLAYER:技巧 < 3
	RETURN 0
;裸エプロン中はダメ
SIF TEQUIP:コスチュームプレイ == 1
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 226) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM226
PRINTL ボールギャグ

;戦闘判定
CALL COM_TRY_BATTLE, 226
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ボールギャグ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	DOWNBASE:0 += 80
	DOWNBASE:1 += 100
ELSEIF EXP:緊縛経験 < EXPLV:4
	DOWNBASE:0 += 60
	DOWNBASE:1 += 80
ELSE
	DOWNBASE:0 += 40
	DOWNBASE:1 += 60
ENDIF

;LOCAL:1 = 250

SOURCE:痛み = 50
SOURCE:中毒充足 = 50
SOURCE:露出 = 80
SOURCE:屈従 = 150
SOURCE:逸脱 = 80
SOURCE:恭順追加 = 80

TCVAR:緊縛経験 += 2

;コマンド属性：道具(調教者)
SIF TEQUIP:触手調教 == 0
	TFLAG:98 = 1

;ボールギャグの着脱
IF TEQUIP:口装着器具
	TEQUIP:口装着器具 = 0
ELSE
	TEQUIP:口装着器具 = 1
ENDIF
RETURN 1

;-------------------------------------------------
;ボールギャグ装備中
;-------------------------------------------------
@EQUIP_COM226
PRINTL ＜ボールギャグ装備中＞

;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	DOWNBASE:0 += 50
	DOWNBASE:1 += 100
ELSEIF EXP:緊縛経験 < EXPLV:4
	DOWNBASE:0 += 40
	DOWNBASE:1 += 80
ELSE
	DOWNBASE:0 += 30
	DOWNBASE:1 += 60
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	LOCAL = 40
ELSEIF ABL:マゾっ気 == 1
	LOCAL = 120
ELSEIF ABL:マゾっ気 == 2
	LOCAL = 250
ELSEIF ABL:マゾっ気 == 3
	LOCAL = 450
ELSEIF ABL:マゾっ気 == 4
	LOCAL = 600
ELSE
	LOCAL = 750
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

SOURCE:露出 += LOCAL
SOURCE:屈従 += LOCAL
SOURCE:逸脱 += LOCAL
SOURCE:恭順追加 += LOCAL

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF

TCVAR:緊縛経験 += 1

;ＳＭ教育経験フラグ
SIF ASSIPLAY
	TFLAG:55 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_226
PRINTFORM %CALLNAME:PLAYER%は、
IF TEQUIP:口装着器具
	PRINTFORML %CALLNAME:TARGET%からボールギャグを取ってやった
ELSE
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML %CALLNAME:TARGET%にボールギャグを嵌めさせた
	ELSE
		;倒錯的で恐怖刻印が1以下で反発刻印がない場合
		IF TALENT:倒錯的 && MARK:恐怖刻印 < 2 && MARK:反発刻印 < 1
			PRINTFORML %CALLNAME:TARGET%にボールギャグを嵌めさせた。
			PRINTFORML %CALLNAME:TARGET%は口をふさがれているだけでも、激しい欲情を催しているようだ…
		;反抗的か抵抗を持ち陥落してない場合
		ELSEIF (TALENT:反抗的 || TALENT:抵抗) && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
			PRINTFORML 激しく嫌がる%CALLNAME:TARGET%に強引にボールギャグを嵌めさせた…
		;臆病持ちか恐怖刻印2以上で気丈と感情乏しいを持たず、陥落していない場合
		ELSEIF (TALENT:臆病 || MARK:恐怖刻印 >= 2) && TALENT:気丈 == 0 && TALENT:感情乏しい == 0 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
			PRINTFORML 恐怖に怯える%CALLNAME:TARGET%にボールギャグを丁寧に嵌めてあげた…
		;プライド高いか神霊でなおかつ、屈服刻印と恥辱刻印が1以下で、陥落していない場合
		ELSEIF (TALENT:プライド高い || TALENT:神霊) && MARK:屈服刻印 < 2 && MARK:恥辱刻印 < 2 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0
			PRINTFORML %CALLNAME:TARGET%にボールギャグを嵌めさせた。
			PRINTFORML %CALLNAME:TARGET%は口を塞がれたことによる屈辱に震えながらも、そのプライドだけは折れてはいないように見えた…
		;解放か快感に素直か春の人で痛みに弱いがなく、反発刻印がない場合
		ELSEIF (TALENT:解放 || TALENT:快感に素直 || TALENT:春の人) && TALENT:痛みに弱い == 0 && MARK:反発刻印 <= 0
			PRINTFORML なんだか楽しそうな%CALLNAME:TARGET%にボールギャグを丁寧に嵌めてあげた…
		ELSE
			PRINTFORML %CALLNAME:TARGET%にボールギャグを嵌めさせた
		ENDIF
	ENDIF
ENDIF