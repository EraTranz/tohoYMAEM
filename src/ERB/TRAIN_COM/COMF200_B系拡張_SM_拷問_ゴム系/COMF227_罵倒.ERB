;=============================================================================
;罵倒
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE227
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:227 > 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 0) == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 227) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM227
PRINTL 罵倒

;戦闘判定
CALL COM_TRY_BATTLE, 227
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 罵倒
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 5
DOWNBASE:1 += 150

SOURCE:屈従 = 200
SOURCE:逸脱 = 300
SOURCE:反感追加 = 200

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	SOURCE:達成感 = 150
	SOURCE:支配 = 60
	SOURCE:欲情追加 = 0
	TIMES DOWNBASE:1 , 2.00
ELSEIF ABL:マゾっ気 == 1
	SOURCE:達成感 = 300
	SOURCE:支配 = 120
	SOURCE:欲情追加 = 100
	TIMES SOURCE:屈従 , 1.50
	TIMES DOWNBASE:1 , 1.50
ELSEIF ABL:マゾっ気 == 2
	SOURCE:達成感 = 600
	SOURCE:支配 = 240
	SOURCE:欲情追加 = 150
	TIMES SOURCE:屈従 , 2.00
	TIMES DOWNBASE:1 , 1.00
ELSEIF ABL:マゾっ気 == 3
	SOURCE:達成感 = 900
	SOURCE:支配 = 360
	SOURCE:欲情追加 = 250
	TIMES SOURCE:屈従 , 2.50
	TIMES DOWNBASE:1 , 0.75
ELSEIF ABL:マゾっ気 == 4
	SOURCE:達成感 = 1500
	SOURCE:支配 = 480
	SOURCE:欲情追加 = 500
	TIMES SOURCE:屈従 , 3.00
	TIMES DOWNBASE:1 , 0.50
ELSE
	SOURCE:達成感 = 2200
	SOURCE:支配 = 600
	SOURCE:欲情追加 = 1000
	TIMES SOURCE:屈従 , 4.00
	TIMES DOWNBASE:1 , 0.25
ENDIF

;TALENT:反抗的をみる
IF TALENT:反抗的
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:反感追加 , 2.50
ENDIF
;TALENT:気丈をみる
IF TALENT:気丈
	TIMES SOURCE:屈従 , 2.50
	TIMES SOURCE:反感追加 , 1.50
ENDIF
;TALENT:生意気をみる
IF TALENT:生意気
	TIMES SOURCE:屈従 , 1.75
	TIMES SOURCE:反感追加 , 1.75
ENDIF
;TALENT:プライド高いをみる
IF TALENT:プライド高い
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:反感追加 , 2.00
;TALENT:プライド低いをみる
ELSEIF TALENT:プライド低い
	TIMES SOURCE:屈従 , 0.75
	TIMES SOURCE:反感追加 , 0.75
ENDIF
;TALENT:感情乏しいをみる
IF TALENT:感情乏しい
	TIMES SOURCE:達成感 , 0.50
	TIMES SOURCE:支配 , 0.50
	TIMES SOURCE:屈従 , 0.50
	TIMES SOURCE:逸脱 , 0.50
	TIMES SOURCE:反感追加 , 0.50
ENDIF

;縄で緊縛中のとき効果上昇
SIF TEQUIP:緊縛関連
	TIMES SOURCE:屈従 , 1.50
;ボールギャグ装着中のとき効果上昇
SIF TEQUIP:口装着器具
	TIMES SOURCE:屈従 , 1.75

IF ABL:マゾっ気 == 0 && TFLAG:84 == 0
	;反発刻印1に相当(プライド高いまたは生意気持ちなら2)
	TFLAG:84 = (TALENT:生意気 || TALENT:プライド高い) ? 2 # 1
ELSEIF ABL:マゾっ気 > 2
	;マゾっ気3以上なら罵られて快楽を感じるように
	TFLAG:100 |= 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_227
PRINTFORML %CALLNAME:PLAYER%は挑発するかのように%CALLNAME:TARGET%の痴態を声に出した
PRINTFORM %CALLNAME:TARGET%は
IF TEQUIP:口装着器具
	IF ABL:マゾっ気 == 0
		PRINTL 何か言い返そうとしているが口枷によって言葉は封じられている
	ELSEIF ABL:マゾっ気 < 3
		PRINTL 一瞬何か言い返したいような仕草を見せるが自分の口が塞がっているのを思い出したのかおとなしくなった
	ELSEIF ABL:マゾっ気 < 5
		PRINTL 罵られることに快感を感じているようだ…
	ELSE
		PRINTL 顔を紅潮させて鼻息を荒げている
		PRINTFORML どうやら罵られることにすっかり快感を感じているようだ…
		PRINTFORML 口と口枷の隙間からは涎が、股間からは愛液がとめどなく流れ出ており%CALLNAME:TARGET%の足元に大きな水たまりを作り上げている
		PRINTFORML 四つん這いになりながら硬く尖りきった乳首の先端を%CALLNAME:PLAYER%に擦りつけ、さらなる罵倒を期待するかの様に淫靡な視線で見上げている…
	ENDIF
ELSE
	IF ABL:マゾっ気 == 0
		IF TALENT:反抗的 == 0 && TALENT:生意気 == 0 && TALENT:プライド高い == 0
			PRINTFORML 目にうっすらと涙を浮かべながら%CALLNAME:PLAYER%の罵詈雑言を堪えている
		ELSEIF TALENT:反抗的 && TALENT:生意気 && TALENT:プライド高い
			PRINTFORML ぎりぎりと歯を軋ませながら%CALLNAME:PLAYER%を睨みつけている
		ELSE
			PRINTFORML 怒りと屈辱に満ちた顔で%CALLNAME:PLAYER%を睨んだ
		ENDIF
	ELSEIF ABL:マゾっ気 < 3
		PRINTFORML %CALLNAME:PLAYER%を睨んでいるもののその表情には僅かな快感がちらついている
	ELSEIF ABL:マゾっ気 < 5
		PRINTL 罵られることに快感を感じているようだ…
	ELSE
		PRINTL 罵られることにすっかり快感を感じているようだ…
		PRINTFORML %CALLNAME:TARGET%は顔を紅潮させて媚びるような眼差しで%CALLNAME:PLAYER%を見上げ、哀願のこもった声音でさらなる言葉責めをおねだりしている…
	ENDIF
ENDIF
