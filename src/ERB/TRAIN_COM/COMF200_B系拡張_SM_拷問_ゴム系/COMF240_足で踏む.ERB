;=============================================================================
;足で踏む
;=============================================================================
;口上用にTFLAG:201を使用。(0=通常, 1=逆流, 2=お漏らし, 3=排泄物逆流, 4=タマゴ逆流, 5=気絶)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE240
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:240 > 0
	RETURN 0
;拷問系、精神操作系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16) == 0
	RETURN 0
;異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 0) == 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 240) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM240
PRINTL 足で踏む

;戦闘判定
CALL COM_TRY_BATTLE, 240
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 足で踏む
TFLAG:201 = 0

;実際には苦痛があるためもっと体力気力をもっていかれる
;妊娠、抱卵中、懐卵なら消耗はより激しくなる
DOWNBASE:0 += (TALENT:妊娠 || TALENT:抱卵中 || TALENT:懐卵) ? 110 # 80
DOWNBASE:1 += (TALENT:妊娠 || TALENT:抱卵中 || TALENT:懐卵) ? 80 # 60

SOURCE:逸脱 = 800

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 600
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 1000
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 1600
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 2400
ELSE
	SOURCE:痛み = 3600
ENDIF

;妊娠、抱卵中、懐卵なら苦痛はより大きくなる
SIF TALENT:妊娠 || TALENT:抱卵中 || TALENT:懐卵
	TIMES SOURCE:痛み , 1.80

;PALAM:恐怖をみる
IF PALAM:恐怖 < PALAMLV:1
	SOURCE:逸脱 += 600
ELSEIF PALAM:恐怖 < PALAMLV:2
	SOURCE:逸脱 += 900
ELSEIF PALAM:恐怖 < PALAMLV:3
	SOURCE:逸脱 += 1300
ELSEIF PALAM:恐怖 < PALAMLV:4
	SOURCE:逸脱 += 1800
ELSEIF PALAM:恐怖 >= PALAMLV:4
	SOURCE:逸脱 += 2400
ENDIF

;マゾやドＭ持ちにはむしろご褒美
SIF TALENT:ドＭ
	SOURCE:欲情追加 = 600
SIF TALENT:マゾ
	SOURCE:欲情追加 = 300

;調教者がサドやドＳ持ちなら破壊力増
IF TALENT:PLAYER:ドＳ
	SOURCE:痛み += 300
	SOURCE:逸脱 += 100
ELSEIF TALENT:PLAYER:サド
	SOURCE:痛み += 150
	SOURCE:逸脱 += 50
ENDIF

CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

TFLAG:198 = 0
;水を飲ませているか、媚薬等の薬を使っていると逆流
IF TEQUIP:媚薬 == 1 || TEQUIP:賢者の血 || TEQUIP:鈴蘭の毒 || TEQUIP:昆虫採集セットの薬 || TEQUIP:46
	TFLAG:201 = 1
	TFLAG:198 |= 1
	IF TEQUIP:46
		SOURCE:痛み += TEQUIP:46 * 250
		TEQUIP:46 = 0
	ENDIF
ENDIF
;利尿剤を使っているとお漏らし
IF TEQUIP:利尿剤
	TCVAR:放尿経験 += 1
	BASE:尿意 = 0
	TFLAG:32 = 1
	TFLAG:201 = 2
	TFLAG:198 |= 2
ENDIF
;排泄物逆流
IF TEQUIP:Ａ系装着器具 == 7
	TEQUIP:Ａ系装着器具 = 0
	TCVAR:Ａ経験 += 5
	IF TFLAG:172 > 20
		LOCAL = 5
	ELSEIF TFLAG:172 > 10
		LOCAL = 3
	ELSEIF TFLAG:172 > 5
		LOCAL = 2
	ELSE
		LOCAL = 1
	ENDIF
	TCVAR:調教排泄経験 += LOCAL
	TFLAG:172 = 0
	TFLAG:140 |= 256
	TFLAG:201 = 3
	TFLAG:198 |= 4
ENDIF
;タマゴ逆流
IF TEQUIP:触手膣内産卵
	TEQUIP:触手膣内産卵 = 0
	TFLAG:201 = 4
	TFLAG:198 |= 8
ENDIF
;残り体力が少ないと気絶
;(一旦体力を500に固定、後にDOWNBASE:0でさらに減らされるようになるので確実に調教終了になる)
IF BASE:体力 < 600 && !(FLAG:16 & 32)
	TFLAG:201 = 5
	TFLAG:198 |= 16
	BASE:体力 = 500
	DOWNBASE:1 += 1200
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;浣腸による排泄初体験は異常経験＋1
IF TFLAG:198 & 4 && (CFLAG:20 & 2) == 0
	TCVAR:異常経験 += 1
	CFLAG:20 |= 2
ENDIF
;気絶するくらい踏みつけされるのも異常と言えるので異常経験＋1
IF TFLAG:198 & 16 && (CFLAG:22 & 2) == 0
	TCVAR:異常経験 += 1
	CFLAG:22 |= 2
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_240
PRINTFORML %CALLNAME:TARGET%の腹を、強く踏みつけてやった

IF EQUIP:睡眠薬 || TFLAG:899 > 0
	;水を飲ませているか、媚薬等の薬を使っていると逆流
	IF TEQUIP:媚薬 == 1 || TEQUIP:賢者の血 || TEQUIP:鈴蘭の毒 || TEQUIP:昆虫採集セットの薬 || TEQUIP:46
		IF TEQUIP:コスチュームプレイ == 1
			PRINTFORML %CALLNAME:TARGET%は吐瀉物をぶちまけ、着せてやったエプロンを汚してしまった
		ELSEIF TEQUIP:コスチュームプレイ == 2
			PRINTFORML %CALLNAME:TARGET%は吐瀉物をぶちまけ、着せてやったスク水を汚してしまった
		ELSE
			PRINTFORML %CALLNAME:TARGET%は大量の吐瀉物をぶちまけた
		ENDIF
	ENDIF
	;利尿剤を使っているとお漏らし
	IF TEQUIP:利尿剤
		PRINTFORML 腹部に見舞われた一撃に%CALLNAME:TARGET%は思わず尿道を緩めてしまったようだ
		PRINTFORML %CALLNAME:TARGET%の股間からチョロチョロと黄色い水が漏れだしてきた
	ENDIF
	;排泄物逆流
	IF TEQUIP:Ａ系装着器具 == 7
		PRINTFORML %CALLNAME:TARGET%は腹部への衝撃に耐えきれず、排泄物を漏らしてしまった
	ENDIF
	;タマゴ逆流
	IF TEQUIP:触手膣内産卵
		PRINTFORML 嫌な手応えがして、びしゃっと何かが%CALLNAME:TARGET%の股間から洪水のように溢れ落ちた
		PRINTL 腹責めの衝撃で、中に詰め込まれていた卵が幾つか壊れたのだろう
		SIF BASE:体力 >= 600
			PRINTFORML %CALLNAME:TARGET%は腹を押さえたまま震えている
	ENDIF
	;残り体力が少ないと気絶
	;(一旦体力を500に固定、後にDOWNBASE:0でさらに減らされるようになるので確実に調教終了になる)
	PRINTFORM %CALLNAME:TARGET%は
	IF BASE:体力 < 600 && !(FLAG:16 & 32)
		PRINTL 床に崩れ落ち、吐瀉物の海に突っ伏したまま起き上がってこない
		PRINTL やや力加減を誤ってしまったようだ……
		PRINTL さすがにこの状態では調教を続けられないだろう
		PRINTFORML %CALLNAME:MASTER%は気絶した%CALLNAME:TARGET%を抱え、奥の部屋へと運んで行った
	ELSEIF TEQUIP:Ａ系装着器具 == 7 && ABL:マゾっ気 >= 2
		PRINTL それでも快感を味わっているようだ……
	ELSE
		PRINTL 苦しげな声を洩らしている……
	ENDIF
ELSE
	;水を飲ませているか、媚薬等の薬を使っていると逆流
	IF TEQUIP:媚薬 == 1 || TEQUIP:賢者の血 || TEQUIP:鈴蘭の毒 || TEQUIP:昆虫採集セットの薬 || TEQUIP:46
		IF TEQUIP:コスチュームプレイ == 1
			PRINTFORML %CALLNAME:TARGET%は吐瀉物をぶちまけ、着せてやったエプロンを汚してしまった
		ELSEIF TEQUIP:コスチュームプレイ == 2
			PRINTFORML %CALLNAME:TARGET%は吐瀉物をぶちまけ、着せてやったスク水を汚してしまった
		ELSE
			PRINTFORML %CALLNAME:TARGET%は大量の吐瀉物をぶちまけた
		ENDIF
	ENDIF
	;利尿剤を使っているとお漏らし
	IF TEQUIP:利尿剤
		PRINTFORML 腹部に見舞われた一撃に%CALLNAME:TARGET%は思わず尿道を緩めてしまったようだ
		PRINTFORML %CALLNAME:TARGET%の股間からチョロチョロと黄色い水が漏れだしてきた
	ENDIF
	;排泄物逆流
	IF TEQUIP:Ａ系装着器具 == 7
		PRINTFORML %CALLNAME:TARGET%は腹部への衝撃に耐えきれず、排泄物を漏らしてしまった
		IF BASE:体力 >= 600 && !TALENT:死亡
			IF ABL:マゾっ気 == 0
				PRINTFORML %CALLNAME:TARGET%は苦悶と恥辱の表情を浮かべている
			ELSEIF ABL:マゾっ気 == 1
				PRINTFORML %CALLNAME:TARGET%は脂汗を浮かべ、いまだ下腹に残る排泄感に堪えている
			ELSEIF ABL:マゾっ気 == 2
				PRINTFORML %CALLNAME:TARGET%は恥ずかしがりながらも、開放感にほっとした表情を浮かべている
			ELSEIF ABL:マゾっ気 == 3
				PRINTFORML %CALLNAME:TARGET%は、肛門を通り過ぎる排泄物の感覚に時折艶っぽい喘ぎ声を上げている
			ELSEIF ABL:マゾっ気 == 4
				PRINTFORML %CALLNAME:TARGET%は、うっとりした表情を浮かべ、排泄感を愉しんでいる
			ELSE
				PRINTFORML %CALLNAME:TARGET%は、排泄感と恥辱を心ゆくまで味わっている
				PRINTFORML %CALLNAME:TARGET%の表情は快感にだらしなく緩み、肛門から汚物をひりだすと同時に、膣口からも愛液をボタボタと滴らせている
			ENDIF
		ENDIF
	ENDIF
	;タマゴ逆流
	IF TEQUIP:触手膣内産卵
		PRINTFORML 嫌な手応えがして、びしゃっと何かが%CALLNAME:TARGET%の股間から洪水のように溢れ落ちた
		PRINTL 腹責めの衝撃で、中に詰め込まれていた卵が幾つか壊れたのだろう
		SIF BASE:体力 >= 600 && !TALENT:死亡
			PRINTFORML %CALLNAME:TARGET%は腹を押さえたまま震えている
	ENDIF
	;残り体力が少ないと気絶
	;(一旦体力を500に固定、後にDOWNBASE:0でさらに減らされるようになるので確実に調教終了になる)
	PRINTFORM %CALLNAME:TARGET%は
	IF BASE:体力 < 600 && !(FLAG:16 & 32)
		PRINTL 吐瀉物の海に突っ伏したまま起き上がってこない
		PRINTL やや力加減を誤ってしまったようだ……
		PRINTL さすがにこの状態では調教を続けられないだろう
		PRINTFORML %CALLNAME:MASTER%は気絶した%CALLNAME:TARGET%を抱え、奥の部屋へと運んで行った
	ELSEIF TEQUIP:Ａ系装着器具 == 7 && ABL:マゾっ気 >= 2 && !TALENT:死亡
		PRINTL それでも快感を味わっているようだ……
	ELSEIF !TALENT:死亡
		PRINTL 苦しげな声を洩らしている……
	ENDIF
ENDIF