;=============================================================================
;ナイフ
;=============================================================================
;針より気力体力を持っていく・苦痛が大きい
;(TODO 手足不自由判定の追加)
;地の文は現在のところなし
;構想：エプロンやスク水装着中は破れて使い物にならなくなるようにする
;(メリットは羞恥大幅アップとシチュ程度?)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE244
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:244 > 0
	RETURN 0
;拷問系、精神操作系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16) == 0
	RETURN 0
;まだ一度もナイフで切られたことが無い場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:22 & 8) == 0
	RETURN 0
;ナイフを購入(開発?)していないとダメ
SIF (FLAG:73 & 8) == 0
	RETURN 0
;従順5未満のASSIの場合、実行できない（サド・狂気・妄信・服従のどれかがあればＯＫ）
IF ASSIPLAY
	SIF ABL:ASSI:従順 < 5 && TALENT:ASSI:サド == 0 && TALENT:ASSI:狂気 == 0 && TALENT:ASSI:妄信 == 0 && TALENT:ASSI:服従 == 0
		RETURN 0
;主人調教の場合、サドか狂気を持っていないとダメ
ELSE
	SIF TALENT:MASTER:サド == 0 && TALENT:MASTER:狂気 == 0
		RETURN 0
ENDIF
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 244) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM244
PRINTL ナイフ

;戦闘判定
CALL COM_TRY_BATTLE, 244
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ナイフ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 0
DOWNBASE:1 += 170

SOURCE:逸脱 = 3000

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 5000
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 5300
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 5600
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 6000
ELSE
	SOURCE:痛み = 6400
ENDIF

;サラシ装着している場合苦痛ソース軽減もサラシ破損
IF TEQUIP:サラシ
	SOURCE:痛み -= 200
	TEQUIP:サラシ = 0
	ITEM:サラシ = 0
ENDIF
;衣装を装着している場合苦痛ソース軽減もエプロンとスク水破損
IF TEQUIP:コスチュームプレイ
	SOURCE:痛み -= 100
	SIF TEQUIP:コスチュームプレイ == 1
		ITEM:エプロン = 0
	SIF TEQUIP:コスチュームプレイ == 2
		ITEM:スクール水着 = 0
	TEQUIP:コスチュームプレイ = 0
ENDIF

;ナイフで切られるのは異常経験なので点を入れる
IF (CFLAG:22 & 8) == 0
	TCVAR:異常経験 += 1
	CFLAG:22 |= 8
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 2
ENDIF

;コマンド属性：道具(調教者)
SIF TEQUIP:触手調教 == 0
	TFLAG:98 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_244
PRINTFORM %CALLNAME:PLAYER%は
IF TEQUIP:コスチュームプレイ
	PRINTFORM %CALLNAME:TARGET%が着ている
	IF TEQUIP:コスチュームプレイ == 1
		PRINT エプロン
	ELSEIF TEQUIP:コスチュームプレイ == 2
		PRINT スクール水着
	ELSE
		PRINT 衣装
	ENDIF
	SIF TEQUIP:サラシ
		PRINT や巻いてあるサラシ
	PRINT ごと
ELSEIF TEQUIP:サラシ
	PRINTFORM %CALLNAME:TARGET%の胸に巻いてあるサラシごと
ELSE
	PRINTFORM %CALLNAME:TARGET%の柔肌を
ENDIF
PRINTL ナイフでずたずたに切り裂いた。
