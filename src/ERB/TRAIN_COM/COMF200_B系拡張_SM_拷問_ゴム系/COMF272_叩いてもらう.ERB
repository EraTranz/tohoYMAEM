;=============================================================================
;叩いてもらう
;=============================================================================
;効果は凄く微妙。
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE272
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:272 > 0
	RETURN 0
;調教者奉仕・ウフフ系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;奴隷がサド持ちまたはドＳ持ちまたはサドっ気Lv1以上じゃないとダメ
SIF TALENT:サド == 0 && TALENT:ドＳ == 0 && ABL:サドっ気 < 1
	RETURN 0
;調教者がハードなコマンドを受けることができるかどうか
SIF COM_ABLE_GAIN_HARDTRAINING(PLAYER, 0) == 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 272) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM272
IF NO:PLAYER == 1011
	PRINTL お尻ペンペンしてもらう

	;戦闘判定
	CALL COM_TRY_BATTLE, 272
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = お尻ペンペンしてもらう
ELSE
	PRINTL 叩いてもらう

	;戦闘判定
	CALL COM_TRY_BATTLE, 272
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 叩いてもらう
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 15
DOWNBASE:1 += 20

DOWNBASE:PLAYER:0 += 160
DOWNBASE:PLAYER:1 += 80

SOURCE:主導権 = 550
SOURCE:達成感 = 150
SOURCE:恭順追加 = 80
SOURCE:逸脱 = 20

;ABL:サドっ気をみる
IF ABL:サドっ気 <= 1
	TIMES SOURCE:主導権 , 0.8
	TIMES SOURCE:達成感 , 0.8
	TIMES SOURCE:恭順追加 , 0.7
ELSEIF ABL:サドっ気 == 2
	TIMES SOURCE:主導権 , 1.0
	TIMES SOURCE:達成感 , 1.0
	TIMES SOURCE:恭順追加 , 0.9
ELSEIF ABL:サドっ気 == 3
	TIMES SOURCE:主導権 , 1.4
	TIMES SOURCE:達成感 , 1.2
	TIMES SOURCE:恭順追加 , 1.0
ELSEIF ABL:サドっ気 == 4
	TIMES SOURCE:主導権 , 1.8
	TIMES SOURCE:達成感 , 1.5
	TIMES SOURCE:恭順追加 , 1.2
ELSE
	TIMES SOURCE:主導権 , 2.2
	TIMES SOURCE:達成感 , 2.0
	TIMES SOURCE:恭順追加 , 1.6
ENDIF

;ABL:調教者のマゾっ気をみる
IF ABL:PLAYER:マゾっ気 == 0
	TIMES SOURCE:主導権 , 0.9
	TIMES SOURCE:達成感 , 0.9
	TIMES SOURCE:恭順追加 , 0.9
ELSEIF ABL:PLAYER:マゾっ気 == 1
	TIMES SOURCE:主導権 , 1.0
	TIMES SOURCE:達成感 , 1.0
	TIMES SOURCE:恭順追加 , 1.0
ELSEIF ABL:PLAYER:マゾっ気 == 2
	TIMES SOURCE:主導権 , 1.2
	TIMES SOURCE:達成感 , 1.1
	TIMES SOURCE:恭順追加 , 1.0
ELSEIF ABL:PLAYER:マゾっ気 == 3
	TIMES SOURCE:主導権 , 1.5
	TIMES SOURCE:達成感 , 1.3
	TIMES SOURCE:恭順追加 , 1.1
ELSEIF ABL:PLAYER:マゾっ気 == 4
	TIMES SOURCE:主導権 , 1.8
	TIMES SOURCE:達成感 , 1.5
	TIMES SOURCE:恭順追加 , 1.2
ELSE
	TIMES SOURCE:主導権 , 2.0
	TIMES SOURCE:達成感 , 1.8
	TIMES SOURCE:恭順追加 , 1.5
ENDIF

;調教者がドＭ
IF TALENT:PLAYER:ドＭ
	TIMES SOURCE:主導権 , 1.5
	TIMES SOURCE:達成感 , 1.5
	TIMES SOURCE:恭順追加 , 1.5
	SOURCE:欲情追加 = 300
;調教者がマゾ
ELSEIF TALENT:PLAYER:マゾ
	TIMES SOURCE:主導権 , 1.2
	TIMES SOURCE:達成感 , 1.2
	TIMES SOURCE:恭順追加 , 1.2
	SOURCE:欲情追加 = 150
ENDIF
;ドＳ
IF TALENT:ドＳ
	TIMES SOURCE:主導権 , 1.5
	TIMES SOURCE:達成感 , 1.5
	TIMES SOURCE:恭順追加 , 1.5
	TIMES DOWNBASE:PLAYER:0 , 1.5
	TIMES DOWNBASE:PLAYER:1 , 1.5
;サド
ELSEIF TALENT:サド
	TIMES SOURCE:主導権 , 1.2
	TIMES SOURCE:達成感 , 1.2
	TIMES SOURCE:恭順追加 , 1.2
	TIMES DOWNBASE:PLAYER:0 , 1.2
	TIMES DOWNBASE:PLAYER:1 , 1.2
ENDIF

;反発刻印2以上
SIF MARK:反発刻印 >= 2
	SOURCE:反感追加 = 500

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 3
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 3
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_272
PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の尻を平手で強く叩いた
PRINTFORM 何度も何度も叩かれている内に、%CALLNAME:PLAYER%の
IF ABL:PLAYER:マゾっ気 >= 2 && TALENT:PLAYER:オトコ == 0
	PRINTL 股間は自身の愛液で濡れはじめていた…
ELSE
	PRINTL 尻は真っ赤に腫れていった…
ENDIF
