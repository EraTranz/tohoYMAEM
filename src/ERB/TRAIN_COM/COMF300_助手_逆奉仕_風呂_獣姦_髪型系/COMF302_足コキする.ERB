;=============================================================================
;足コキする
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE302
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:302 > 0
	RETURN 0
;オトコかふたなりじゃないとダメ
SIF EXIST_BAR(TARGET) == 0
	RETURN 0
;オナホール・電極オナホール使用中はダメ
SIF TEQUIP:Ｃ系装着器具 == 2 || TEQUIP:Ｃ系装着器具 == 6
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 302) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM302
PRINTL 足コキする

;戦闘判定
CALL COM_TRY_BATTLE, 302
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 足コキする
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	LOCAL = 20
ELSEIF ABL:PLAYER:技巧 == 1
	LOCAL = 80
ELSEIF ABL:PLAYER:技巧 == 2
	LOCAL = 200
ELSEIF ABL:PLAYER:技巧 == 3
	LOCAL = 500
ELSEIF ABL:PLAYER:技巧 == 4
	LOCAL = 1200
ELSE
	LOCAL = 2500
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)

;調教対象のマゾっ気をみる
LOCAL = EJACULATIONCHECK_ABL_21_1(TARGET, LOCAL)
;調教対象のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(TARGET, LOCAL)

SIF EXIST_PENIS(TARGET)
	BASE:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:1 += 150

SOURCE:逸脱 = 100

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 40
	SOURCE:屈従 = 40
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 160
	SOURCE:屈従 = 160
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 700
	SOURCE:屈従 = 700
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1500
	SOURCE:屈従 = 1500
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2400
	SOURCE:屈従 = 2400
ELSE
	SOURCE:快Ｃ = 3300
	SOURCE:屈従 = 3300
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	SOURCE:達成感 = 150
ELSEIF ABL:マゾっ気 == 1
	SOURCE:達成感 = 300
ELSEIF ABL:マゾっ気 == 2
	SOURCE:達成感 = 600
ELSEIF ABL:マゾっ気 == 3
	SOURCE:達成感 = 900
ELSEIF ABL:マゾっ気 == 4
	SOURCE:達成感 = 1500
ELSE
	SOURCE:達成感 = 2200
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:屈従 , 0.50
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:屈従 , 1.50
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:屈従 , 2.50
ELSE
	TIMES SOURCE:快Ｃ , 2.00
	TIMES SOURCE:屈従 , 4.00
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES SOURCE:達成感 , 1.80

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(TARGET)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:射精中毒をみる
	IF ABL:射精中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:快Ｃ , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:射精中毒 == 1
		SOURCE:中毒充足 += 50
		TIMES SOURCE:快Ｃ , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:射精中毒 == 2
		SOURCE:中毒充足 += 150
		TIMES SOURCE:快Ｃ , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:射精中毒 == 3
		SOURCE:中毒充足 += 300
		TIMES SOURCE:快Ｃ , 4.50
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:射精中毒 == 4
		SOURCE:中毒充足 += 600
		TIMES SOURCE:快Ｃ , 6.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 += 1200
		TIMES SOURCE:快Ｃ , 8.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = TARGET
	;射精された人物を設定
	TFLAG:94 = PLAYER
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_302
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;対象のＰ⇔調教者の足の汚れが相互に移動
STAIN:ペニス |= STAIN:PLAYER:足
STAIN:PLAYER:足 |= STAIN:ペニス

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 4
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 4
ENDIF

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_302
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%のペニスを
IF TEQUIP:焦らしプレイ
	PRINTFORML 足の窪みでくるくると撫でている…
ELSE
	SIF ABL:PLAYER:サドっ気 > 2 || TALENT:PLAYER:両刀 || TALENT:PLAYER:ドＳ
		PRINT 踏みつけるように
	PRINTL 足でシゴいている…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_302
;デフォルト射精箇所タイプを設定
TFLAG:703 = 14
;射精箇所選択の可・不可
TFLAG:704 = 0
