;=============================================================================
;二人フェラチオ(ダブルフェラ)
;=============================================================================
;奴隷と助手に舐めさせる
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE303
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:303 > 0
	RETURN 0
;助手がいないとダメ
SIF ASSI < 0
	RETURN 0
;主人にペニスがないとダメ
SIF EXIST_BAR(MASTER) == 0
	RETURN 0
;主人が[魅惑]か[謎の魅力]じゃないとダメ
SIF TALENT:MASTER:魅惑 == 0 && TALENT:MASTER:謎の魅力 == 0
	RETURN 0
;助手が奉仕精神3以上、技巧3以上じゃないとダメ
SIF ABL:ASSI:技巧 < 3 || ABL:ASSI:奉仕精神 < 3
	RETURN 0
;対象が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF (TALENT:猫舌 || TALENT:ASSI:猫舌) && (FLAG:14 & 16) == 0
	RETURN 0
;対象が[猫舌]だと技巧4・奉仕精神3・従順3が必要
SIF TALENT:猫舌 && (ABL:技巧 < 4 || ABL:奉仕精神 < 3 || ABL:従順 < 3)
	RETURN 0
;助手が[猫舌]だと助手に技巧4・奉仕精神3・従順3が必要
SIF TALENT:ASSI:猫舌 && (ABL:ASSI:技巧 < 4 || ABL:ASSI:奉仕精神 < 3 || ABL:ASSI:従順 < 3)
	RETURN 0
;二人とも[猫舌]だと二人とも技巧5じゃないとダメ
SIF TALENT:猫舌 && TALENT:ASSI:猫舌 && (ABL:技巧 < 5 || ABL:ASSI:技巧 < 5)
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;助手が褌着用中の場合はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ == 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 303) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM303
PRINTL 二人フェラチオ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 303
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER,303
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
SELECTCOM = 303
TSTR:0 = 二人フェラチオ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 800
ELSEIF ABL:技巧 == 1
	LOCAL = 1100
ELSEIF ABL:技巧 == 2
	LOCAL = 1500
ELSEIF ABL:技巧 == 3
	LOCAL = 2000
ELSEIF ABL:技巧 == 4
	LOCAL = 2400
ELSE
	LOCAL = 2800
ENDIF

;ABL:ASSI:技巧をみる
IF ABL:ASSI:技巧 == 0
	LOCAL += 800
ELSEIF ABL:ASSI:技巧 == 1
	LOCAL += 1100
ELSEIF ABL:ASSI:技巧 == 2
	LOCAL += 1500
ELSEIF ABL:ASSI:技巧 == 3
	LOCAL += 2000
ELSEIF ABL:ASSI:技巧 == 4
	LOCAL += 2400
ELSE
	LOCAL += 2800
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00
;TALENT:猫舌をみる
SIF TALENT:猫舌
	TIMES LOCAL , 0.70
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7

;助手の技巧をみる
;LOCAL = EJACULATIONCHECK_ABL_12_3(ASSI, LOCAL)

;助手のTALENT:舌使いをみる
SIF TALENT:ASSI:舌使い
	TIMES LOCAL , 2.00
;助手のTALENT:猫舌をみる
SIF TALENT:ASSI:猫舌
	TIMES LOCAL , 0.70

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(MASTER)
	BASE:MASTER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:主導権 = 1000
SOURCE:屈従 = 1500
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*40 + 100

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 300
	SOURCE:性行動 = 420
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 400
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 550
	SOURCE:性行動 = 580
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 700
	SOURCE:性行動 = 660
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 900
	SOURCE:性行動 = 740
	SOURCE:達成感 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:情愛 = 1000
	SOURCE:性行動 = 820
	SOURCE:達成感 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:情愛 , 0.50
ELSEIF ABL:従順 == 1
	TIMES SOURCE:情愛 , 0.80
ELSEIF ABL:従順 == 2
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:従順 == 4
	TIMES SOURCE:情愛 , 1.50
ELSE
	TIMES SOURCE:情愛 , 2.00
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(MASTER)
LOCAL = TFLAG:91
;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 += 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 += 1200
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 += 3000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 += 6000
		TIMES SOURCE:達成感 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 += 12000
		TIMES SOURCE:達成感 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = MASTER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_303
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
TCVAR:フェラ経験 += 1
TCVAR:ASSI:フェラ経験 += 1

;奴隷の口⇔調教者のＰの汚れが移動
STAIN:口 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:口

;助手の口⇔調教者のＰの汚れが移動
STAIN:ASSI:口 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:ASSI:口

;奉仕精神ＬＶ２以上、技巧ＬＶ２以上ならＰの汚れをなめ取る
;ただし対象が猫舌の場合、猫フェラパッチ有効でなおかつ
;奉仕精神ＬＶ４以上、技巧ＬＶ５、従順ＬＶ３以上ないとなめ取らない
IF (TALENT:猫舌 == 0 && ABL:奉仕精神 >= 2 && ABL:技巧 >= 2) || (TALENT:猫舌 && FLAG:14 & 16 && ABL:奉仕精神 >= 4 && ABL:技巧 >= 5 && ABL:従順 >=3)
	STAIN:MASTER:ペニス = 2
	SIF LOCAL >= 1
		TFLAG:10 = 1
ENDIF

;奴隷と助手が共に女性でどちらかが精液中毒を持っているとき、奴隷の口⇔助手の口の汚れが移動
IF TALENT:オトコ == 0 && TALENT:ASSI:オトコ == 0 && (ABL:精液中毒 >= 1 || ABL:ASSI:精液中毒 >= 1)
	STAIN:口 |= STAIN:ASSI:口
	STAIN:ASSI:口 |= STAIN:口
	IF LOCAL >= 1
		TFLAG:10 = 3
		TCVAR:キス経験 += 1
		TCVAR:ASSI:キス経験 += 1
	ENDIF
ENDIF

;マスターと奴隷のレズorBL
IF TALENT:オトコ == 0 && TALENT:MASTER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:MASTER:オトコ == 1
	TCVAR:ＢＬ経験 += 7
ENDIF

;助手と奴隷のレズorBL
IF TALENT:オトコ == 0 && TALENT:ASSI:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:ASSI:オトコ == 1
	TCVAR:ＢＬ経験 += 7
ENDIF

;調教者がふたなり
SIF TALENT:MASTER:ふたなり
	SOURCE:屈従 /= 2
;奉仕快楽経験入手判定
TFLAG:100 |= 1
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_303
PRINTFORML %CALLNAME:TARGET%に%CALLNAME:ASSI%と共に自分のペニスを咥えるように命令してみたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_303
IF (TALENT:親愛 || TALENT:相愛) && MARK:反発刻印 < 1 && (TALENT:ASSI:親愛 || TALENT:ASSI:相愛) && MARK:ASSI:反発刻印 < 1
	PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は互いに寵愛を競うべく、愛する%CALLNAME:MASTER%のペニスに熱心に二人で舌を這わせている…
ELSE
	PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は%CALLNAME:MASTER%のペニスに二人で舌を這わせている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_303, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:ＢＬっ気＆TALENT:オトコ
IF (TALENT:PLAYER:オトコ || TALENT:MASTER:オトコ) && TALENT:オトコ
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓のLOCAL:99の目安(手淫5, フェラ/A愛撫強制10)
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%同士({LOCAL:99})
	IF ABL:ＢＬっ気
		LOCAL:99 = ABL:ＢＬっ気
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 34

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_303
;デフォルト射精箇所タイプを設定
TFLAG:703 = 5
;射精箇所選択の可・不可
TFLAG:704 = 1
