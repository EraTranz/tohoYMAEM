;=============================================================================
;二人でパイズリ(ダブルパイズリ)
;=============================================================================
;奴隷と助手に胸で奉仕させる
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE305
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:305 > 0
	RETURN 0
;助手じゃないとダメ
SIF PLAYER != ASSI
	RETURN 0
;主人にペニスがないとダメ
SIF EXIST_BAR(MASTER) == 0
	RETURN 0
;主人が[魅惑]か[謎の魅力]じゃないとダメ
SIF TALENT:MASTER:魅惑 == 0 && TALENT:MASTER:謎の魅力 == 0
	RETURN 0
;助手が奉仕精神3以上、技巧4以上じゃないとダメ
SIF ABL:ASSI:技巧 < 4 || ABL:ASSI:奉仕精神 < 3
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;助手の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(ASSI) == 0
	RETURN 0
;全員体のサイズが同程度か、主人が禁断の知識持ちじゃないとダメ
IF TALENT:MASTER:禁断の知識 == 0
	IF TALENT:MASTER:小人体型
		SIF TALENT:小人体型 == 0 || TALENT:ASSI:小人体型 == 0
			RETURN 0
	ELSE
		SIF TALENT:小人体型 || TALENT:ASSI:小人体型
			RETURN 0
	ENDIF
ENDIF
;調教対象が[絶壁]だと不可
IF TALENT:絶壁
	RETURN 0
;調教対象が[貧乳]だと技巧4以上必要
ELSEIF TALENT:貧乳 && ABL:技巧 < 4
	RETURN 0
;調教対象が[巨乳]なら技巧2以上必要
ELSEIF TALENT:巨乳 && ABL:技巧 < 2
	RETURN 0
;調教対象が[爆乳]なら技巧LV1以上必要
ELSEIF TALENT:爆乳 && ABL:技巧 < 1
	RETURN 0
;調教対象がそれ以外の場合は技巧3以上必要
ELSEIF TALENT:貧乳 == 0 && TALENT:巨乳 == 0 && TALENT:爆乳 == 0 && ABL:技巧 < 3
	RETURN 0
ENDIF
;助手が[絶壁]だと不可
IF TALENT:ASSI:絶壁
	RETURN 0
;助手が[貧乳]だと技巧4以上必要
ELSEIF TALENT:ASSI:貧乳 && ABL:ASSI:技巧 < 4
	RETURN 0
;助手が[巨乳]なら技巧2以上必要
ELSEIF TALENT:ASSI:巨乳 && ABL:ASSI:技巧 < 2
	RETURN 0
;助手が[爆乳]なら技巧LV1以上必要
ELSEIF TALENT:ASSI:爆乳 && ABL:ASSI:技巧 < 1
	RETURN 0
;助手がそれ以外の場合は技巧3以上必要
ELSEIF TALENT:ASSI:貧乳 == 0 && TALENT:ASSI:巨乳 == 0 && TALENT:ASSI:爆乳 == 0 && ABL:ASSI:技巧 < 3
	RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;サラシ装着中はダメ
SIF TEQUIP:サラシ
	RETURN 0
;助手が褌と裸Ｙシャツ以外のコスプレ中はだめ
SIF ASSI >= 0 && TEQUIP:ASSI:コスチュームプレイ > 1 && TEQUIP:ASSI:コスチュームプレイ != 11 && TEQUIP:ASSI:コスチュームプレイ != 17
	RETURN 0
;褌と裸Ｙシャツ以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 11 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 305) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM305
PRINTL 二人でパイズリ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 305
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 二人でパイズリ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 800
ELSEIF ABL:技巧 == 1
	LOCAL = 1100
ELSEIF ABL:技巧 == 2
	LOCAL = 1500
ELSEIF ABL:技巧 == 3
	LOCAL = 2000
ELSEIF ABL:技巧 == 4
	LOCAL = 2400
ELSE
	LOCAL = 2800
ENDIF

;ABL:ASSI:技巧をみる
IF ABL:ASSI:技巧 == 0
	LOCAL += 800
ELSEIF ABL:ASSI:技巧 == 1
	LOCAL += 1100
ELSEIF ABL:ASSI:技巧 == 2
	LOCAL += 1500
ELSEIF ABL:ASSI:技巧 == 3
	LOCAL += 2000
ELSEIF ABL:ASSI:技巧 == 4
	LOCAL += 2400
ELSE
	LOCAL += 2800
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;助手の技巧をみる
LOCAL = EJACULATIONCHECK_ABL_12_3(ASSI, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(MASTER)
	BASE:MASTER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:屈従 = 1500
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*40 + 100

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 300
	SOURCE:性行動 = 420
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 400
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 550
	SOURCE:性行動 = 580
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 700
	SOURCE:性行動 = 660
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 900
	SOURCE:性行動 = 740
	SOURCE:達成感 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:情愛 = 1000
	SOURCE:性行動 = 820
	SOURCE:達成感 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL:1 = 100
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:1 = 200
ELSEIF ABL:Ｂ感覚== 2
	LOCAL:1 = 400
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:1 = 800
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:1 = 1200
ELSE
	LOCAL:1 = 1500
ENDIF

;Ｂ敏感、Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:1 , 0.70
ENDIF
;巨乳、爆乳
SIF TALENT:巨乳 || TALENT:爆乳
	TIMES LOCAL:1 , 1.20
;淫乳
SIF TALENT:淫乳
	TIMES LOCAL:1 , 1.40

SOURCE:快Ｂ += LOCAL:1

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:情愛 , 0.50
ELSEIF ABL:従順 == 1
	TIMES SOURCE:情愛 , 0.80
ELSEIF ABL:従順 == 2
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:従順 == 4
	TIMES SOURCE:情愛 , 1.50
ELSE
	TIMES SOURCE:情愛 , 2.00
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(MASTER)

IF TFLAG:91
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成感 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 = 12000
		TIMES SOURCE:達成感 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = MASTER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_305
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;奴隷のＢ⇔調教者のＰの汚れが移動
STAIN:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:胸

;助手のＢ⇔調教者のＰの汚れが移動
STAIN:ASSI:胸 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:ASSI:胸

IF TALENT:オトコ == 0 && TALENT:MASTER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:MASTER:オトコ == 1
	TCVAR:ＢＬ経験 += 7
ENDIF
IF TALENT:オトコ == 0 && TALENT:ASSI:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:ASSI:オトコ == 1
	TCVAR:ＢＬ経験 += 7
ENDIF

;調教者がふたなり
SIF TALENT:MASTER:ふたなり
	SOURCE:屈従 /= 2
;奉仕快楽経験入手判定
TFLAG:100 |= 1
;調教対象と助手のパイズリ実行回数
CFLAG:パイズリした回数 += 1
CFLAG:ASSI:パイズリした回数 += 1
;主人のパイズリ堪能回数
CFLAG:MASTER:パイズリされた回数 += 1
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_305
PRINTFORML %CALLNAME:TARGET%に%CALLNAME:ASSI%と一緒に自分のペニスを胸で挟んで奉仕するように命令してみたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_305
IF (TALENT:親愛 || TALENT:相愛) && MARK:反発刻印 < 1 && (TALENT:ASSI:親愛 || TALENT:ASSI:相愛) && MARK:ASSI:反発刻印 < 1
	PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は互いにうなづきあい、愛する%CALLNAME:MASTER%のために
	PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%の胸で%CALLNAME:MASTER%のペニスを挟んだ。
	PRINTL そして、愛を込めて熱心に二人の間で扱きあげている…
ELSE
	PRINTFORML %CALLNAME:TARGET%と%CALLNAME:ASSI%は胸で%CALLNAME:MASTER%のペニスを扱きあげている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_305, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF
;淫乳
IF TALENT:淫乳
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:174%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 38

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_305
;デフォルト射精箇所タイプを設定
TFLAG:703 = 6
;射精箇所選択の可・不可
TFLAG:704 = 1
