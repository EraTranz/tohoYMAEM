;=============================================================================
;二人のＡをフィスト(ダブルアナルフィスト)
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE313
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:313 > 0
	RETURN 0
;助手がいないとダメ
SIF ASSI < 0
	RETURN 0
;主人じゃないとダメ
SIF ASSIPLAY
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURN 0
ENDIF
;助手も小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:ASSI:小柄体型
	SIF ABL:ASSI:従順 < 4 || ABL:ASSI:マゾっ気 < 4
		RETURN 0
ENDIF
;調教対象か助手が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF (TALENT:小人体型 || TALENT:ASSI:小人体型) && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;主人がサドか狂気じゃないとできない。
SIF TALENT:MASTER:サド == 0 && TALENT:MASTER:狂気 == 0
	RETURN 0
;一定のＡ経験が必要([巨躯]の場合は軽減)
IF TALENT:巨躯
	LOCAL = 75
ELSE
	LOCAL = 150
ENDIF
SIF EXP:Ａ経験 < LOCAL
	RETURN 0
;助手にも一定のＡ経験が必要([巨躯]の場合は軽減)
IF TALENT:ASSI:巨躯
	LOCAL = 75
ELSE
	LOCAL = 150
ENDIF
SIF EXP:ASSI:Ａ経験 < LOCAL
	RETURN 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;クスコ着けているとダメ
SIF TEQUIP:クスコ
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;助手コスプレ中はダメ
SIF TEQUIP:ASSI:コスチュームプレイ
	RETURN 0
;裸エプロン、スク水プレイ、コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 313) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM313
IF (NO:PLAYER == 1014 && CFLAG:PLAYER:0 == 0) || (TALENT:人形 && TALENT:ASSI:人形)
	PRINTFORML パペットマペット

	;戦闘判定
	CALL COM_TRY_BATTLE, 313
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = パペットマペット
ELSE
	PRINTFORML 二人のＡをフィスト

	;戦闘判定
	CALL COM_TRY_BATTLE, 313
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 二人のＡをフィスト
ENDIF

CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 550
DOWNBASE:1 += 550
DOWNBASE:ASSI:0 += 550
DOWNBASE:ASSI:1 += 250

SOURCE:痛み = 1200
SOURCE:露出 = 1200
SOURCE:屈従 = 1200
SOURCE:逸脱 = 2000
SOURCE:恭順追加 = 1200

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 = 10
	SOURCE:屈従 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 30
	SOURCE:情愛 = 30
	SOURCE:屈従 += 700
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 500
	SOURCE:情愛 = 100
	SOURCE:屈従 += 1500
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:情愛 = 200
	SOURCE:屈従 += 3000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1700
	SOURCE:情愛 = 450
	SOURCE:屈従 += 5000
ELSE
	SOURCE:快Ａ = 2200
	SOURCE:情愛 = 750
	SOURCE:屈従 += 8000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み += 20000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 12000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み += 5000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1800
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 1000
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み += 600
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 200
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;ABL:欲望をみる
IF ABL:欲望 == 0
	SOURCE:液体追加 = 100
	SOURCE:屈従 += 3200
ELSEIF ABL:欲望 == 1
	SOURCE:液体追加 = 200
	SOURCE:屈従 += 3800
ELSEIF ABL:欲望 == 2
	SOURCE:液体追加 = 350
	SOURCE:屈従 += 4600
ELSEIF ABL:欲望 == 3
	SOURCE:液体追加 = 600
	SOURCE:屈従 += 5400
ELSEIF ABL:欲望 == 4
	SOURCE:液体追加 = 850
	SOURCE:屈従 += 6200
ELSE
	SOURCE:液体追加 = 1200
	SOURCE:屈従 += 7000
ENDIF


;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	TIMES SOURCE:液体追加 , 0.50
ELSEIF ABL:奉仕精神 == 1
	TIMES SOURCE:液体追加 , 0.80
ELSEIF ABL:奉仕精神 == 2
	TIMES SOURCE:液体追加 , 1.00
ELSEIF ABL:奉仕精神 == 3
	TIMES SOURCE:液体追加 , 1.50
ELSEIF ABL:奉仕精神 == 4
	TIMES SOURCE:液体追加 , 2.00
ELSE
	TIMES SOURCE:液体追加 , 2.50
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

SIF EXP:Ａ拡張経験 == 0
	TCVAR:異常経験 += 1
TCVAR:Ａ経験 += 25
TCVAR:Ａ拡張経験 += 1

SIF EXP:ASSI:Ａ拡張経験 == 0
	TCVAR:ASSI:異常経験 += 1
TCVAR:ASSI:Ａ経験 += 25
TCVAR:ASSI:Ａ拡張経験 += 1

RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_313
PRINTFORML %CALLNAME:TARGET%の肛門と%CALLNAME:ASSI%の肛門のそれぞれに、%CALLNAME:MASTER%は拳を入れてかき回してやった。
IF ABL:Ａ感覚 <= 1
	PRINTFORML %CALLNAME:TARGET%の肛門は痙攣しているかのようにヒクついている
ELSEIF ABL:Ａ感覚 == 2
	PRINTFORML %CALLNAME:TARGET%の肛門はぽっかり口を開いている
ELSEIF ABL:Ａ感覚 == 3
	PRINTFORML %CALLNAME:TARGET%の肛門は拡がったままなかなか戻らない
ELSEIF ABL:Ａ感覚 == 4
	PRINTFORML %CALLNAME:TARGET%の肛門からは肉ビラがはみ出し、腸の中が丸見えになっている
ELSE
	PRINTFORML %CALLNAME:TARGET%は、自分の肛門を指で拡げ、恍惚としている
ENDIF
IF ABL:ASSI:Ａ感覚 <= 1
	PRINTFORML 一方、%CALLNAME:ASSI%の肛門は痙攣しているかのようにヒクついている
ELSEIF ABL:ASSI:Ａ感覚 == 2
	PRINTFORML 一方、%CALLNAME:ASSI%の肛門はぽっかり口を開いている
ELSEIF ABL:ASSI:Ａ感覚 == 3
	PRINTFORML 一方、%CALLNAME:ASSI%の肛門は拡がったままなかなか戻らない
ELSEIF ABL:ASSI:Ａ感覚 == 4
	PRINTFORML 一方、%CALLNAME:ASSI%の肛門からは肉ビラがはみ出し、腸の中が丸見えになっている
ELSE
	PRINTFORML 一方、%CALLNAME:ASSI%は、自分の肛門を指で拡げ、恍惚としている
ENDIF
