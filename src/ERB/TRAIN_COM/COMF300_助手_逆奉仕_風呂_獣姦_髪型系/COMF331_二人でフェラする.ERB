;=============================================================================
;二人でフェラする
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE331
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:331 > 0
	RETURN 0
;調教者奉仕・ウフフ系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;助手がいないとダメ
SIF ASSI < 0
	RETURN 0
;ペニスが汚れており、調教者が[汚れ無視]でない場合(愛液、ペニス、母乳の汚れは条件対象外)
IF COM_ABLE_STAIN(TARGET, 2, 19) == 0 && TALENT:PLAYER:汚れ無視 == 0
	;調教者が主人の場合はダメ
	SIF PLAYER == MASTER
		RETURN 0
	;調教者が助手で[汚臭敏感]を持ち、《従順》4未満の場合はダメ
	SIF ASSIPLAY && TALENT:PLAYER:汚臭敏感 && ABL:PLAYER:従順 < 4
		RETURN 0
ENDIF
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)4未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 4
		RETURN 0
ENDIF
;主人と助手のいずれかもしくは双方が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF (TALENT:MASTER:猫舌 || TALENT:ASSI:猫舌) && (FLAG:14 & 16) == 0
	RETURN 0
;主人と助手のいずれかもしくは双方が[猫舌]だと該当者の技巧4必要
SIF (TALENT:MASTER:猫舌 && ABL:MASTER:技巧 < 4) || (TALENT:ASSI:猫舌 && ABL:ASSI:技巧 < 4)
	RETURN 0
;調教対象にペニスがないとダメ
SIF EXIST_BAR(TARGET) == 0
	RETURN 0
;オナホール・電極オナホール使用中はダメ
SIF TEQUIP:Ｃ系装着器具 == 2 || TEQUIP:Ｃ系装着器具 == 6
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 331) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM331
;特定の主人と助手の組み合わせを調べる
CALL JUDGE_TRAIN_PAIR, MASTER, ASSI
IF TFLAG:175 == 1
	LOCALS = 紅魔姉妹が
ELSEIF TFLAG:175 == 2
	LOCALS = 腋巫女二人が
ELSEIF TFLAG:175 == 3
	LOCALS = 秋の実りを祝う二柱が
ELSEIF TFLAG:175 == 4
	LOCALS = 月の姉妹が
ELSEIF TFLAG:175 == 5
	LOCALS = 秘封倶楽部の二人が
ELSEIF TFLAG:175 == 6
	LOCALS = 春告精、Ｗリリーが
ELSEIF TFLAG:175 == 7
	LOCALS = 覚り姉妹がいやらしく
ELSEIF TFLAG:175 == 8
	LOCALS = 夢幻姉妹が幻想的に
ELSEIF TFLAG:175 == 9
	LOCALS = ゆゆゆかが優しく
ELSEIF TFLAG:175 == 10 || TFLAG:175 == 11 || TFLAG:175 == 12
	LOCALS = 騒霊姉妹が賑やかに
ELSE
	LOCALS = 
ENDIF
PRINTFORML %LOCALS%二人でフェラする

;戦闘判定
CALL COM_TRY_BATTLE, 331
;実行できない
SIF RESULT == 1
	RETURN 1

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = %LOCALS%二人でフェラする
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;主人のABL:技巧をみる
IF ABL:MASTER:技巧 == 0
	LOCAL = 40
ELSEIF ABL:MASTER:技巧 == 1
	LOCAL = 140
ELSEIF ABL:MASTER:技巧 == 2
	LOCAL = 270
ELSEIF ABL:MASTER:技巧 == 3
	LOCAL = 540
ELSEIF ABL:MASTER:技巧 == 4
	LOCAL = 1000
ELSE
	LOCAL = 2000
ENDIF

;助手のABL:技巧をみる
IF ABL:ASSI:技巧 == 0
	LOCAL = 40
ELSEIF ABL:ASSI:技巧 == 1
	LOCAL = 140
ELSEIF ABL:ASSI:技巧 == 2
	LOCAL = 270
ELSEIF ABL:ASSI:技巧 == 3
	LOCAL = 540
ELSEIF ABL:ASSI:技巧 == 4
	LOCAL = 1000
ELSE
	LOCAL = 2000
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)

;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(MASTER, LOCAL)
;TALENT:舌使いをみる
SIF TALENT:MASTER:舌使い
	TIMES LOCAL , 2.00
;TALENT:猫舌をみる
SIF TALENT:MASTER:猫舌
	TIMES LOCAL , 0.70

;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(ASSI, LOCAL)
;助手のTALENT:舌使いをみる
SIF TALENT:ASSI:舌使い
	TIMES LOCAL , 2.00
;助手のTALENT:猫舌をみる
SIF TALENT:ASSI:猫舌
	TIMES LOCAL , 0.70

;調教対象のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(TARGET, LOCAL)

SIF EXIST_PENIS(TARGET)
	BASE:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 100

SOURCE:露出 = 320
SOURCE:逸脱 = 80

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 1000
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 1700
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 2400
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 3000
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 3500
ELSE
	SOURCE:快Ｃ = 4000
ENDIF

;主人のTALENT:舌使いをみる
IF TALENT:MASTER:舌使い
	TIMES SOURCE:快Ｃ , 2.00
	SOURCE:恭順追加 += SOURCE:快Ｃ / 20
ENDIF
;助手のTALENT:舌使いをみる
IF TALENT:ASSI:舌使い
	TIMES SOURCE:快Ｃ , 2.00
	SOURCE:恭順追加 += SOURCE:快Ｃ / 20
ENDIF
;主人の猫舌をみる
SIF TALENT:MASTER:猫舌
	TIMES SOURCE:快Ｃ , 0.70
;助手の猫舌をみる
SIF TALENT:ASSI:猫舌
	TIMES SOURCE:快Ｃ , 0.70

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(TARGET)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:射精中毒をみる
	IF ABL:射精中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:快Ｃ , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:射精中毒 == 1
		SOURCE:中毒充足 += 50
		TIMES SOURCE:快Ｃ , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:射精中毒 == 2
		SOURCE:中毒充足 += 150
		TIMES SOURCE:快Ｃ , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:射精中毒 == 3
		SOURCE:中毒充足 += 300
		TIMES SOURCE:快Ｃ , 4.50
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:射精中毒 == 4
		SOURCE:中毒充足 += 600
		TIMES SOURCE:快Ｃ , 6.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 += 1200
		TIMES SOURCE:快Ｃ , 8.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = TARGET
	;射精された人物を設定
	TFLAG:94 = PLAYER
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_331
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;奴隷のＰ⇔主人の口の汚れが移動
STAIN:ペニス |= STAIN:MASTER:口
STAIN:MASTER:口 |= STAIN:ペニス
;奴隷のＰ⇔助手の口の汚れが移動
STAIN:ペニス |= STAIN:ASSI:口
STAIN:ASSI:口 |= STAIN:ペニス

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ 
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF

TCVAR:Ｃ調律経験 += 1
TCVAR:MASTER:フェラ経験 += 1
TCVAR:ASSI:フェラ経験 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_331
;主人と助手がいずれも猫舌
IF TALENT:MASTER:猫舌 && TALENT:ASSI:猫舌
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	IF TALENT:肥大陰核
		PRINT ペニスのようなクリトリス
	ELSE
		PRINT ペニス
	ENDIF
	PRINTFORML を%CALLNAME:ASSI%と二人でかわりばんこに口に咥えつつ、舌先でくすぐった…
;主人が猫舌
ELSEIF TALENT:MASTER:猫舌 && TALENT:ASSI:猫舌 == 0
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	IF TALENT:肥大陰核
		PRINT ペニスのようなクリトリス
	ELSE
		PRINT ペニス
	ENDIF
	PRINTFORML を%CALLNAME:ASSI%と二人でかわりばんこに口に咥えつつ、%CALLNAME:MASTER%は優しく舌先でくすぐり、%CALLNAME:ASSI%がしっかりと舐めまわした…
;助手が猫舌
ELSEIF TALENT:MASTER:猫舌 == 0 && TALENT:ASSI:猫舌
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	IF TALENT:肥大陰核
		PRINT ペニスのようなクリトリス
	ELSE
		PRINT ペニス
	ENDIF
	PRINTFORML を%CALLNAME:ASSI%と二人でかわりばんこに口に咥えつつ、%CALLNAME:ASSI%は優しく舌先でくすぐり、%CALLNAME:MASTER%がしっかりと舐めまわした…
;焦らし
ELSEIF TEQUIP:焦らしプレイ
	PRINTFORM %CALLNAME:TARGET%の
	IF TALENT:肥大陰核
		PRINT ピクピクと小刻みに震えているクリトリスに、
	ELSE
		SIF EX:30
			PRINT 先走り汁まみれで
		PRINT ビクビクと脈打つペニスに、
	ENDIF
	PRINTFORML %CALLNAME:MASTER%と%CALLNAME:ASSI%が二人で交互に軽い口付けを繰り返した…
;ノーマル
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	IF TALENT:肥大陰核
		PRINT ペニスのようなクリトリス
	ELSE
		PRINT ペニス
	ENDIF
	PRINTFORML を、%CALLNAME:ASSI%と二人でかわりばんこに口に咥えて舐めまわした…
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_331
;デフォルト射精箇所タイプを設定
TFLAG:703 = 5
;射精箇所選択の可・不可
TFLAG:704 = 0
