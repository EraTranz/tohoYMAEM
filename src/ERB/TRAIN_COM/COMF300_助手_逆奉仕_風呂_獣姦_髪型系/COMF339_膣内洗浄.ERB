;=============================================================================
;膣内洗浄
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE339
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:339 > 0
	RETURN 0
;Ｖ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2) == 0
	RETURN 0
;調教者奉仕・ウフフ系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;中出しされてて、∨に精液汚れが無いとダメ
SIF CFLAG:3666 <= 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;他のＶ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;焦らしプレイ中はダメ
SIF TEQUIP:焦らしプレイ
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 339) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM339
PRINTL 膣内洗浄

;戦闘判定
CALL COM_TRY_BATTLE, 339
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 膣内洗浄
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 30
DOWNBASE:1 += 80

SOURCE:露出 = 300
SOURCE:性行動 = 70
SOURCE:欲情追加 = 100
SOURCE:痛み = 50
SOURCE:屈従 = 100
SOURCE:羞恥扇情 = 1000
SOURCE:逸脱 = 20

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:情愛 = 25
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 50
	SOURCE:情愛 = 50
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 100
	SOURCE:情愛 = 80
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 250
	SOURCE:情愛 = 100
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 500
	SOURCE:情愛 = 115
ELSE
	SOURCE:快Ｖ = 800
	SOURCE:情愛 = 125
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.00
	TIMES SOURCE:屈従 , 0.20
	SOURCE:痛み += 100
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.25
	TIMES SOURCE:屈従 , 0.50
	SOURCE:痛み += 60
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:屈従 , 0.80
	SOURCE:痛み += 20
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 0.75
	TIMES SOURCE:屈従 , 1.00
	SOURCE:痛み += 5
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:屈従 , 1.10
ELSE
	TIMES SOURCE:屈従 , 1.20
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:屈従 , 0.80
ENDIF

;親愛・相愛
SIF TALENT:親愛 || TALENT:相愛
	TIMES SOURCE:情愛 , 1.20

;精液ふき取り。
SIF STAIN:ヴァギナ & 4
	STAIN:ヴァギナ &= 4
CALL MINUS_RAND_LOOP, TARGET, 4, 3, 1
CFLAG:3666 = MAX(CFLAG:3666 / 5, 0)

;とりあえず抜いとく。
TFLAG:102 = 0

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_339
PRINTFORM %CALLNAME:TARGET%の秘部へ押し当て、
IF STAIN:ヴァギナ & 64 && TFLAG:103 > 0
	SIF STAIN:ヴァギナ & 32
		PRINT 破瓜の血と
	SIF STAIN:ヴァギナ & 128
		PRINT 経血と
	PRINT 精液と触手の体液が混ざり合ってできた濁った液体を
ELSEIF TFLAG:103 > 0
	IF STAIN:ヴァギナ & 32 && STAIN:ヴァギナ & 128
		PRINT 血と入り混じって生々しいピンク色の液体となった
	ELSEIF STAIN:ヴァギナ & 128
		PRINT 経血と入り混じって生々しいピンク色の液体となった
	ELSEIF STAIN:ヴァギナ & 32
		PRINT 破瓜の血と入り混じって生々しいピンク色の液体となった
	ENDIF
	IF CFLAG:3666 > 4
		;60%以上
		PRINTFORM 止め処なく溢れる精液を
	ELSE
		PRINTFORM 精液を
	ENDIF
ENDIF
PRINTL 洗い流した…
