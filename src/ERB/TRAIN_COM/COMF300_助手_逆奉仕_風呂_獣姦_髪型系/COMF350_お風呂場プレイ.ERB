;=============================================================================
;お風呂場プレイ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE350
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:350 > 0
	RETURN 0
;羞恥プレイ中は移動はできない（鏡の前から離れないと)
SIF TEQUIP:羞恥プレイ
	RETURN 0
;羞恥プレイおよび風呂コマンドを使っている場合を除き、解除はいつでも可能
SIF TEQUIP:お風呂場プレイ && TEQUIP:特殊風呂入浴中 == 0 && TEQUIP:触手催眠 == 0
	RETURN 1
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)3未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 3
		RETURN 0
ENDIF
;対象の従順が1以上ないとダメ
SIF ABL:従順 < 1
	RETURN 0
;対象か助手が動物耳で、従順が3未満だとダメ
SIF TALENT:動物耳 && ABL:従順 < 3
	RETURN 0
SIF ASSI >= 0 && TALENT:ASSI:動物耳 && ABL:ASSI:従順 < 3
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;蜘蛛の巣にかけているときは移動できない
SIF TEQUIP:緊縛関連 == 7
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;野外プレイ中はダメ
SIF TEQUIP:野外プレイ
	RETURN 0
;特殊風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;お医者さんプレイ中はダメ
SIF TEQUIP:お医者さんプレイ
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;裸エプロン中はダメ
SIF TEQUIP:コスチュームプレイ == 1
	RETURN 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSIPLAY && TEQUIP:PLAYER:コスチュームプレイ > 1 && TEQUIP:PLAYER:コスチュームプレイ != 2 && TEQUIP:PLAYER:コスチュームプレイ != 12 && TEQUIP:PLAYER:コスチュームプレイ != 17
	RETURN 0
;ビキニと褌以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 12 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 350) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM350
PRINTL お風呂場プレイ

;戦闘判定
CALL COM_TRY_BATTLE, 350
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = お風呂場プレイ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;風呂場への出入り(終了時は修正なし)
;シャワーは強制的に止められる
IF TEQUIP:お風呂場プレイ
	TEQUIP:シャワープレイ = 0
	TEQUIP:お風呂場プレイ = 0
	RETURN 1
ENDIF

DOWNBASE:0 += 0
DOWNBASE:1 += 30

LOCAL:1 = 100
LOCAL:2 = 50

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL:1 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL:1 , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL:1 , 1.10
ELSE
	TIMES LOCAL:1 , 1.20
ENDIF
;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:中毒充足 += 0
	SOURCE:液体追加 += 0
	TIMES LOCAL:1, 1.00
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 += 50
	SOURCE:液体追加 += 50
	TIMES LOCAL:1, 1.10
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 += 80
	SOURCE:液体追加 += 80
	TIMES LOCAL:1, 1.20
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 += 100
	SOURCE:液体追加 += 100
	TIMES LOCAL:1, 1.30
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 += 200
	SOURCE:液体追加 += 200
	TIMES LOCAL:1, 1.40
ELSE
	SOURCE:中毒充足 += 300
	SOURCE:液体追加 += 300
	TIMES LOCAL:1, 1.50
ENDIF
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.30
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 1.40
ELSE
	TIMES LOCAL:1 , 1.50
ENDIF

;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES LOCAL:1 , 1.50
;解放
SIF TALENT:解放
	TIMES LOCAL:1 , 1.50
;恥じらい
SIF TALENT:恥じらい
	TIMES LOCAL:2 , 1.20

SOURCE:羞恥扇情 += LOCAL:1 /2
SOURCE:トラウマ += LOCAL:2 /2

;臆病
SIF TALENT:臆病
	TIMES LOCAL:2 , 1.10

SOURCE:露出 += LOCAL:1
SOURCE:逸脱 += LOCAL:2
SOURCE:恭順追加 += LOCAL:1 /2
TEQUIP:お風呂場プレイ = 1
RETURN 1

;-------------------------------------------------
;お風呂場プレイ中
;-------------------------------------------------
@EQUIP_COM350
PRINTL ＜お風呂場プレイ中＞

DOWNBASE:0 += 20
DOWNBASE:1 += 50

LOCAL = 70
LOCAL:1 = 50

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF
;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:中毒充足 += 0
	SOURCE:液体追加 += 0
	TIMES LOCAL, 1.00
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 += 50
	SOURCE:液体追加 += 50
	TIMES LOCAL, 1.10
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 += 80
	SOURCE:液体追加 += 80
	TIMES LOCAL, 1.20
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 += 100
	SOURCE:液体追加 += 100
	TIMES LOCAL, 1.30
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 += 200
	SOURCE:液体追加 += 20
	TIMES LOCAL, 1.40
ELSE
	SOURCE:中毒充足 += 300
	SOURCE:液体追加 += 300
	TIMES LOCAL, 1.50
ENDIF
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL , 1.00
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL , 1.10
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL , 1.20
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL , 1.30
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL , 1.40
ELSE
	TIMES LOCAL , 1.50
ENDIF

;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES LOCAL , 1.50
;解放
SIF TALENT:解放
	TIMES LOCAL , 1.50
;恥じらい
SIF TALENT:恥じらい
	TIMES LOCAL:1 , 1.20

SOURCE:羞恥扇情 += LOCAL/2
SOURCE:トラウマ += LOCAL:1 /2

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 1.10

SOURCE:露出 += LOCAL
SOURCE:逸脱 += LOCAL:1
SOURCE:恭順追加 += LOCAL/2

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:従順 >= 1
	TFLAG:50 += 1
SIF ASSIPLAY == 0 && ABL:奉仕精神 >= 3
	TFLAG:50 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_350
IF TEQUIP:お風呂場プレイ
	PRINTFORML 風呂場を退出して、調教部屋に戻ってきた…
ELSE
	PRINTFORML %CALLNAME:TARGET%を風呂場に連れて行った…
ENDIF
