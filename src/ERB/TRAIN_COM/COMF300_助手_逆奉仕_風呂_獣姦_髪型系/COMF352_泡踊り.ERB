;=============================================================================
;泡踊り
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE352
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:352 > 0
	RETURN 0
;お風呂場プレイ中でプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ == 0 || ITEM:プレイマット == 0
	RETURN 0
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)3未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 3
		RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:Ｃ系装着器具
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;アイマスク使用中は不可
SIF TEQUIP:目装着器具
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSIPLAY && TEQUIP:PLAYER:コスチュームプレイ > 1 && TEQUIP:PLAYER:コスチュームプレイ != 2 && TEQUIP:PLAYER:コスチュームプレイ != 12 && TEQUIP:PLAYER:コスチュームプレイ != 17
	RETURN 0
;ビキニと褌以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 12 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 352) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM352
PRINTL 泡踊り

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 352
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

TSTR:0 = 泡踊り
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1500
ELSEIF ABL:技巧 == 1
	LOCAL = 2100
ELSEIF ABL:技巧 == 2
	LOCAL = 2900
ELSEIF ABL:技巧 == 3
	LOCAL = 4000
ELSEIF ABL:技巧 == 4
	LOCAL = 5000
ELSE
	LOCAL = 6000
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_2(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 50
DOWNBASE:1 += 180

SOURCE:屈従 = 2000
SOURCE:逸脱 = 1500

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	SOURCE:不潔 = 200
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 700
	SOURCE:達成感 = 500
	SOURCE:不潔 = 1500
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 900
	SOURCE:達成感 = 800
	SOURCE:不潔 = 100
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 1100
	SOURCE:達成感 = 1200
	SOURCE:不潔 = 50
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 1300
	SOURCE:達成感 = 1800
	SOURCE:不潔 = 20
ELSE
	SOURCE:性行動 = 1500
	SOURCE:達成感 = 2500
	SOURCE:不潔 = 0
ENDIF

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 0
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 50
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 200
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 400
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1000
ELSE
	SOURCE:快Ｃ = 2000
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL:1 = 200
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:1 = 300
ELSEIF ABL:Ｂ感覚== 2
	LOCAL:1 = 500
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:1 = 1000
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:1 = 1500
ELSE
	LOCAL:1 = 1800
ENDIF

;Ｂ敏感、Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:1 , 0.70
ENDIF
;巨乳、爆乳
SIF TALENT:巨乳 || TALENT:爆乳
	TIMES LOCAL:1 , 1.20
;淫乳
SIF TALENT:淫乳
	TIMES LOCAL:1 , 1.20

SOURCE:快Ｂ += LOCAL:1
;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.30
	TIMES SOURCE:達成感 , 1.30
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.60
	TIMES SOURCE:達成感 , 1.60
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.90
	TIMES SOURCE:達成感 , 1.90
ELSE
	TIMES SOURCE:性行動 , 2.30
	TIMES SOURCE:達成感 , 2.30
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 600
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成感 , 5.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_352
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF

;汚れはリセット
RESET_STAIN TARGET

RESET_STAIN PLAYER

;主人経験フラグ
SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:50 += 1
;奉仕快楽経験入手判定
TFLAG:100 |= 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_352
PRINTFORML %CALLNAME:TARGET%に泡踊りを実行するように命令してみたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_352
IF TFLAG:699 > 0
	PRINTFORM %CALLNAME:MASTER%が何もしないでいると、命令していないのに
	IF TALENT:サド
		PRINTL 男が喜ぶ行為を知り尽くしているかの様に
	ELSE
		PRINTL 思いついたかの様に
	ENDIF
ENDIF
PRINTFORM %CALLNAME:TARGET%はソープを泡立たせて自分に塗りたくり、
SIF TALENT:恋慕
	PRINT うっとりとした表情をしつつ
IF TALENT:小人体型 || TALENT:小柄体型
	PRINTFORM %CALLNAME:PLAYER%に小さな身体を目一杯擦りつけて
ELSEIF TALENT:巨乳 || TALENT:爆乳
	PRINTFORM %CALLNAME:PLAYER%に豊満な胸とふくよかな肌を擦りつけて
ELSE
	PRINTFORM %CALLNAME:PLAYER%に全身を擦りつけて
ENDIF
PRINTL 愛撫した…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_352, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 30

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_352
;デフォルト射精箇所タイプを設定
TFLAG:703 = 7
;射精箇所選択の可・不可
TFLAG:704 = 1
