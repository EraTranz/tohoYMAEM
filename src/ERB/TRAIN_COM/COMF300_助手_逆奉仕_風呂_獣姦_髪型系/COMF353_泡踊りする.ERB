;=============================================================================
;泡踊りする
;=============================================================================
;調教者が奴隷に泡踊りで反撃する
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE353
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:353 > 0
	RETURN 0
;調教者奉仕・ウフフ系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;お風呂場プレイ中でプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ == 0 || ITEM:プレイマット == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;助手調教で[妄信][服従]もなく、調教対象と同性の場合、《従順》4未満または《レズっ気》(《ＢＬっ気》)3未満だと実行できない
IF ASSIPLAY && !(TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:PLAYER:妄信 == 0 && TALENT:PLAYER:服従 == 0
	SIF ABL:PLAYER:従順 < 4 || ABL:PLAYER:(22 + TALENT:オトコ) < 3
		RETURN 0
ENDIF
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;Ｃ系装着具使用中は不可
SIF TEQUIP:Ｃ系装着器具
	RETURN 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;アイマスク使用中は不可
SIF TEQUIP:目装着器具
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSIPLAY && TEQUIP:PLAYER:コスチュームプレイ > 1 && TEQUIP:PLAYER:コスチュームプレイ != 2 && TEQUIP:PLAYER:コスチュームプレイ != 12 && TEQUIP:PLAYER:コスチュームプレイ != 17
	RETURN 0
;ビキニと褌以外のコスプレ中はだめ
SIF TEQUIP:コスチュームプレイ > 2 && TEQUIP:コスチュームプレイ != 12 && TEQUIP:コスチュームプレイ != 17
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 353) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM353
PRINTL 泡踊りする

;戦闘判定
CALL COM_TRY_BATTLE, 353
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 泡踊りする
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 40
ELSEIF ABL:技巧 == 1
	LOCAL = 260
ELSEIF ABL:技巧 == 2
	LOCAL = 800
ELSEIF ABL:技巧 == 3
	LOCAL = 2500
ELSEIF ABL:技巧 == 4
	LOCAL = 3500
ELSE
	LOCAL = 5600
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)

;調教者の技巧をみる
LOCAL = EJACULATIONCHECK_ABL_12_2(PLAYER, LOCAL)
;調教者の奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(PLAYER, LOCAL)

;調教対象のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(TARGET, LOCAL)

SIF EXIST_PENIS(TARGET)
	BASE:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:1 += 260

SOURCE:接触 = 1000
SOURCE:情愛 = 1000
SOURCE:露出 = 60
SOURCE:主導権 = 1500
SOURCE:欲情追加 = 300
SOURCE:中毒充足 = 400

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 500
	SOURCE:性行動 = 1000
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 900
	SOURCE:性行動 = 1300
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 1300
	SOURCE:性行動 = 1600
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1800
	SOURCE:性行動 = 2000
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2300
	SOURCE:性行動 = 2400
ELSE
	SOURCE:快Ｃ = 2900
	SOURCE:性行動 = 3000
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 15
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 50
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 300
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 700
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1100
ELSE
	SOURCE:快Ｂ = 1600
ENDIF

;ABL:調教者の技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｂ , 0.50
	TIMES SOURCE:性行動 , 0.50
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｂ , 0.80
	TIMES SOURCE:性行動 , 0.80
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｂ , 1.00
	TIMES SOURCE:性行動 , 1.00
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:性行動 , 1.50
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:快Ｃ , 2.50
	TIMES SOURCE:快Ｂ , 1.50
	TIMES SOURCE:性行動 , 2.50
ELSE
	TIMES SOURCE:快Ｃ , 4.00
	TIMES SOURCE:快Ｂ , 2.50
	TIMES SOURCE:性行動 , 4.00
ENDIF

;Ｂ敏感、Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES SOURCE:快Ｂ , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;巨乳、爆乳
SIF TALENT:巨乳 || TALENT:爆乳
	TIMES SOURCE:快Ｂ , 1.20
;淫乳
SIF TALENT:淫乳
	TIMES SOURCE:快Ｂ , 1.20

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(TARGET)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:射精中毒をみる
	IF ABL:射精中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:射精中毒 == 1
		SOURCE:中毒充足 += 200
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 1.60
	ELSEIF ABL:射精中毒 == 2
		SOURCE:中毒充足 += 600
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:射精中毒 == 3
		SOURCE:中毒充足 += 1500
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 0.70
	ELSEIF ABL:射精中毒 == 4
		SOURCE:中毒充足 += 3000
		TIMES SOURCE:達成感 , 5.00
		TIMES SOURCE:屈従, 0.40
	ELSE
		SOURCE:触手汚染 += 6000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 0.10
	ENDIF

	;射精した人物を設定
	TFLAG:92 = TARGET
	;射精された人物を設定
	TFLAG:94 = PLAYER
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_353
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF

;汚れはリセット
RESET_STAIN TARGET

RESET_STAIN PLAYER

;主人経験フラグ
SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:50 += 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_353
PRINTFORM %CALLNAME:PLAYER%はソープを泡立たせて自分に塗りたくり、
IF TALENT:PLAYER:小人体型 || TALENT:PLAYER:小柄体型
	PRINTFORM %CALLNAME:TARGET%に小さな身体を目一杯擦りつけて
ELSEIF TALENT:PLAYER:巨乳 || TALENT:PLAYER:爆乳
	PRINTFORM %CALLNAME:TARGET%に豊満な胸とふくよかな肌を擦りつけて
ELSE
	PRINTFORM %CALLNAME:TARGET%に全身を擦りつけて
ENDIF
PRINTL 愛撫した…

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_353
;デフォルト射精箇所タイプを設定
TFLAG:703 = 7
;射精箇所選択の可・不可
TFLAG:704 = 0

