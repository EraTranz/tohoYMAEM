;=============================================================================
;蟲風呂
;=============================================================================
;触手改変　バランスとりは有志に丸投げﾍ(ﾟﾛﾍﾟ)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE360
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:360 > 0
	RETURN 0
;特殊風呂系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 1024) == 0
	RETURN 0
;風呂を改装していないとダメ
SIF (FLAG:74 & 1) == 0
	RETURN 0
;一度も蟲風呂に入ったことが無い場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:23 & 1) == 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 2) == 0
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:特殊風呂入浴中 == 1
	RETURN 1
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;電極使用中は不可
SIF TEQUIP:Ｖ系装着器具 == 4 || TEQUIP:Ａ系装着器具 == 4 || TEQUIP:Ｃ系装着器具 == 4 || TEQUIP:Ｕ系装着器具 == 4 || TEQUIP:Ｂ系装着器具 == 4
	RETURN 0
;緊縛中じゃないとダメ
SIF TEQUIP:緊縛関連 == 0
	RETURN 0
;蜘蛛の巣にかけているときはダメ
SIF TEQUIP:緊縛関連 == 7
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;野外はダメ
SIF TEQUIP:野外プレイ
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場プレイ
	RETURN 0
;他の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 1
	RETURN 0
;お医者さんプレイ中はダメ
SIF TEQUIP:お医者さんプレイ
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ > 2
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 360) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM360
PRINTL 蟲風呂

;戦闘判定
CALL COM_TRY_BATTLE, 360
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 蟲風呂
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

IF TEQUIP:特殊風呂入浴中 == 1
	TEQUIP:特殊風呂入浴中 = 0
	;淫蟲射精ゲージリセット
	TFLAG:997 = 0
	TFLAG:998 = 0
	RETURN 1
ENDIF

TEQUIP:特殊風呂入浴中 = 1
TFLAG:997 = 0
TFLAG:998 = 10000

LOCAL:1 = 100
;EXP:異常経験をみる
IF EXP:異常経験 < EXPLV:1
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:異常経験 < EXPLV:2
	TIMES LOCAL:1 , 2.50
ELSEIF EXP:異常経験 < EXPLV:3
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:異常経験 < EXPLV:4
	TIMES LOCAL:1 , 1.00
ELSEIF EXP:異常経験 < EXPLV:5
	TIMES LOCAL:1 , 0.80
ELSE
	TIMES LOCAL:1 , 0.60
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 2.00
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1 , 0.60

;リグル、ヤマメ
IF NO:TARGET == 1024 || NO:TARGET == 1058
	TIMES LOCAL:1 , 0.30
	LOCAL:1 = MAX(LOCAL:1, 8)

	DOWNBASE:0 += LOCAL:1 / 4
	DOWNBASE:1 += LOCAL:1 / 2

	SOURCE:トラウマ += LOCAL:1 * 20
	SOURCE:逸脱 += LOCAL:1 * 3
ELSE
	DOWNBASE:0 += LOCAL:1 / 2
	DOWNBASE:1 += LOCAL:1

	SOURCE:トラウマ += LOCAL:1 * 30
	SOURCE:逸脱 += LOCAL:1 * 10
	SOURCE:反感追加 = 25000
ENDIF

IF (CFLAG:23 & 1) == 0
	CFLAG:23 |= 1
	TCVAR:異常経験 += 1
ENDIF
;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;蟲風呂入浴中
;--------------------------------------------------
@EQUIP_COM360
PRINTL ＜蟲風呂入浴中＞

LOCAL:3 = 100
;EXP:異常経験をみる
IF EXP:異常経験 < EXPLV:1
	TIMES LOCAL:3 , 3.00
ELSEIF EXP:異常経験 < EXPLV:2
	TIMES LOCAL:3 , 2.50
ELSEIF EXP:異常経験 < EXPLV:3
	TIMES LOCAL:3 , 2.00
ELSEIF EXP:異常経験 < EXPLV:4
	TIMES LOCAL:3 , 1.00
ELSEIF EXP:異常経験 < EXPLV:5
	TIMES LOCAL:3 , 0.80
ELSE
	TIMES LOCAL:3 , 0.60
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:3 , 2.00

;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:3 , 0.60

;リグル、ヤマメ
IF NO:TARGET == 1024 || NO:TARGET == 1058
	TIMES LOCAL:3 , 0.30
	LOCAL:3 = MAX(LOCAL:3, 8)
	DOWNBASE:0 += LOCAL:3 / 8
	DOWNBASE:1 += LOCAL:3 / 4

	SOURCE:トラウマ += LOCAL:3 * 20
	SOURCE:不潔 += LOCAL:3 * 6
	SOURCE:逸脱 += LOCAL:3 * 3
ELSE
	DOWNBASE:0 += LOCAL:3 / 4
	DOWNBASE:1 += LOCAL:3 / 2

	SOURCE:トラウマ += LOCAL:3 * 30
	SOURCE:不潔 += LOCAL:3 * 10
	SOURCE:逸脱 += LOCAL:3 * 15
	SOURCE:反感追加 += 5000
ENDIF

SOURCE:液体追加 += 1000
TIMES SOURCE:快Ｃ , 2.00
TIMES SOURCE:快Ｖ , 2.00
TIMES SOURCE:快Ａ , 2.00
TIMES SOURCE:快Ｂ , 2.00
TIMES SOURCE:屈従 , 1.80

;何が出るかな？(ランダムでイベントが発生するのもありかな？と思って作ってみた)
;基本リグル、ヤマメ以外で[感情乏しい]、[狂気]、[精神崩壊]、[傀儡]、[壊造人格]を持っていないキャラの恐怖が一定以上溜まった場合低確率で発動
IF TALENT:感情乏しい == 0 && TALENT:狂気 == 0 && TALENT:精神崩壊 == 0 && TALENT:傀儡 == 0 && TALENT:壊造人格 == 0 && NO:TARGET != 1024 && NO:TARGET != 1058 && PALAM:恐怖 > PALAMLV:4
	IF RAND:25 == 0
		LOCAL:4 = RAND:10
		IF LOCAL:4 == 0
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 0
			PRINTFORML %CALLNAME:TARGET%は発狂した
			TALENT:狂気 = 1
			CFLAG:12 |= 1
			CFLAG:17 |= 2
		ELSEIF (LOCAL:4 == 1 || LOCAL:4 == 2) && MARK:反発刻印 > 2
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 1
			PRINTL 反発刻印がLV2に下がった
			MARK:反発刻印 = 2
		ELSEIF (LOCAL:4 > 2 && LOCAL:4 < 6) && BASE:体力 >= 500
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 2
			PRINTFORML %CALLNAME:TARGET%の体力と気力は大きく消耗してしまった
			BASE:体力 /= 5
			BASE:気力 = 0
		ELSEIF LOCAL:4 >= 6 && TALENT:臆病 == 0 && BASE:体力 >= 500
			IF TALENT:幼児退行 == 0 && LOCAL:4 == 9
				CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 3
				PRINTFORML %CALLNAME:TARGET%は[幼児退行]してしまった
				TALENT:幼児退行 = 1
				CFLAG:12 |= 2
				CFLAG:17 |= 1
			ELSE
				CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 4
			ENDIF
			PRINTFORML %CALLNAME:TARGET%は[臆病]になった
			TALENT:臆病 = 1
			CFLAG:12 |= 4
		ELSEIF LOCAL:4 >= 8 && TALENT:臆病 && TALENT:幼児退行 == 0 && BASE:体力 >= 500
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 6, 3
			PRINTFORML %CALLNAME:TARGET%は[幼児退行]してしまった
			TALENT:幼児退行 = 1
			CFLAG:12 |= 2
			CFLAG:17 |= 1
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF TFLAG:998 != 0
	LOCAL = 0
	;ABL:技巧をみる
	IF ABL:技巧 == 0
		LOCAL = 500
	ELSEIF ABL:技巧 == 1
		LOCAL = 600
	ELSEIF ABL:技巧 == 2
		LOCAL = 800
	ELSEIF ABL:技巧 == 3
		LOCAL = 1000
	ELSEIF ABL:技巧 == 4
		LOCAL = 1400
	ELSE
		LOCAL = 2000
	ENDIF

	;ABL:従順をみる
	IF ABL:従順 == 0
		TIMES LOCAL , 0.80
	ELSEIF ABL:従順 == 1
		TIMES LOCAL , 0.90
	ELSEIF ABL:従順 == 2
		TIMES LOCAL , 1.00
	ELSEIF ABL:従順 == 3
		TIMES LOCAL , 1.10
	ELSEIF ABL:従順 == 4
		TIMES LOCAL , 1.20
	ELSE
		TIMES LOCAL , 1.30
	ENDIF

	;PALAM:欲情をみる
	IF PALAM:欲情 < PALAMLV:1
		TIMES LOCAL , 1.00
	ELSEIF PALAM:欲情 < PALAMLV:2
		TIMES LOCAL , 1.10
	ELSEIF PALAM:欲情 < PALAMLV:3
		TIMES LOCAL , 1.20
	ELSEIF PALAM:欲情 < PALAMLV:4
		TIMES LOCAL , 1.30
	ELSEIF PALAM:欲情 < PALAMLV:5
		TIMES LOCAL , 1.40
	ELSE
		TIMES LOCAL , 1.50
	ENDIF

	TFLAG:997 += LOCAL

	LOCAL:2 = TFLAG:997
	EJAC = TFLAG:998

	IF LOCAL:2 > EJAC * 4
		LOCAL:1 = 3
	ELSEIF LOCAL:2 > EJAC * 2
		LOCAL:1 = 2
	ELSEIF LOCAL:2 > EJAC
		LOCAL:1 = 1
	ELSE
		LOCAL:1 = 0
	ENDIF

	;超大量射精
	IF LOCAL:1 == 3
		TCVAR:精液経験 += 5
		CFLAG:3666 += 5
		PRINTL 淫蟲超大量射精
		SOURCE:欲情追加 += EJAC*4
		TFLAG:997 -= EJAC*4
		
	;大量射精
	ELSEIF LOCAL:1 == 2
		TCVAR:精液経験 += 3
		CFLAG:3666 += 3
		PRINTL 淫蟲大量射精
		SOURCE:欲情追加 += EJAC*2
		TFLAG:997 -= EJAC*2
	;通常の射精
	ELSEIF LOCAL:1 == 1
		TCVAR:精液経験 += 1
		CFLAG:3666 += 1
		PRINTL 淫蟲射精
		SOURCE:欲情追加 += EJAC
		TFLAG:997 -= EJAC
	ENDIF
	TFLAG:997 = LIMIT(TFLAG:997, 0, (EJAC-1))
	CALL CALCULATE_LIMIT, TARGET
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_360
IF TEQUIP:特殊風呂入浴中 == 1
	PRINTFORM %CALLNAME:TARGET%を蟲達の中から
	IF NO:TARGET == 1024 || NO:TARGET == 1058
		PRINTL 引っ張り出すと、少し複雑そうな顔をしていた…
	ELSE
		PRINTL 救い出すと、ほっとしたような顔をしていた…
	ENDIF
ELSE
	PRINTFORML 無数の蟲達がうごめく風呂桶に%CALLNAME:TARGET%を突き落とした
	PRINTFORM %CALLNAME:TARGET%は
	IF NO:TARGET == 1024 || NO:TARGET == 1058
		PRINTFORML 一瞬驚いたような\@(TEQUIP:口装着器具) ? 表情を見せ # 声をあげ\@たものの、すぐに平静を取り戻していた…
	ELSE
		PRINTFORML \@(TEQUIP:口装着器具) ? 激しく首を振って嫌がっ # 声にならない悲鳴をあげ\@ている…
	ENDIF
ENDIF
