;=============================================================================
;豚とキスさせる
;=============================================================================
;キスをするから作成。
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE388
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:388 > 0
	RETURN 0
;獣姦系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 8192) == 0
	RETURN 0
;初体験のときは異常系調教フィルタがオフになってないとダメ
SIF (FLAG:25 & 128) == 0 && FLAG:2 & 4
	RETURN 0
;動物小屋と豚が必要
SIF (FLAG:72 & 1) == 0 || (FLAG:72 & 4) == 0
	RETURN 0
;主人じゃないとダメ
SIF ASSIPLAY
	RETURN 0
;主人に禁断の知識か狂気がないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && TALENT:MASTER:狂気 == 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;対象が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:猫舌 && (FLAG:14 & 16) == 0
	RETURN 0
;対象が[猫舌]だと技巧5・奉仕精神4・従順3が必要
SIF TALENT:猫舌 && (ABL:技巧 < 5 || ABL:奉仕精神 < 4 || ABL:従順 < 3)
	RETURN 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場プレイ
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 388) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM388
PRINTL 豚とキスさせる

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 388
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 豚とキスさせる
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:1 += 50

SOURCE:不潔 = 8900
SOURCE:屈従 = 4500
SOURCE:逸脱 = 38000

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 110
	SOURCE:達成感 = 2
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 330
	SOURCE:達成感 = 78
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 440
	SOURCE:達成感 = 157
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 550
	SOURCE:達成感 = 280
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 660
	SOURCE:達成感 = 470
	TIMES SOURCE:不潔 , 0.80
ELSE
	SOURCE:性行動 = 770
	SOURCE:達成感 = 785
	TIMES SOURCE:不潔 , 0.50
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;ABL:獣姦中毒をみる
IF ABL:獣姦中毒 == 0
	SOURCE:中毒充足 = 1500
ELSEIF ABL:獣姦中毒 == 1
	SOURCE:中毒充足 = 1700
	TIMES SOURCE:不潔 , 0.90
ELSEIF ABL:獣姦中毒 == 2
	SOURCE:中毒充足 = 2000
	TIMES SOURCE:不潔 , 0.80
ELSEIF ABL:獣姦中毒 == 3
	SOURCE:中毒充足 = 2500
	TIMES SOURCE:不潔 , 0.70
ELSEIF ABL:獣姦中毒 == 4
	SOURCE:中毒充足 = 3300
	TIMES SOURCE:不潔 , 0.60
ELSE
	SOURCE:中毒充足 = 4200
	TIMES SOURCE:不潔 , 0.50
ENDIF

;キス魔
IF TALENT:キス魔
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成感 , 2.50
ENDIF

;奴隷の口に動物の汚れを付けたいところだが今後の課題とする

TCVAR:キス経験 += 1
IF (CFLAG:25 & 128) == 0
	CFLAG:25 |= 128
	TCVAR:異常経験 += 1
ENDIF
TCVAR:動物性愛経験 += 1

;コマンド属性：獣姦
TFLAG:98 = 9

RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_388
PRINTFORML %CALLNAME:TARGET%に豚と口付けするように命じたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_388
PRINTFORM %CALLNAME:TARGET%は
SIF TALENT:淫乱
	PRINT 自ら
PRINT 豚の口
;猫舌キス
IF TALENT:猫舌
	PRINTL を軽く舐めて、興奮した豚の震える唇を何度も刺激している…
;通常のキス
ELSE
	PRINTL の中に舌を入れ、興奮した豚の舌と濃密に絡め合わせている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_388, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;ABL:獣姦中毒
IF ABL:獣姦中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:獣姦中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:38%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF
;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF

;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF

;実行値は78
TFLAG:241 = 78
