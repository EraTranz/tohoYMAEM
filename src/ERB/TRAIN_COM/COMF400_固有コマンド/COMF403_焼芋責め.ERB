;=============================================================================
;焼芋責め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE403
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:403 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;Ｖ経験レベルが0の場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1
	RETURN 0
;調教者が秋姉妹か[野菜の錬金術師]でないとダメ
SIF NO:PLAYER != 1037 && NO:PLAYER != 1038 && TALENT:PLAYER:野菜の錬金術師 == 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ｖ系装着器具 == 9
	RETURN 1
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;対象が[処女]
IF TALENT:処女 == 1
	;対象の処女の判定
	SIF COM_ABLE_VIRGIN(TARGET, PLAYER) == 0
		RETURN 0
ELSE
	;対象の《潤滑》は大丈夫かチェック
	SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
		RETURN 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;体力が1000未満では装着不可能
SIF BASE:体力 < 1000 && !(FLAG:16 & 32)
	RETURN 0
;気力が0の時はダメ
SIF BASE:気力 <= 0 && !(FLAG:16 & 32)
	RETURN 0
;Ｖ拡張経験少ないとダメ
SIF EXP:Ｖ拡張経験 < 5
	RETURN 0
;他のＶ系装着具使用中は不可
SIF (TEQUIP:Ｖ系装着器具 && TEQUIP:Ｖ系装着器具 != 9) || TEQUIP:クスコ
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 403) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM403
IF NO:TARGET == 1008 || NO:TARGET == 1022
	PRINTL 焼紫芋責め

	;戦闘判定
	CALL COM_TRY_BATTLE, 403
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 焼紫芋責め
ELSEIF NO:TARGET == 1031
	PRINTL 石焼芋責め

	;戦闘判定
	CALL COM_TRY_BATTLE, 403
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 石焼芋責め
ELSE
	PRINTL 焼芋責め

	;戦闘判定
	CALL COM_TRY_BATTLE, 403
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 焼芋責め
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 400
DOWNBASE:1 += 500

SOURCE:露出 = 1000
SOURCE:恭順追加 = 800
SOURCE:屈従 = 1500
SOURCE:逸脱 = 1800

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 2000
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 2500
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 3000
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 3500
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 4000
ELSE
	SOURCE:快Ｖ = 5000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 7000
	TCVAR:異常経験 += 2
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 3500
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 1000
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 250
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 50
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 400
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.10
ELSE
	TIMES SOURCE:快Ｖ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;野菜性活
IF TEQUIP:Ａ系装着器具 == 9 && TEQUIP:Ｕ系装着器具 == 9
	TIMES SOURCE:快Ｖ , 1.80
ELSEIF TEQUIP:Ａ系装着器具 == 9 || TEQUIP:Ｕ系装着器具 == 9
	TIMES SOURCE:快Ｖ , 1.20
ENDIF

IF NO:TARGET == 1008 || NO:TARGET == 1022
	TIMES SOURCE:快Ｖ , 1.50
ELSEIF NO:TARGET == 1031
	TIMES SOURCE:痛み , 1.50
ENDIF

TCVAR:Ｖ経験 += 3
TCVAR:Ｖ拡張経験 += 4

;焼き芋の着脱
IF TEQUIP:Ｖ系装着器具 == 9
	TEQUIP:Ｖ系装着器具 = 0
	TFLAG:163 += 5
ELSE
	TEQUIP:Ｖ系装着器具 = 9
	TFLAG:163 += 10
ENDIF

RETURN 1

;--------------------------------------------------
;焼芋挿入中
;--------------------------------------------------
@EQUIP_COM403
IF NO:TARGET == 1008 || NO:TARGET == 1022
	PRINTL ＜焼紫芋挿入中＞
ELSEIF NO:TARGET == 1031
	PRINTL ＜石焼芋挿入中＞
ELSE
	PRINTL ＜焼芋挿入中＞
ENDIF

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 100
DOWNBASE:1 += 90

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 40
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 120
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 360
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 600
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 900
ELSE
	LOCAL = 1300
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:1 = 240
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:1 = 120
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:1 = 60
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:1 = 30
ELSE
	TIMES LOCAL , 1.60
	LOCAL:1 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.10
	LOCAL:1 += 400
	TIMES LOCAL:1 , 3.00
	LOCAL:2 += 1000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.40
	LOCAL:1 += 150
	TIMES LOCAL:1 , 1.00
	LOCAL:2 += 200
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 0.50
	LOCAL:2 += 80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:1 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:1 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:1 , 2.00

;貞操観念(処女はありえない)
SIF TALENT:貞操観念
	TIMES LOCAL:2, 3.00

IF NO:TARGET == 1008 || NO:TARGET == 1022
	TIMES LOCAL , 1.20
ELSEIF NO:TARGET == 1031
	TIMES LOCAL:1 , 1.20
ENDIF

SOURCE:快Ｖ += LOCAL
SOURCE:痛み += LOCAL:1
SOURCE:屈従 += LOCAL:2

;自慰実行時は経験＋１
IF CHECK_COM_MASTURBATION(SELECTCOM)
	TCVAR:Ｖ経験 += 3
	TCVAR:Ｖ拡張経験 += 3
ELSE
	TCVAR:Ｖ経験 += 2
	TCVAR:Ｖ拡張経験 += 2
ENDIF

TFLAG:163 += 1

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_403
IF TEQUIP:Ｖ系装着器具 == 9
	PRINTFORML %CALLNAME:TARGET%の性器に突き刺さっているを大きな焼芋をゆっくりと引き抜いた。
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%
		IF EXP:Ｖ拡張経験 >= 100 && TALENT:淫乱
			PRINT の開発させきった膣は、さらなる快楽を貪るかのように焼芋に絡みつい
		ELSEIF TALENT:恋慕 && TALENT:淫乱
			PRINT は、芋から生えているひげのような根が膣内のひだを撫でられる度、まるで痙攣するかのように四肢をはねさせ
		ELSEIF TALENT:恋慕
			PRINTFORM は、荒い息をつきながらも%CALLNAME:PLAYER%と目が合うと満足げな表情を見せ
		ELSEIF TALENT:淫乱
			PRINT は、芋がもたらす快感にだらしなく舌をたらして嬌声をあげ
		ELSEIF TALENT:痛みに弱い
			PRINT は、陰唇から巨大な質量を誇る物体が引きずり出される感覚に耐えきれず、聞くに堪えない絶叫をあげ
		ELSE
			PRINT は、抜かれた瞬間ほっとした表情を浮かべているものの、膣内の喪失感に対する違和感が拭えず困惑し
		ENDIF
		PRINTL ている…
	ENDIF
ELSE
	TEQUIP:Ｖ系装着器具 = 9
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:5
		PRINT 床に水溜まりができるほどの
	SIF PALAM:潤滑 >= PALAMLV:3
		PRINT 愛液で溢れている
	IF TALENT:小柄体型
		PRINT 幼いワレメに
	ELSE
		PRINT 秘裂に
	ENDIF
	PRINTL 拳よりも大きな焼芋をあてがった…
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:処女 == 1
			IF TALENT:恋慕 && TALENT:淫乱
				PRINT 処女である事を疑いたくなるような淫靡な笑みを浮かべ、中に誘っているように腰を振っ
			ELSEIF TALENT:恋慕
				PRINTFORM 目をまん丸に開いて%CALLNAME:PLAYER%の手にあるものを見つめたまま固まっ
			ELSEIF TALENT:淫乱
				PRINT 処女である事もお構いなしに、自ら陰唇を大きく拡げて挿入されるのを待っ
			ELSEIF TALENT:一線越えない
				PRINT 目尻に涙を見せながら固く目を閉ざし、目の前の現実を否定するかのように首を左右に振っ
			ELSEIF TALENT:痛みに弱い
				PRINT 半狂乱になりながらこの責めからなんとか逃れようと懸命に抵抗し
			ELSE
				PRINT 顔面蒼白でこれから自分の性器に入れられる物体を見つめ
			ENDIF
		ELSE
			IF TALENT:恋慕 && TALENT:淫乱
				PRINT 熱のこもった視線で“それ”を見つめ、自分から進んで膣口を大きく拡げている
			ELSEIF TALENT:恋慕
				PRINTFORM 笑みを浮かべてはいるものの戸惑いの表情を隠しきれないまま%CALLNAME:PLAYER%を見
			ELSEIF TALENT:淫乱
				PRINT 挿入される快感を限界まで引き出そうと膣を自分の指でかき回し、陰唇から愛液を滴らせ
			ELSEIF TALENT:痛みに弱い
				PRINT 涙を流しながら叫び声をあげ、手で今まさに自分の秘部に潜り込もうとするモノを払いのけようとし
			ELSE
				PRINTFORM 乾いた笑みを浮かべ、%CALLNAME:PLAYER%の手の中の芋を見つめ
			ENDIF
		ENDIF
		PRINTL ている…
	ENDIF
ENDIF
