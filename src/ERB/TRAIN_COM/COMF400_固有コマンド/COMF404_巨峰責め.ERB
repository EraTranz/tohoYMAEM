;=============================================================================
;巨峰責め
;=============================================================================
;Ａ拡張経験がついた強化アナルビーズみたいなもの。
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE404
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:404 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;調教者が秋姉妹か[野菜の錬金術師]でないとダメ
SIF NO:PLAYER != 1037 && NO:PLAYER != 1038 && TALENT:PLAYER:野菜の錬金術師 == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:Ａ系装着器具 == 9
	RETURN 1
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;体力が1000未満では装着不可能
SIF BASE:体力 < 1000 && !(FLAG:16 & 32)
	RETURN 0
;気力が0の時はダメ
SIF BASE:気力 <= 0 && !(FLAG:16 & 32)
	RETURN 0
;Ａ拡張経験少ないとダメ
SIF EXP:Ａ拡張経験 < 5
	RETURN 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURN 0
;他のＡ系装着具使用中は不可
SIF (TEQUIP:Ａ系装着器具 && TEQUIP:Ａ系装着器具 != 9) || TEQUIP:氷柱
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 404) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM404
PRINTL 巨峰責め

;戦闘判定
CALL COM_TRY_BATTLE, 404
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 巨峰責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 300
DOWNBASE:1 += 400

SOURCE:逸脱 = 2000

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 100
	SOURCE:屈従 = 300
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 300
	SOURCE:屈従 = 800
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 600
	SOURCE:屈従 = 1400
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:屈従 = 1800
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1500
	SOURCE:屈従 = 2100
ELSE
	SOURCE:快Ａ = 2100
	SOURCE:屈従 = 2400
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み = 2000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み = 300
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 1.10
	SOURCE:痛み = 50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.20
	SOURCE:痛み = 10
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 1200
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 700
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 400
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 150
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.10
ELSE
	TIMES SOURCE:快Ａ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ａ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ａ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ａ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ａ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ａ , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.30
ENDIF

;小柄体型
IF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00
SIF TALENT:幼児 || TALENT:幼稚
	TIMES SOURCE:快Ａ , 2.00
ENDIF

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;野菜性活
IF TEQUIP:Ｖ系装着器具 == 9 && TEQUIP:Ｕ系装着器具 == 9
	TIMES SOURCE:快Ａ , 1.80
ELSEIF TEQUIP:Ｖ系装着器具 == 9 || TEQUIP:Ｕ系装着器具 == 9
	TIMES SOURCE:快Ａ , 1.20
ENDIF

IF TEQUIP:Ｖ系装着器具 == 9
	TCVAR:Ａ経験 += 3
	TCVAR:Ａ拡張経験 += 4
ELSE
	TCVAR:Ａ経験 += 2
	TCVAR:Ａ拡張経験 += 3
ENDIF

;巨峰の着脱
IF TEQUIP:Ａ系装着器具 == 9
	TEQUIP:Ａ系装着器具 = 0
	TFLAG:164 += 5
ELSE
	TEQUIP:Ａ系装着器具 = 9
	TFLAG:164 += 10
ENDIF
RETURN 1

;--------------------------------------------------
;巨峰挿入中
;--------------------------------------------------
@EQUIP_COM404
PRINTL ＜巨峰挿入中＞

DOWNBASE:0 += 70
DOWNBASE:1 += 120

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	LOCAL = 40
	LOCAL:1 = 100
ELSEIF ABL:Ａ感覚 == 1
	LOCAL = 80
	LOCAL:1 = 300
ELSEIF ABL:Ａ感覚 == 2
	LOCAL = 100
	LOCAL:1 = 500
ELSEIF ABL:Ａ感覚 == 3
	LOCAL = 150
	LOCAL:1 = 700
ELSEIF ABL:Ａ感覚 == 4
	LOCAL = 200
	LOCAL:1 = 900
ELSE
	LOCAL = 300
	LOCAL:1 = 1200
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES LOCAL , 0.50
	LOCAL:2 = 3000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES LOCAL , 1.00
	LOCAL:2 = 1000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES LOCAL , 1.10
	LOCAL:2 = 500
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:2 = 100
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:2 = 50
ELSE
	TIMES LOCAL , 1.60
	LOCAL:2 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.40
	LOCAL:2 += 900
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.80
	LOCAL:2 += 600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	LOCAL:2 += 300
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	LOCAL:2 += 150
ELSE
	TIMES LOCAL , 1.80
	LOCAL:2 += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:2 , 1.50

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	LOCAL:1 /= 3

SOURCE:快Ａ += LOCAL
SOURCE:屈従 += LOCAL:1
SOURCE:痛み += LOCAL:2

TCVAR:Ａ経験 += 2
TCVAR:Ａ拡張経験 += 2

TFLAG:164 += 1

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_404
IF TEQUIP:Ａ系装着器具 == 9
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の菊穴から生えている蔓をつまむと勢い良く引き抜いた…
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF ABL:Ａ感覚 >= 5 && TALENT:淫乱
			PRINTFORML 一時は幸せそうな顔をしていた%CALLNAME:TARGET%だったが、次第に直腸の喪失感が増してきたのか
			PRINTFORM 涙と涎をこぼし、腰を振って自らの手で肛門を拡げながら哀願するような目で%CALLNAME:PLAYER%を見上げて
		ELSEIF ABL:Ａ感覚 >= 3 && EXP:Ａ経験 < 5 && TALENT:淫乱
			PRINTFORM 葡萄の実が肛門から排泄されるとその度に%CALLNAME:TARGET%はぶるっと腰を震わせ
		ELSEIF ABL:Ａ感覚 >= 1 && EXP:Ａ経験 < 3 && TALENT:淫乱
			PRINTFORM 大きく拡がった%CALLNAME:TARGET%の肛門から腸内に溜まっていたであろう粘液が滴り出し
		ELSEIF ABL:Ａ感覚 == 0 && TALENT:淫乱
			PRINTFORM %CALLNAME:TARGET%は痛そうに顔を歪ませながらも陰唇からは愛液を滲ませ
		ELSEIF ABL:Ａ感覚 >= 4
			PRINTFORM %CALLNAME:TARGET%は恍惚感を浮かべた目付きで%CALLNAME:PLAYER%の方を振り返っ
		ELSEIF ABL:Ａ感覚 >= 2
			PRINTFORM %CALLNAME:TARGET%は荒い息をつきながら涙を流し
		ELSE
			PRINTFORM %CALLNAME:TARGET%は挿入された時以上の大きな叫び声を上げ
		ENDIF
		PRINTL ている…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	SIF PALAM:潤滑 >= PALAMLV:5
		PRINT たっぷりと
	SIF PALAM:潤滑 >= PALAMLV:3
		PRINT 粘液をたたえた
	SIF PALAM:潤滑 < PALAMLV:3
		PRINT 硬く閉ざされた
	IF TALENT:小柄体型
		PRINT 可愛らしい菊門を
	ELSE
		PRINT アヌスを
	ENDIF
	PRINTL 無理やり拡げ、大粒の葡萄の実をひとつずつ捻じ込んでいった…
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%
		IF EXP:Ａ経験 > 200 && TALENT:恋慕
			PRINT は１粒挿入されただけでガクガクと腰を震わせてイきそうな気配を漂わせて
		ELSEIF EXP:Ａ経験 <= 200 && TALENT:恋慕
			PRINT の肛門はまるで食べ盛りの子供みたく、貪欲に葡萄を飲み込んで
		ELSEIF EXP:Ａ経験 <= 100 && TALENT:恋慕
			PRINT は葡萄の実が腸内を転がっていく感触を楽しむため腰を高々と上げて
		ELSEIF EXP:Ａ経験 <= 50 && TALENT:恋慕
			PRINT は苦痛に顔をしかめていたが実を飲み込むごとに快楽を感じ始めたのか徐々に頬を緩ませて
		ELSEIF EXP:Ａ経験 > 200
			PRINT は直腸に巨大な質量をもつ何かが転がり込むたびに恍惚とした表情を浮かべて
		ELSEIF EXP:Ａ経験 > 100
			PRINT は実が腸壁に擦れる感触を楽しんでいるかのように腰を振って
		ELSE
			PRINT は苦痛に顔を歪ませながらも肛門から頭頂まで駆け抜けるような快楽に一瞬弛んだ表情を見せて
		ENDIF
		PRINTL いる…
	ENDIF
ENDIF
