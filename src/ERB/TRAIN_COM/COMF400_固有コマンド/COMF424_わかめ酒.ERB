;=============================================================================
;わかめ酒
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE424
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:424 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;調教者が萃香または勇儀、若しくは調教対象が萃香または勇儀でないとダメ
IF NO:TARGET != 1023 && NO:TARGET != 1060
	SIF NO:PLAYER != 1023 && NO:PLAYER != 1060
		RETURN 0
ENDIF
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;ヴァギナが汚れており、調教者が[汚れ無視]でない場合(愛液、母乳の汚れは条件対象外)
IF COM_ABLE_STAIN(TARGET, 3, 17) == 0 && TALENT:PLAYER:汚れ無視 == 0
	;調教者が主人の時はダメ
	SIF PLAYER == MASTER
		RETURN 0
	;調教者が助手で[汚臭敏感]を持ち、《従順》4未満の場合はダメ
	SIF ASSIPLAY && TALENT:PLAYER:汚臭敏感 && ABL:PLAYER:従順 < 4
		RETURN 0
ENDIF
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;小柄体型の場合、巨乳か爆乳じゃないとダメ
SIF TALENT:小柄体型 && TALENT:貧乳 == 0 && TALENT:巨乳 == 0
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;Ｖ系装着具使用中のときは技巧が4以上ないとダメ
SIF TEQUIP:Ｖ系装着器具 && ABL:PLAYER:技巧 < 4
	RETURN 0
;クスコ着けているとダメ
SIF TEQUIP:クスコ
	RETURN 0
;縄で緊縛中または対象の従順が3以上ないとダメ
SIF TEQUIP:緊縛関連 && ABL:従順 < 2
	RETURN 0
;蜘蛛の巣にかけているときはダメ
SIF TEQUIP:緊縛関連 == 7
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;スク水プレイ中はダメ
SIF TEQUIP:コスチュームプレイ == 2
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;コスプレ中はだめ(色々と面倒なので)
SIF TEQUIP:コスチュームプレイ > 2
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 424) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM424
IF TEQUIP:Ｖ系装着器具 == 9
	PRINTL 芋焼酎

	;戦闘判定
	CALL COM_TRY_BATTLE, 424
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 芋焼酎
ELSE
	PRINTL わかめ酒

	;戦闘判定
	CALL COM_TRY_BATTLE, 424
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = わかめ酒
ENDIF
;口上用にTFLAG:201を使用。(0=胸に, 1=正座で股に, 2=普通に股に)
TFLAG:201 = 0

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF FLAG:14 & 8
	PRINTL どこを使う？
	SIF TALENT:巨乳 || TALENT:爆乳
		PRINTL [0] - 胸
	PRINTL [1] - 股(正座)
	PRINTL [2] - 股
	$INPUT_LOOP
	INPUT
	IF RESULT == 0 && (TALENT:巨乳 || TALENT:爆乳)
		LOCAL:1 = 0
	ELSEIF RESULT == 1 || RESULT == 2
		LOCAL:1 = RESULT
	ELSE
		CLEARLINE 1
		REUSELASTLINE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	LOCAL:1 = RAND:3
ENDIF
;口上用
TFLAG:201 = LOCAL:1
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 60

SOURCE:液体追加 = 5000
SOURCE:欲情追加 = 1000
SOURCE:露出 = 500
SOURCE:屈従 = 500
SOURCE:逸脱 = 1000

IF LOCAL:1 == 1
	SOURCE:痛み = 500
	TIMES SOURCE:屈従 , 1.30
ENDIF

IF (TALENT:巨乳 || TALENT:爆乳) && LOCAL:1 == 0
	;ABL:Ｂ感覚をみる
	IF ABL:Ｂ感覚 == 0
		SOURCE:快Ｂ = 75
	ELSEIF ABL:Ｂ感覚 == 1
		SOURCE:快Ｂ = 150
	ELSEIF ABL:Ｂ感覚 == 2
		SOURCE:快Ｂ = 450
	ELSEIF ABL:Ｂ感覚 == 3
		SOURCE:快Ｂ = 900
	ELSEIF ABL:Ｂ感覚 == 4
		SOURCE:快Ｂ = 1700
	ELSE
		SOURCE:快Ｂ = 2600
	ENDIF
	
	;Ｂ敏感、鈍感をみる
	IF TALENT:Ｂ敏感
		TIMES SOURCE:快Ｂ , 1.50
	ELSEIF TALENT:Ｂ鈍感
		TIMES SOURCE:快Ｂ , 0.60
	ENDIF
ELSE
	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ = 100
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ = 250
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ = 500
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ = 1000
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ = 1800
	ELSE
		SOURCE:快Ｃ = 2900
	ENDIF
	
	;Ｃ敏感、鈍感をみる
	IF TALENT:Ｃ敏感
		TIMES SOURCE:快Ｃ , 1.50
	ELSEIF TALENT:Ｃ鈍感
		TIMES SOURCE:快Ｃ , 0.60
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:欲情追加, 0.30
	TIMES SOURCE:屈従, 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:欲情追加, 0.60
	TIMES SOURCE:屈従, 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:欲情追加, 1.00
	TIMES SOURCE:屈従, 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:欲情追加, 1.30
	TIMES SOURCE:屈従, 1.30
ELSE
	TIMES SOURCE:欲情追加, 1.60
	TIMES SOURCE:屈従, 1.60
ENDIF

;プライド高い
SIF TALENT:プライド高い
	SOURCE:屈従 /= 2
;保守的
SIF TALENT:保守的
	SOURCE:屈従 /= 3
;薬毒耐性
SIF TALENT:薬毒耐性
	SOURCE:欲情追加 /= 4

IF (TALENT:巨乳 == 0 && TALENT:爆乳 == 0) || LOCAL:1 > 0
	SIF PLAYER == MASTER
		TCVAR:Ｃ調律経験 += 1
	;奴隷のＶ⇔調教者の口の汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:口
	STAIN:PLAYER:口 |= STAIN:ヴァギナ
ENDIF

;酔いが回る(皮膚吸収……は無理矢理か?)
SOURCE:酒酔い += 100

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_424
IF (TALENT:巨乳 || TALENT:爆乳) && TFLAG:201 == 0
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に豊かな胸を
	PRINTFORML 抱き寄せるように命令すると、胸の窪みに酒を注ぎこんだ
	PRINTFORML 酒の刺激なのか%CALLNAME:TARGET%の顔は仄かに紅みを帯びている……
	PRINTL 
	PRINTL 
	PRINTFORM 十分酒が馴染んだ頃、%CALLNAME:PLAYER%は
	IF TALENT:母乳体質 && EX:10 && PALAM:潤滑 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の唾液と母乳が混ざった酒を
	ELSEIF TALENT:母乳体質 && EX:10
		PRINTFORML 母乳が混ざり、マイルドな味になった酒を
	ELSEIF PALAM:潤滑 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の唾液がたっぷり混ざった酒を
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両乳房で作られた窪みに注がれている酒を
	ENDIF
	IF NO:PLAYER == 1023 || NO:PLAYER == 1060
		PRINTFORML 待っていましたと言わんばかりに飲み干した。
		PRINTFORML そしてもったいないと言いながら%CALLNAME:TARGET%の乳房に付いている
		PRINTFORML 僅かな酒を根こそぎ舐めとった……
	ELSE
		PRINTFORML 一気に飲み干した。
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
	IF TFLAG:201 == 1
		PRINTFORML %CALLNAME:TARGET%を正座させ、両足を閉じさせると
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両足を閉じさせると
	ENDIF
	PRINTFORML 太腿と下腹部とで作られた窪みに酒を注ぎこんだ
	PRINTFORML 酒の刺激なのか%CALLNAME:TARGET%の顔は仄かに紅みを帯びている……
	PRINTL 
	PRINTL 
	PRINTFORM 十分酒が馴染んだ頃、%CALLNAME:PLAYER%は
	IF TEQUIP:Ｖ系装着器具 == 9 && PALAM:潤滑 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の愛液と芋の香りが混ざった酒を
	ELSEIF TEQUIP:Ｖ系装着器具 == 9
		PRINTFORML 芋のよい香りが移った酒を
	ELSEIF PALAM:潤滑 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の愛液がたっぷり混ざった酒を
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両足とデルタ地帯にできた窪みに注がれている酒を
	ENDIF
	IF NO:PLAYER == 1023 || NO:PLAYER == 1060
		PRINTFORML 待っていましたと言わんばかりに飲み干した。
		PRINTFORML そしてもったいないと言いながら%CALLNAME:TARGET%の太腿に残った
		PRINTFORML 僅かな酒をも逃すまいと舐め始めた……
	ELSE
		PRINTFORML 一気に飲み干した。
	ENDIF
ENDIF
