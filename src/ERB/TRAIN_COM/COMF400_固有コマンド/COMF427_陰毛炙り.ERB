;=============================================================================
;陰毛炙り
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE427
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:427 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;調教者がＥＸ化していない妹紅でないとダメ
SIF NO:PLAYER != 1031 || CFLAG:PLAYER:0
	RETURN 0
;羞恥プレイ系拡張コマンドを有効にしてないとダメ
SIF (FLAG:15 & 512) == 0
	RETURN 0
;[ふたなり]かつ調教者の技巧が3未満だとダメ
SIF TALENT:ふたなり && ABL:PLAYER:技巧 < 3
	RETURN 0
;[パイパン]だと不可、生えそろっていないと無理
SIF TALENT:パイパン || (CFLAG:36 && BASE:陰毛 < 250)
	RETURN 0
;テキストより[小柄体型]はデフォでツルツル。[小人体型]もそれに準じる
SIF (TALENT:小柄体型 || TALENT:小人体型) && CFLAG:36 == 0
	RETURN 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;オナホール・電極オナホール使用中はダメ
SIF TEQUIP:Ｃ系装着器具 == 2 || TEQUIP:Ｃ系装着器具 == 6
	RETURN 0
;亀甲縛りのときは無理
SIF TEQUIP:緊縛関連 == 5
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;裸エプロン、スク水プレイ、コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ
	RETURN 0
;時止め中は無理
SIF TEQUIP:時間止め
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 427) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM427
PRINTL 陰毛炙り

;戦闘判定
CALL COM_TRY_BATTLE, 427
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 陰毛炙り
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 55
DOWNBASE:1 += 160

LOCAL:1 = 2000
SOURCE:達成感 = 750
SOURCE:中毒充足 = 200
SOURCE:屈従 = 300
SOURCE:逸脱 = 1500
SOURCE:痛み = 800

;調教者のABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:痛み , 4.00
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:痛み , 2.00
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:痛み , 1.50
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:痛み , 1.20
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:痛み , 1.10
ELSE
	TIMES SOURCE:痛み , 1.00
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:露出癖 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:露出癖 == 2
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:露出癖 == 3
	TIMES LOCAL:1 , 1.50
ELSEIF ABL:露出癖 == 4
	TIMES LOCAL:1 , 2.00
ELSE
	TIMES LOCAL:1 , 2.60
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.40
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 1.70
ELSE
	TIMES LOCAL:1 , 2.00
ENDIF

SOURCE:中毒充足 += LOCAL:1
SOURCE:欲情追加 += (LOCAL:1 / 2)
SOURCE:露出 += LOCAL:1

;サドを見る
SIF TALENT:サド
	TIMES SOURCE:痛み , 2.00
;小柄体型、小人体型
SIF TALENT:小柄体型 || TALENT:小人体型
	TIMES SOURCE:露出 , 2.50

;炎が苦手そうな方々(チルノ、レティ、リグル、ミスティア、夜雀)
IF ABURARE_ITAIYO_BONUS(TARGET)
	TIMES SOURCE:痛み , 2.00
	SOURCE:トラウマ += 3000
ENDIF

IF TALENT:パイパン == 0
	PRINTL 
	PRINTFORML %CALLNAME:TARGET%は[%TALENTNAME:65%]になった
	TALENT:パイパン = 1
ENDIF
;毛は後から生えてこないようになる
CFLAG:36 = 1
;ツルツルになる
SIF BASE:陰毛
	BASE:陰毛 = 0

;露出快楽経験入手判定
TFLAG:100 |= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_427
PRINTFORML %CALLNAME:PLAYER%は指先に火を灯すと%CALLNAME:TARGET%の陰毛を炙りはじめた
IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORM %CALLNAME:TARGET%は
	;炎が苦手そうな方々(チルノ、レティ、リグル、ミスティア、夜雀)
	IF ABURARE_ITAIYO_BONUS(TARGET)
		PRINTL あまりの熱さに悲鳴をあげつつ、逃れようと必死になってもがいている…
	;痛みに弱い
	ELSEIF TALENT:痛みに弱い
		PRINTL 悲鳴をあげ、大粒の涙を目から大量にこぼしている…
	;痛みに強い
	ELSEIF TALENT:痛みに強い
		PRINTL 目を堅く閉じ、時々呻くような小さな声をあげている…
	;その他
	ELSE
		PRINTL 小さく悲鳴をあげている…
	ENDIF
ENDIF
