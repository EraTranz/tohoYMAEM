;=============================================================================
;アナル卒塔婆
;=============================================================================
;「貴女は少し反抗的すぎる」って言わせてみたかっただけでs
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE433
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:433 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;調教者が映姫様じゃないとダメ
SIF NO:PLAYER != 1036
	RETURN 0
;助手調教の場合、従順3以上じゃないとできない。
SIF ASSIPLAY && ABL:PLAYER:従順 < 4
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;小人体型または小柄体型なら従順が4以上かつマゾっ気が4以上必要
;また、Ａ経験が20未満かつＡ拡張経験が5未満だと実行できない
IF TALENT:小人体型 || TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURN 0
	SIF EXP:Ａ経験 < 20 && EXP:Ａ拡張経験 < 20
		RETURN 0
ENDIF
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURN 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;裸エプロン、スク水プレイ、コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 433) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM433
PRINTL アナル卒塔婆

;戦闘判定
CALL COM_TRY_BATTLE, 433
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = アナル卒塔婆

DOWNBASE:0 += 400
DOWNBASE:1 += 600

SOURCE:露出 = 1500
SOURCE:屈従 = 1500
SOURCE:逸脱 = 10000
SOURCE:痛み = 100

LOCAL:1 = 700
LOCAL:2 = (ABL:PLAYER:欲望 * 2) + (ABL:PLAYER:奉仕精神 * 3)
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	LOCAL:2 += (ABL:PLAYER:レズっ気 * 3) + (ABL:PLAYER:レズ中毒 * 2)
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	LOCAL:2 += (ABL:PLAYER:ＢＬっ気 * 3) + (ABL:PLAYER:ＢＬ中毒 * 2)
ENDIF
LOCAL:3 = 1 + RAND:10
;口上分岐用
TFLAG:99 = LOCAL:2 + LOCAL:3
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;MARK:反発刻印をみる
IF MARK:反発刻印 < 2
	TIMES LOCAL:1, 1.00
ELSEIF MARK:反発刻印 == 2
	TIMES LOCAL:1, 0.75
ELSE
	TIMES LOCAL:1, 0.50
ENDIF

;MARK:屈服刻印をみる
IF MARK:屈服刻印 > 2
	TIMES LOCAL:1, 1.25
ELSEIF MARK:屈服刻印 == 2
	TIMES LOCAL:1, 1.00
ELSEIF MARK:屈服刻印 == 1
	TIMES LOCAL:1, 0.75
ELSE
	TIMES LOCAL:1, 0.50
ENDIF

;MARK:苦痛刻印をみる
IF MARK:苦痛刻印 > 2
	TIMES LOCAL:1, 1.20
ELSEIF MARK:苦痛刻印 == 2
	TIMES LOCAL:1, 1.00
ELSEIF MARK:苦痛刻印 == 1
	TIMES LOCAL:1, 0.80
ELSE
	TIMES LOCAL:1, 0.60
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1, 0.50
	SOURCE:中毒充足 = 70
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1, 0.70
	SOURCE:中毒充足 = 150
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1, 1.00
	SOURCE:中毒充足 = 330
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1, 1.40
	SOURCE:中毒充足 = 650
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1, 1.90
	SOURCE:中毒充足 = 1400
ELSE
	TIMES LOCAL:1, 2.50
	SOURCE:中毒充足 = 2500
ENDIF

SOURCE:痛み += 100 * (LOCAL:2 + LOCAL:3)
SOURCE:恭順追加 = 100 * (LOCAL:2 + LOCAL:3)

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 100
	SOURCE:情愛 = 10
	SOURCE:屈従 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 230
	SOURCE:情愛 = 30
	SOURCE:屈従 += 300
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 500
	SOURCE:情愛 = 100
	SOURCE:屈従 += 600
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 900
	SOURCE:情愛 = 200
	SOURCE:屈従 += 1000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1500
	SOURCE:情愛 = 450
	SOURCE:屈従 += 1500
ELSE
	SOURCE:快Ａ = 2100
	SOURCE:情愛 = 750
	SOURCE:屈従 += 2100
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み += 25000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 16000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み += 9000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 5000
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 2700
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み += 1500
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 12000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 4000
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1600
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 500
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 120
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;プライド高い
IF TALENT:プライド高い
	TIMES LOCAL:1, 0.90
;プライド低い
ELSEIF TALENT:プライド低い
	TIMES LOCAL:1, 1.50
ENDIF
;自制心
SIF TALENT:自制心
	TIMES LOCAL:1, 0.30
;抵抗
SIF TALENT:抵抗
	TIMES LOCAL:1, 0.80
;弱味
SIF TALENT:弱味
	TIMES LOCAL:1, 2.50
;倒錯的
SIF TALENT:倒錯的
	TIMES LOCAL:1, 1.50
;中毒しやすい
SIF TALENT:中毒しやすい
	TIMES LOCAL:1, 1.50
;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

LOCAL:1 *= LOCAL:2
LOCAL:1 /= LOCAL:3

;[生意気]、[プライド高い]は反発
SIF TALENT:生意気 || TALENT:プライド高い
	SOURCE:反感追加 += 30000
;ゆかりん、[霊体]、[鬼]、[求聞持]、[死神]は反発低下
SIF NO:TARGET == 1022 || TALENT:霊体 || TALENT:鬼 || TALENT:求聞持 || TALENT:死神
	LOCAL:1 *= 3

IF LOCAL:1 > 0
	FOR PC_VAR1, 20, 23
		IF PALAM:PC_VAR1
			PRINTFORML %CALLNAME:TARGET%の%PALAMNAME:PC_VAR1%が下がった
			PALAM:PC_VAR1 = MAX(PALAM:PC_VAR1 - LOCAL:1, 0)
		ENDIF
	NEXT
ENDIF

TCVAR:Ａ経験 += 5
TCVAR:Ａ拡張経験 += 2

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 2
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ａ感覚 >= 3) ? 2 # 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_433
PRINTFORML %CALLNAME:PLAYER%は右手に持った卒塔婆を%CALLNAME:TARGET%のアナルに押し込んだ
PRINTFORML そして説教しつつ、左手に持った悔悟の棒でお尻を叩き始めた
PRINTFORML 説教中に{TFLAG:99}回叩かれ、%CALLNAME:TARGET%のお尻は真っ赤になっている
