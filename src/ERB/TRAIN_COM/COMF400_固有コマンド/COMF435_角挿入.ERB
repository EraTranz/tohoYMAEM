;=============================================================================
;角挿入
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE435
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:435 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者が助手でその助手がオトコではなくＶ経験Lvが0の場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1 && ASSIPLAY && TALENT:PLAYER:オトコ == 0
	RETURN 0
;調教者が[鬼]またはハクタク化している[半白澤]でないとダメ
IF TALENT:PLAYER:半白澤 == 0 && TALENT:PLAYER:鬼 == 0
	RETURN 0
ELSEIF TALENT:PLAYER:半白澤 == 1
	RETURN 0
ENDIF
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;対象が[処女]
IF TALENT:処女 == 1
	;対象の処女の判定
	SIF COM_ABLE_VIRGIN(TARGET, PLAYER) == 0
		RETURN 0
ELSE
	;対象の《潤滑》は大丈夫かチェック
	SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
		RETURN 0
ENDIF
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;褌着用中の場合はダメ
SIF TEQUIP:コスチュームプレイ == 17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 435) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM435
PRINTL 角挿入

;戦闘判定
CALL COM_TRY_BATTLE, 435
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 角挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 700
DOWNBASE:1 += 350

SOURCE:情愛 = 200
SOURCE:痛み = 2500
SOURCE:露出 = 1200
SOURCE:屈従 = 1800
SOURCE:逸脱 = 2500

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 1700
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 2000
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 2500
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 3000
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 3500
ELSE
	SOURCE:快Ｖ = 4000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 5000
	SIF ASSIPLAY && TALENT:PLAYER:オトコ == 0
		TCVAR:異常経験 += 2
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み += 600
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み += 300
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み += 30
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み += 3
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み += 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 900
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 250
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 10000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 300
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:反感追加 = 3000
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.50
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:反感追加 , 0.30
ENDIF

TCVAR:Ｖ経験 += 20
TCVAR:Ｖ拡張経験 += 1

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 4
;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ｖ感覚 >= 3) ? 2 # 1

RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_435
PRINTFORM %CALLNAME:PLAYER%は自分の角で\@(TALENT:小柄体型) ? 小柄な # \@%CALLNAME:TARGET%の
IF TALENT:処女 == 1
	PRINT 男を知らない
ELSEIF PALAM:潤滑 >= PALAMLV:4
	PRINT 濡れそぼった
ELSEIF PALAM:欲情 <= PALAMLV:2
	PRINT まだあまり欲情していない
ENDIF
IF TALENT:小柄体型
	PRINT 幼いワレメを
ELSE
	PRINT 秘裂を
ENDIF
PRINTL 貫いた…
