;=============================================================================
;薀蓄語り
;=============================================================================
;霖之助に好き放題語らせる。
;調教対象の場合、達成感が尋常じゃない量増える。ゲームモードがeratohoYMかABNORMALなら好感度も増える
;一方調教者だった場合、トラウマが素敵に増える
;ただし他の調教実行後は使用不可な上、使うと調教終了
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE445
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:445 > 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;対象、または調教者が霖之助でないとダメ
SIF NO:TARGET != 1069 && NO:PLAYER != 1069
	RETURN 0
;調教開始直後じゃないとダメ
SIF PREVCOM != -1
	RETURN 0
;対象だった場合、反発刻印があったらダメ
SIF NO:TARGET == 1069 && MARK:反発刻印 < 0
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 445) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM445
PRINTL 薀蓄語り

;戦闘判定
CALL COM_TRY_BATTLE, 445
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 薀蓄語り
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;霖之助を調教
IF NO:TARGET == 1069
	DOWNBASE:0 += 10
	DOWNBASE:1 += 10
	DOWNBASE:MASTER:1 += 333
	SIF ASSI >= 0
		DOWNBASE:ASSI:1 += 333
	SOURCE:達成感 = 80000
	SIF EQUIP:戦闘中
		SOURCE:達成感 /= 4
	SIF CHECK_GAMEMODE_ENDING_EXIST() == 1
		CFLAG:好感度 += 99

	;主人および存在すれば助手の受講回数(こっちは助手は逃げられない)
	CFLAG:MASTER:調教説教薀蓄受講数 += 1
	SIF ASSI >= 0 && EQUIP:ASSI:睡眠薬 == 0
		CFLAG:ASSI:調教説教薀蓄受講数 += 1
;霖之助が調教
ELSE
	DOWNBASE:0 += 50
	DOWNBASE:1 += 9999
	SOURCE:トラウマ = 6000
	SIF EQUIP:戦闘中
		SOURCE:トラウマ /= 3

	;ABL:従順をみる
	IF ABL:従順 == 0
		TIMES SOURCE:トラウマ , 1.00
	ELSEIF ABL:従順 == 1
		TIMES SOURCE:トラウマ , 0.50
	ELSEIF ABL:従順 == 2
		TIMES SOURCE:トラウマ , 0.40
	ELSEIF ABL:従順 == 3
		TIMES SOURCE:トラウマ , 0.30
	ELSEIF ABL:従順 == 4
		TIMES SOURCE:トラウマ , 0.20
	ELSE
		TIMES SOURCE:トラウマ , 0.10
	ENDIF

	;調教対象の受講回数(助手がいたとしても逃げるｗ)
	CFLAG:調教説教薀蓄受講数 += 1
ENDIF
TFLAG:107 = 999
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_445
IF NO:TARGET == 1069
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に向けて薀蓄を披露し始めた
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に対し、薀蓄をたらし始めた
ENDIF
