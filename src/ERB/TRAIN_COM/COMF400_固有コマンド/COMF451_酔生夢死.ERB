;=============================================================================
;酔生夢死
;=============================================================================
;こちらもお酒を飲ませるの丸パクリでございます
;各種酒を1本ずつ消耗することにより、阿呆のように酒を呑ませる
;効果は絶大だが実行させるまでもめんどくさい
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE451
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:451 > 0
	RETURN 0
;調教者奉仕・ウフフ系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 2048) == 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;調教者が天治でないとダメ
SIF NO:PLAYER != 2042
	RETURN 0
;天治の専用料理を3種とも見て、かつ奴隷に『ピッチャー二つ』を実行したことがないとダメ。もしくは対象が伊佐美かレミリアでないとダメ
SIF ((CFLAG:PLAYER:160 & 14) != 14 || (CFLAG:160 & 1) == 0) && NO:TARGET != 1010 && NO:TARGET != 2039
	RETURN 0
;各種酒を1つずつ持っていないとダメ
SIF ITEM:カルーア == 0 || ITEM:ビール == 0 || ITEM:ワイン == 0 || ITEM:大吟醸酒 == 0 || ITEM:スピリタス == 0 || ITEM:神便鬼毒酒 == 0
	RETURN 0
;主人に対する好感度が100以上必要
SIF CFLAG:好感度 < 100
	RETURN 0
;反発刻印が0でないとダメ
SIF MARK:反発刻印
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;猿轡付けていると無理
SIF TEQUIP:口装着器具
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURN 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURN 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 451) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM451
PRINTL 酔生夢死「空飛ぶ呑んべぇのレムリア」

;戦闘判定
CALL COM_TRY_BATTLE, 451
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 酔生夢死「空飛ぶ呑んべぇのレムリア」
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 150
DOWNBASE:1 += 250
SOURCE:酒酔い = 6000
SOURCE:鬼殺し = 3200

;すべての酒を1つずつ消費
ITEM:カルーア -= 1
ITEM:ビール -= 1
ITEM:ワイン -= 1
ITEM:大吟醸酒 -= 1
ITEM:スピリタス -= 1
ITEM:神便鬼毒酒 -= 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_451
PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の生み出した酒の弾幕に呑み込まれた
PRINTFORML %CALLNAME:TARGET%の気分は異常なまでに高揚している
