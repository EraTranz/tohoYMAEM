;=============================================================================
;触手召喚
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE500

RETURN (EXCOM_ABLE500() == 1) ? 1 # 0

;………………………………………………
;補助判定
;………………………………………………
@EXCOM_ABLE500
#FUNCTION
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:500 > 0
	RETURNF 0
;対象が触手娘かつ触手使役術を触手娘から取得していないかつ触手娘の触手使役Lvを適用していない場合はダメ
SIF NO:TARGET == 5001 && (FLAG:570 & 48) == 0
	RETURNF 0
;調教者が禁断の知識をもっていないとダメ
SIF TALENT:PLAYER:禁断の知識 == 0
	RETURNF 0
;調教者が触手使役術を持っていないとダメ
SIF TALENT:PLAYER:触手使役術 == 0
	RETURNF 0
;触手経験が0の場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:触手経験 == 0
	RETURNF 0
;触手服由来じゃない道具使用中はダメ
SIF TEQUIP:触手調教 == 0 && ((TEQUIP:Ｖ系装着器具 && !(TEQUIP:触手服 & 8)) || TEQUIP:クスコ || (TEQUIP:Ａ系装着器具 && !(TEQUIP:触手服 & 16)) || TEQUIP:氷柱 || (TEQUIP:Ｃ系装着器具 && !(TEQUIP:触手服 & 2)) || TEQUIP:コンドーム || (TEQUIP:Ｕ系装着器具 && !(TEQUIP:触手服 & 32)) || (TEQUIP:Ｂ系装着器具 && !(TEQUIP:触手服 & 4)) || TEQUIP:サラシ || INRANGE(TEQUIP:緊縛関連, 1, 7) || TEQUIP:目装着器具 || GROUPMATCH(TEQUIP:口装着器具, 1, 3) || GROUPMATCH(TEQUIP:鼻装着器具, 1, 5) || TEQUIP:耳装着器具 == 1)
	RETURNF 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURNF 0
;ビデオ鑑賞中はダメ
SIF TEQUIP:ビデオ鑑賞
	RETURNF 0
;野外プレイ中はダメ
SIF TEQUIP:野外プレイ
	RETURNF 0
;お風呂場プレイ中はダメ
SIF TEQUIP:お風呂場プレイ
	RETURNF 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURNF 0
;羞恥プレイ中はダメ
SIF TEQUIP:羞恥プレイ
	RETURNF 0
;お医者さんプレイ中はダメ
SIF TEQUIP:お医者さんプレイ
	RETURNF 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURNF 0
;助手がコスプレ中はダメ
SIF ASSI >= 0 && TEQUIP:ASSI:コスチュームプレイ > 1
	RETURNF 0
;裸エプロン、スク水プレイ、コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ
	RETURNF 0
;焦らしプレイ中はダメ
SIF TEQUIP:焦らしプレイ
	RETURNF 0
;射精止め中はダメ
SIF TEQUIP:射精止め
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 500) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM500
IF TEQUIP:触手調教
	PRINTL 触手召還

	;戦闘判定
	CALL COM_TRY_BATTLE, 500
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 触手召還
ELSE
	PRINTL 触手召喚

	;戦闘判定
	CALL COM_TRY_BATTLE, 500
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 触手召喚
ENDIF
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;触手経験フラグ
TFLAG:90 = 0
IF TEQUIP:触手調教
	TEQUIP:触手調教 = 0

	IF TEQUIP:触手膣内産卵 && TEQUIP:触手腸内産卵
		CALL DOUBLE_EGGPLAY_OUT
	ELSEIF TEQUIP:触手膣内産卵
		CALL EGGPLAY_OUT
	ELSEIF TEQUIP:触手腸内産卵
		CALL KANTYO_EGGPLAY_OUT
	ENDIF

	;触手挿入(バイブ他)
	SIF !(TEQUIP:触手服 & 8)
		TEQUIP:Ｖ系装着器具 = 0
	TEQUIP:クスコ = 0
	;アナル触手(アナルバイブ他)
	SIF !(TEQUIP:触手服 & 16)
		TEQUIP:Ａ系装着器具 = 0
	;触手クリ責め(クリキャップ他)
	SIF !(TEQUIP:触手服 & 2)
		TEQUIP:Ｃ系装着器具 = 0

	;触手カテーテル(カテーテル他)
	SIF !(TEQUIP:触手服 & 32)
		TEQUIP:Ｕ系装着器具 = 0
	;触手乳首責め(ニプルキャップ他)
	SIF !(TEQUIP:触手服 & 4)
		TEQUIP:Ｂ系装着器具 = 0
	;触手緊縛(縄)
	TEQUIP:緊縛関連 = 0
	;触手口辱(猿轡)
	SIF TEQUIP:口装着器具 == 2 || TEQUIP:口装着器具 == 4
		TEQUIP:口装着器具 = 0
	;触手鼻穴挿入(鼻フック)
	SIF TEQUIP:鼻装着器具 == 2
		TEQUIP:鼻装着器具 = 0
	;触手耳穴挿入(耳栓)
	SIF TEQUIP:耳装着器具 == 2
		TEQUIP:耳装着器具 = 0

	;触手強制回復
	SIF !(TEQUIP:触手服 & 64)
		TEQUIP:触手強制回復 = 0
	;触手催眠
	SIF !(TEQUIP:触手服 & 128)
		TEQUIP:触手催眠 = 0
	;触手膣内産卵
	TEQUIP:触手膣内産卵 = 0
	;触手腸内産卵
	TEQUIP:触手腸内産卵 = 0
	;触手チキチキ
	SIF !(TEQUIP:触手服 & 256)
		TEQUIP:触手チキチキ = 0

	;触手読心責め
	TEQUIP:読心 = 0
	;
	TEQUIP:精神制御系 = 0
	;キャラ触手汎用
	TEQUIP:キャラ触手 = 0

	;触手射精ゲージリセット
	TFLAG:997 = 0
	TFLAG:998 = 0
	RETURN 1
ENDIF
TEQUIP:触手調教 = 1

LOCAL:1 = 100
;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	TIMES LOCAL:1 , 2.50
ELSEIF EXP:触手経験 < EXPLV:2
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:触手経験 < EXPLV:3
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:触手経験 < EXPLV:4
	TIMES LOCAL:1 , 1.00
ELSEIF EXP:触手経験 < EXPLV:5
	TIMES LOCAL:1 , 0.80
ELSE
	TIMES LOCAL:1 , 0.60
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
ELSE
	TIMES LOCAL:1 , 0.50
ENDIF
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1 , 0.60
;寄生
SIF TALENT:寄生
	TIMES LOCAL:1 , 0.70
;懐卵
SIF TALENT:懐卵
	TIMES LOCAL:1 , 0.80
;触手インプラント
SIF CFLAG:41
	TIMES LOCAL:1 , 0.75

LOCAL:2 = LOCAL:1
TIMES LOCAL:2 , 0.75

DOWNBASE:0 += LOCAL:2
DOWNBASE:1 += LOCAL:2 * 2
SOURCE:トラウマ += LOCAL:1 * 10

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 2.00

SOURCE:逸脱 += LOCAL:1 * 5

;瞬間拘束
IF EQUIP:戦闘中 && !TEQUIP:緊縛関連
	STAIN:手 |= 2
	STAIN:手 |= 4
	STAIN:胸 |= 2
	STAIN:胸 |= 4
	TEQUIP:緊縛関連 = 9
ENDIF

;コマンド属性：触手
TFLAG:98 = 8
RETURN 1

;-------------------------------------------------
;触手調教中
;-------------------------------------------------
@EQUIP_COM500
PRINTL ＜触手調教中＞

LOCAL:1 = 100
;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:触手経験 < EXPLV:2
	TIMES LOCAL:1 , 2.50
ELSEIF EXP:触手経験 < EXPLV:3
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:触手経験 < EXPLV:4
	TIMES LOCAL:1 , 1.00
ELSEIF EXP:触手経験 < EXPLV:5
	TIMES LOCAL:1 , 0.80
ELSE
	TIMES LOCAL:1 , 0.60
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
ELSE
	TIMES LOCAL:1 , 0.50
ENDIF

;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1 , 0.60
;寄生
SIF TALENT:寄生
	TIMES LOCAL:1 , 0.70
;懐卵
SIF TALENT:懐卵
	TIMES LOCAL:1 , 0.80
;触手インプラント
SIF CFLAG:41
	TIMES LOCAL:1 , 0.75

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 * 2
SOURCE:トラウマ += LOCAL:1 * 10

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 2.00

SOURCE:液体追加 += 2000
SOURCE:触手汚染 += LOCAL:1 * 2
SOURCE:不潔 += LOCAL:1 * 10
SOURCE:逸脱 += LOCAL:1 * 5

TIMES SOURCE:快Ｃ , 2.00
TIMES SOURCE:快Ｖ , 2.00
TIMES SOURCE:快Ａ , 2.00
TIMES SOURCE:快Ｂ , 2.00
TIMES SOURCE:屈従 , 1.80

CALL ADD_SOURCE_27

;-------------------------------------------------
;触手射精チェック
;-------------------------------------------------
IF TFLAG:998 != 0
	LOCAL = 0
	;ABL:技巧をみる
	IF ABL:技巧 == 0
		LOCAL = 500
	ELSEIF ABL:技巧 == 1
		LOCAL = 600
	ELSEIF ABL:技巧 == 2
		LOCAL = 800
	ELSEIF ABL:技巧 == 3
		LOCAL = 1000
	ELSEIF ABL:技巧 == 4
		LOCAL = 1400
	ELSE
		LOCAL = 2000
	ENDIF

	;ABL:従順をみる
	IF ABL:従順 == 0
		TIMES LOCAL , 0.80
	ELSEIF ABL:従順 == 1
		TIMES LOCAL , 0.90
	ELSEIF ABL:従順 == 2
		TIMES LOCAL , 1.00
	ELSEIF ABL:従順 == 3
		TIMES LOCAL , 1.10
	ELSEIF ABL:従順 == 4
		TIMES LOCAL , 1.20
	ELSE
		TIMES LOCAL , 1.30
	ENDIF

	;PALAM:欲情をみる
	IF PALAM:欲情 < PALAMLV:1
		TIMES LOCAL , 1.00
	ELSEIF PALAM:欲情 < PALAMLV:2
		TIMES LOCAL , 1.10
	ELSEIF PALAM:欲情 < PALAMLV:3
		TIMES LOCAL , 1.20
	ELSEIF PALAM:欲情 < PALAMLV:4
		TIMES LOCAL , 1.30
	ELSEIF PALAM:欲情 < PALAMLV:5
		TIMES LOCAL , 1.40
	ELSE
		TIMES LOCAL , 1.50
	ENDIF

	;触手挿入他
	SIF TEQUIP:Ｖ系装着器具
		TIMES LOCAL , 1.50
	;触手貫通
	IF TEQUIP:Ａ系装着器具 == 3 || TEQUIP:Ａ系装着器具 == 23
		TIMES LOCAL , 1.20
	;触手浣腸
	ELSEIF TEQUIP:Ａ系装着器具 == 7 || TEQUIP:Ａ系装着器具 == 27
		TIMES LOCAL , 1.30
	;触手アナル挿入
	ELSEIF TEQUIP:Ａ系装着器具 == 1 || TEQUIP:Ａ系装着器具 == 21
		TIMES LOCAL , 1.50
	;触手小腸責め他
	ELSE
		TIMES LOCAL , 1.00
	ENDIF
	;触手ペニス責め
	IF TEQUIP:Ｃ系装着器具 == 2 || TEQUIP:Ｃ系装着器具 == 22
		TIMES LOCAL , 1.10
	;触手放電ペニス責め
	ELSEIF TEQUIP:Ｃ系装着器具 == 6 || TEQUIP:Ｃ系装着器具 == 26
		TIMES LOCAL , 1.50
	;触手クリ責め他
	ELSEIF TEQUIP:Ｃ系装着器具 && !GROUPMATCH(TEQUIP:Ｃ系装着器具, 4, 5)
		TIMES LOCAL , 1.20
	ENDIF
	;触手搾乳
	IF TEQUIP:Ｂ系装着器具 == 2 || TEQUIP:Ｂ系装着器具 == 22
		TIMES LOCAL , 1.30
	;触手乳首責め
	ELSEIF TEQUIP:Ｂ系装着器具 == 1 || TEQUIP:Ｂ系装着器具 == 21
		TIMES LOCAL , 1.20
	;触手乳腺責め他
	ELSE
		TIMES LOCAL , 1.00
	ENDIF
	;触手カテーテル
	SIF GROUPMATCH(TEQUIP:Ｕ系装着器具, 1, 2, 21, 22)
		TIMES LOCAL , 1.30
	;触手緊縛
	SIF TEQUIP:緊縛関連 && !GROUPMATCH(TEQUIP:緊縛関連, 8, 9)
		TIMES LOCAL , 1.20
	;触手口辱
	SIF TEQUIP:口装着器具 == 2 || TEQUIP:口装着器具 == 4
		TIMES LOCAL , 1.50
	;触手催眠
	SIF TEQUIP:触手催眠
		TIMES LOCAL , 1.10
	;触手[精液放出量強化]
	SIF FLAG:15 & 4096 && FLAG:75 & 16384
		TIMES LOCAL , 1.80

	TFLAG:997 += LOCAL
	SOURCE:触手汚染 += LOCAL / 100

	EJAC = TFLAG:998

	IF TFLAG:997 > EJAC * 4 && FLAG:15 & 4096 && FLAG:75 & 16384
		TFLAG:91 = 3
	ELSEIF TFLAG:997 > EJAC * 2
		TFLAG:91 = 2
	ELSEIF TFLAG:997 > EJAC
		TFLAG:91 = 1
	ELSE
		TFLAG:91 = 0
	ENDIF
	SETCOLOR COLOR("AQUA2")
	;超大量射精
	IF TFLAG:91 == 3
		TCVAR:精液経験 += 5
		IF TEQUIP:Ｖ系装着器具
			TCVAR:精液経験 += 5
			CFLAG:3666 += 5
		ENDIF
		SOURCE:触手汚染 += 1000
		PRINTL 触手超大量射精
		TFLAG:90 += 1

		TFLAG:997 -= EJAC*4
	;大量射精
	ELSEIF TFLAG:91 == 2
		TCVAR:精液経験 += 3
		IF TEQUIP:Ｖ系装着器具
			TCVAR:精液経験 += 3
			CFLAG:3666 +=3
		ENDIF
		SOURCE:触手汚染 += 500
		PRINTL 触手大量射精

		TFLAG:997 -= EJAC*2
	;通常の射精
	ELSEIF TFLAG:91 == 1
		TCVAR:精液経験 += 1
		IF TEQUIP:Ｖ系装着器具
			TCVAR:精液経験 += 1
			CFLAG:3666 +=1
		ENDIF
		SOURCE:触手汚染 += 100
		PRINTL 触手射精

		TFLAG:997 -= EJAC
	ENDIF
	SETCOLOR COLOR("P-RED")
	;触手が射精したフラグ
	TFLAG:37 = TFLAG:91
	CALL CALCULATE_LIMIT, TARGET
	;口に精液を飲んだフラグ
	SIF TEQUIP:口装着器具 == 2 || TEQUIP:口装着器具 == 4
		TFLAG:3 += TFLAG:91
	IF TFLAG:91
		TFLAG:90 += 1
		SIF TFLAG:997 >= EJAC
			TFLAG:997 = EJAC-1
	ENDIF
ENDIF

SIF EXP:触手経験 == 0
	TCVAR:異常経験 += 1

TFLAG:90 += 1
TCVAR:触手経験 += TFLAG:90
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_500
IF TEQUIP:触手調教
	PRINTFORML 触手を送り還し、%CALLNAME:TARGET%の体を解放した…
ELSE
	;[ALICE触手]を取得していると地の文が変化
	PRINTFORML %CALLNAME:PLAYER%の呼び出した\@(FLAG:97 & 16 && FLAG:98 & 16) ? 七色に彩られた不気味な # 異形の\@触手が、%CALLNAME:TARGET%の体を絡め取った…
ENDIF

;-------------------------------------------------
;触手調教における触手汚染のソース計算処理
;-------------------------------------------------
@ADD_SOURCE_27
LOCAL = 0

;触手インプラント(触手成長LVの限界拡張処理を流用)
SIF CFLAG:41
	LOCAL = PARASITIC_TENTACLE_INPLANT(LOCAL)

;そのターンで使用した触手コマンド
IF (SELECTCOM >= 500 && SELECTCOM < 510)
	LOCAL += 100
ELSEIF (SELECTCOM >= 510 && SELECTCOM < 600)
	LOCAL += 200
ENDIF

;触手避妊膜・触手排卵剤
SIF TEQUIP:排卵誘発剤 == 2 || TEQUIP:緊急避妊薬 == 2
	LOCAL += 50
;触手挿入・触手愛液採取
SIF TEQUIP:Ｖ系装着器具
	LOCAL += 100
;触手Ｇスポ責め・触手子宮責め
SIF GROUPMATCH(TEQUIP:Ｖ系装着器具, 2, 3, 22, 23)
	LOCAL += 100
;アナル触手・触手浣腸
SIF TEQUIP:Ａ系装着器具
	LOCAL += 100
;触手貫通
SIF TEQUIP:Ａ系装着器具 == 3 || TEQUIP:Ａ系装着器具 == 23
	LOCAL += 250
;触手クリ責め・触手ペニス責め・触手放電ペニス責め
SIF TEQUIP:Ｃ系装着器具
	LOCAL += 100
;触手カテーテル
SIF TEQUIP:Ｕ系装着器具
	LOCAL += 100
;触手乳首責め・触手搾乳
SIF TEQUIP:Ｂ系装着器具
	LOCAL += 100
;触手緊縛
SIF TEQUIP:緊縛関連
	LOCAL += 50
;触手口辱
SIF TEQUIP:口装着器具 == 2
	LOCAL += 50
;触手鼻穴挿入
SIF TEQUIP:鼻装着器具 == 2
	LOCAL += 50
;触手耳穴挿入
SIF TEQUIP:耳装着器具 == 2
	LOCAL += 50
;触手読心責め
SIF TEQUIP:読心 == 2
	LOCAL += 300
;触手強制回復
SIF TEQUIP:触手強制回復
	LOCAL += 300
;触手催眠
SIF TEQUIP:触手催眠
	LOCAL += 250
;触手膣内産卵
SIF TEQUIP:触手膣内産卵
	LOCAL += 200
;触手腸内産卵
SIF TEQUIP:触手腸内産卵
	LOCAL += 200
;触手チキチキ
SIF TEQUIP:触手チキチキ
	LOCAL += 350
;触手服
SIF TEQUIP:触手服
	LOCAL += 250

;触手読心責め
SIF TEQUIP:読心 == 2
	LOCAL += 400
;触手幽霊膣鏡
SIF TEQUIP:クスコ
	LOCAL += 400
;触手強制飲酒
SIF TEQUIP:口装着器具 == 4
	LOCAL += 400
;触手精神制御
SIF TEQUIP:精神制御系 == 2
	LOCAL += 400
;触手腸内撮影
SIF TEQUIP:Ａ系装着器具 == 4
	LOCAL += 400
;触手妖精按摩
SIF TEQUIP:Ｃ系装着器具 == 7
	LOCAL += 400
;触手氷精手淫
SIF TEQUIP:Ｃ系装着器具 == 8
	LOCAL += 400
;触手潮干狩り
SIF TEQUIP:キャラ触手 & 1
	LOCAL += 400
;触手厄落とし
SIF TEQUIP:キャラ触手 & 2
	LOCAL += 400
;触手言葉責め
SIF TEQUIP:キャラ触手 & 4
	LOCAL += 400
;触手鈴蘭媚薬
SIF TEQUIP:キャラ触手 & 8
	LOCAL += 400
;触手官能図書
SIF TEQUIP:キャラ触手 & 16
	LOCAL += 400
;触手淫脈探査
SIF TEQUIP:キャラ触手 & 32
	LOCAL += 400
;触手最終審判
SIF TEQUIP:キャラ触手 & 64
	LOCAL += 400
;触手百裂紅葉
SIF TEQUIP:キャラ触手 & 128
	LOCAL += 400
;触手豊穣淫香
SIF TEQUIP:キャラ触手 & 256
	LOCAL += 400

;寄生
SIF TALENT:寄生
	TIMES LOCAL , 1.50
;懐卵
SIF TALENT:懐卵
	TIMES LOCAL , 1.80

SOURCE:触手汚染 += LOCAL
