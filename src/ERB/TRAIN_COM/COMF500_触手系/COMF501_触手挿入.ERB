;=============================================================================
;触手挿入
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE501
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:501 > 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｖ系装着器具 == 1 || TEQUIP:Ｖ系装着器具 == 21) && TEQUIP:触手服 & 8
	RETURN 1
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 8) && (TEQUIP:Ｖ系装着器具 == 1 || TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 21 || TEQUIP:Ｖ系装着器具 == 22 || TEQUIP:Ｖ系装着器具 == 23)
	RETURN 0
;幽霊膣鏡挿入中は不可
SIF TEQUIP:クスコ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 501) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM501
PRINTL 触手挿入

;戦闘判定
CALL COM_TRY_BATTLE, 501
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

DOWNBASE:0 += 30
DOWNBASE:1 += 100

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 80
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1000
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1300
ELSE
	SOURCE:快Ｖ = 1700
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 5500
	SIF TEQUIP:触手調教
		TCVAR:異常経験 += 1
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 400
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.10
ELSE
	TIMES SOURCE:快Ｖ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.30
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;先にどんな責めを受けてたかで増えるＶ経験を決定
IF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	LOCAL = 2
ELSEIF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	LOCAL = 3
ELSE
	LOCAL = 1
ENDIF

;触手の着脱
IF TEQUIP:Ｖ系装着器具
	SIF TEQUIP:触手膣内産卵
		CALL EGGPLAY_OUT

	TEQUIP:Ｖ系装着器具 = 0
	
	SIF TEQUIP:触手服 & 8
		TEQUIP:触手服 &= 0b1110111
ELSE
	STAIN:ヴァギナ |= 2
	STAIN:ヴァギナ |= 4
	STAIN:ヴァギナ |= 64

	TEQUIP:Ｖ系装着器具 = 1
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 8))
		TEQUIP:触手服 |= 8
		TEQUIP:Ｖ系装着器具 = 21
	ENDIF
ENDIF

TCVAR:Ｖ経験 += LOCAL

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_501
IF TEQUIP:Ｖ系装着器具 == 0
	PRINTFORML 細かい突起に覆われた触手が%CALLNAME:TARGET%の性器に潜り込んでいった…
ELSEIF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	PRINTFORML %CALLNAME:TARGET%の膣内の最も敏感な所を撫でまわしていた触手が引き抜かれた…
ELSEIF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	PRINTFORML %CALLNAME:TARGET%の子宮をかき回していた触手が引き抜かれた…
ELSEIF (TEQUIP:Ｖ系装着器具 == 7 || TEQUIP:Ｖ系装着器具 == 27)
	PRINTFORML %CALLNAME:TARGET%の愛液を貪欲に採集していた触手が引き抜かれた…
ELSE
	PRINTFORML 触手が%CALLNAME:TARGET%の性器から引き抜かれた…
ENDIF
