;=============================================================================
;触手クリ責め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE503
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:503 > 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｃ系装着器具 == 1 || TEQUIP:Ｃ系装着器具 == 21) && TEQUIP:触手服 & 2
	RETURN 1
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;対象にペニスがあるならダメ
SIF EXIST_BAR(TARGET)
	RETURN 0
;他のＣ系触手責め中はダメ
SIF TEQUIP:Ｃ系装着器具 && TEQUIP:Ｃ系装着器具 != 1 && TEQUIP:Ｃ系装着器具 != 21
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 2) && (TEQUIP:Ｃ系装着器具 == 1 || TEQUIP:Ｃ系装着器具 == 21)
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 503) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM503
PRINTL 触手クリ責め

;戦闘判定
CALL COM_TRY_BATTLE, 503
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手クリ責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 10
DOWNBASE:1 += 80

SOURCE:露出 = 120
SOURCE:逸脱 = 70

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 200
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 400
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 900
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1600
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2400
ELSE
	SOURCE:快Ｃ = 3000
ENDIF

;クリ責め触手の着脱
IF TEQUIP:Ｃ系装着器具
	TEQUIP:Ｃ系装着器具 = 0
	
	SIF TEQUIP:触手服 & 2
		TEQUIP:触手服 &= 0b1111101
ELSE
	STAIN:ヴァギナ |= 2
	STAIN:ヴァギナ |= 4

	TEQUIP:Ｃ系装着器具 = 1
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 2))
		TEQUIP:Ｃ系装着器具 = 21
		TEQUIP:触手服 |= 2
	ENDIF
ENDIF

SIF PLAYER == MASTER && TEQUIP:Ｃ系装着器具 == 1
	TCVAR:Ｃ調律経験 += 1

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_503
IF TEQUIP:Ｃ系装着器具
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML 触手は%CALLNAME:TARGET%のクリ責めを中断した…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML 触手は%CALLNAME:TARGET%のクリ責めを中断した
			PRINTFORML %CALLNAME:TARGET%は、少々物足りなさそうな表情を見せた…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML 触手は%CALLNAME:TARGET%のクリ責めを中断した
			PRINTFORML %CALLNAME:TARGET%は、触手により汚された部分を厳しい表情で見つめている…
		ELSE
			PRINTFORML 触手は%CALLNAME:TARGET%のクリ責めを中断した…
		ENDIF
	ENDIF
ELSE
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML 細かく枝分かれした触手が%CALLNAME:TARGET%のクリトリスを責めはじめた…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML 細かく枝分かれした触手が%CALLNAME:TARGET%のクリトリスを責めはじめると、%CALLNAME:TARGET%はその刺激を全身で感じながら色っぽい\@(TEQUIP:口装着器具) ? 仕草で # 喘ぎ声を出して\@、%CALLNAME:PLAYER%を喜ばせた…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML 細かく枝分かれした触手が%CALLNAME:TARGET%のクリトリスを責めはじめた
			PRINTFORML %CALLNAME:TARGET%は、なんとか触手から逃れようと身体を一生懸命捩っている…
		ELSE
			PRINTFORML 細かく枝分かれした触手が%CALLNAME:TARGET%のクリトリスを責めはじめた…
		ENDIF
	ENDIF
ENDIF
