;=============================================================================
;触手乳首責め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE504
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:504 > 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｂ系装着器具 == 1 || TEQUIP:Ｂ系装着器具 == 21) && TEQUIP:触手服 & 4
	RETURN 1 
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;他のＢ系触手責め中は不可
SIF (TEQUIP:Ｂ系装着器具 && TEQUIP:Ｂ系装着器具 != 1 && TEQUIP:Ｂ系装着器具 != 21) || TEQUIP:サラシ
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 4) && (TEQUIP:Ｂ系装着器具 == 1 || TEQUIP:Ｂ系装着器具 == 21)
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 504) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM504
PRINTL 触手乳首責め

;戦闘判定
CALL COM_TRY_BATTLE, 504
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手乳首責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 70

SOURCE:露出 = 120
SOURCE:逸脱 = 70

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 100
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 300
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 800
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1500
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2300
ELSE
	SOURCE:快Ｂ = 2900
ENDIF

;母乳体質のときは、噴乳中毒を見る
IF TALENT:母乳体質
	IF ABL:噴乳中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:快Ｂ , 1.00
		TIMES SOURCE:逸脱 , 1.00
	ELSEIF ABL:噴乳中毒 == 1
		SOURCE:中毒充足 += 20
		TIMES SOURCE:快Ｂ , 1.05
		TIMES SOURCE:逸脱 , 1.10
	ELSEIF ABL:噴乳中毒 == 2
		SOURCE:中毒充足 += 80
		TIMES SOURCE:快Ｂ , 1.10
		TIMES SOURCE:逸脱 , 1.15
	ELSEIF ABL:噴乳中毒 == 3
		SOURCE:中毒充足 += 200
		TIMES SOURCE:快Ｂ , 1.15
		TIMES SOURCE:逸脱 , 1.20
	ELSEIF ABL:噴乳中毒 == 4
		SOURCE:中毒充足 += 500
		TIMES SOURCE:快Ｂ , 1.20
		TIMES SOURCE:逸脱 , 1.60
	ELSE
		SOURCE:中毒充足 += 1000
		TIMES SOURCE:快Ｂ , 1.40
		TIMES SOURCE:逸脱 , 2.00
	ENDIF
ENDIF

;Ｂ敏感・Ｂ鈍感
IF TALENT:Ｂ敏感
	TIMES SOURCE:快Ｂ , 1.50
ELSEIF TALENT:Ｂ鈍感
	TIMES SOURCE:快Ｂ , 0.60
ENDIF
;巨乳・爆乳
IF TALENT:巨乳 || TALENT:爆乳
	TIMES SOURCE:快Ｂ , 1.50
ENDIF
;調教者が［幼児］または［幼稚］または［幼児退行］
SIF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚 || TALENT:PLAYER:幼児退行
	TIMES SOURCE:快Ｂ , 1.20

;乳首責め触手の着脱
IF TEQUIP:Ｂ系装着器具
	TEQUIP:Ｂ系装着器具 = 0
	
	SIF TEQUIP:触手服 & 4
		TEQUIP:触手服 &= 0b1111011
ELSE
	STAIN:胸 |= 2
	STAIN:胸 |= 4
	STAIN:胸 |= 64

	TEQUIP:Ｂ系装着器具 = 1
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 4))
		TEQUIP:Ｂ系装着器具 = 21
		TEQUIP:触手服 |= 4
	ENDIF
ENDIF

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_504
IF TEQUIP:Ｂ系装着器具
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML 触手は%CALLNAME:TARGET%の乳首から離れた…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML 触手は%CALLNAME:TARGET%の乳首を解放した
			PRINTFORML %CALLNAME:TARGET%は、少々物足りなさそうな表情を見せた…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML 触手は%CALLNAME:TARGET%の乳首から離れた
			PRINTFORML %CALLNAME:TARGET%は、触手により汚された胸を厳しい表情で見つめている…
		ELSE
			PRINTFORML 触手は%CALLNAME:TARGET%の乳首から離れた…
		ENDIF
	ENDIF
ELSE
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML 細かく蠢く触手が%CALLNAME:TARGET%の乳首を弄り始めた…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML 細かく蠢く触手を%CALLNAME:TARGET%は自らの乳首へ導いた
			PRINTFORML 触手によるリズミカルな刺激を%CALLNAME:TARGET%は気持ちよさそうに堪能している…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML 細かく蠢く触手が%CALLNAME:TARGET%の乳首を弄り始めた
			PRINTFORML %CALLNAME:TARGET%は、なんとか触手から逃れようと身体を一生懸命捩っている…
		ELSE
			PRINTFORML 細かく蠢く触手が%CALLNAME:TARGET%の乳首を弄り始めた…
		ENDIF
	ENDIF
ENDIF
