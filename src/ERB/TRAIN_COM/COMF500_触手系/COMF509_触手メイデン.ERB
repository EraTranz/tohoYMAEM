;=============================================================================
;触手メイデン
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE509
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:509 > 0
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ(対象が[オトコ]の場合は除く)
SIF FLAG:2 & 1 && TALENT:オトコ == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;調教者は触手使役LV5必要
SIF ABL:PLAYER:触手使役 < 5
	RETURN 0
;触手召喚と共通
SIF EXCOM_ABLE500() == 0
	RETURN 0
;ボールギャグ、鼻フック、耳栓装着中はダメ
SIF TEQUIP:口装着器具 || TEQUIP:鼻装着器具 || TEQUIP:耳装着器具
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 509) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM509
PRINTL 触手メイデン

;戦闘判定
CALL COM_TRY_BATTLE, 509
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手メイデン
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

LOCAL:1 = 120
;触手経験をみる
SELECTCASE EXP:触手経験
	CASE IS < EXPLV:1
		TIMES LOCAL:1 , 3.00
	CASE IS < EXPLV:2
		TIMES LOCAL:1 , 2.50
	CASE IS < EXPLV:3
		TIMES LOCAL:1 , 2.00
	CASE IS < EXPLV:4
		TIMES LOCAL:1 , 1.00
	CASE IS < EXPLV:5
		TIMES LOCAL:1 , 0.80
	CASEELSE
		TIMES LOCAL:1 , 0.60
ENDSELECT

;触手中毒をみる
SELECTCASE ABL:触手中毒
	CASE 0
		TIMES LOCAL:1 , 1.00
	CASE 1
		TIMES LOCAL:1 , 0.90
	CASE 2
		TIMES LOCAL:1 , 0.80
	CASE 3
		TIMES LOCAL:1 , 0.70
	CASE 4
		TIMES LOCAL:1 , 0.60
	CASEELSE
		TIMES LOCAL:1 , 0.50
ENDSELECT
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1 , 0.60
;寄生
SIF TALENT:寄生
	TIMES LOCAL:1 , 0.70
;懐卵
SIF TALENT:懐卵
	TIMES LOCAL:1 , 0.80
;触手インプラント
SIF CFLAG:41
	TIMES LOCAL:1 , 0.75

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 * 2
SOURCE:トラウマ += LOCAL:1 * 20

;臆病
SIF TALENT:臆病
	TIMES LOCAL:1 , 2.00

SOURCE:逸脱 += LOCAL:1 * 10

;触手挿入
SIF TALENT:オトコ == 0
	TEQUIP:Ｖ系装着器具 = 1
;アナル触手
TEQUIP:Ａ系装着器具 = 1
;触手ペニス責め
IF EXIST_PENIS(TARGET)
	TEQUIP:Ｃ系装着器具 = 2
;触手クリ責め
ELSE
	TEQUIP:Ｃ系装着器具 = 1
ENDIF
;触手カテーテル
SIF TALENT:オトコ == 0 && FLAG:75 & 64
	TEQUIP:Ｕ系装着器具 = 1
;触手乳首責め
TEQUIP:Ｂ系装着器具 = 1
;触手緊縛
TEQUIP:緊縛関連 = 1
;触手口辱
TEQUIP:口装着器具 = 2
;触手鼻穴挿入
TEQUIP:鼻装着器具 = 2
;触手耳穴挿入
TEQUIP:耳装着器具 = 2

;コマンド属性：触手
TFLAG:98 = 8

TEQUIP:触手調教 = 1
;触手経験フラグ
TFLAG:90 = 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_509
PRINTFORML %CALLNAME:PLAYER%の呼び出した異形の触手が、%CALLNAME:TARGET%の体に殺到した…
PRINTFORM 触手は%CALLNAME:TARGET%の四肢を絡め捕ると高々と持ち上げ、
SIF TALENT:オトコ == 0
	PRINT ヴァギナ、
PRINT アヌス、
IF EXIST_PENIS(TARGET)
	PRINT ペニス、
ELSE
	PRINT クリトリス、
ENDIF
SIF TALENT:オトコ == 0 && FLAG:75 & 64
	PRINT 尿道、
PRINTL 乳首、口内、鼻や耳…
PRINTL まるで穴という穴を犯し尽くすかのごとく凄惨な責めを始めた……
