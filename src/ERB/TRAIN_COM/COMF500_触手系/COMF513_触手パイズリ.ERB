;=============================================================================
;触手パイズリ
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE513
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:513 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;[絶壁]だと不可
SIF TALENT:絶壁
	RETURN 0
;[爆乳]なら技巧LV1以上、[巨乳]でなければ技巧3以上、[貧乳]だと技巧4以上必要
SIF (TALENT:爆乳 && ABL:技巧 == 0) || (TALENT:巨乳 && ABL:技巧 < 2) || (TALENT:貧乳 == 0 && TALENT:巨乳 == 0 && ABL:技巧 < 3) || (TALENT:貧乳 && ABL:技巧 < 4)
	RETURN 0
;触手緊縛中は不可
SIF TEQUIP:緊縛関連
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 513) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM513
PRINTL 触手パイズリ

;戦闘判定
CALL COM_TRY_BATTLE, 513
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手パイズリ
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 30
DOWNBASE:1 += 180

SOURCE:屈従 = 1000
SOURCE:逸脱 = 1500

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 180
	SOURCE:達成感 = 100
	SOURCE:不潔 = 1200
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 270
	SOURCE:達成感 = 160
	SOURCE:不潔 = 800
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 360
	SOURCE:達成感 = 240
	SOURCE:不潔 = 400
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 450
	SOURCE:達成感 = 380
	SOURCE:不潔 = 200
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 540
	SOURCE:達成感 = 600
	SOURCE:不潔 = 100
ELSE
	SOURCE:性行動 = 600
	SOURCE:達成感 = 850
	SOURCE:不潔 = 10
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL:1 = 50
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:1 = 100
ELSEIF ABL:Ｂ感覚== 2
	LOCAL:1 = 250
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:1 = 500
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:1 = 800
ELSE
	LOCAL:1 = 1000
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
	TIMES SOURCE:不潔 , 2.00
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 1.20
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 1.50
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 1.80
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
	TIMES SOURCE:不潔 , 0.50
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 2.00
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
	TIMES SOURCE:不潔 , 0.10
ELSE
	TIMES LOCAL:1 , 2.40
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
	TIMES SOURCE:不潔 , 0.00
ENDIF

;Ｂ敏感・鈍感
IF TALENT:Ｂ敏感
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:1 , 0.70
ENDIF
;巨乳・爆乳
IF TALENT:巨乳
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:爆乳
	TIMES LOCAL:1 , 1.50
ENDIF
;淫乳
SIF TALENT:淫乳
	TIMES LOCAL:1 , 1.20

SOURCE:快Ｂ += LOCAL:1
SOURCE:支配 += LOCAL:1 /2

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
;調教対象のパイズリ実行回数
CFLAG:パイズリした回数 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_513
PRINTFORM 左右から近づいた触手は%CALLNAME:TARGET%の
IF TALENT:貧乳
	PRINT 薄い
ELSEIF TALENT:巨乳
	PRINT 豊満な
ELSEIF TALENT:爆乳
	PRINT 豊満すぎる程の
ELSE
	PRINT 形の良い
ENDIF
PRINTL 胸を中央へと抱き寄せ、乳房で出来たトンネルを別の触手が下から上へと潜り抜けピストン運動を始めた…
