;=============================================================================
;触手放電ペニス責め
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE515
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:515 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｃ系装着器具 == 6 || TEQUIP:Ｃ系装着器具 == 26) && TEQUIP:触手服 & 2
	RETURN 1
;調教対象にペニスがないとダメ
SIF EXIST_BAR(TARGET) == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;他のＣ系触手責め中はダメ
SIF TEQUIP:Ｃ系装着器具 && TEQUIP:Ｃ系装着器具 != 6 && TEQUIP:Ｃ系装着器具 != 26
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 2) && (TEQUIP:Ｃ系装着器具 == 6 || TEQUIP:Ｃ系装着器具 == 26)
	RETURN 0
;調教対象がオトコのとき触手尿道責め中やコンドーム装着中はダメ
SIF TALENT:オトコ && (TEQUIP:コンドーム || TEQUIP:Ｕ系装着器具)
	RETURN 0
;触手が[放電触手]を得ていないとダメ
SIF (FLAG:75 & 4) == 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 515) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM515
PRINTL 触手放電ペニス責め

;戦闘判定
CALL COM_TRY_BATTLE, 515
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手放電ペニス責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 160

SOURCE:露出 = 120
SOURCE:逸脱 = 470

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 500
	SOURCE:痛み = 1000
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 1000
	SOURCE:痛み = 1000
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 2250
	SOURCE:痛み = 1000
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 4000
	SOURCE:痛み = 1200
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 6000
	SOURCE:痛み = 1500
ELSE
	SOURCE:快Ｃ = 7500
	SOURCE:痛み = 1800
ENDIF

;放電ペニス責め触手の着脱
IF TEQUIP:Ｃ系装着器具
	TEQUIP:Ｃ系装着器具 = 0
	
	SIF TEQUIP:触手服 & 2
		TEQUIP:触手服 &= 0b1111101
ELSE
	STAIN:ヴァギナ |= 2
	STAIN:ヴァギナ |= 4

	TEQUIP:Ｃ系装着器具 = 6
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 2))
		TEQUIP:Ｃ系装着器具 = 26
		TEQUIP:触手服 |= 2
	ENDIF
ENDIF

SIF PLAYER == MASTER && (TEQUIP:Ｃ系装着器具 == 6 || TEQUIP:Ｃ系装着器具 == 26)
	TCVAR:Ｃ調律経験 += (TALENT:オトコ) ? 1 # 2

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_515
IF TEQUIP:Ｃ系装着器具
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML 触手は放電をやめ、%CALLNAME:TARGET%のペニスを解放した…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML 触手は放電をやめ、%CALLNAME:TARGET%のペニスを解放した
			PRINTFORML %CALLNAME:TARGET%は、刺激の余韻に浸りつつも少々物足りなさそうだった…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML 触手は放電をやめ、%CALLNAME:TARGET%のペニスを解放した
			PRINTFORML %CALLNAME:TARGET%は、触手により汚されたペニスを厳しい表情で見つめている…
		ELSE
			PRINTFORML 触手は放電をやめ、%CALLNAME:TARGET%のペニスを解放した…
		ENDIF
	ENDIF
ELSE
	IF EQUIP:睡眠薬 || TFLAG:899 > 0
		PRINTFORML いやらしく肉ヒダをうねらせた触手が%CALLNAME:TARGET%のペニスに巻き付き放電しつつしごき始めた…
	ELSE
		IF ABL:触手中毒 > 2
			PRINTFORML いやらしく肉ヒダをうねらせた触手が%CALLNAME:TARGET%のペニスに巻き付き放電しつつしごき始めた
			PRINTFORML %CALLNAME:TARGET%は、その強烈な刺激に全身で悦びを表現している…
		ELSEIF MARK:反発刻印 > 1
			PRINTFORML いやらしく肉ヒダをうねらせた触手が%CALLNAME:TARGET%のペニスに巻き付き放電しつつしごき始めた
			PRINTFORML %CALLNAME:TARGET%は、なんとか刺激を堪えようとするも振動と電撃に屈するかのごとく\@(TEQUIP:口装着器具) ? 身体をくねらせ # 喘ぎ声を出し始め\@た…
		ELSE
			PRINTFORML いやらしく肉ヒダをうねらせた触手が%CALLNAME:TARGET%のペニスに巻き付き放電しつつしごき始めた…
		ENDIF
	ENDIF
ENDIF