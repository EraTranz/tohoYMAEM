;=============================================================================
;触手Ｇスポ責め
;=============================================================================
;触手挿入からのコンボ的扱い
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE520
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:520 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;解除はいつでも可能＜意味がない事がわかったのでとりあえず保留
;SIF TEQUIP:Ｖ系装着器具 == 2 && TEQUIP:触手服 & 8
;	RETURN 1
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 8) && (TEQUIP:Ｖ系装着器具 == 1 || TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 21 || TEQUIP:Ｖ系装着器具 == 22 || TEQUIP:Ｖ系装着器具 == 23)
	RETURN 0
;触手挿入中じゃないとダメ
SIF TEQUIP:Ｖ系装着器具 == 0
	RETURN 0
;幽霊膣鏡挿入中は不可
SIF TEQUIP:クスコ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 520) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM520
PRINTL 触手Ｇスポ責め

;戦闘判定
CALL COM_TRY_BATTLE, 520
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手Ｇスポ責め
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;先にどんな責めを受けてたかで減る体力を決定
IF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	LOCAL:1 = 50
ELSEIF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	LOCAL:1 = 40
ELSE
	LOCAL:1 = 25
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 2.00
	LOCAL:2 = 0
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 1.50
	LOCAL:2 = 50
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 1.20
	LOCAL:2 = 120
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 1.00
	LOCAL:2 = 350
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 900
ELSE
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 2000
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 *3

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL:3 = 1

SOURCE:中毒充足 = 200
SOURCE:液体追加 = 500
SOURCE:逸脱 = 2000

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 150
	SOURCE:屈従 = 100
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 200
	SOURCE:屈従 = 180
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
	SOURCE:屈従 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 2000
	SOURCE:屈従 = 380
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 2600
	SOURCE:屈従 = 500
ELSE
	SOURCE:快Ｖ = 3400
	SOURCE:屈従 = 800
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 300
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱, 5.00
		SOURCE:反感追加 = 2000
	ELSE
		TIMES SOURCE:屈従 , 0.60
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		SOURCE:反感追加 = 300
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:反感追加 = 3000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:中毒充足 , 0.90
	TIMES SOURCE:液体追加 , 0.90
	TIMES SOURCE:反感追加 , 1.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:液体追加 , 1.00
	TIMES SOURCE:反感追加 , 1.50
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:中毒充足 , 1.10
	TIMES SOURCE:液体追加 , 1.10
	TIMES SOURCE:反感追加 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:中毒充足 , 1.20
	TIMES SOURCE:液体追加 , 1.20
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:中毒充足 , 1.30
	TIMES SOURCE:液体追加 , 1.30
	TIMES SOURCE:反感追加 , 0.30
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

SOURCE:性行動 = LOCAL:2 /2
SOURCE:不潔 = LOCAL:1 *150
SOURCE:支配 = LOCAL:2 /4

;先にどんな責めを受けてたかで増えるＶ経験を決定
IF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	LOCAL:3 += 1
ELSEIF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	LOCAL:3 += 2
	TEQUIP:Ｖ系装着器具 = 2

	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 8))
		TEQUIP:Ｖ系装着器具 = 22
	ELSEIF !TEQUIP:触手調教 && TEQUIP:触手服 && SELECTCOM == 520
		TEQUIP:Ｖ系装着器具 = 22
	ENDIF
ELSE
	TEQUIP:Ｖ系装着器具 = 2

	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 8))
		TEQUIP:Ｖ系装着器具 = 22
	ELSEIF !TEQUIP:触手調教 && TEQUIP:触手服 && SELECTCOM == 520
		TEQUIP:Ｖ系装着器具 = 22
	ENDIF
ENDIF

TCVAR:Ｖ経験 += LOCAL:3

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;触手挿入中(Ｇスポ責め)
;--------------------------------------------------
@EQUIP_COM520
PRINTL ＜触手Ｇスポ責め中＞
CALL DISPLAY_EQUIP_MESSAGE_COM, 520

IF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	LOCAL:4 = 25
ELSEIF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	LOCAL:4 = 20
ELSE
	LOCAL:4 = 12
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:4 , 2.00
	LOCAL:3 = 0
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:4 , 1.50
	LOCAL:3 = 25
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:4 , 1.20
	LOCAL:3 = 60
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:4 , 1.00
	LOCAL:3 = 180
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:4 , 0.80
	LOCAL:3 = 450
ELSE
	TIMES LOCAL:4 , 0.60
	LOCAL:3 = 1000
ENDIF

DOWNBASE:0 += LOCAL:4
DOWNBASE:1 += LOCAL:4 *3

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	LOCAL = 40
ELSEIF ABL:Ｖ感覚 == 1
	LOCAL = 120
ELSEIF ABL:Ｖ感覚 == 2
	LOCAL = 300
ELSEIF ABL:Ｖ感覚 == 3
	LOCAL = 500
ELSEIF ABL:Ｖ感覚 == 4
	LOCAL = 650
ELSE
	LOCAL = 850
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.60
	LOCAL:1 = 150
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 1.00
	LOCAL:1 = 20
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 1.20
	LOCAL:1 = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 1.40
	LOCAL:1 = 0
ELSE
	TIMES LOCAL , 1.60
	LOCAL:1 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES LOCAL , 0.10
	LOCAL:1 += 400
	TIMES LOCAL:1 , 3.00
	LOCAL:2 += 1000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.40
	LOCAL:1 += 150
	TIMES LOCAL:1 , 1.00
	LOCAL:2 += 200
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 0.50
	LOCAL:2 += 80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES LOCAL , 1.40
	TIMES LOCAL:1 , 0.20
ELSE
	TIMES LOCAL , 1.80
	TIMES LOCAL:1 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL , 0.80
ELSEIF ABL:従順 == 1
	TIMES LOCAL , 0.90
ELSEIF ABL:従順 == 2
	TIMES LOCAL , 1.00
ELSEIF ABL:従順 == 3
	TIMES LOCAL , 1.10
ELSEIF ABL:従順 == 4
	TIMES LOCAL , 1.20
ELSE
	TIMES LOCAL , 1.30
ENDIF

;貞操観念(処女はありえない)
SIF TALENT:貞操観念
	TIMES LOCAL:1, 3.00
;小柄体型
SIF TALENT:小柄体型
	TIMES LOCAL:1 , 2.00

SOURCE:快Ｖ += LOCAL/2
SOURCE:性行動 += LOCAL/5
SOURCE:痛み += LOCAL:1
SOURCE:不潔 += LOCAL:3 *50
SOURCE:逸脱 += LOCAL:2 /2
SOURCE:反感追加 += LOCAL:2

TCVAR:Ｖ経験 += 2
;触手経験フラグ
TFLAG:90 += 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_520
IF (TEQUIP:Ｖ系装着器具 == 2 || TEQUIP:Ｖ系装着器具 == 22)
	PRINTFORML 触手は引き続き%CALLNAME:TARGET%の膣内の最も敏感な部分に対して緩急の付いた責めを展開した
ELSEIF (TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23)
	PRINTFORML 一旦%CALLNAME:TARGET%の子宮に潜り込んでいた触手は突如、子宮口を抉るように逆流し
	PRINTFORML 次の瞬間%CALLNAME:TARGET%の膣の天蓋を突起でひっかくように撫でまわし始めた
ELSE
	PRINTFORML %CALLNAME:TARGET%の性器に潜り込んでいた触手は突如、%CALLNAME:TARGET%の膣内の天井を責め立て始めた
ENDIF

;--------------------------------------------------
;装着時メッセージ
;--------------------------------------------------
@SYSTXT_EQUIP_MESSAGE_COM_520
PRINTFORML 触手が膣壁を撫でまわしているのか、ときおり%CALLNAME:TARGET%の下腹部がぽこっと動いている…
