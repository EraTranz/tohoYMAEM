;=============================================================================
;触手浣腸
;=============================================================================
;TFLAG:99 = 口上分岐用(0=触手浣腸)
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE522
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:522 > 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ａ系装着器具 == 7 || TEQUIP:Ａ系装着器具 == 27) && TEQUIP:触手服 & 16
	RETURN 1
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;他のＡ系触手責め中は不可
SIF TEQUIP:Ａ系装着器具 && TEQUIP:Ａ系装着器具 != 7 && TEQUIP:Ａ系装着器具 != 27
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 16) && (TEQUIP:Ａ系装着器具 == 7 || TEQUIP:Ａ系装着器具 == 27)
	RETURN 0
;卵が邪魔で浣腸できない
SIF TEQUIP:触手腸内産卵
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 522) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM522
PRINTL 触手浣腸

;戦闘判定
CALL COM_TRY_BATTLE, 522
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手浣腸
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 60 
DOWNBASE:1 += 150

SOURCE:中毒充足 += 600

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 80
	SOURCE:屈従 = 300
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 250
	SOURCE:屈従 = 800
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 600
	SOURCE:屈従 = 1400
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:屈従 = 1800
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1300
	SOURCE:屈従 = 2100
ELSE
	SOURCE:快Ａ = 1700
	SOURCE:屈従 = 2400
ENDIF

;ABL:排泄中毒をみる
IF ABL:排泄中毒 == 0
	SOURCE:痛み = 2000
	SOURCE:屈従 += 200
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 2000
ELSEIF ABL:排泄中毒 == 1
	SOURCE:痛み = 1600
	SOURCE:屈従 += 500
	SOURCE:不潔 += 2000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 1000
ELSEIF ABL:排泄中毒 == 2
	SOURCE:痛み = 1200
	SOURCE:屈従 += 800
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 500
ELSEIF ABL:排泄中毒 == 3
	SOURCE:痛み = 800
	SOURCE:屈従 += 1200
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 100
ELSEIF ABL:排泄中毒 == 4
	SOURCE:痛み = 600
	SOURCE:屈従 += 1500
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 0
ELSE
	SOURCE:痛み = 400
	SOURCE:屈従 += 2000
	SOURCE:不潔 += 1000
	SOURCE:逸脱 += 1000
	SOURCE:反感追加 += 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 800
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 500
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 300
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 120
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.10
ELSE
	TIMES SOURCE:快Ａ , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ａ , 0.80
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ａ , 0.90
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ａ , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ａ , 1.10
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ａ , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.30
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;浣腸触手の着脱
IF (TEQUIP:Ａ系装着器具 == 7 || TEQUIP:Ａ系装着器具 == 27)
	TEQUIP:Ａ系装着器具 = 0
	IF TFLAG:172 > 20
		LOCAL = 5
	ELSEIF TFLAG:172 > 10
		LOCAL = 3
	ELSEIF TFLAG:172 > 5
		LOCAL = 2
	ELSE
		LOCAL = 1
	ENDIF
	
	SIF TEQUIP:触手服 & 16
		TEQUIP:触手服 &= 0b1101111
ELSE
	TEQUIP:Ａ系装着器具 = 7
	IF TEQUIP:触手調教
		TEQUIP:Ａ系装着器具 = 7
		STAIN:アナル |= 2
		STAIN:アナル |= 4
		STAIN:アナル |= 64
	ENDIF
	LOCAL = 0
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 16))
		TEQUIP:Ａ系装着器具 = 27
		TEQUIP:触手服 |= 16
	ENDIF
ENDIF

TCVAR:Ａ経験 += 5
IF LOCAL > 0
	TCVAR:調教排泄経験 += LOCAL
	IF (CFLAG:20 & 2) == 0
		TCVAR:異常経験 += 1
		CFLAG:20 |= 2
	ENDIF
	TFLAG:172 = 0
	TFLAG:140 |= 256
ENDIF

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_522
IF (TEQUIP:Ａ系装着器具 == 7 || TEQUIP:Ａ系装着器具 == 27)
	PRINTFORML 肛門から触手が引き抜かれると、%CALLNAME:TARGET%は腸内の汚物を肛門から撒き散らしてしまった
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF ABL:マゾっ気 == 0
			PRINTFORML %CALLNAME:TARGET%は苦悶と恥辱の表情を浮かべている
		ELSEIF ABL:マゾっ気 == 1
			PRINTFORML %CALLNAME:TARGET%は脂汗を浮かべ、いまだ下腹に残る排泄感に堪えている
		ELSEIF ABL:マゾっ気 == 2
			PRINTFORML %CALLNAME:TARGET%は恥ずかしがりながらも、開放感にほっとした表情を浮かべている
		ELSEIF ABL:マゾっ気 == 3
			PRINTFORML %CALLNAME:TARGET%は、肛門を通り過ぎる排泄物の感覚に時折艶っぽい喘ぎ声を上げている
		ELSEIF ABL:マゾっ気 == 4
			PRINTFORML %CALLNAME:TARGET%は、うっとりした表情を浮かべ、排泄感を愉しんでいる
		ELSE
			PRINTFORML %CALLNAME:TARGET%は、排泄感と恥辱を心ゆくまで味わっている
			PRINTFORML %CALLNAME:TARGET%の表情は快感にだらしなく緩み、肛門から汚物をひりだすと同時に、膣口からも愛液をボタボタと滴らせている
		ENDIF
	ENDIF
ELSE
	PRINTFORML 極太の触手が%CALLNAME:TARGET%の菊穴をふさぎ、体液を流し込んだ
ENDIF
