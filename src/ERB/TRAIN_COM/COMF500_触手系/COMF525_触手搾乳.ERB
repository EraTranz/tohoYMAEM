;=============================================================================
;触手搾乳
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE525
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:525 > 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｂ系装着器具 == 2 || TEQUIP:Ｂ系装着器具 == 22) && TEQUIP:触手服 & 4
	RETURN 1
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;他のＢ系触手責め中は不可
SIF TEQUIP:Ｂ系装着器具 && TEQUIP:Ｂ系装着器具 != 2 && TEQUIP:Ｂ系装着器具 != 22
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 4) && (TEQUIP:Ｂ系装着器具 == 2 || TEQUIP:Ｂ系装着器具 == 22)
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 525) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM525
PRINTL 触手搾乳

;戦闘判定
CALL COM_TRY_BATTLE, 525
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手搾乳
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 50
DOWNBASE:1 += 100

SOURCE:露出 = 100
SOURCE:性行動 = 100
SOURCE:達成感 = 100
SOURCE:恭順追加 = 100
SOURCE:痛み = 100
SOURCE:屈従 = 100
SOURCE:中毒充足 = 100
SOURCE:逸脱 = 200

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL:1 = 200
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL:1 = 400
ELSEIF ABL:Ｂ感覚 == 2
	LOCAL:1 = 900
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL:1 = 1600
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL:1 = 2400
ELSE
	LOCAL:1 = 3000
ENDIF

;ABL:噴乳中毒をみる
IF ABL:噴乳中毒 == 0
	SOURCE:中毒充足 += 0
	TIMES LOCAL:1 , 1.00
	TIMES SOURCE:逸脱 , 1.00
ELSEIF ABL:噴乳中毒 == 1
	SOURCE:中毒充足 += 60
	TIMES LOCAL:1 , 1.10
	TIMES SOURCE:逸脱 , 1.10
ELSEIF ABL:噴乳中毒 == 2
	SOURCE:中毒充足 += 120
	TIMES LOCAL:1 , 1.15
	TIMES SOURCE:逸脱 , 1.25
ELSEIF ABL:噴乳中毒 == 3
	SOURCE:中毒充足 += 240
	TIMES LOCAL:1 , 1.25
	TIMES SOURCE:逸脱 , 1.50
ELSEIF ABL:噴乳中毒 == 4
	SOURCE:中毒充足 += 480
	TIMES LOCAL:1 , 1.30
	TIMES SOURCE:逸脱 , 1.75
ELSE
	SOURCE:中毒充足 += 960
	TIMES LOCAL:1 , 1.50
	TIMES SOURCE:逸脱 , 2.00
ENDIF

;淫乳
SIF TALENT:淫乳
	TIMES LOCAL:1 , 1.20

;Ｂ鈍感とＢ敏感
IF TALENT:Ｂ敏感
	TIMES LOCAL:1 , 1.50
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL:1 , 0.60
ENDIF

;巨乳と爆乳、貧乳と絶壁
IF TALENT:絶壁
	TIMES SOURCE:痛み , 1.75
ELSEIF TALENT:貧乳
	TIMES SOURCE:痛み , 1.50
ELSEIF TALENT:巨乳
	TIMES LOCAL:1 , 1.50
ELSEIF TALENT:爆乳
	TIMES LOCAL:1 , 1.75
ENDIF

SOURCE:快Ｂ += LOCAL:1

;搾乳触手の着脱
IF (TEQUIP:Ｂ系装着器具 == 2 || TEQUIP:Ｂ系装着器具 == 22)
	TEQUIP:Ｂ系装着器具 = 0
	
	SIF TEQUIP:触手服 & 4
		TEQUIP:触手服 &= 0b1111011
ELSE
	STAIN:胸 |= 2
	STAIN:胸 |= 4
	STAIN:胸 |= 64

	TEQUIP:Ｂ系装着器具 = 2
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 4))
		TEQUIP:Ｂ系装着器具 = 22
		TEQUIP:触手服 |= 4
	ENDIF
ENDIF

;コマンド属性：触手
TFLAG:98 = 8

;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_525
IF TEQUIP:Ｂ系装着器具 == 2 || TEQUIP:Ｂ系装着器具 == 22
	PRINTFORML 触手は%CALLNAME:TARGET%の乳首を解放した
ELSE
	PRINTFORML 吸盤状の触手が%CALLNAME:TARGET%の乳首を吸いあげはじめた
ENDIF

;-------------------------------------------------
;触手搾乳処理
;-------------------------------------------------
@SYOKUSYU_MILK
;［Ｂ感度］３以上、［母乳体質］無しかつ［小柄体型］と［貧乳］と［絶壁］無しかつ［オトコ］無し
IF ABL:Ｂ感覚 >= 3 && TALENT:母乳体質 == 0 && (TALENT:小柄体型 == 0 || (TALENT:絶壁 == 0 && TALENT:貧乳 == 0)) && TALENT:オトコ == 0
	PRINTFORML 触手に乳を吸われるうちに%CALLNAME:TARGET%は母乳が出るようになった。
	TALENT:母乳体質 = 1
	CALL CALC_GAUGE3_SLAVE, TARGET, 1
ENDIF
