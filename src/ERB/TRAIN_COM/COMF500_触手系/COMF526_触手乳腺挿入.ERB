;=============================================================================
;触手乳腺挿入
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE526
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:526 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;解除はいつでも可能
SIF (TEQUIP:Ｂ系装着器具 == 3 || TEQUIP:Ｂ系装着器具 == 23) && TEQUIP:触手服 & 4
	RETURN 1 
;一度も触手乳腺挿入をしたことが無い場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && (CFLAG:24 & 2) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとはダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[極細触手]を得ていないとダメ
SIF (FLAG:75 & 16) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;他のＢ系触手責め中は不可
SIF TEQUIP:Ｂ系装着器具 && TEQUIP:Ｂ系装着器具 != 3 && TEQUIP:Ｂ系装着器具 != 23
	RETURN 0
;責め始めた記憶がないのにすでに責められてる場合はダメ
SIF !TEQUIP:触手調教 && !(TEQUIP:触手服 & 4) && (TEQUIP:Ｂ系装着器具 == 3 || TEQUIP:Ｂ系装着器具 == 23)
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 526) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM526
PRINTL 触手乳腺挿入

;戦闘判定
CALL COM_TRY_BATTLE, 526
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手乳腺挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

IF (TEQUIP:Ｂ系装着器具 == 3 || TEQUIP:Ｂ系装着器具 == 23)
	DOWNBASE:0 += (TALENT:母乳体質) ? 225 # 450
	DOWNBASE:1 += (TALENT:母乳体質) ? 1250 # 2500
ELSE
	DOWNBASE:0 += (TALENT:母乳体質) ? 250 # 500
	DOWNBASE:1 += (TALENT:母乳体質) ? 1500 # 3000
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 200
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1600
ELSE
	SOURCE:快Ｂ = 2800
ENDIF

;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	SOURCE:痛み = 10000
	SOURCE:不潔 = 10000
	SOURCE:逸脱 = 10000
ELSEIF EXP:触手経験 < EXPLV:2
	SOURCE:痛み = 5000
	SOURCE:不潔 = 6500
	SOURCE:逸脱 = 8000
ELSEIF EXP:触手経験 < EXPLV:3
	SOURCE:痛み = 3000
	SOURCE:不潔 = 4500
	SOURCE:逸脱 = 6000
ELSEIF EXP:触手経験 < EXPLV:4
	SOURCE:痛み = 2000
	SOURCE:不潔 = 3000
	SOURCE:逸脱 = 4000
	TIMES DOWNBASE:0 , 0.90
ELSEIF EXP:触手経験 < EXPLV:5
	SOURCE:痛み = 1000
	SOURCE:不潔 = 1500
	SOURCE:逸脱 = 2000
	TIMES DOWNBASE:0 , 0.70
ELSE
	SOURCE:痛み = 500
	SOURCE:不潔 = 750
	SOURCE:逸脱 = 1000
	TIMES DOWNBASE:0 , 0.50
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:欲情追加 = 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
	TIMES SOURCE:不潔 , 2.00
	TIMES SOURCE:逸脱 , 4.00
ELSEIF ABL:触手中毒 == 1
	SOURCE:欲情追加 = 60
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
	TIMES SOURCE:不潔 , 1.50
	TIMES SOURCE:逸脱 , 1.80
	TIMES DOWNBASE:1 , 0.90
ELSEIF ABL:触手中毒 == 2
	SOURCE:欲情追加 = 180
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
	TIMES SOURCE:不潔 , 1.00
	TIMES SOURCE:逸脱 , 1.00
	TIMES DOWNBASE:1 , 0.80
ELSEIF ABL:触手中毒 == 3
	SOURCE:欲情追加 = 360
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:逸脱 , 0.70
	TIMES DOWNBASE:1 , 0.70
ELSEIF ABL:触手中毒 == 4
	SOURCE:欲情追加 = 640
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
	TIMES SOURCE:不潔 , 0.10
	TIMES SOURCE:逸脱 , 0.20
	TIMES DOWNBASE:1 , 0.60
ELSE
	SOURCE:欲情追加 = 900
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
	TIMES SOURCE:不潔 , 0.00
	TIMES SOURCE:逸脱 , 0.10
	TIMES DOWNBASE:1 , 0.50
ENDIF

;Ｂ敏感・鈍感
IF TALENT:Ｂ敏感
	TIMES SOURCE:快Ｂ , 1.20
ELSEIF TALENT:Ｂ鈍感
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;絶壁・貧乳・巨乳・爆乳
IF TALENT:絶壁
	TIMES SOURCE:痛み , 1.75
ELSEIF TALENT:貧乳
	TIMES SOURCE:痛み , 1.50
ELSEIF TALENT:巨乳
	TIMES SOURCE:快Ｂ , 1.50
ELSEIF TALENT:爆乳
	TIMES SOURCE:快Ｂ , 1.20
ENDIF
;淫乳
SIF TALENT:淫乳
	TIMES SOURCE:快Ｂ , 2.50

IF (TEQUIP:Ｂ系装着器具 == 3 || TEQUIP:Ｂ系装着器具 == 23)
	SIF TALENT:母乳体質
		STAIN:胸 |= 16
	TEQUIP:Ｂ系装着器具 = 0
	;触手経験フラグ
	TFLAG:90 = 0
	
	SIF TEQUIP:触手服 & 4
		TEQUIP:触手服 &= 0b1111011
ELSE
	IF TALENT:母乳体質 == 0
		IF FLAG:75 & 1024 && PALAM:欲情 > PALAMLV:5
			SOURCE:トラウマ += 3000
			TALENT:母乳体質 = 1
			CALL CALC_GAUGE3_SLAVE, TARGET, 1
		ENDIF
	ELSE
		TIMES SOURCE:痛み , 0.60
	ENDIF
	;初体験のとき異常経験+1
	IF (CFLAG:24 & 2) == 0
		TCVAR:異常経験 += 1
		CFLAG:24 |= 2
	ENDIF
	TCVAR:乳腺開発経験 += 1
	TEQUIP:Ｂ系装着器具 = 3
	
	;触手服への命令として飛んできた場合
	IF !TEQUIP:触手調教 && (TEQUIP:触手服 && !(TEQUIP:触手服 & 4))
		TEQUIP:Ｂ系装着器具 = 23
		TEQUIP:触手服 |= 4
	ENDIF
	;触手経験フラグ
	TFLAG:90 = 1
ENDIF

;コマンド属性：触手
TFLAG:98 = 8
RETURN 1

;--------------------------------------------------
;触手乳腺挿入中
;--------------------------------------------------
@EQUIP_COM526
PRINTL ＜触手乳腺挿入中＞

DOWNBASE:0 += 75
DOWNBASE:1 += 75

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	LOCAL = 20
ELSEIF ABL:Ｂ感覚 == 1
	LOCAL = 60
ELSEIF ABL:Ｂ感覚 == 2
	LOCAL = 100
ELSEIF ABL:Ｂ感覚 == 3
	LOCAL = 250
ELSEIF ABL:Ｂ感覚 == 4
	LOCAL = 400
ELSE
	LOCAL = 600
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL , 0.80
	SOURCE:中毒充足 += 10
	SOURCE:逸脱 += 2500
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL , 0.90
	SOURCE:中毒充足 += 40
	SOURCE:逸脱 += 1500
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL , 1.00
	SOURCE:中毒充足 += 100
	SOURCE:屈従 += 10
	SOURCE:逸脱 += 500
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL , 1.10
	SOURCE:中毒充足 += 150
	SOURCE:屈従 += 40
	SOURCE:逸脱 += 150
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL , 1.20
	SOURCE:中毒充足 += 300
	SOURCE:屈従 += 70
	SOURCE:逸脱 += 10
ELSE
	TIMES LOCAL , 1.30
	SOURCE:中毒充足 += 450
	SOURCE:屈従 += 100
ENDIF

;EXP:触手経験をみる
IF EXP:触手経験 <= EXPLV:0
	TIMES LOCAL , 0.80
	SOURCE:痛み += 500
ELSEIF EXP:触手経験 <= EXPLV:1
	TIMES LOCAL , 1.00
	SOURCE:痛み += 100
ELSEIF EXP:触手経験 <= EXPLV:2
	TIMES LOCAL , 1.30
	SOURCE:痛み += 50
ELSEIF EXP:触手経験 <= EXPLV:3
	TIMES LOCAL , 1.60
	SOURCE:痛み += 10
ELSEIF EXP:触手経験 <= EXPLV:4
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 2.25
ENDIF

;EXP:墳乳経験をみる
IF EXP:噴乳経験 <= EXPLV:0
	TIMES LOCAL , 0.80
ELSEIF EXP:噴乳経験 <= EXPLV:1
	TIMES LOCAL , 1.00
ELSEIF EXP:噴乳経験 <= EXPLV:2
	TIMES LOCAL , 1.30
ELSEIF EXP:噴乳経験 <= EXPLV:3
	TIMES LOCAL , 1.60
ELSEIF EXP:噴乳経験 <= EXPLV:4
	TIMES LOCAL , 2.00
ELSE
	TIMES LOCAL , 2.25
ENDIF

;Ｂ鈍感とＢ敏感
IF TALENT:Ｂ敏感
	TIMES LOCAL , 1.40
ELSEIF TALENT:Ｂ鈍感
	TIMES LOCAL , 0.60
ENDIF

;巨乳と爆乳、貧乳と絶壁
IF TALENT:爆乳
	TIMES LOCAL , 1.20
ELSEIF TALENT:巨乳
	TIMES LOCAL , 1.50
ELSEIF TALENT:貧乳
	TIMES SOURCE:痛み , 1.50
ELSEIF TALENT:絶壁
	TIMES SOURCE:痛み , 1.75
ENDIF
SOURCE:快Ｂ += LOCAL

;乳腺開発経験
TCVAR:乳腺開発経験 += 1

STAIN:胸 |= 64
;触手経験フラグ
TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_526
IF (TEQUIP:Ｂ系装着器具 == 3 || TEQUIP:Ｂ系装着器具 == 23)
	PRINTFORML 極細の触手が%CALLNAME:TARGET%の乳腺から引き抜かれた
	IF TALENT:母乳体質
		PRINTFORML 解放された%CALLNAME:TARGET%の乳腺から一筋の母乳が垂れ出てきた
	ELSE
		PRINTFORML 解放された%CALLNAME:TARGET%の乳首がぴくんと動いた
	ENDIF
ELSE
	PRINTFORM 極細の触手が%CALLNAME:TARGET%の
	IF TALENT:絶壁
		PRINT 辛うじてふっくらとしていると分かる胸に
	ELSEIF TALENT:貧乳
		PRINT 小さめの胸に
	ELSEIF TALENT:巨乳
		PRINT 豊かな胸に
	ELSEIF TALENT:爆乳
		PRINT 豊満すぎる胸に
	ELSE
		PRINT 形が良い胸に
	ENDIF
	PRINTL 近づくと、双丘の先端から乳腺の中へと侵入を試みた…
	IF TALENT:母乳体質 == 0
		IF FLAG:75 & 1024 && PALAM:欲情 > PALAMLV:5
			PRINTFORML 触手から分泌されているホルモンの作用なのか%CALLNAME:TARGET%の乳腺から母乳が垂れ出るようになった
		ELSE
			PRINTFORML 乳房内の異物感に%CALLNAME:TARGET%の額に脂汗が浮かんでいる
		ENDIF
	ELSE
		PRINTFORML 母乳によって侵入が容易になっているのか、%CALLNAME:TARGET%の乳腺は触手を受け入れているように見える…
	ENDIF
ENDIF
