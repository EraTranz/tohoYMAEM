;=============================================================================
;触手耳穴挿入
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE529
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:529 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;調教者は触手使役LV1以上必要
SIF ABL:PLAYER:触手使役 < 1
	RETURN 0
;他の触手耳穴責め中はダメ
SIF TEQUIP:耳装着器具 && TEQUIP:耳装着器具 != 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 529) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM529
PRINTL 触手耳穴挿入

;戦闘判定
CALL COM_TRY_BATTLE, 529
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手耳穴挿入
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 60

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:屈従 = 50
	SOURCE:中毒充足 = 100
	SOURCE:不潔 = 3000
ELSEIF ABL:触手中毒 == 1
	SOURCE:屈従 = 100
	SOURCE:中毒充足 = 200
	SOURCE:不潔 = 1000
ELSEIF ABL:触手中毒 == 2
	SOURCE:屈従 = 250
	SOURCE:中毒充足 = 400
	SOURCE:不潔 = 200
ELSEIF ABL:触手中毒 == 3
	SOURCE:屈従 = 500
	SOURCE:中毒充足 = 800
	SOURCE:不潔 = 50
ELSEIF ABL:触手中毒 == 4
	SOURCE:屈従 = 1000
	SOURCE:中毒充足 = 1200
	SOURCE:不潔 = 0
ELSE
	SOURCE:屈従 = 1750
	SOURCE:中毒充足 = 2000
	SOURCE:不潔 = 0
ENDIF

;臆病
SIF TALENT:臆病
	SOURCE:トラウマ = 250 * (1 + MARK:恐怖刻印)

;触手の抜き挿し
IF TEQUIP:耳装着器具
	TEQUIP:耳装着器具 = 0
ELSE
	TEQUIP:耳装着器具 = 2
ENDIF
;触手経験フラグ
TFLAG:90 = 0

;コマンド属性：触手
TFLAG:98 = 8
RETURN 1

;-------------------------------------------------
;触手耳穴挿入中
;-------------------------------------------------
@EQUIP_COM529
PRINTL ＜触手耳穴挿入中＞

DOWNBASE:0 += 10
DOWNBASE:1 += 30

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:屈従 += 25
	SOURCE:中毒充足 += 40
	SOURCE:不潔 += 1500
ELSEIF ABL:触手中毒 == 1
	SOURCE:屈従 += 50
	SOURCE:中毒充足 += 80
	SOURCE:不潔 += 500
ELSEIF ABL:触手中毒 == 2
	SOURCE:屈従 += 100
	SOURCE:中毒充足 += 160
	SOURCE:不潔 += 100
ELSEIF ABL:触手中毒 == 3
	SOURCE:屈従 += 250
	SOURCE:中毒充足 += 300
	SOURCE:不潔 += 50
ELSEIF ABL:触手中毒 == 4
	SOURCE:屈従 += 500
	SOURCE:中毒充足 += 500
ELSE
	SOURCE:屈従 += 900
	SOURCE:中毒充足 += 800
ENDIF

;臆病
SIF TALENT:臆病
	SOURCE:トラウマ += 75 * (1 + MARK:恐怖刻印)
;触手経験フラグ
TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_529
IF TEQUIP:耳装着器具
	PRINTFORML 触手は%CALLNAME:TARGET%の耳穴から引き抜かれた
ELSE
	PRINTFORML 細長い触手が%CALLNAME:TARGET%の耳の穴に侵入した
ENDIF
