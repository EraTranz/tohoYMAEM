;=============================================================================
;触手ヴァギナ拡張
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE530
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:530 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;Ｖ経験レベルが0かつ調教者がオトコではない場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1 && TALENT:PLAYER:オトコ == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;触手が[ゴーヤ型触手]を得ていないとダメ
SIF (FLAG:75 & 128) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 1) == 0
	RETURN 0
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURN 0
ENDIF
;一定のＶ経験が必要
SIF EXP:Ｖ経験 < 150
	RETURN 0
;Ｖ系触手責め中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURN 0
;卵が邪魔で拡張できない
SIF TEQUIP:触手膣内産卵
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 530) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM530
PRINTL 触手ヴァギナ拡張

;戦闘判定
CALL COM_TRY_BATTLE, 530
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手ヴァギナ拡張
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 250
DOWNBASE:1 += 150

SOURCE:痛み = 1500
SOURCE:露出 = 1000
SOURCE:屈従 = 1000
SOURCE:逸脱 = 4200
SOURCE:支配 = 1000

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 2000
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 2500
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 3000
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 3300
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 3600
ELSE
	SOURCE:快Ｖ = 3800
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 20000
	SOURCE:欲情追加 += 2000
	SIF TALENT:PLAYER:オトコ == 0
		TCVAR:異常経験 += 1
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み += 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み += 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み += 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み += 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み += 0
ENDIF

;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	TIMES DOWNBASE:0 , 3.00
	TIMES DOWNBASE:1 , 3.00
	SOURCE:痛み += 1000
ELSEIF EXP:触手経験 < EXPLV:2
	TIMES DOWNBASE:0 , 2.50
	TIMES DOWNBASE:1 , 2.50
	SOURCE:痛み += 800
ELSEIF EXP:触手経験 < EXPLV:3
	TIMES DOWNBASE:0 , 2.00
	TIMES DOWNBASE:1 , 2.00
	SOURCE:痛み += 500
ELSEIF EXP:触手経験 < EXPLV:4
	TIMES DOWNBASE:0 , 1.00
	TIMES DOWNBASE:1 , 1.00
	SOURCE:痛み += 100
ELSEIF EXP:触手経験 < EXPLV:5
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:痛み += 50
ELSE
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:中毒充足 = 1500
	TIMES SOURCE:支配 , 0.50
ELSEIF ABL:触手中毒 == 1
	TIMES DOWNBASE:0 , 0.90
	TIMES DOWNBASE:1 , 0.90
	SOURCE:中毒充足 = 1700
	TIMES SOURCE:支配 , 0.80
ELSEIF ABL:触手中毒 == 2
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:中毒充足 = 2000
ELSEIF ABL:触手中毒 == 3
	TIMES DOWNBASE:0 , 0.70
	TIMES DOWNBASE:1 , 0.70
	SOURCE:中毒充足 = 2500
	TIMES SOURCE:支配 , 1.20
ELSEIF ABL:触手中毒 == 4
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
	SOURCE:中毒充足 = 3300
	TIMES SOURCE:支配 , 1.50
ELSE
	TIMES DOWNBASE:0 , 0.50
	TIMES DOWNBASE:1 , 0.50
	SOURCE:中毒充足 = 4200
	TIMES SOURCE:支配 , 1.80
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;汚れ処理
STAIN:ヴァギナ |= 2
STAIN:ヴァギナ |= 64

SIF EXP:Ｖ拡張経験 == 0
	TCVAR:異常経験 += 1
TCVAR:Ｖ経験 += 25
TCVAR:Ｖ拡張経験 += 1

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_530
PRINTFORML %CALLNAME:TARGET%の膣はゴーヤの様な極太の触手でかき回された。
IF ABL:Ｖ感覚 <= 1
	PRINTFORML 触手が引き抜かれた後も%CALLNAME:TARGET%の膣は痙攣しているかのようにヒクついている
ELSEIF ABL:Ｖ感覚 == 2
	PRINTFORML 触手が抜かれた後も%CALLNAME:TARGET%の膣はぽっかり口を開いている
ELSEIF ABL:Ｖ感覚 == 3
	PRINTFORML 触手が抜かれた後も%CALLNAME:TARGET%の膣は拡がったままなかなか戻らない
ELSEIF ABL:Ｖ感覚 == 4
	PRINTFORML 触手が抜かれた後の%CALLNAME:TARGET%の性器からは肉ビラがはみ出し、ぽっかり開いた膣口のおくには子宮口が見えている。
ELSEIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML %CALLNAME:TARGET%は大きく拡がった自分の膣口に目を向けたまま、恍惚とした表情を浮かべている。
ENDIF
