;=============================================================================
;触手アナル拡張
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE531
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:531 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;触手が[ゴーヤ型触手]を得ていないとダメ
SIF (FLAG:75 & 128) == 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 1) == 0
	RETURN 0
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURN 0
ENDIF
;一定のＡ経験が必要
SIF EXP:Ａ経験 < 150
	RETURN 0
;Ａ系触手責め中は不可
SIF TEQUIP:Ａ系装着器具
	RETURN 0
;卵が邪魔で拡張できない
SIF TEQUIP:触手腸内産卵
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 531) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM531
PRINTL 触手アナル拡張

;戦闘判定
CALL COM_TRY_BATTLE, 531
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手アナル拡張
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 240
DOWNBASE:1 += 130

SOURCE:痛み = 1200
SOURCE:露出 = 1200
SOURCE:屈従 = 1200
SOURCE:逸脱 = 2200
SOURCE:支配 = 1100

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 = 10
	SOURCE:屈従 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 30
	SOURCE:情愛 = 30
	SOURCE:屈従 += 700
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 500
	SOURCE:情愛 = 100
	SOURCE:屈従 += 1500
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:情愛 = 200
	SOURCE:屈従 += 3000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1700
	SOURCE:情愛 = 450
	SOURCE:屈従 += 5000
ELSE
	SOURCE:快Ａ = 2200
	SOURCE:情愛 = 750
	SOURCE:屈従 += 8000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み += 20000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 12000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み += 5000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1800
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 1000
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み += 600
ENDIF

;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	TIMES DOWNBASE:0 , 3.00
	TIMES DOWNBASE:1 , 3.00
	SOURCE:痛み += 1000
ELSEIF EXP:触手経験 < EXPLV:2
	TIMES DOWNBASE:0 , 2.50
	TIMES DOWNBASE:1 , 2.50
	SOURCE:痛み += 800
ELSEIF EXP:触手経験 < EXPLV:3
	TIMES DOWNBASE:0 , 2.00
	TIMES DOWNBASE:1 , 2.00
	SOURCE:痛み += 500
ELSEIF EXP:触手経験 < EXPLV:4
	TIMES DOWNBASE:0 , 1.00
	TIMES DOWNBASE:1 , 1.00
	SOURCE:痛み += 100
ELSEIF EXP:触手経験 < EXPLV:5
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:痛み += 50
ELSE
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:中毒充足 = 1500
	TIMES SOURCE:支配 , 0.50
ELSEIF ABL:触手中毒 == 1
	TIMES DOWNBASE:0 , 0.90
	TIMES DOWNBASE:1 , 0.90
	SOURCE:中毒充足 = 1700
	TIMES SOURCE:支配 , 0.80
ELSEIF ABL:触手中毒 == 2
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:中毒充足 = 2000
ELSEIF ABL:触手中毒 == 3
	TIMES DOWNBASE:0 , 0.70
	TIMES DOWNBASE:1 , 0.70
	SOURCE:中毒充足 = 2500
	TIMES SOURCE:支配 , 1.20
ELSEIF ABL:触手中毒 == 4
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
	SOURCE:中毒充足 = 3300
	TIMES SOURCE:支配 , 1.50
ELSE
	TIMES DOWNBASE:0 , 0.50
	TIMES DOWNBASE:1 , 0.50
	SOURCE:中毒充足 = 4200
	TIMES SOURCE:支配 , 1.80
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:痛み += 10000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	SOURCE:痛み += 3600
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 1200
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 200
ELSE
	TIMES SOURCE:快Ａ , 1.80
	SOURCE:痛み += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	SOURCE:屈従 /= 3

;汚れ処理
STAIN:アナル |= 2
STAIN:アナル |= 64

SIF EXP:Ａ拡張経験 == 0
	TCVAR:異常経験 += 1
TCVAR:Ａ経験 += 25
TCVAR:Ａ拡張経験 += 1

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_531
PRINTFORML %CALLNAME:TARGET%の肛門はゴーヤの様な極太の触手でかき回された。
IF ABL:Ａ感覚 <= 1
	PRINTFORML 触手が引き抜かれた後も%CALLNAME:TARGET%の肛門は痙攣しているかのようにヒクついている
ELSEIF ABL:Ａ感覚 == 2
	PRINTFORML 触手が抜かれた後も%CALLNAME:TARGET%の肛門はぽっかり口を開いている
ELSEIF ABL:Ａ感覚 == 3
	PRINTFORML 触手が抜かれた後も%CALLNAME:TARGET%の肛門は拡がったままなかなか戻らない
ELSEIF ABL:Ａ感覚 == 4
	PRINTFORML 触手が抜かれた後の%CALLNAME:TARGET%の肛門からは肉ビラがはみ出し、腸の中が丸見えになっている。
ELSEIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML %CALLNAME:TARGET%は大きく拡がった自分の肛門に目を向けたまま、恍惚とした表情を浮かべている。
ENDIF
