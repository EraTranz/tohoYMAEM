;=============================================================================
;触手両穴拡張
;=============================================================================
;聞こえるか?こちらは新世代触手。
;あまり時間が無いのでよく聞いてくれ。
;奴をイかせるためには君の助けが必要だ。
;我々は別々の穴から同時に挿入し、
;2箇所の性感帯で互いに1回ずつ絶頂させなければならない。
;無論、どちらか片方だけでは駄目だ。2人とも成功しない限り、我々に未来は無い。
;……選択の余地はなさそうだな。
;よし、行くぞ！
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE532
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:532 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;Ｖ経験レベルが0かつ調教者がオトコではない場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:Ｖ経験 < EXPLV:1 && TALENT:PLAYER:オトコ == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURN 0
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[ゴーヤ型触手]を得ていないとダメ
SIF (FLAG:75 & 128) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 1) == 0
	RETURN 0
;小柄体型なら従順が4以上かつマゾっ気が4以上じゃないと実行できない
IF TALENT:小柄体型
	SIF ABL:従順 < 4 || ABL:マゾっ気 < 4
		RETURN 0
ENDIF
;一定のＶ・Ａ経験が必要
SIF EXP:Ｖ経験 < 300 || EXP:Ａ経験 < 300
	RETURN 0
;Ｖ・Ａ系触手責め中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ || TEQUIP:Ａ系装着器具
	RETURN 0
;卵が邪魔で膣内洗浄できない
SIF TEQUIP:触手膣内産卵
	RETURN 0
;卵が邪魔で浣腸できない
SIF TEQUIP:触手腸内産卵
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 532) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM532
PRINTL 触手両穴拡張

;戦闘判定
CALL COM_TRY_BATTLE, 532
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手両穴拡張
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 320
DOWNBASE:1 += 200

SOURCE:痛み = 2200
SOURCE:液体追加 = 2800
SOURCE:欲情追加 = 500
SOURCE:露出 = 2500
SOURCE:屈従 = 2700
SOURCE:逸脱 = 7500
SOURCE:支配 = 2300

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 40
	SOURCE:情愛 = 50
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 150
	SOURCE:情愛 = 150
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 400
	SOURCE:情愛 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1000
	SOURCE:情愛 = 350
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1700
	SOURCE:情愛 = 600
ELSE
	SOURCE:快Ｖ = 2200
	SOURCE:情愛 = 850
ENDIF

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 += 10
	SOURCE:屈従 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 30
	SOURCE:情愛 += 30
	SOURCE:屈従 += 700
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 500
	SOURCE:情愛 += 100
	SOURCE:屈従 += 1500
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:情愛 += 200
	SOURCE:屈従 += 3000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1700
	SOURCE:情愛 += 450
	SOURCE:屈従 += 5000
ELSE
	SOURCE:快Ａ = 2200
	SOURCE:情愛 += 750
	SOURCE:屈従 += 8000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 20000
	SOURCE:欲情追加 += 2000
	SIF TALENT:PLAYER:オトコ == 0
		TCVAR:異常経験 += 1
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み += 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み += 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み += 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み += 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み += 0
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み += 5000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 1000
	SOURCE:欲情追加 += 200
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み += 600
ENDIF

;EXP:触手経験をみる
IF EXP:触手経験 < EXPLV:1
	TIMES DOWNBASE:0 , 3.00
	TIMES DOWNBASE:1 , 3.00
	SOURCE:痛み += 1000
ELSEIF EXP:触手経験 < EXPLV:2
	TIMES DOWNBASE:0 , 2.50
	TIMES DOWNBASE:1 , 2.50
	SOURCE:痛み += 800
ELSEIF EXP:触手経験 < EXPLV:3
	TIMES DOWNBASE:0 , 2.00
	TIMES DOWNBASE:1 , 2.00
	SOURCE:痛み += 500
ELSEIF EXP:触手経験 < EXPLV:4
	TIMES DOWNBASE:0 , 1.00
	TIMES DOWNBASE:1 , 1.00
	SOURCE:痛み += 100
ELSEIF EXP:触手経験 < EXPLV:5
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:痛み += 50
ELSE
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	SOURCE:中毒充足 = 1500
	TIMES SOURCE:支配 , 0.50
ELSEIF ABL:触手中毒 == 1
	TIMES DOWNBASE:0 , 0.90
	TIMES DOWNBASE:1 , 0.90
	SOURCE:中毒充足 = 1700
	TIMES SOURCE:支配 , 0.80
ELSEIF ABL:触手中毒 == 2
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	SOURCE:中毒充足 = 2000
ELSEIF ABL:触手中毒 == 3
	TIMES DOWNBASE:0 , 0.70
	TIMES DOWNBASE:1 , 0.70
	SOURCE:中毒充足 = 2500
	TIMES SOURCE:支配 , 1.20
ELSEIF ABL:触手中毒 == 4
	TIMES DOWNBASE:0 , 0.60
	TIMES DOWNBASE:1 , 0.60
	SOURCE:中毒充足 = 3300
	TIMES SOURCE:支配 , 1.50
ELSE
	TIMES DOWNBASE:0 , 0.50
	TIMES DOWNBASE:1 , 0.50
	SOURCE:中毒充足 = 4200
	TIMES SOURCE:支配 , 1.80
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:快Ａ , 0.20
	SOURCE:欲情追加 += 1000
	SOURCE:痛み += 1900
	TIMES SOURCE:痛み , 9.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:欲情追加 += 800
	SOURCE:痛み += 1250
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 0.60
	SOURCE:欲情追加 += 600
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:欲情追加 += 200
	SOURCE:痛み += 200
	TIMES SOURCE:痛み , 0.30
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:快Ａ , 0.70
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:欲情追加, 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.90
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:欲情追加, 1.20
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:欲情追加, 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.10
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:欲情追加, 0.60
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:欲情追加, 0.30
ELSE
	TIMES SOURCE:快Ｖ , 1.70
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:欲情追加, 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 3.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:欲情追加 , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:欲情追加 , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:欲情追加, 5.00
	ELSE
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:欲情追加, 1.80
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:欲情追加, 0.50
	ELSE
		TIMES SOURCE:欲情追加, 0.30
	ENDIF
ENDIF

;汚れ処理
STAIN:ヴァギナ |= 2
STAIN:ヴァギナ |= 64
STAIN:アナル |= 2
STAIN:アナル |= 64

IF EXP:Ｖ拡張経験 == 0 && EXP:Ａ拡張経験 == 0
	TCVAR:異常経験 += 2
ELSEIF EXP:Ｖ拡張経験 == 0 || EXP:Ａ拡張経験 == 0
	TCVAR:異常経験 += 1
ENDIF
TCVAR:Ｖ経験 += 25
TCVAR:Ａ経験 += 25
TCVAR:Ｖ拡張経験 += 2
TCVAR:Ａ拡張経験 += 2

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_532
PRINTFORML %CALLNAME:TARGET%の膣と肛門はゴーヤの様な極太の触手でかき回された。
IF EQUIP:睡眠薬 || TFLAG:899 > 0
	IF ABL:Ｖ感覚 >= 5 && ABL:Ａ感覚 >= 5
		PRINTFORML %CALLNAME:TARGET%の膣口と肛門はすっかり拡がって中身を曝しており、触手が抜き取られた後もなお元に戻る気配を見せていない…
	ELSEIF ABL:Ｖ感覚 > 4 || ABL:Ａ感覚 > 4
		PRINTL 触手によって拡げられた膣口と肛門からは粘液が垂れ流しになっているのが見てとれる…
	ENDIF
ELSE
	IF ABL:Ｖ感覚 >= 5 && ABL:Ａ感覚 >= 5
		PRINTFORML %CALLNAME:TARGET%は両穴を拡張された余韻を味わっている。
		PRINTFORML %CALLNAME:TARGET%の膣口と肛門はすっかり拡がって中身を曝しており、触手が抜き取られた後もなお元に戻る気配を見せていない…
	ELSEIF ABL:Ｖ感覚 > 4 || ABL:Ａ感覚 > 4
		PRINTFORML %CALLNAME:TARGET%は両足を広げ、ぐったりしている。
		PRINTL 触手によって拡げられた膣口と肛門からは粘液が垂れ流しになっているのが見てとれる…
	ENDIF
ENDIF