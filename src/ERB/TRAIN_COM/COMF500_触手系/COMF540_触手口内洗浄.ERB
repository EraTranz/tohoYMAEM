;=============================================================================
;触手口内洗浄
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE540
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:540 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;触手強制回復中(口内)はダメ
SIF TEQUIP:触手強制回復 == 1
	RETURN 0
;触手口辱中じゃないとダメ
SIF TEQUIP:口装着器具 != 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 540) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM540
PRINTL 触手口内洗浄

;戦闘判定
CALL COM_TRY_BATTLE, 540
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手口内洗浄
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

LOCAL:1 = 80
;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
	LOCAL:2 = 100
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
	LOCAL:2 = 180
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 240
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
	LOCAL:2 = 380
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 500
ELSE
	TIMES LOCAL:1 , 0.50
	LOCAL:2 = 700
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:2 *2

LOCAL:3 = 0
SIF STAIN:口 & 1
	LOCAL:3 += 1
SIF STAIN:口 & 2
	LOCAL:3 += 1
SIF STAIN:口 & 4
	LOCAL:3 += 1
SIF STAIN:口 & 8
	LOCAL:3 += 1
SIF STAIN:口 & 16
	LOCAL:3 += 1

SOURCE:性行動 = 300
SOURCE:達成感 = LOCAL:2
SOURCE:不潔 = 3000-(500*LOCAL:3)
SOURCE:液体追加 = 1000
SOURCE:逸脱 = 2500
SOURCE:支配 = 1500

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

SOURCE:屈従 = LOCAL:2
SOURCE:恭順追加 = LOCAL:2

;口内の汚れがリセット
STAIN:口 = 0

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_540
PRINTFORML 触手の先端から液体が%CALLNAME:TARGET%の口に流し込まれ、強制的に口内を濯がされた
SIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML 口の中を綺麗に掃除された%CALLNAME:TARGET%はどこか腑に落ちないといった表情を浮かべている…
