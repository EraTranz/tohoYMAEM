;=============================================================================
;触手膣内洗浄
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE541
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:541 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;妊娠中はダメ
SIF TALENT:妊娠
	RETURN 0
;懐卵中はダメ
SIF TALENT:懐卵
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;触手挿入中じゃないとダメ
SIF TEQUIP:Ｖ系装着器具 == 0
	RETURN 0
;触手が邪魔で膣内洗浄できない;下で想定されてるっぽいのでコメントアウト
;SIF TEQUIP:Ｖ系装着器具 == 3
;	RETURN 0
;それ以外の触手挿入系は設定していないので排除
SIF TEQUIP:Ｖ系装着器具 >= 4 && TEQUIP:Ｖ系装着器具 != 21 && TEQUIP:Ｖ系装着器具 !=22 && TEQUIP:Ｖ系装着器具 != 23
	RETURN 0
;幽霊膣鏡挿入中は不可
SIF TEQUIP:クスコ
	RETURN 0
;卵が邪魔で膣内洗浄できない
SIF TEQUIP:触手膣内産卵
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 541) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM541
PRINTL 触手膣内洗浄

;戦闘判定
CALL COM_TRY_BATTLE, 541
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手膣内洗浄
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

LOCAL:1 = 120
;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
	LOCAL:2 = 80
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
	LOCAL:2 = 110
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 220
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
	LOCAL:2 = 350
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 600
ELSE
	TIMES LOCAL:1 , 0.50
	LOCAL:2 = 1000
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 *2

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------

LOCAL:3 = 0
SIF STAIN:ヴァギナ & 1
	LOCAL:3 += 1
SIF STAIN:ヴァギナ & 2
	LOCAL:3 += 1
SIF STAIN:ヴァギナ & 4
	LOCAL:3 += 1
SIF STAIN:ヴァギナ & 8
	LOCAL:3 += 1
SIF STAIN:ヴァギナ & 16
	LOCAL:3 += 1

SOURCE:達成感 = LOCAL:2
SOURCE:不潔 = 5000-(600*LOCAL:3)
SOURCE:液体追加 = 2000
SOURCE:逸脱 = 4500
SOURCE:支配 = 3500

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 60
	SOURCE:性行動 = 150
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 180
	SOURCE:性行動 = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 420
	SOURCE:性行動 = 350
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 900
	SOURCE:性行動 = 500
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1600
	SOURCE:性行動 = 700
ELSE
	SOURCE:快Ｖ = 2000
	SOURCE:性行動 = 1000
ENDIF

;EXP:Ｖ経験をみる(処女はあり得ない)
IF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 450
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 75
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 15
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 300
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	TIMES SOURCE:性行動 , 0.60
	SOURCE:反感追加 = 1000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:性行動 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:性行動 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:性行動 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:性行動 , 1.50
ELSE
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:性行動 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:性行動 , 0.60
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:性行動 , 1.40
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:性行動 , 1.60
	TIMES SOURCE:反感追加 , 0.30
ENDIF

SOURCE:屈従 = LOCAL:2
SOURCE:恭順追加 = LOCAL:2

TCVAR:Ｖ経験 += 1

;膣内の汚れがリセット
STAIN:ヴァギナ = 0
CALL MINUS_RAND_LOOP, TARGET, 5, 3, 1
CFLAG:3666 = 0
TFLAG:103 = 0

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_541
IF TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23
	PRINTFORML %CALLNAME:TARGET%の子宮に居座っていた触手は突如先端から洗浄液を放出した
	PRINTFORML 大量の洗浄液を注ぎ込まれ、%CALLNAME:TARGET%の下腹部は異常なまでに大きく膨らんでいる…
ELSE
	PRINTFORML %CALLNAME:TARGET%の膣に鎮座していた触手から突如先端から洗浄液をまき散らた
	PRINTFORML 勢いよく発射された洗浄液は子宮口を通り、%CALLNAME:TARGET%の子宮内にも流れ込んだ
ENDIF
IF STAIN:ヴァギナ & 4
	PRINT その後触手は激しく身をくねらせ、
	IF TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23
		IF TEQUIP:87 == 2
			PRINT 塗られていた避妊膜ごと
			TEQUIP:87 = 0
		ENDIF
		PRINTL 膣と子宮内に溜まっていた精液を根こそぎかき出した
	ELSE
		PRINTL 膣内に残っていた精液を全てこそぎ取った
	ENDIF
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM 洗浄液と共に下の口から吐き出される様子を見た%CALLNAME:TARGET%は
		IF ABL:精液中毒 >= 4
			PRINTL 露骨にガッカリした表情を浮かべている…
		ELSEIF TALENT:淫壷
			PRINTL もっと膣内をかき回してほしいとねだってきた…
		ELSEIF TALENT:淫乱
			PRINTL どことなく残念そうな表情を浮かべている…
		ELSEIF CFLAG:好感度 < 500
			PRINTL 嫌悪感に満ちた表情でそっぽを向いた…
		ELSE
			PRINTL 反応に困っているのか完全に固まっている…
		ENDIF
	ENDIF
ELSE
	PRINT その後触手は激しく身をくねらせ、
	IF TEQUIP:Ｖ系装着器具 == 3 || TEQUIP:Ｖ系装着器具 == 23
		PRINT 子宮内の
	ELSE
		PRINT 膣内の
	ENDIF
	PRINTL 襞を一つ一つまるで舐めるようなしぐさで汚れを落としていった
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM 大量の洗浄液が下の口からごぽごぽと吐き出される様子を見た%CALLNAME:TARGET%は
		IF TALENT:淫壷
			PRINTL もっと膣内をかき回してほしいとねだってきた…
		ELSEIF TALENT:淫乱
			PRINTL どことなく残念そうな表情を浮かべている…
		ELSEIF CFLAG:好感度 < 500
			PRINTL 嫌悪感に満ちた表情でそっぽを向いた…
		ELSE
			PRINTL 反応に困っているのか完全に固まっている…
		ENDIF
	ENDIF
ENDIF
