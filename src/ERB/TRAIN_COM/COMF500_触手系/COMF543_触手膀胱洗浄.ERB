;=============================================================================
;触手膀胱洗浄
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE543
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:543 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;触手カテーテル中じゃないとダメ
SIF TEQUIP:Ｕ系装着器具 != 1 && TEQUIP:Ｕ系装着器具 != 21
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 543) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM543
PRINTL 触手膀胱洗浄

;戦闘判定
CALL COM_TRY_BATTLE, 543
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手膀胱洗浄
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

LOCAL:1 = 70
;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
	LOCAL:2 = 100
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
	LOCAL:2 = 200
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 400
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
	LOCAL:2 = 800
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 1200
ELSE
	TIMES LOCAL:1 , 0.50
	LOCAL:2 = 1800
ENDIF

DOWNBASE:0 += LOCAL:1 *2
DOWNBASE:1 += LOCAL:1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL:3 = 1

SOURCE:達成感 = LOCAL:2
SOURCE:不潔 = 2000
SOURCE:液体追加 = 700
SOURCE:逸脱 = 7000
SOURCE:支配 = 4000

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 60
	SOURCE:中毒充足 = 150
	SOURCE:屈従 = 150
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 180
	SOURCE:中毒充足 = 250
	SOURCE:屈従 = 300
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 420
	SOURCE:中毒充足 = 350
	SOURCE:屈従 = 600
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 900
	SOURCE:中毒充足 = 500
	SOURCE:屈従 = 1000
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1600
	SOURCE:中毒充足 = 700
	SOURCE:屈従 = 1800
ELSE
	SOURCE:快Ｃ = 2000
	SOURCE:中毒充足 = 1000
	SOURCE:屈従 = 2300
ENDIF

;EXP:尿道経験をみる
IF EXP:尿道開発経験 < EXPLV:2
	TIMES SOURCE:快Ｃ , 0.60
	SOURCE:痛み = 400
ELSEIF EXP:尿道開発経験 < EXPLV:3
	TIMES SOURCE:快Ｃ , 1.00
	SOURCE:痛み = 60
ELSEIF EXP:尿道開発経験 < EXPLV:4
	TIMES SOURCE:快Ｃ , 1.20
	SOURCE:痛み = 12
ELSEIF EXP:尿道開発経験 < EXPLV:5
	TIMES SOURCE:快Ｃ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｃ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.40
	SOURCE:痛み += 300
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｃ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	TIMES SOURCE:中毒充足 , 0.60
	SOURCE:反感追加 = 1000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:中毒充足 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:中毒充足 , 1.50
ELSE
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:中毒充足 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:中毒充足 , 0.60
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:中毒充足 , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｃ , 1.30
	TIMES SOURCE:中毒充足 , 1.20
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｃ , 1.60
	TIMES SOURCE:中毒充足 , 1.40
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:快Ｃ , 2.00
	TIMES SOURCE:中毒充足 , 1.60
	TIMES SOURCE:反感追加 , 0.30
ENDIF

;膀胱内圧力上昇（放尿レベル増加/尿意が催促されることで利尿剤効果）
IF TALENT:おもらし癖 == 0
	LOCAL:4 = MIN(1 + (EXP:放尿経験 / 10), 10)
	IF TEQUIP:利尿剤
		CFLAG:放尿レベル += LOCAL:4 *2
	ELSE
		CFLAG:放尿レベル += LOCAL:4
	ENDIF
ENDIF
IF TEQUIP:利尿剤 == 0
	PRINTFORML %CALLNAME:TARGET%は急激に尿意を催す様になった
	TEQUIP:利尿剤 = 1
ENDIF

TCVAR:尿道開発経験 += 1

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_543
PRINTFORML 触手を通じて%CALLNAME:TARGET%の尿道から膀胱へ洗浄液を注ぎ込んだ
PRINTL そして膀胱が限界まで膨らんだ瞬間、今度は逆に中の溶液が吸い出されていった…
PRINTFORML %CALLNAME:TARGET%は洗浄液を何度も膀胱内に出し入れされるうちにまるで放尿しているような錯覚に襲われた
IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:尿道狂 && ABL:マゾっ気 >= 5
		PRINTL 下腹部の鈍痛と尿意をおおいに悦しんでいるのか、膀胱内に洗浄液が出し入れされる度に黄色い声をあげ、妖しげに腰を振っている…
	ELSEIF ABL:マゾっ気 >= 5
		PRINTL 大量の洗浄液を膀胱内に注がれる責めにも快楽で満ちた笑顔を浮かべている…
	ELSEIF ABL:マゾっ気 >= 3
		PRINTL 強制的に尿意を催されたものの、それすら快楽を感じているのか切なげな声を上げて身震いしている…
	ELSEIF ABL:マゾっ気 >= 1
		PRINTL 僅かに快楽を感じているのか、うめき声に交じって時折艶っぽい声をあげている…
	ELSE
		PRINTL 激しい尿意と下腹部の激痛に顔を歪ませ、額には大粒の脂汗を浮かべている…
	ENDIF
ENDIF
