;=============================================================================
;触手鼻腔洗浄
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE544
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:544 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;鼻フック装着中だとダメ
SIF TEQUIP:鼻装着器具 == 1
	RETURN 0
;触手鼻穴挿入中じゃないとダメ
SIF TEQUIP:鼻装着器具 != 2
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 544) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM544
PRINTL 触手鼻腔洗浄

;戦闘判定
CALL COM_TRY_BATTLE, 544
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手鼻腔洗浄
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;EXP:緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	LOCAL:1 = 60
ELSEIF EXP:緊縛経験 < EXPLV:4
	LOCAL:1 = 30
ELSE
	LOCAL:1 = 20
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES LOCAL:1 , 1.00
	LOCAL:2 = 50
ELSEIF ABL:触手中毒 == 1
	TIMES LOCAL:1 , 0.90
	LOCAL:2 = 90
ELSEIF ABL:触手中毒 == 2
	TIMES LOCAL:1 , 0.80
	LOCAL:2 = 120
ELSEIF ABL:触手中毒 == 3
	TIMES LOCAL:1 , 0.70
	LOCAL:2 = 190
ELSEIF ABL:触手中毒 == 4
	TIMES LOCAL:1 , 0.60
	LOCAL:2 = 250
ELSE
	TIMES LOCAL:1 , 0.50
	LOCAL:2 = 350
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 *2

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------

SOURCE:性行動 = 160
SOURCE:達成感 = LOCAL:2
SOURCE:不潔 = 3000
SOURCE:液体追加 = 800
SOURCE:逸脱 = 2400
SOURCE:支配 = 1200

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

SOURCE:屈従 = LOCAL:2
SOURCE:恭順追加 = LOCAL:2

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_544
PRINTFORML 触手の先端から液体が%CALLNAME:TARGET%の鼻の穴から流し込まれ、強制的に鼻の中を濯がされた
SIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML 鼻の中を綺麗に掃除された%CALLNAME:TARGET%はどこか腑に落ちないといった表情を浮かべている…
