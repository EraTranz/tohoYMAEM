;=============================================================================
;触手吸血
;=============================================================================
;蒐集品「血液」を採取可能な数少ないコマンド。
;触手が蛭みたいに奴隷の肌に吸いつき、血を吸い取っていくイメージ。
;「吸血」「針」を参考に作成。
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE554
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:554 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;吸血経験が0の場合、異常系調教フィルタがオフになってないとダメ
SIF FLAG:2 & 4 && EXP:吸血経験 == 0
	RETURN 0
;調教者は触手使役LV2以上必要
SIF ABL:PLAYER:触手使役 < 2
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0 && TEQUIP:触手服 == 0
	RETURN 0
;触手が[吸血触手]を得ていないとダメ
SIF (FLAG:75 & 65536) == 0
	RETURN 0
;調教者がハードなコマンドをできるかどうか
SIF COM_ABLE_HARDTRAINING(PLAYER, 2) == 0
	RETURN 0
;すでに実行しているとダメ
SIF TFLAG:140 & 64
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;媚薬効果発揮中はダメ
SIF TEQUIP:媚薬
	RETURN 0
;利尿剤効果発揮中はダメ
SIF TEQUIP:利尿剤
	RETURN 0
;賢者の血効果発揮中はダメ
SIF TEQUIP:賢者の血
	RETURN 0
;睡眠薬効果発揮中はダメ
SIF EQUIP:睡眠薬
	RETURN 0
;排卵誘発剤効果発揮中はダメ
SIF TEQUIP:排卵誘発剤
	RETURN 0
;緊急避妊薬効果発揮中はダメ
SIF TEQUIP:緊急避妊薬
	RETURN 0
;鈴蘭の毒効果発揮中はダメ
SIF TEQUIP:鈴蘭の毒
	RETURN 0
;昆虫採集セットの薬効果発揮中はダメ
SIF TEQUIP:昆虫採集セットの薬
	RETURN 0
;触手強制回復効果発揮中はダメ
SIF TEQUIP:触手強制回復
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 554) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM554
PRINTL 触手吸血

;戦闘判定
CALL COM_TRY_BATTLE, 554
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手吸血
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 70
DOWNBASE:1 += 80

SOURCE:中毒充足 = 1000
SOURCE:不潔 = 3000
SOURCE:逸脱 = 6000
SOURCE:支配 = 1500

;PALAM:苦痛をみる
IF PALAM:苦痛 < PALAMLV:1
	SOURCE:痛み = 2000
ELSEIF PALAM:苦痛 < PALAMLV:2
	SOURCE:痛み = 2200
ELSEIF PALAM:苦痛 < PALAMLV:3
	SOURCE:痛み = 2500
ELSEIF PALAM:苦痛 < PALAMLV:4
	SOURCE:痛み = 2900
ELSE
	SOURCE:痛み = 3400
ENDIF

;EXP:吸血経験をみる
IF EXP:吸血経験 < EXPLV:1
	TIMES SOURCE:中毒充足 , 0.10
	TIMES SOURCE:不潔 , 3.00
	TIMES SOURCE:逸脱 , 2.50
	TIMES SOURCE:支配 , 0.10
ELSEIF EXP:吸血経験 < EXPLV:2
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:不潔 , 1.50
	TIMES SOURCE:逸脱 , 1.00
	TIMES SOURCE:支配 , 0.50
ELSEIF EXP:吸血経験 < EXPLV:3
	TIMES SOURCE:中毒充足 , 1.50
	TIMES SOURCE:不潔 , 1.00
	TIMES SOURCE:逸脱 , 0.75
	TIMES SOURCE:支配 , 1.00
ELSE
	TIMES SOURCE:中毒充足 , 2.00
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:逸脱 , 0.50
	TIMES SOURCE:支配 , 1.50
ENDIF

;ABL:触手中毒をみる
IF ABL:触手中毒 == 0
	TIMES SOURCE:痛み , 2.00
	TIMES SOURCE:逸脱 , 5.00
ELSEIF ABL:触手中毒 == 1
	TIMES SOURCE:痛み , 1.75
	TIMES SOURCE:逸脱 , 2.00
ELSEIF ABL:触手中毒 == 2
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:逸脱 , 1.00
ELSEIF ABL:触手中毒 == 3
	TIMES SOURCE:痛み , 1.25
	TIMES SOURCE:逸脱 , 0.50
ELSEIF ABL:触手中毒 == 4
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:逸脱 , 0.25
ELSE
	TIMES SOURCE:痛み , 0.75
	TIMES SOURCE:逸脱 , 0.00
ENDIF

LOCAL:1 = SOURCE:逸脱 / 10

;触手催眠
IF TEQUIP:触手催眠
	TIMES SOURCE:痛み , 0.75
	TIMES SOURCE:不潔 , 0.75
	TIMES LOCAL:1 , 0.25
ENDIF

SOURCE:トラウマ += LOCAL:1

SIF EXP:吸血経験 == 0
	TCVAR:異常経験 += 1

TCVAR:吸血経験 += 1
TFLAG:140 |= 64

;血液採取処理
SIF FLAG:12 & 8
	CALL SWEET_POTATO_RED_BLOOD

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_554
IF !TEQUIP:触手服
	PRINT 先端にヤツメウナギのような口が付いた触手が
	IF TEQUIP:緊縛関連 && TEQUIP:Ａ系装着器具 == 2
		PRINT 全身を緊縛され、さらに肛門から口に触手を貫通されて全く動くことができない
	ELSEIF TEQUIP:緊縛関連
		PRINT 全身雁字搦めに束縛されて動けない
	ELSEIF TEQUIP:Ａ系装着器具 == 2
		PRINT 貫通されて身動きがとれない
	ELSE
		PRINT 四肢を拘束されて動けない
	ENDIF
	PRINTFORML %CALLNAME:TARGET%の首筋に取り付くと、血を吸い始めた…

	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF TEQUIP:触手催眠
			PRINTL 触手の催眠術にかかっているせいか、ぼーっとした表情を浮かべたまま抵抗することなく血を吸われている……
		ELSEIF ABL:触手中毒
			PRINTL 頬を上気させ、為すがまま触手に血を吸わせている……
		ELSEIF ABL:マゾっ気
			PRINTL 触手に対する嫌悪感と血を抜かれていく恍惚感に翻弄されているようだ……
		ELSEIF TALENT:臆病 || TALENT:痛みに弱い
			PRINTL 泣きじゃくりながら取り付いた触手を振り払おうとしている……
		ELSE
			PRINTL 血を吸われる感覚に不快感を露わにしている……
		ENDIF
	ENDIF
ELSE
	PRINTFORML 触手服は%CALLNAME:TARGET%の首筋に触手を伸ばすと、血を吸い始めた…
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		PRINTFORM %CALLNAME:TARGET%は
		IF TEQUIP:触手催眠
			PRINTL 触手の催眠術にかかっているせいか、ぼーっとした表情を浮かべたまま抵抗することなく血を吸われている……
		ELSEIF ABL:触手中毒
			PRINTL 頬を上気させ、為すがまま触手に血を吸わせている……
		ELSEIF ABL:マゾっ気
			PRINTL 触手に対する嫌悪感と血を抜かれていく恍惚感に翻弄されているようだ……
		ELSEIF TALENT:臆病 || TALENT:痛みに弱い
			PRINTL おかしな服を着せられ血まで吸われたためか泣きじゃくっている……
		ELSE
			PRINTL 血を吸われる感覚に不快感を露わにしている……
		ENDIF
	ENDIF
ENDIF
