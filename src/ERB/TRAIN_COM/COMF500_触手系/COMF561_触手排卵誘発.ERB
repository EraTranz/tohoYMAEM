;=============================================================================
;触手排卵誘発
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE561
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:561 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとはダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;薬毒耐性の場合ダメ
SIF TALENT:薬毒耐性
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;妊娠中なら不可
SIF TALENT:妊娠
	RETURN 0
;懐卵中はダメ
SIF TALENT:懐卵
	RETURN 0
;助手プレイ中は一応禁止（暫定）
SIF ASSIPLAY
	RETURN 0
;産卵促進剤が効果を発揮している場合は使えない
SIF TEQUIP:産卵促進剤
	RETURN 0
;排卵誘発が効果を発揮している場合は使えない
SIF TEQUIP:排卵誘発剤
	RETURN 0
;避妊が効果を発揮している場合は使えない
SIF TEQUIP:緊急避妊薬
	RETURN 0
;幽霊膣鏡挿入中は不可
SIF TEQUIP:クスコ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 561) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM561
PRINTL 触手排卵誘発

;戦闘判定
CALL COM_TRY_BATTLE, 561
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手排卵誘発
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

IF TEQUIP:Ｖ系装着器具 == 3
	LOCAL:1 = 60
ELSEIF TEQUIP:Ｖ系装着器具 == 3
	LOCAL:1 = 30
ELSEIF TEQUIP:Ｖ系装着器具 == 1
	LOCAL:1 = 20
ELSE
	LOCAL:1 = 10
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 *4

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL:2 = 1

SOURCE:中毒充足 = 750
SOURCE:液体追加 = 2000
SOURCE:欲情追加 = 2000
SOURCE:逸脱 = 6000

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 150
	SOURCE:屈従 = 100
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 200
	SOURCE:屈従 = 180
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
	SOURCE:屈従 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1000
	SOURCE:屈従 = 380
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1200
	SOURCE:屈従 = 500
ELSE
	SOURCE:快Ｖ = 1800
	SOURCE:屈従 = 800
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 700
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 200
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱, 5.00
		SOURCE:不潔 += 3000
	ELSE
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱, 2.00
		SOURCE:不潔 += 2000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		SOURCE:不潔 += 500
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:不潔 += 1000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:中毒充足 , 0.90
	TIMES SOURCE:不潔 , 1.80
	TIMES SOURCE:液体追加 , 0.90
	TIMES SOURCE:欲情追加 , 0.90
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:不潔 , 1.50
	TIMES SOURCE:液体追加 , 1.00
	TIMES SOURCE:欲情追加 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:中毒充足 , 1.10
	TIMES SOURCE:不潔 , 1.00
	TIMES SOURCE:液体追加 , 1.10
	TIMES SOURCE:欲情追加 , 1.10
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:中毒充足 , 1.20
	TIMES SOURCE:不潔 , 0.60
	TIMES SOURCE:液体追加 , 1.20
	TIMES SOURCE:欲情追加 , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:中毒充足 , 1.30
	TIMES SOURCE:不潔 , 0.30
	TIMES SOURCE:液体追加 , 1.30
	TIMES SOURCE:欲情追加 , 1.30
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

SOURCE:支配 = SOURCE:欲情追加 / 2
SOURCE:薬物浸潤 = 600

SIF TALENT:処女
	LOCAL:2 = 0

TEQUIP:排卵誘発剤 = 2
STAIN:ヴァギナ |= 64
IF LOCAL:2
	TCVAR:Ｖ経験 += LOCAL:2
	;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
	TFLAG:1 = 1
ENDIF

;薬物経験
TCVAR:薬物経験 += 1

;コマンド属性：触手
TFLAG:98 = 8
;排卵誘発剤使用判定ON
CFLAG:21 |= 128
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_561
PRINT 極細の触手が二本、
IF TEQUIP:Ｖ系装着器具
	PRINTFORML %CALLNAME:TARGET%の性器に潜り込んでいる触手の間隙を縫って膣内へと滑り込んだ
ELSE
	PRINTFORML %CALLNAME:TARGET%の秘唇をめくり、性器の中とするりと侵入した
ENDIF
SIF TALENT:処女
	PRINTFORML 触手達は%CALLNAME:TARGET%の膜を傷つけることなく、僅かな隙間から子宮口を目指して前進した…
PRINTL 触手は子宮の奥深くまで侵入すると二手に分かれ、それぞれが細い卵管に潜り込んだ
PRINTL 最奥までたどり着いた触手達は先端卵巣に突き立てると排卵を促す分泌物を容赦なく流し込んでいく
SIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML %CALLNAME:TARGET%は両脇腹にまるで溶岩を流しこまれたような灼熱感と、それに続く抗いようもない欲望に身も心も蝕まれていった…
