;=============================================================================
;触手避妊膜塗布
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE562
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:562 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURN 0
;触手調教中じゃないとはダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURN 0
;妊娠中なら不可
SIF TALENT:妊娠
	RETURN 0
;懐卵中はダメ
SIF TALENT:懐卵
	RETURN 0
;産卵促進剤が効果を発揮している場合は使えない
SIF TEQUIP:産卵促進剤
	RETURN 0
;排卵誘発が効果を発揮している場合は使えない
SIF TEQUIP:排卵誘発剤
	RETURN 0
;避妊が効果を発揮している場合は使えない
SIF TEQUIP:緊急避妊薬
	RETURN 0
;触手が邪魔で子宮壁に膜を塗布できない
SIF TEQUIP:Ｖ系装着器具 == 3
	RETURN 0
;幽霊膣鏡挿入中は不可
SIF TEQUIP:クスコ
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 562) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM562
PRINTL 触手避妊膜塗布

;戦闘判定
CALL COM_TRY_BATTLE, 562
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手避妊膜塗布
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

IF TEQUIP:Ｖ系装着器具 == 3
	LOCAL:1 = 60
ELSEIF TEQUIP:Ｖ系装着器具 == 3
	LOCAL:1 = 30
ELSEIF TEQUIP:Ｖ系装着器具 == 1
	LOCAL:1 = 20
ELSE
	LOCAL:1 = 10
ENDIF

DOWNBASE:0 += LOCAL:1
DOWNBASE:1 += LOCAL:1 *4

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOCAL:2 = 1

SOURCE:中毒充足 = 900
SOURCE:逸脱 = 7000
SOURCE:支配 = 500

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:屈従 = 20
	TIMES SOURCE:支配 , 0.50
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 200
	SOURCE:屈従 = 120
	TIMES SOURCE:支配 , 0.75
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 700
	SOURCE:屈従 = 500
	TIMES SOURCE:支配 , 1.00
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1600
	SOURCE:屈従 = 1200
	TIMES SOURCE:支配 , 1.50
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 2500
	SOURCE:屈従 = 1800
	TIMES SOURCE:支配 , 2.00
ELSE
	SOURCE:快Ｖ = 3400
	SOURCE:屈従 = 2400
	TIMES SOURCE:支配 , 3.00
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み = 3000
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 1500
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 500
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 10
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 250
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み += 50
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱, 5.00
		SOURCE:不潔 += 3000
	ELSE
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱, 2.00
		SOURCE:不潔 += 2000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:屈従 , 0.60
		SOURCE:不潔 += 500
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:不潔 += 1000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:中毒充足 , 0.90
	TIMES SOURCE:不潔 , 1.80
	TIMES SOURCE:屈従 , 0.90
	TIMES SOURCE:支配 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:不潔 , 1.50
	TIMES SOURCE:屈従 , 1.00
	TIMES SOURCE:支配 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:中毒充足 , 1.20
	TIMES SOURCE:不潔 , 1.00
	TIMES SOURCE:液体追加 , 1.10
	TIMES SOURCE:屈従 , 1.10
	TIMES SOURCE:支配 , 1.20
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:中毒充足 , 1.40
	TIMES SOURCE:不潔 , 0.60
	TIMES SOURCE:屈従 , 1.20
	TIMES SOURCE:支配 , 1.40
ELSE
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:中毒充足 , 1.60
	TIMES SOURCE:不潔 , 0.30
	TIMES SOURCE:屈従 , 1.30
	TIMES SOURCE:支配 , 1.60
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
SIF TALENT:処女
	LOCAL:2 = 0

TEQUIP:緊急避妊薬 = 2
STAIN:ヴァギナ |= 64
IF LOCAL:2
	TCVAR:Ｖ経験 += LOCAL:2
	;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
	TFLAG:1 = 1
ENDIF

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_562
PRINT 先端が磯巾着のように無数に分かれている触手が
IF TEQUIP:Ｖ系装着器具
	PRINTFORML %CALLNAME:TARGET%の性器に潜り込んでいる触手の間隙を縫って膣内へと滑り込んだ
ELSE
	PRINTFORML %CALLNAME:TARGET%の秘唇をめくり、性器の中とするりと侵入した
ENDIF
SIF TALENT:処女
	PRINTFORML 触手達は%CALLNAME:TARGET%の膜を傷つけることなく、僅かな隙間から子宮口を目指して前進した…
PRINTL 触手は子宮まで侵入すると先端から卵子の着床を妨害する特殊な粘液を分泌し、ありとあらゆる襞を逃すことなく子宮壁に塗りつけていった

IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORM %CALLNAME:TARGET%は触手が子宮内を蹂躙している感触と奇妙な冷感に
	IF TALENT:淫乱
		IF TEQUIP:Ｖ系装着器具
			PRINT 触手に埋め尽くされた膣口のわずかな隙間から愛液を滴らせつつ、
		ELSE
			PRINT 秘所から愛液をどばどばと溢れさせながら、
		ENDIF
	ENDIF
	PRINTL 目と口を大きく見開き、身体をのけ反らせている…
ENDIF
