;=============================================================================
;触手脱毛剤
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE564
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:564 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;触手が[特殊ホルモン分泌]を得ていないとダメ
SIF (FLAG:75 & 1024) == 0
	RETURN 0
;脱毛状態ではダメ、小柄体型・小人体型では手を付けていない状態でもダメ
SIF CFLAG:36 == 1 || ((TALENT:小柄体型 || TALENT:小人体型) && CFLAG:36 == 0)
	RETURN 0
;薬毒耐性だとダメ
SIF TALENT:薬毒耐性
	RETURN 0
;機械だとダメ
SIF TALENT:機械
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 564) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM564
PRINTL 触手脱毛剤

;戦闘判定
CALL COM_TRY_BATTLE, 564
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手脱毛剤
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 25
DOWNBASE:1 += 70

SOURCE:欲情追加 = 1500
SOURCE:液体追加 = 1500
SOURCE:中毒充足 = 1600
SOURCE:薬物浸潤 = 75
SOURCE:逸脱 = 7000

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:逸脱 , 0.50

CFLAG:36 = 1

;薬物経験
TCVAR:薬物経験 += 1

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_564
PRINTFORML 触手は先端から分泌させた脱毛剤を%CALLNAME:TARGET%の陰部に擦りつけた
PRINTFORML しばらくすれば%CALLNAME:TARGET%の陰毛が根こそぎ抜け落ちてツルツルになるだろう……
