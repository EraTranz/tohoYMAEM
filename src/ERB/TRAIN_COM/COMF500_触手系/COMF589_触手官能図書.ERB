;=============================================================================
;触手官能図書
;=============================================================================
;言葉責めから地の文と処理の一部を拝借
;触手生贄を通じて触手と一体化したパチュリーを召喚し、
;図書館秘蔵の官能小説を朗読して羞恥心を煽ってくれる……という超カオスなプレイ
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE589
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:589 > 0
	RETURN 0
;紳士的な触手系コマンドを有効にしてないとダメ
SIF (FLAG:15 & 4096) == 0
	RETURN 0
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURN 0
;アイテム進化の秘法を持っていないとダメ
SIF ITEM:進化の秘法 == 0
	RETURN 0
;触手調教中じゃないとダメ
SIF TEQUIP:触手調教 == 0
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:キャラ触手 & 16
	RETURN 1
;調教者は触手使役LV3以上必要
SIF ABL:PLAYER:触手使役 < 3
	RETURN 0
;触手が[PATCHOULI触手]を得ていないとダメ
SIF (FLAG:98 & 512) == 0
	RETURN 0
;パチュリーを触手生贄に捧げていない(=パチュリーが何らかの形で存在している)とダメ
SIF (FLAG:97 & 512) == 0
	RETURN 0
;耳が塞がっているとダメ
SIF TEQUIP:耳装着器具
	RETURN 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURN 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURN 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 589) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM589
PRINTL 触手官能図書

;戦闘判定
CALL COM_TRY_BATTLE, 589
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手官能図書
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 5
DOWNBASE:1 += 80

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	LOCAL:1 = 500
	LOCAL:2 = 50
	LOCAL:3 = 800
ELSEIF ABL:露出癖 == 1
	LOCAL:1 = 400
	LOCAL:2 = 100
	LOCAL:3 = 700
ELSEIF ABL:露出癖 == 2
	LOCAL:1 = 300
	LOCAL:2 = 150
	LOCAL:3 = 600
ELSEIF ABL:露出癖 == 3
	LOCAL:1 = 200
	LOCAL:2 = 200
	LOCAL:3 = 500
ELSEIF ABL:露出癖 == 4
	LOCAL:1 = 100
	LOCAL:2 = 250
	LOCAL:3 = 400
ELSE
	LOCAL:1 = 50
	LOCAL:2 = 300
	LOCAL:3 = 300
ENDIF

;ABL:自慰中毒をみる
IF ABL:自慰中毒 == 0
	LOCAL:1 += 50
	LOCAL:2 += 10
	LOCAL:3 += 1000
ELSEIF ABL:自慰中毒 == 1
	LOCAL:1 += 100
	LOCAL:2 += 50
	LOCAL:3 += 600
ELSEIF ABL:自慰中毒 == 2
	LOCAL:1 += 150
	LOCAL:2 += 100
	LOCAL:3 += 400
ELSEIF ABL:自慰中毒 == 3
	LOCAL:1 += 200
	LOCAL:2 += 250
	LOCAL:3 += 200
ELSEIF ABL:自慰中毒 == 4
	LOCAL:1 += 300
	LOCAL:2 += 500
	LOCAL:3 += 100
ELSE
	LOCAL:1 += 400
	LOCAL:2 += 800
	LOCAL:3 += 50
ENDIF

;パチュリーとの相性あるなしで補正が上下
LOCAL:4 = 8
IF RELATION:(LOCAL:4) > 0 && RELATION:(LOCAL:4) != 100
	LOCAL:1 *= RELATION:(LOCAL:4)
	LOCAL:1 /= 100
	LOCAL:2 *= RELATION:(LOCAL:4)
	LOCAL:2 /= 100
	LOCAL:3 *= RELATION:(LOCAL:4)
	LOCAL:3 /= 100
ENDIF

SOURCE:露出 += LOCAL:1
SOURCE:屈従 += LOCAL:2
SOURCE:中毒充足 += (LOCAL:1 + LOCAL:2)/2
SOURCE:逸脱 += LOCAL:3

IF TEQUIP:キャラ触手 & 16
	TEQUIP:キャラ触手 -= 16
ELSE
	TEQUIP:キャラ触手 |= 16
ENDIF

;コマンド属性：触手
TFLAG:98 = 8
;触手経験フラグ
TFLAG:90 = 0
RETURN 1

;-------------------------------------------------
;触手有害図書朗読中
;-------------------------------------------------
@EQUIP_COM589
PRINTL ＜触手有害図書朗読中＞

;耳栓を着けていると効果なし
SIF TEQUIP:耳装着器具
	RETURN 0

DOWNBASE:0 += 0
DOWNBASE:1 += 80

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	LOCAL:1 = 500
	LOCAL:2 = 50
	LOCAL:3 = 800
ELSEIF ABL:露出癖 == 1
	LOCAL:1 = 400
	LOCAL:2 = 100
	LOCAL:3 = 700
ELSEIF ABL:露出癖 == 2
	LOCAL:1 = 300
	LOCAL:2 = 150
	LOCAL:3 = 600
ELSEIF ABL:露出癖 == 3
	LOCAL:1 = 200
	LOCAL:2 = 200
	LOCAL:3 = 500
ELSEIF ABL:露出癖 == 4
	LOCAL:1 = 100
	LOCAL:2 = 250
	LOCAL:3 = 400
ELSE
	LOCAL:1 = 50
	LOCAL:2 = 300
	LOCAL:3 = 300
ENDIF

;ABL:自慰中毒をみる
IF ABL:自慰中毒 == 0
	LOCAL:1 += 50
	LOCAL:2 += 10
	LOCAL:3 += 1000
ELSEIF ABL:自慰中毒 == 1
	LOCAL:1 += 100
	LOCAL:2 += 50
	LOCAL:3 += 600
ELSEIF ABL:自慰中毒 == 2
	LOCAL:1 += 150
	LOCAL:2 += 100
	LOCAL:3 += 400
ELSEIF ABL:自慰中毒 == 3
	LOCAL:1 += 200
	LOCAL:2 += 250
	LOCAL:3 += 200
ELSEIF ABL:自慰中毒 == 4
	LOCAL:1 += 300
	LOCAL:2 += 500
	LOCAL:3 += 100
ELSE
	LOCAL:1 += 400
	LOCAL:2 += 800
	LOCAL:3 += 50
ENDIF

;EXP:自慰経験をみる
IF EXP:自慰経験 < EXPLV:1
	TIMES LOCAL:2 , 0.20
	TIMES LOCAL:3 , 1.50
ELSEIF EXP:自慰経験 < EXPLV:2
	TIMES LOCAL:2 , 0.50
	TIMES LOCAL:3 , 1.20
ELSEIF EXP:自慰経験 < EXPLV:3
	TIMES LOCAL:2 , 0.80
	TIMES LOCAL:3 , 1.00
ELSEIF EXP:自慰経験 < EXPLV:4
	TIMES LOCAL:2 , 1.00
	TIMES LOCAL:3 , 0.80
ELSEIF EXP:自慰経験 < EXPLV:5
	TIMES LOCAL:2 , 1.20
	TIMES LOCAL:3 , 0.50
ELSE
	TIMES LOCAL:2 , 1.50
	TIMES LOCAL:3 , 0.20
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL:1 , 0.50
	TIMES LOCAL:3 , 1.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL:1 , 0.80
	TIMES LOCAL:3 , 1.50
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL:1 , 1.20
	TIMES LOCAL:3 , 1.20
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:3 , 0.80
ELSE
	TIMES LOCAL:1 , 1.80
	TIMES LOCAL:3 , 0.50
ENDIF

;貞操観念・貞操無頓着
IF TALENT:貞操観念
	TIMES LOCAL:2 , 1.20
ELSEIF TALENT:貞操無頓着
	TIMES LOCAL:2 , 0.50
ENDIF
;恥じらい・恥薄い
IF TALENT:恥じらい
	TIMES LOCAL:1 , 1.20
ELSEIF TALENT:恥薄い
	TIMES LOCAL:1 , 0.50
ENDIF

;パチュリーとの相性あるなしで補正が上下
LOCAL = 8
IF RELATION:LOCAL > 0 && RELATION:LOCAL != 100
	LOCAL:1 *= RELATION:LOCAL
	LOCAL:1 /= 100
	LOCAL:2 *= RELATION:LOCAL
	LOCAL:2 /= 100
	LOCAL:3 *= RELATION:LOCAL
	LOCAL:3 /= 100
ENDIF

SOURCE:露出 += LOCAL:1
SOURCE:屈従 += LOCAL:2
SOURCE:中毒充足 += (LOCAL:1 + LOCAL:2)/2
SOURCE:逸脱 += LOCAL:3

;パチュリーの性別上、調教対象が♀の時のみレズ経験が入る仕様に
SIF TALENT:オトコ == 0
	TCVAR:レズ経験 += 1

;触手経験フラグ
TFLAG:90 += 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_589
IF TEQUIP:キャラ触手 & 16
	PRINTFORML 興に乗って官能小説を朗読しているパチュリーにとって%CALLNAME:PLAYER%の命令は不服だったのか、一瞬頬を膨らませた表情を作った
	PRINTFORML しかし気持ちを切り替えたらしく、パチュリーは召喚した官能小説を持ったまま触手に身を潜ませた
	PRINTFORML 恥ずかしさのあまり顔どころか耳まで真っ赤にさせた%CALLNAME:TARGET%を残し、触手の塊は去って行った
	PRINTFORML …まさかあの中で続きを読むつもりだろうか……？
ELSE
	PRINTFORML %CALLNAME:PLAYER%は命令を下すと、棺桶ほどの太さの触手が林立する肉の管をかき分けて現れた
	PRINTFORML 触手は%CALLNAME:TARGET%の目の前で分厚い辞書が開くように二つに割れ、
	PRINTFORML その中からは無数の触手と結合し、真っ白ともいえるような素肌のパチュリーが姿を現した
	PRINTFORML パチュリーは%CALLNAME:MASTER%に軽くお辞儀をすると呪文を唱え、一冊の本を呼び出した
	PRINTFORML 訝しげな表情を浮かべる%CALLNAME:TARGET%を余所に、パチュリーはその場で本を開くと声を上げて読み始めた
	PRINTFORML 本の内容が非常に濃い官能小説だったことに%CALLNAME:TARGET%は面食らったのか、顔を真っ赤にして思わずといった風にパチュリーから顔をそむけた
	PRINTFORML しかし朗々と響きわたるパチュリーの平坦な声が容赦なく耳に入り、%CALLNAME:TARGET%の羞恥心を一層煽りたてている…
ENDIF
