;=============================================================================
;Ｇスポット刺激
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE600
#FUNCTION
;Ｖ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 1
	RETURNF 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURNF 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURNF 0
;調教者の技巧が3未満はだめ
SIF ABL:PLAYER:技巧 < 3
	RETURNF 0
;Ｖ系装着具使用中は不可(バイブ、クスコ除く)
SIF TEQUIP:Ｖ系装着器具 && TEQUIP:Ｖ系装着器具 != 1
	RETURNF 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURNF 0
;蒸し風呂と岩盤浴以外の○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中 && TEQUIP:特殊風呂入浴中 != 12 && TEQUIP:特殊風呂入浴中 != 15
	RETURNF 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURNF 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 600) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM600
PRINTL Ｇスポット刺激

;戦闘判定
CALL COM_TRY_BATTLE, 600
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = Ｇスポット刺激

SELECTCOM = 600
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 50
DOWNBASE:1 += 180

SOURCE:情愛 = 300
SOURCE:中毒充足 = 200
SOURCE:液体追加 = 500
SOURCE:露出 = 300
SOURCE:逸脱 = 400
SOURCE:反感追加 = 50

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:屈従 = 20
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 150
	SOURCE:屈従 = 120
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
	SOURCE:屈従 = 500
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1800
	SOURCE:屈従 = 1200
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 2400
	SOURCE:屈従 = 1800
ELSE
	SOURCE:快Ｖ = 3200
	SOURCE:屈従 = 2400
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:屈従, 0.20
	SOURCE:痛み = 150
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:屈従, 0.50
	SOURCE:痛み = 80
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:屈従, 0.80
	SOURCE:痛み = 80
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:屈従, 1.00
	SOURCE:痛み = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:屈従, 1.20
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:屈従, 1.50
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 2000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 800
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:痛み , 0.80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSE
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:中毒充足 , 0.90
	TIMES SOURCE:液体追加 , 0.90
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:中毒充足 , 1.00
	TIMES SOURCE:液体追加 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 2.10
	TIMES SOURCE:中毒充足 , 1.10
	TIMES SOURCE:液体追加 , 1.10
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 3.20
	TIMES SOURCE:中毒充足 , 1.20
	TIMES SOURCE:液体追加 , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 4.30
	TIMES SOURCE:中毒充足 , 1.30
	TIMES SOURCE:液体追加 , 1.30
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
SIF TALENT:処女 == 1 && TALENT:貞操観念
	TIMES SOURCE:屈従 , 2.00

TCVAR:Ｖ経験 += 1

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 3
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_600
IF TEQUIP:焦らしプレイ
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の敏感な天井を指先で静かに圧迫した…
ELSEIF TEQUIP:クスコ
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣内のぷっくり膨らんだ上側を指で撫で上げた…
ELSEIF TEQUIP:Ｖ系装着器具 == 1
	PRINTFORML %CALLNAME:PLAYER%は巧みにバイブを操り、敏感な天井をなぶった…
ELSE
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣の中で指を曲げ、ザラついた天井を刺激した…
ENDIF

IF TEQUIP:Ｖ系装着器具 == 1
	IF PALAM:欲情 >= PALAMLV:3 && ABL:Ｖ感覚 <= 3
		PRINTFORML %CALLNAME:TARGET%は頬を染めて時折声を上げている…
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINTFORML %CALLNAME:TARGET%の膣は、蜜に濡れたバイブにねっとりと絡み付いている…
	ELSEIF PALAM:欲情 < PALAMLV:3 && ABL:Ｖ感覚 <= 3
		PRINTFORML %CALLNAME:TARGET%は少し苦しそうな表情をしている…
	ELSEIF PALAM:欲情 < PALAMLV:3
		PRINTFORML %CALLNAME:TARGET%はバイブの刺激に悶えている…
	ENDIF
ELSEIF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	IF EXP:Ｖ経験 <= 1
		PRINTFORML %CALLNAME:TARGET%は膣内の異物感に怯えている…
	ELSEIF EXP:Ｖ経験 <= 30
		PRINTFORML %CALLNAME:TARGET%は指の感触に吐息を吐いて耐えている…
	ELSEIF EXP:Ｖ経験 <= 50
		PRINTFORML %CALLNAME:TARGET%は指の感触を楽しんでいるようだ…
	ELSEIF EXP:Ｖ経験 <= 80
		PRINTFORML %CALLNAME:TARGET%は指のうごめく感触にうっとりとしている…
	ELSEIF EXP:Ｖ経験 <= 120
		PRINTFORML %CALLNAME:TARGET%はお腹の中をかき回される感覚に熱い吐息を吐いて悶えている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は涎をたらし、自ら腰を振って快楽を貪っている…
	ENDIF
ENDIF
