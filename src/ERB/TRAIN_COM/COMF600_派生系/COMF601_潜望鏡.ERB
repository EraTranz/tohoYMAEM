;=============================================================================
;潜望鏡
;=============================================================================
;フェラと殆ど同じ
;浴槽から出てるTNKをしゃぶって貰うという体勢、愛好者はそれなりに居るらしい
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE601
#FUNCTION
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURNF 0
;対象が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:猫舌 && (FLAG:14 & 16) == 0
	RETURNF 0
;対象が[猫舌]だと技巧5・奉仕精神4・従順4が必要
SIF TALENT:猫舌 && (ABL:技巧 < 5 || ABL:奉仕精神 < 4 || ABL:従順 < 4)
	RETURNF 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURNF 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURNF 0
;パニック中は不可
SIF TFLAG:108 > 0
	RETURNF 0
;縄使用中は不可
SIF TEQUIP:緊縛関連
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURNF 0
;電極オナホールはダメ
SIF TEQUIP:Ｃ系装着器具 == 6
	RETURNF 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURNF 0
;助手がスク水とビキニと褌以外のコスプレ中はだめ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ > 1 && TEQUIP:ASSI:コスチュームプレイ != 2 && TEQUIP:ASSI:コスチュームプレイ != 12 && TEQUIP:ASSI:コスチュームプレイ != 17
	RETURNF 0
;コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ > 2
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 601) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM601
PRINTL 潜望鏡

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;戦闘判定
CALL COM_TRY_BATTLE, 601
;実行できない
SIF RESULT == 1
	RETURN 1

IF TFLAG:699 == 0
	CALL COM_ORDER,601
	;実行できない
	SIF RESULT == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
TSTR:0 = 潜望鏡
SELECTCOM = 601
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1300
ELSEIF ABL:技巧 == 1
	LOCAL = 1800
ELSEIF ABL:技巧 == 2
	LOCAL = 2400
ELSEIF ABL:技巧 == 3
	LOCAL = 3100
ELSEIF ABL:技巧 == 4
	LOCAL = 3700
ELSE
	LOCAL = 4300
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00

;TALENT:猫舌をみる
SIF TALENT:猫舌
	TIMES LOCAL , 0.70

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 10
DOWNBASE:1 += 150

SOURCE:屈従 = 1500
SOURCE:逸脱 = 500

;上のほうで計算した汚れデータ
SOURCE:不潔 = TFLAG:242*40 + 100

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 420
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 580
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 660
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 740
	SOURCE:達成感 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 820
	SOURCE:達成感 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)
LOCAL:2 = TFLAG:91
;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 3000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 6000
		TIMES SOURCE:達成感 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 = 12000
		TIMES SOURCE:達成感 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_601
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
TCVAR:フェラ経験 += 1

;奴隷の口⇔調教者のＰの汚れが移動
STAIN:口 |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:口

;奉仕精神ＬＶ２以上、技巧ＬＶ２以上ならＰの汚れをなめ取る
;ただし対象が猫舌の場合、猫フェラパッチ有効でなおかつ
;奉仕精神ＬＶ４以上、技巧ＬＶ５、従順ＬＶ３以上ないとなめ取らない
IF (TALENT:猫舌 == 0 && ABL:奉仕精神 >= 2 && ABL:技巧 >= 2) || (TALENT:猫舌 && FLAG:14 & 16 && ABL:奉仕精神 >= 4 && ABL:技巧 >= 5 && ABL:従順 >=3)
	STAIN:PLAYER:ペニス = 2
	SIF LOCAL:2 >= 1
		TFLAG:10 = 1
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 7
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 7
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:50 += 1

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
;奉仕快楽経験入手判定
TFLAG:100 |= 1
RETURN 1

;--------------------------------------------------
;命令拒否のメッセージ
;--------------------------------------------------
@SYSTXT_DENIAL_MESSAGE_COM_601
PRINTFORML %CALLNAME:TARGET%に%CALLNAME:PLAYER%のペニスを口で奉仕するように命じたが拒否された。
PRINTL もう少し調教が必要なようだ。

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_601
PRINTFORM %CALLNAME:TARGET%は
SIF TALENT:恋慕
	PRINT 慈しむような表情で、
SIF TALENT:服従 || TALENT:淫乱
	PRINT 自ら
;猫舌
IF TALENT:猫舌
	PRINTFORML 水面にそそり勃っている%CALLNAME:PLAYER%のペニスを咥え、舌先と顎の裏、時にはざらつく表面を巧みに用いて刺激している…
ELSE
	PRINTFORML 湯船から顔を出している%CALLNAME:PLAYER%のペニスに舌を這わせて舐めまわしている…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_601, ARG
LOCAL:99 = ARG
;ABL:欲望
IF ABL:欲望
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:欲望
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %ABLNAME:11%LV{LOCAL:99}({LOCAL:99})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:奉仕精神
	TFLAG:240 += LOCAL:99 * 4
	RESULTS = %RESULTS% %ABLNAME:13%LV{LOCAL:99}({LOCAL:99 * 4})
ENDIF
;ABL:ＢＬっ気＆TALENT:オトコ
IF TALENT:PLAYER:オトコ && TALENT:オトコ
	SIF LOCAL:99
		RESULTS = %RESULTS% - 
	;↓のLOCAL:99の目安(手淫5, フェラ/A愛撫強制10)
	LOCAL:99 = 10
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:120%同士({LOCAL:99})
	IF ABL:ＢＬっ気
		LOCAL:99 = ABL:ＢＬっ気
		TFLAG:240 += LOCAL:99 * 4
		RESULTS = %RESULTS% + %ABLNAME:23%LV{LOCAL:99}({LOCAL:99 * 4})
	ENDIF
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = ABL:精液中毒
	TFLAG:240 += LOCAL:99 * 3
	RESULTS = %RESULTS% %ABLNAME:31%LV{LOCAL:99}({LOCAL:99 * 3})
ENDIF

;快楽刻印
IF MARK:快楽刻印
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = MARK:快楽刻印
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %MARKNAME:1%LV{LOCAL:99}({LOCAL:99})
ENDIF

;PALAM:欲情
GETPALAMLV PALAM:欲情, 5
LOCAL:2 = RESULT
IF LOCAL:2
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = LOCAL:2
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %PALAMNAME:13%LV{LOCAL:2}({LOCAL:99})
ENDIF

;恥じらい
IF TALENT:恥じらい
	RESULTS = %RESULTS% - 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:35%({LOCAL:99})
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:37%({LOCAL:99})
;女嫌い
ELSEIF TALENT:女嫌い && TALENT:PLAYER:オトコ == 0
	RESULTS = %RESULTS% - 
	LOCAL:99 = 12
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:38%({LOCAL:99})
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 1
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:61%({LOCAL:99})
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:62%({LOCAL:99})
ENDIF
;献身的
IF TALENT:献身的
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 6
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:63%({LOCAL:99})
ENDIF
;快感の否定
IF TALENT:快感の否定
	RESULTS = %RESULTS% - 
	LOCAL:99 = 3
	TFLAG:240 -= LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:71%({LOCAL:99})
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 5
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %TALENTNAME:150%({LOCAL:99})
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SIF LOCAL:99
		RESULTS = %RESULTS% + 
	LOCAL:99 = 8
	TFLAG:240 += LOCAL:99
	RESULTS = %RESULTS% %CALLNAME:PLAYER%が%TALENTNAME:121%({LOCAL:99})
ENDIF

;調教者のＰの汚れ
TFLAG:242 = COMORDER_STAIN_CALC(2, 0)

;汚れあり
IF TFLAG:242
	RESULTS = %RESULTS% - 
	TFLAG:240 -= TFLAG:242
	RESULTS = %RESULTS% 汚れあり
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		RESULTS = %RESULTS%、%TALENTNAME:61%
	ELSEIF TALENT:汚臭敏感
		RESULTS = %RESULTS%、%TALENTNAME:62%
	ENDIF
	RESULTS = %RESULTS%({TFLAG:242})
	LOCAL:99 = 1
ENDIF

TFLAG:241 = 24

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_601
;デフォルト射精箇所タイプを設定
TFLAG:703 = 5
;射精箇所選択の可・不可
TFLAG:704 = 0
