;=============================================================================
;ペッティング
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE606
#FUNCTION
;対象が[猫舌]持ちのときはCONFIGの猫フェラパッチを有効にし、なおかつ対象の技巧が5以上必要となる。
SIF (FLAG:14 & 16) == 0 && TALENT:猫舌 && ABL:技巧 < 5
	RETURNF 0
;調教者が[猫舌]持ちのときはCONFIGの猫フェラパッチを有効にし、なおかつ調教者の技巧が5以上必要となる。
SIF (FLAG:14 & 16) == 0 && TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 5
	RETURNF 0
;調教者に[汚れ無視]がない場合、対象の口に愛液および母乳によるものでない汚れがついている場合にはダメ
SIF STAIN:口 >= 2 && (STAIN:口 != 16 && STAIN:口 != 17) && TALENT:PLAYER:汚れ無視 == 0
	RETURNF 0
;口枷がついているときはダメ
SIF TEQUIP:口装着器具
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 606) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM606
PRINTL ペッティング

;戦闘判定
CALL COM_TRY_BATTLE, 606
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = ペッティング
SELECTCOM = 606
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 5
DOWNBASE:1 += 50

SOURCE:露出 = 100
SOURCE:性行動 = 60
SOURCE:不潔 = 30

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 20
	SOURCE:情愛 = 50
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 500
	SOURCE:情愛 = 160
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1200
	SOURCE:情愛 = 200
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2000
	SOURCE:情愛 = 230
ELSE
	SOURCE:快Ｃ = 2800
	SOURCE:情愛 = 250
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 15
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 50
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 300
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 700
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1100
ELSE
	SOURCE:快Ｂ = 1600
ENDIF

;猫舌持ちの場合、猫フェラパッチ機能を有効にし、かつ調教者の技巧が5以上でないとキスをしない
;汚臭鈍感
IF TALENT:汚臭鈍感
	SOURCE:不潔 /= 4
;汚臭敏感
ELSEIF TALENT:汚臭敏感
	SOURCE:不潔 *= 3
ENDIF
;プライド高い
SIF TALENT:プライド高い
	SOURCE:不潔 *= 2
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SOURCE:情愛 *= 2
	SOURCE:不潔 /= 10
ENDIF
;調教者の口の汚れがあると不潔150%
SIF STAIN:PLAYER:口
	TIMES SOURCE:不潔 , 1.50

;奴隷の口⇔調教者の口の汚れが移動
STAIN:口 |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:口

;キス経験
TCVAR:キス経験 += 1
TCVAR:PLAYER:キス経験 += 1
CFLAG:主人と調教キス数 += (ASSIPLAY == 0) ? 1 # 0

;オムツプレイ中は快Ｃ半減
SIF TEQUIP:オムツプレイ
	SOURCE:快Ｃ /= 2

IF TEQUIP:コスチュームプレイ == 2 || TEQUIP:コスチュームプレイ == 12 || TEQUIP:コスチュームプレイ == 13
	;スク水・ビキニ・危ないレオタード装着中
	SOURCE:接触 = 1200
	SOURCE:露出 += 400
ELSEIF TEQUIP:コスチュームプレイ == 17 || SOURCE:59 == 21
	;褌・裸サスペンダー装着中
	SOURCE:接触 = 1400
	SOURCE:露出 += 800
ELSEIF TFLAG:899 <= 0 && EQUIP:睡眠薬 <= 0 && (TALENT:親愛 || TALENT:相愛) && (TEQUIP:コスチュームプレイ == 1 || TEQUIP:コスチュームプレイ == 19)
	;親愛か相愛でエプロンかウェディングドレス装着中(ただし睡眠や失神していないとき)
	TIMES SOURCE:情愛 , 1.50 
ELSEIF TEQUIP:サラシ
	;サラシ装着中
	SOURCE:接触 = 1200
	TIMES SOURCE:快Ｂ , 1.80
	TIMES SOURCE:情愛 , 1.40
ENDIF

;奴隷のＶ⇔調教者の指の汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:ヴァギナ

;奴隷のＢ⇔調教者の指の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_606
VARSET LOCALS, ""
SPLIT "ダミー,エプロン,スクール水着,女教師のコスチューム,和服,メイド服,体操服,ナースの衣装,チャイナドレス,セーラー服,ネグリジェ,Ｙシャツ,ビキニ水着,危ないレオタード,バニースーツ,ボンデージ衣装,園児服,褌,軍服,ウェディングドレス,ゴシックロリータ,裸にサスペンダー,巫女衣装",",",LOCALS
PRINT キスをしながら
IF TEQUIP:オムツプレイ
	PRINT オムツ姿の上から愛でるように
ELSEIF TEQUIP:コスチュームプレイ
	PRINTFORM %LOCALS:(TEQUIP:コスチュームプレイ)%を着けた
ELSEIF TEQUIP:サラシ
	PRINT サラシの上から
ENDIF
PRINTFORM %CALLNAME:TARGET%の
SIF TALENT:小柄体型
	PRINT 未発達な
IF TEQUIP:焦らしプレイ
	PRINT 体をくすぐるように愛撫した
	IF PALAM:快Ｃ >= (PALAMLV:4 - 1) || PALAM:快Ｂ >= (PALAMLV:4 - 1)
		PRINTL 
		IF PALAM:快Ｃ >= (PALAMLV:4 - 1)
			IF EXIST_PENIS(TARGET)
				PRINT 膨張したペニスを撫で回すと、
			ELSE
				PRINT 膨張したクリトリスの周囲を撫でると、
			ENDIF
		ELSE
			PRINT 自己主張する乳首の周囲をなぞると、
		ENDIF
		IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
			IF TALENT:恋慕
				PRINT 切なそうに
			ELSEIF TALENT:プライド高い
				PRINT 睨むように
			ELSE
				PRINT 潤んだ瞳で
			ENDIF
			PRINTL 見上げてくる…
		ELSE
			IF EQUIP:睡眠薬
				PRINT 寝ながらも
			ELSEIF TFLAG:899 > 0
				PRINT 失神しながらも
			ENDIF
			IF ABL:欲望 > 2
				PRINTL かなり感じているらしく、身体は頻繁に反応を示している…
			ELSEIF MARK:反発刻印 == 3
				PRINTL 明らかに避けるような仕草を繰り返している…
			ELSE
				PRINTFORML 敏感な場所を触る度に、\@(TEQUIP:口装着器具) ? 身体をかわいらしく震わせ # 寝息にわずかな嬌声を交え\@ている…
			ENDIF
		ENDIF
	ELSE
		PRINTL …
	ENDIF
ELSE
	PRINTL 体を丹念に愛撫した…
ENDIF
