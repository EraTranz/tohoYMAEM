;=============================================================================
;岩清水
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE611
#FUNCTION
;羞恥プレイ中じゃないとダメ
SIF TEQUIP:羞恥プレイ == 0
	RETURNF 0
;性器が汚れているとMASTERの場合クンニはできない（調教者が汚れ無視か愛液、母乳のよごれならＯＫ）
SIF STAIN:ヴァギナ >= 2 && (STAIN:ヴァギナ != 16 && STAIN:ヴァギナ != 17) && ASSIPLAY == 0 && TALENT:MASTER:汚れ無視 == 0
	RETURNF 0
;ヴァギナが汚れており、調教者が[汚れ無視]でない場合(愛液、母乳の汚れは条件対象外)
IF COM_ABLE_STAIN(TARGET, 3, 17) == 0 && TALENT:PLAYER:汚れ無視 == 0
	;調教者が助手で[汚臭敏感]を持ち、《従順》4未満の場合はダメ
	SIF ASSIPLAY && TALENT:PLAYER:汚臭敏感 && ABL:PLAYER:従順 < 4
		RETURNF 0
ENDIF
;調教者が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:PLAYER:猫舌 && (FLAG:14 & 16) == 0
	RETURNF 0
;調教者が[猫舌]だと調教者に技巧4必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 4
	RETURNF 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURNF 0
;睡眠中は不可
SIF EQUIP:睡眠薬
	RETURNF 0
;失神中は不可
SIF TFLAG:899 > 0
	RETURNF 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURNF 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURNF 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURNF 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURNF 0
;褌着用中の場合調教者の技巧4以上必須
SIF TEQUIP:コスチュームプレイ == 17 && ABL:PLAYER:技巧 < 4
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURNF 0
;人魚はダメ
SIF TALENT:人魚 == 1
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 611) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM611
PRINTL 岩清水

;戦闘判定
CALL COM_TRY_BATTLE, 611
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 岩清水
SELECTCOM = 611
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 150

SOURCE:液体追加 += 1000
SOURCE:露出 += 300
SOURCE:逸脱 += 500

LOCAL:1 = 500

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ += 50
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ += 260
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ += 800
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ += 1600
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ += 2500
ELSE
	SOURCE:快Ｃ += 3400
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:快Ｃ += 10
	SOURCE:液体追加 += 100
	SOURCE:屈従 += 800
	TIMES LOCAL:1, 1.00
ELSEIF ABL:露出癖 == 1
	SOURCE:快Ｃ += 150
	SOURCE:液体追加 += 500
	SOURCE:屈従 += 700
	TIMES LOCAL:1, 1.60
ELSEIF ABL:露出癖 == 2
	SOURCE:快Ｃ += 300
	SOURCE:液体追加 += 1000
	SOURCE:屈従 += 600
	TIMES LOCAL:1, 2.40
ELSEIF ABL:露出癖 == 3
	SOURCE:快Ｃ += 700
	SOURCE:液体追加 += 1800
	SOURCE:屈従 += 500
	TIMES LOCAL:1, 3.60
ELSEIF ABL:露出癖 == 4
	SOURCE:快Ｃ += 1200
	SOURCE:液体追加 += 3000
	SOURCE:屈従 += 400
	TIMES LOCAL:1, 4.80
ELSE
	SOURCE:快Ｃ += 1800
	SOURCE:液体追加 += 5000
	SOURCE:屈従 += 300
	TIMES LOCAL:1, 6.00
ENDIF

SOURCE:露出 += LOCAL:1
SOURCE:屈従 += LOCAL:1
SOURCE:恭順追加 += LOCAL:1 /2
SOURCE:液体追加 += LOCAL:1
SOURCE:羞恥扇情 += LOCAL:1

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 2.00
	SOURCE:恭順追加 += SOURCE:快Ｃ/20
ENDIF

;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快Ｃ , 0.70

;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:ヴァギナ

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 3

SIF PLAYER == MASTER
	TCVAR:Ｃ調律経験 += 2

;露出快楽経験入手判定
TFLAG:100 |= 2
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_611
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を顔の上に跨らせ、わざと音を立てながら秘唇を啜り上げた…
