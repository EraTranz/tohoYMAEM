;=============================================================================
;挿入子宮口責め
;=============================================================================
;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM615
TFLAG:132 = 0
IF SELECTCOM == 24
	PRINTL 騎乗子宮口責め

	;戦闘判定
	CALL COM_TRY_BATTLE, 615
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 騎乗子宮口責め
ELSE
	PRINTL 挿入子宮口責め

	;戦闘判定
	CALL COM_TRY_BATTLE, 615
	;実行できない
	SIF RESULT == 1
		RETURN 1

	TSTR:0 = 挿入子宮口責め
ENDIF

SELECTCOM = 615
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;Ｖ経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:1 = 1
SIF TALENT:処女 && TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:2 = TALENT:処女

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 1500
ELSEIF ABL:技巧 == 1
	LOCAL = 1600
ELSEIF ABL:技巧 == 2
	LOCAL = 1800
ELSEIF ABL:技巧 == 3
	LOCAL = 2000
ELSEIF ABL:技巧 == 4
	LOCAL = 2400
ELSE
	LOCAL = 3000
ENDIF

;従順をみる
LOCAL = EJACULATIONCHECK_ABL_10_1(TARGET, LOCAL)
;欲望をみる
LOCAL = EJACULATIONCHECK_ABL_11_1(TARGET, LOCAL)
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7
;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF PREVCOM == 24
	DOWNBASE:0 += 60
	DOWNBASE:1 += 150
	SOURCE:露出 = 900
ELSE
	DOWNBASE:0 += 50
	DOWNBASE:1 += 100
	SOURCE:露出 = 400
ENDIF

;乱れ牡丹直後の派生は効果増強
SIF PREVCOM == 612
	SOURCE:露出 += 2500

SOURCE:快Ｃ = 0

;前方向挿入の場合（正常位・対面座位・騎乗位）
IF PREVCOM == 20 || PREVCOM == 22 || PREVCOM == 24 || PREVCOM == 27
	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ = 20
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ = 100
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ = 400
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ = 900
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ = 1400
	ELSE
		SOURCE:快Ｃ = 2100
	ENDIF
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:情愛 = 100
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 30
	SOURCE:情愛 = 180
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 400
	SOURCE:情愛 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 800
	SOURCE:情愛 = 380
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 2700
	SOURCE:情愛 = 500
ELSE
	SOURCE:快Ｖ = 3800
	SOURCE:情愛 = 800
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 300
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;対面座位だと効果が高い
IF PREVCOM == 22 || PREVCOM == 27 || PREVCOM == 617
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.10
;背面座位だと効果が低い
ELSEIF PREVCOM == 23 || PREVCOM == 28 || PREVCOM == 612
	TIMES SOURCE:快Ｖ , 0.70
	TIMES SOURCE:情愛 , 0.90
ENDIF

;調教者が[オトコ]
SIF TALENT:PLAYER:オトコ
	TIMES SOURCE:快Ｖ , 2.50

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:逸脱, 5.00
		SOURCE:反感追加 = 2000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 300
	ENDIF
ELSE
	SIF TALENT:処女 == 1
		SOURCE:反感追加 = 3000
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:情愛 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:情愛 , 1.50
ELSE
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｃ , 1.40
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感追加 , 0.60
ELSE
	TIMES SOURCE:快Ｃ , 1.60
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:反感追加 , 0.30
ENDIF

IF PREVCOM == 24
	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 == 0
		SOURCE:性行動 = 200
		SOURCE:達成感 = 50
		SOURCE:逸脱 = 300
	ELSEIF ABL:奉仕精神 == 1
		SOURCE:性行動 = 250
		SOURCE:達成感 = 200
		SOURCE:逸脱 = 100
	ELSEIF ABL:奉仕精神 == 2
		SOURCE:性行動 = 350
		SOURCE:達成感 = 550
		SOURCE:逸脱 = 30
	ELSEIF ABL:奉仕精神 == 3
		SOURCE:性行動 = 450
		SOURCE:達成感 = 900
		SOURCE:逸脱 = 0
	ELSEIF ABL:奉仕精神 == 4
		SOURCE:性行動 = 600
		SOURCE:達成感 = 1500
		SOURCE:逸脱 = 0
	ELSE
		SOURCE:性行動 = 750
		SOURCE:達成感 = 2200
		SOURCE:逸脱 = 0
	ENDIF
	;ABL:技巧をみる
	IF ABL:技巧 == 0
		TIMES SOURCE:快Ｖ , 0.30
		TIMES SOURCE:情愛 , 0.30
	ELSEIF ABL:技巧 == 1
		TIMES SOURCE:快Ｖ , 0.60
		TIMES SOURCE:情愛 , 0.60
	ELSEIF ABL:技巧 == 2
		TIMES SOURCE:快Ｖ , 1.00
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:技巧 == 3
		TIMES SOURCE:快Ｖ , 1.10
		TIMES SOURCE:情愛 , 1.10
	ELSEIF ABL:技巧 == 4
		TIMES SOURCE:快Ｖ , 1.30
		TIMES SOURCE:情愛 , 1.20
	ELSE
		TIMES SOURCE:快Ｖ , 1.50
		TIMES SOURCE:情愛 , 1.40
	ENDIF
ENDIF

;乱れ牡丹からの派生
IF PREVCOM == 612
	;ABL:Ｂ感覚をみる
	IF ABL:Ｂ感覚 == 0
		SOURCE:快Ｂ = 50
		SOURCE:情愛 += 50
		SOURCE:性行動 += 50
	ELSEIF ABL:Ｂ感覚 == 1
		SOURCE:快Ｂ = 200
		SOURCE:情愛 += 200
		SOURCE:性行動 += 200
	ELSEIF ABL:Ｂ感覚 == 2
		SOURCE:快Ｂ = 500
		SOURCE:情愛 += 500
		SOURCE:性行動 += 400
	ELSEIF ABL:Ｂ感覚 == 3
		SOURCE:快Ｂ = 1000
		SOURCE:情愛 += 600
		SOURCE:性行動 += 600
	ELSEIF ABL:Ｂ感覚 == 4
		SOURCE:快Ｂ = 1600
		SOURCE:情愛 += 1000
		SOURCE:性行動 += 1000
	ELSE
		SOURCE:快Ｂ = 2100
		SOURCE:情愛 += 1400
		SOURCE:性行動 += 1400
	ENDIF

	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ += 40
		SOURCE:性行動 += 50
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ += 160
		SOURCE:性行動 += 200
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ += 700
		SOURCE:性行動 += 400
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ += 1500
		SOURCE:性行動 += 600
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ += 2400
		SOURCE:性行動 += 1000
	ELSE
		SOURCE:快Ｃ += 3600
		SOURCE:性行動 += 1400
	ENDIF

	;プレイヤーのABL:技巧をみる
	IF ABL:PLAYER:技巧 == 0
		SOURCE:快Ｃ += 100
		SOURCE:快Ｖ += 100
		SOURCE:快Ｂ += 50
		SOURCE:情愛 += 100
		SOURCE:性行動 += 50
		SOURCE:液体追加 += 0
		SOURCE:欲情追加 += 0
		LOCAL:1 = 0
	ELSEIF ABL:PLAYER:技巧 == 1
		SOURCE:快Ｃ += 200
		SOURCE:快Ｖ += 200
		SOURCE:快Ｂ += 150
		SOURCE:情愛 += 150
		SOURCE:性行動 += 100
		SOURCE:液体追加 += 50
		SOURCE:欲情追加 += 100
		LOCAL:1 = 0
	ELSEIF ABL:PLAYER:技巧 == 2
		SOURCE:快Ｃ += 300
		SOURCE:快Ｖ += 300
		SOURCE:快Ｂ += 250
		SOURCE:情愛 += 200
		SOURCE:性行動 += 200
		SOURCE:液体追加 += 100
		SOURCE:欲情追加 += 200
		LOCAL:1 = 50
	ELSEIF ABL:PLAYER:技巧 == 3
		SOURCE:快Ｃ += 500
		SOURCE:快Ｖ += 400
		SOURCE:快Ｂ += 500
		SOURCE:情愛 += 300
		SOURCE:性行動 += 300
		SOURCE:液体追加 += 150
		SOURCE:欲情追加 += 300
		LOCAL:1 = 100
	ELSEIF ABL:PLAYER:技巧 == 4
		SOURCE:快Ｃ += 800
		SOURCE:快Ｖ += 500
		SOURCE:快Ｂ += 800
		SOURCE:情愛 += 500
		SOURCE:性行動 += 400
		SOURCE:液体追加 += 250
		SOURCE:欲情追加 += 400
		LOCAL:1 = 300
	ELSE
		SOURCE:快Ｃ += 1200
		SOURCE:快Ｖ += 600
		SOURCE:快Ｂ += 1200
		SOURCE:情愛 += 800
		SOURCE:性行動 += 600
		SOURCE:液体追加 += 400
		SOURCE:欲情追加 += 500
		LOCAL:1 = 500
	ENDIF

	;ABL:露出癖をみる
	IF ABL:露出癖 == 0
		SOURCE:中毒充足 += 100
		SOURCE:液体追加 += 100
		TIMES LOCAL:1, 0.60
	ELSEIF ABL:露出癖 == 1
		SOURCE:中毒充足 += 200
		SOURCE:液体追加 += 200
		TIMES LOCAL:1, 1.00
	ELSEIF ABL:露出癖 == 2
		SOURCE:中毒充足 += 400
		SOURCE:液体追加 += 400
		TIMES LOCAL:1, 1.50
	ELSEIF ABL:露出癖 == 3
		SOURCE:中毒充足 += 700
		SOURCE:液体追加 += 700
		TIMES LOCAL:1, 2.40
	ELSEIF ABL:露出癖 == 4
		SOURCE:中毒充足 += 1200
		SOURCE:液体追加 += 1200
		TIMES LOCAL:1, 3.60
	ELSE
		SOURCE:中毒充足 += 2000
		SOURCE:液体追加 += 2000
		TIMES LOCAL:1, 5.80
	ENDIF

	SOURCE:快Ｃ += LOCAL:1
	SOURCE:快Ｖ += LOCAL:1
	SOURCE:快Ｂ += LOCAL:1

	SIF EXP:Ｖ経験 >= EXPLV:3
		SOURCE:快Ｖ += LOCAL:1

	;TALENT:恋慕をみる
	SIF TALENT:恋慕
		TIMES SOURCE:情愛 , 2.00
ENDIF

TCVAR:Ｖ経験 += 1
IF PLAYER == MASTER
	TCVAR:Ｖ調律経験 += 2
	SIF EXIST_BAR(MASTER)
		CFLAG:主人にＶ捧げ回数 += 1
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	IF ABL:従順 >= 3 && ABL:欲望 >= 3 && CFLAG:9 & 2 && FLAG:10 & 1 && TEQUIP:PLAYER:コンドーム == 0
		PRINTFORML %CALLNAME:TARGET%の子宮に熱い精液が注ぎ込まれていった…
		PRINTL 
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_615
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 4

;主人経験フラグ
SIF ASSIPLAY == 0
	TFLAG:50 += (ABL:Ｖ感覚 >= 3) ? 2 # 1

;処女喪失の後でペニスを抜いているか
TFLAG:101 = (TFLAG:101 == 2) ? 1 # 0
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_615
;騎乗子宮口責め
IF PREVCOM == 24 && EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の胸に手を衝き、ペニスを根元まで飲み込んだ…
	IF EXP:Ｖ経験 <= 30
		PRINTFORML %CALLNAME:TARGET%はそのまま腰を前後に揺すっている…
	ELSEIF EXP:Ｖ経験 <= 50 && TALENT:ふたなり
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腹にペニスを押し付けている…
	ELSEIF EXP:Ｖ経験 <= 50
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の陰毛でクリトリスを刺激しているようだ…
	ELSEIF EXP:Ｖ経験 <= 80
		PRINTFORML %CALLNAME:TARGET%は腰をくねらせ、挿入感を愉しんでは熱い息を吐いている…
	ELSEIF EXP:Ｖ経験 <= 120
		PRINTFORML %CALLNAME:TARGET%はクン、クンと腰を沈ませ、深奥を突く感触に酔っている…
		PRINTFORML コリコリした子宮口は綻び、法悦とともに仔を孕む用意を始めている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%はリズミカルに腰を小さく縦に揺すり、牝そのものの顔\@(TEQUIP:口装着器具) ? をし # で涎を垂らし\@ている…
		PRINTFORML 子作りの快楽に理性は蕩け、牝の肉体は子種を求めて貪欲にひくついている…
	ENDIF
;焦らし挿入子宮口責め
ELSEIF TEQUIP:焦らしプレイ
	;正常位
	SIF PREVCOM == 20 || PREVCOM == 27
		PRINTFORML %CALLNAME:TARGET%に体重をかけ、静かに深く覆い被さった…
	;後背位
	SIF PREVCOM == 21 || PREVCOM == 28
		PRINTFORML 膝を折って腰を落とさせ、静かに%CALLNAME:TARGET%の腰を抱きかかえた…
	;対面座位
	SIF PREVCOM == 22
		PRINTFORML 動けないよう深く抱き、腰の後ろを親指でぐりぐり押してやった…
	;背面座位
	SIF PREVCOM == 23
		PRINTFORML 腰を持ち上げ、%CALLNAME:TARGET%の背中を下に押した…
	;乱れ牡丹
	IF PREVCOM == 612
		PRINTFORML %CALLNAME:PLAYER%は鏡の前でさらなる%CALLNAME:PLAYER%の痴態を見せ付けるように、
		PRINTFORML 腰を持ち上げ、%CALLNAME:TARGET%の背中を下に押した…
	ENDIF
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF EXP:Ｖ経験 <= 30 || (EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50 && (PREVCOM == 21 || PREVCOM == 23 || PREVCOM == 612))
			PRINTFORML %CALLNAME:TARGET%は異物感が強いのか、苦しそうにしている…
		ELSEIF EXP:Ｖ経験 <= 50 && TALENT:ふたなり
			PRINTFORML %CALLNAME:TARGET%は陰毛と臍の窪みにペニスを嬲られ、\@(TEQUIP:口装着器具) ? 身体を震わせ # 鼻声を漏らし\@ている…
		ELSEIF EXP:Ｖ経験 <= 50
			PRINTFORML %CALLNAME:TARGET%は陰毛にクリトリスを擦られ、\@(TEQUIP:口装着器具) ? 身体を震わせ # 鼻声を漏らし\@ている…
		ELSEIF EXP:Ｖ経験 <= 80
			PRINTFORML %CALLNAME:TARGET%は長\@(TEQUIP:口装着器具) ? いストロークを全身に受け # く甘い息を吐き\@ながら、挿入感を噛み締めている…
		ELSE
			PRINTFORML %CALLNAME:TARGET%は半狂乱で暴れるが、深く抱かれてビクともしない…
			PRINTFORML ガタガタ震えて絶頂を切望し、四肢を強張らせ爪を立てる…
		ENDIF
	ENDIF
;挿入子宮口責め
ELSE
	;正常位
	SIF PREVCOM == 20 || PREVCOM == 27
		PRINTFORML 腰を掴んで深く突き、%CALLNAME:TARGET%の子宮をコツンと叩いた…
	;後背位
	SIF PREVCOM == 21 || PREVCOM == 28
		PRINTFORML 尻を掴んで深く突き、%CALLNAME:TARGET%の子宮をコツンと叩いた…
	;対面座位
	SIF PREVCOM == 22
		PRINTFORML 腰の括れを深く抱いて、%CALLNAME:TARGET%の子宮をグリグリと刺激した…
	;背面座位
	SIF PREVCOM == 23
		PRINTFORML 腰を前に押すようにして、%CALLNAME:TARGET%の深奥まで突き上げた…
	;乱れ牡丹
	IF PREVCOM == 612
		PRINTFORML %CALLNAME:PLAYER%は鏡の前でさらなる%CALLNAME:PLAYER%の痴態を見せ付けるように、
		PRINTFORML 腰を前に押すようにして、%CALLNAME:TARGET%の深奥まで突き上げた…
	ENDIF
	IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
		IF EXP:Ｖ経験 <= 30 || (EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50 && (PREVCOM == 21 || PREVCOM == 23 || PREVCOM == 612))
			PRINTFORML %CALLNAME:TARGET%は異物感が強いのか、苦しそうにしている…
		ELSEIF EXP:Ｖ経験 <= 50 && TALENT:ふたなり
			PRINTFORML %CALLNAME:TARGET%は陰毛と臍の窪みにペニスを嬲られ、\@(TEQUIP:口装着器具) ? 身体を震わせ # 鼻声を漏らし\@ている…
		ELSEIF EXP:Ｖ経験 <= 50
			PRINTFORML %CALLNAME:TARGET%は陰毛にクリトリスを擦られ、\@(TEQUIP:口装着器具) ? 身体を震わせ # 鼻声を漏らし\@ている…
		ELSEIF EXP:Ｖ経験 <= 80
			PRINTFORML %CALLNAME:TARGET%は長\@(TEQUIP:口装着器具) ? いストロークを全身に受け # く甘い息を吐き\@ながら、挿入感を噛み締めている…
		ELSEIF EXP:Ｖ経験 <= 120
			PRINTFORML 子宮が子種を受ける準備を済ませたらしく、先端のくびれが膣奥の窪みで引っかかる…
			PRINTFORML %CALLNAME:TARGET%の子宮をノックする度、\@(TEQUIP:口装着器具) ? 身体全体で # 口をパクパクさせて\@激しく震えている…
		ELSE
			PRINTFORML 低い位置に下りてきた%CALLNAME:TARGET%の子宮を突いて揺すってやると、
			PRINTFORML %CALLNAME:TARGET%は狂ったように\@(TEQUIP:口装着器具) ? 全身を震わせ # 悶え\@、牝として最高の快楽に溺れた…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_615
;デフォルト射精箇所タイプを設定
TFLAG:703 = 1
;射精箇所選択の可・不可
TFLAG:704 = 0

