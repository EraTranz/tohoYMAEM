;=============================================================================
;搾乳
;=============================================================================
;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM626
PRINTFORML \@(TALENT:風祝) ? グレイ聖乳タージ # 搾乳\@

;戦闘判定
CALL COM_TRY_BATTLE, 626
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 搾乳
SELECTCOM = 626
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 20
DOWNBASE:1 += 100

SOURCE:性行動 = 60
SOURCE:不潔 = 20
SOURCE:露出 = 100

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 40
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 70
	SOURCE:情愛 = 70
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 300
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
	SOURCE:情愛 = 140
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2000
	SOURCE:情愛 = 180
ELSE
	SOURCE:快Ｂ = 2800
	SOURCE:情愛 = 250
ENDIF

;ABL:噴乳中毒をみる
IF ABL:噴乳中毒 == 0
	TIMES SOURCE:快Ｂ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:噴乳中毒 == 1
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.00
ELSEIF ABL:噴乳中毒 == 2
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:噴乳中毒 == 3
	TIMES SOURCE:快Ｂ , 1.40
	TIMES SOURCE:情愛 , 1.20
ELSEIF ABL:噴乳中毒 == 4
	TIMES SOURCE:快Ｂ , 1.40
	TIMES SOURCE:情愛 , 1.40
ELSE
	TIMES SOURCE:快Ｂ , 1.60
	TIMES SOURCE:情愛 , 1.60
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:快Ｂ , 1.40
ENDIF

;調教者が［幼児］、［幼稚］
IF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.40
	TIMES SOURCE:情愛 , 1.40
ENDIF
;調教者が［幼児退行］
SIF TALENT:PLAYER:幼児退行
	TIMES SOURCE:情愛 , 1.20

;奴隷のＢ⇔調教者の指の汚れが移動
STAIN:胸 |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:胸

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_626
PRINTFORM %CALLNAME:TARGET%の
SIF PALAM:欲情 > PALAMLV:3
	PRINT 期待に震える
IF TALENT:小柄体型 && TALENT:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:爆乳
	PRINT 手に余るほど大きな
ENDIF
IF TEQUIP:焦らしプレイ
	PRINTFORML 胸を下からそっと揉み上げた…
ELSEIF PALAM:欲情 > PALAMLV:3 && TALENT:恋慕
	PRINTL 胸を強く搾り上げてやると、
	PRINTFORM %CALLNAME:TARGET%は嬌声をあげて
	IF TALENT:貧乳
		PRINT その控えめな
	ELSEIF TALENT:巨乳
		PRINT その大きな
	ELSEIF TALENT:爆乳
		PRINT その巨大な
	ENDIF
	SIF TALENT:母乳体質
		PRINT 母乳が滴る
	PRINTL 胸を強調するように突き出し、
	PRINTL より強い刺激を求めた…
ELSEIF PALAM:欲情 > PALAMLV:3
	PRINTL 胸を強く搾り上げた…
ELSE
	PRINTL 胸を搾り上げた…
ENDIF
