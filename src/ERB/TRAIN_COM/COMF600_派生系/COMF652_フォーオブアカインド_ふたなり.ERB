;=============================================================================
;フォーオブアカインド(ふたなりフランちゃんウフフ)
;=============================================================================
;背面座位アナル(胸弄らないで腕を斜め後ろ下に引っ張ってる)＋クンニ＋胸愛撫＋フェラチオな5P
;気力をほぼ根こそぎに持ってかれます
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE652
#FUNCTION
;キャラ専用コマンドを有効にしてないとダメ
SIF (FLAG:15 & 16384) == 0
	RETURNF 0
;Ａ調教フィルタがオフになってないとダメ
SIF FLAG:2 & 2
	RETURNF 0
;調教者がフランちゃんじゃないとダメだよウフフ
SIF NO:PLAYER != 1011
	RETURNF 0
;調教者にペニスがないとダメ
SIF EXIST_BAR(PLAYER) == 0
	RETURNF 0
;対象の性別が♀限定
SIF COM_ABLE_CHECK_FEMALE(TARGET) == 0
	RETURNF 0
;主人が[禁断の知識]持ちじゃないとき、体格差が2段階以内か調教者が[禁断の知識]持ちじゃないとダメ
SIF TALENT:MASTER:禁断の知識 == 0 && COMMON_BODYSIZE_CHECK(PLAYER, TARGET) == 0
	RETURNF 0
;対象の《潤滑》は大丈夫かチェック
SIF COM_ABLE_SLICK(TARGET, PLAYER) == 0
	RETURNF 0
;調教者が[猫舌]だと猫フェラパッチ有効でないとダメ
SIF TALENT:PLAYER:猫舌 && (FLAG:14 & 16) == 0
	RETURNF 0
;調教者が[猫舌]だと技巧5が必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 5
	RETURNF 0
;気力が500または体力が1500未満では使用不可
SIF BASE:体力 < 1500 || BASE:気力 < 500
	RETURNF 0
;一定のＡ経験が必要
SIF EXP:Ａ経験 < EXPLV:3
	RETURNF 0
;その調教中に浣腸プレイ＋排泄があった場合、調教者が汚れ無視もしくは汚臭鈍感を持たないなら実行できない
SIF TFLAG:140 & 256 && TALENT:PLAYER:汚臭鈍感 == 0 && TALENT:PLAYER:汚れ無視 == 0
	RETURNF 0
;Ｖ系装着具使用中は不可
SIF TEQUIP:Ｖ系装着器具 || TEQUIP:クスコ
	RETURNF 0
;Ａ系装着具使用中は不可
SIF TEQUIP:Ａ系装着器具 || TEQUIP:氷柱
	RETURNF 0
;Ｂ系装着具使用中は不可
SIF TEQUIP:Ｂ系装着器具 || TEQUIP:サラシ
	RETURNF 0
;ボールギャグ使用中は不可
SIF TEQUIP:口装着器具
	RETURNF 0
;乗馬中はダメ
SIF TEQUIP:三角木馬乗馬中
	RETURNF 0
;お風呂場プレイ中はプレイマットが無いとダメ
SIF TEQUIP:お風呂場プレイ && ITEM:プレイマット == 0
	RETURNF 0
;シャワー中はダメ
SIF TEQUIP:シャワープレイ
	RETURNF 0
;○○風呂入浴中はダメ
SIF TEQUIP:特殊風呂入浴中
	RETURNF 0
;オムツプレイ中はダメ
SIF TEQUIP:オムツプレイ
	RETURNF 0
;助手がコスプレ中はダメ
SIF ASSI >= 0 && ASSIPLAY && TEQUIP:ASSI:コスチュームプレイ > 2
	RETURNF 0
;コスプレ中はダメ
SIF TEQUIP:コスチュームプレイ > 2
	RETURNF 0
;触手調教中はダメ
SIF TEQUIP:触手調教
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 652) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM652
PRINTL フォーオブアカインドⅡ

;戦闘判定
CALL COM_TRY_BATTLE, 652
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = フォーオブアカインドⅡ
SELECTCOM = 652
TFLAG:99 = 1
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
LOCAL = 0

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL = 2200
ELSEIF ABL:技巧 == 1
	LOCAL = 2300
ELSEIF ABL:技巧 == 2
	LOCAL = 2500
ELSEIF ABL:技巧 == 3
	LOCAL = 2800
ELSEIF ABL:技巧 == 4
	LOCAL = 3200
ELSE
	LOCAL = 3700
ENDIF

;奉仕精神をみる
LOCAL = EJACULATIONCHECK_ABL_13_1(TARGET, LOCAL)
;精液中毒をみる
LOCAL = EJACULATIONCHECK_ABL_31_1(TARGET, LOCAL)
;潤滑をみる
LOCAL = EJACULATIONCHECK_PALAM_10_1(TARGET, LOCAL)

;TALENT:舌使いをみる
SIF TALENT:舌使い
	TIMES LOCAL , 2.00
;TALENT:猫舌をみる
SIF TALENT:猫舌
	TIMES LOCAL , 0.70
;睡眠薬で寝ているor失神している場合
SIF EQUIP:睡眠薬 || TFLAG:899 > 0
	TIMES LOCAL, 0.7

;調教者のＣ感覚をみる
LOCAL = EJACULATIONCHECK_ABL_0_1(PLAYER, LOCAL)

SIF EXIST_PENIS(PLAYER)
	BASE:PLAYER:射精 += LOCAL

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 1500
DOWNBASE:1 += 9999

TFLAG:242 = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	TFLAG:242 += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	TFLAG:242 += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	TFLAG:242 += 7
SIF STAIN:PLAYER:ペニス & 16
	TFLAG:242 += 15
SIF TALENT:汚臭鈍感
	TFLAG:242 /= 3
SIF TALENT:汚臭敏感
	TFLAG:242 *= 2
SOURCE:不潔 = TFLAG:242*40 + 250

SOURCE:接触 = 5000
SOURCE:欲情追加 = 1500
SOURCE:露出 = 3500
SOURCE:逸脱 = 25000

SOURCE:快Ｃ = 0
SOURCE:快Ａ = 0
SOURCE:情愛 = 0
SOURCE:性行動 = 0
SOURCE:達成感 = 0
SOURCE:痛み = 0
SOURCE:屈従 = 1500

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 40
	SOURCE:屈従 += 200
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 140
	SOURCE:屈従 += 300
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 500
	SOURCE:屈従 += 600
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 900
	SOURCE:屈従 += 900
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 1800
	SOURCE:屈従 += 1300
ELSE
	SOURCE:快Ｃ = 2400
	SOURCE:屈従 += 1700
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:不潔 += 2000
	SOURCE:露出 += 1500
ELSEIF ABL:露出癖 == 1
	SOURCE:不潔 += 1000
	SOURCE:露出 += 1250
ELSEIF ABL:露出癖 == 2
	SOURCE:不潔 += 500
	SOURCE:露出 += 750
ELSEIF ABL:露出癖 == 3
	SOURCE:不潔 += 200
	SOURCE:露出 += 500
ELSEIF ABL:露出癖 == 4
	SOURCE:不潔 += 100
	SOURCE:露出 += 250
ELSE
	SOURCE:不潔 += 50
	SOURCE:露出 += 200
ENDIF

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 10
	SOURCE:情愛 += 5
	SOURCE:屈従 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 30
	SOURCE:情愛 += 10
	SOURCE:屈従 += 400
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 500
	SOURCE:情愛 += 50
	SOURCE:屈従 += 800
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 1000
	SOURCE:情愛 += 100
	SOURCE:屈従 += 1600
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 1700
	SOURCE:情愛 += 250
	SOURCE:屈従 += 3200
ELSE
	SOURCE:快Ａ = 2200
	SOURCE:情愛 += 500
	SOURCE:屈従 += 6400
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:快Ａ , 0.10
	SOURCE:痛み += 5000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:快Ａ , 0.30
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:快Ａ , 0.50
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:痛み += 2000
	SOURCE:欲情追加 += 1000
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:快Ａ , 1.40
	SOURCE:痛み += 1000
	SOURCE:欲情追加 += 200
ELSE
	TIMES SOURCE:快Ａ , 1.60
	SOURCE:痛み += 600
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 += 40
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
	SOURCE:情愛 += 60
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 400
	SOURCE:情愛 += 80
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 800
	SOURCE:情愛 += 140
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 1500
	SOURCE:情愛 += 180
ELSE
	SOURCE:快Ｂ = 2200
	SOURCE:情愛 += 230
ENDIF

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 420
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 580
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 660
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 740
	SOURCE:達成感 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 820
	SOURCE:達成感 = 2200
	TIMES SOURCE:不潔 , 0.10
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF

;EXP:自慰経験をみる
IF EXP:自慰経験 < EXPLV:1
	TIMES SOURCE:不潔, 1.50
	TIMES SOURCE:屈従, 0.20
ELSEIF EXP:自慰経験 < EXPLV:2
	TIMES SOURCE:不潔, 1.20
	TIMES SOURCE:屈従, 0.50
ELSEIF EXP:自慰経験 < EXPLV:3
	TIMES SOURCE:不潔, 1.00
	TIMES SOURCE:屈従, 0.80
ELSEIF EXP:自慰経験 < EXPLV:4
	TIMES SOURCE:不潔, 0.80
	TIMES SOURCE:屈従, 1.00
ELSEIF EXP:自慰経験 < EXPLV:5
	TIMES SOURCE:不潔, 0.50
	TIMES SOURCE:屈従, 1.20
ELSE
	TIMES SOURCE:不潔, 0.20
	TIMES SOURCE:屈従, 1.50
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:快Ａ , 0.20
	SOURCE:欲情追加 += 1000
	SOURCE:痛み += 1900
	TIMES SOURCE:痛み , 9.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:快Ａ , 0.40
	SOURCE:欲情追加 += 800
	SOURCE:痛み += 1250
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 0.60
	SOURCE:欲情追加 += 600
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:快Ａ , 1.00
	SOURCE:欲情追加 += 200
	SOURCE:痛み += 200
	TIMES SOURCE:痛み , 0.30
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:快Ａ , 0.60
	TIMES SOURCE:達成感 , 0.60
	TIMES SOURCE:屈従 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.80
	TIMES SOURCE:達成感 , 0.80
	TIMES SOURCE:屈従 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:達成感 , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:達成感 , 1.20
	TIMES SOURCE:屈従 , 1.20
ELSE
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ａ , 1.40
	TIMES SOURCE:達成感 , 1.40
	TIMES SOURCE:屈従 , 1.40
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:快Ａ , 0.70
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:性行動 , 0.60
	TIMES SOURCE:欲情追加, 2.00
ELSEIF ABL:従順 == 1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ａ , 0.90
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:欲情追加, 1.20
ELSEIF ABL:従順 == 2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ａ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:欲情追加, 1.00
ELSEIF ABL:従順 == 3
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ａ , 1.10
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:欲情追加, 0.60
ELSEIF ABL:従順 == 4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ａ , 1.20
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:性行動 , 1.40
	TIMES SOURCE:欲情追加, 0.30
ELSE
	TIMES SOURCE:快Ｖ , 1.70
	TIMES SOURCE:快Ａ , 1.30
	TIMES SOURCE:情愛 , 1.60
	TIMES SOURCE:性行動 , 1.60
	TIMES SOURCE:欲情追加, 0.10
ENDIF

;貞操観念
IF TALENT:貞操観念
	IF TALENT:処女 == 1
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:欲情追加, 5.00
	ELSE
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:欲情追加, 1.80
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF TALENT:処女 == 1
		TIMES SOURCE:欲情追加, 0.50
	ELSE
		TIMES SOURCE:欲情追加, 0.30
	ENDIF
ENDIF
;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:欲情追加 , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:欲情追加 , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF
;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00
;調教者が［幼児退行］
IF TALENT:PLAYER:幼児退行
	TIMES SOURCE:情愛 , 1.20
;調教者が［幼児］、［幼稚］
ELSEIF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

TCVAR:Ａ経験 += 5
SIF ASSIPLAY == 0 && EXIST_BAR(MASTER)
	CFLAG:主人にＡ捧げ回数 += 5

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
TFLAG:91 = SAMEN_CHECK(PLAYER)

;射精時の処理
IF TFLAG:91 >= 1
	;コマンドごとに固有の処理はこの下に書く
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 += 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 += 500
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 += 1200
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 += 3000
		TIMES SOURCE:達成感 , 6.00
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 += 6000
		TIMES SOURCE:達成感 , 9.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 += 12000
		TIMES SOURCE:達成感 , 15.00
		TIMES SOURCE:屈従, 1.00
	ENDIF

	;射精した人物を設定
	TFLAG:92 = PLAYER
	;射精された人物を設定
	TFLAG:94 = TARGET
	;このコマンドのデフォルト射精箇所タイプ、射精箇所選択の可・不可を設定
	CALL COM_SHOOT_652
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

TFLAG:99 = 2
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;奴隷のＡ⇔調教者のＰの汚れが移動
	STAIN:アナル |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:アナル
;奴隷の口⇔調教者のＰの汚れが移動
	STAIN:口 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:口

SIF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 20

SIF PLAYER == MASTER
	TCVAR:Ｃ調律経験 += 2

TCVAR:PLAYER:フェラ経験 += 1

;奉仕快楽経験入手判定
TFLAG:100 |= 1
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_652
;口上内ではTFLAG:99と組み合わせて分岐してください(SELECTCOM == 652 && TFLAG:99 == 1 or 2)
IF TFLAG:99 == 1
	PRINTL 
	PRINTFORM %CALLNAME:PLAYER%は一瞬
	IF TALENT:サド
		PRINT 嗜虐心に満ちた
	ELSEIF TALENT:淫乱
		PRINT 蠱惑的な
	ELSEIF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
		PRINT 悪戯っ子のような
	ELSE
		PRINT 無邪気な
	ENDIF
	PRINT 笑みを浮かべると
	SIF PREVCOM == 431 || PREVCOM == 652
		PRINT 再び
	PRINTL 四人に分身した
	PRINTFORML %CALLNAME:PLAYER%達は不安げな表情を浮かべている%CALLNAME:TARGET%を取り囲むと、
	PRINTFORM 一人目は後ろから%CALLNAME:TARGET%の身体を抱き上げ、
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 粘液にまみれた
	PRINTFORML アヌスを待ち構えていたモノでゆっくり貫いた
	PRINTFORML そして%CALLNAME:TARGET%が暴れることができないように手首を掴むとそのまま身体をしなだれかからせるように手繰り寄せた……
	PRINTFORM 二人目はいったん四つん這いになると露わになった恥部に近づき、腕で%CALLNAME:TARGET%の脚を押さえつけながら
	PRINTFORM %CALLNAME:TARGET%の秘唇に舌先を突き入れて丁寧に舐めまわし
	IF TALENT:ふたなり && PALAM:欲情 >= PALAMLV:3
		PRINT 、そそり勃ったペニスに舌を這わせて舐め上げ
	ELSEIF TALENT:ふたなり
		PRINT 、ペニスの付け根を舌で舐め上げ
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINT 、ぷっくりと膨らんだお豆さんを丁寧に舐め上げ
	ENDIF
	PRINTL た
	PRINTFORML 三人目は%CALLNAME:TARGET%のそばでやや前かがみになると、片方の乳首を自らの口で吸いついたり、
	PRINTL 舌先で舐め上げたり甘噛みしつつ、もう片方の乳首を指先で摘み、弾き、こねくり回した
	PRINTFORML そして最後の一人は三人目と%CALLNAME:TARGET%をはさむような位置に立つと、
	PRINTFORML %CALLNAME:TARGET%の口に自らのモノを突き入れ、奉仕するように命じた……
ELSE
	PRINTL 
	PRINTFORML 一通り%CALLNAME:TARGET%の体をたっぷりと楽しんだ%CALLNAME:PLAYER%達は、彼女を開放すると本物を残して空気に溶け込むように消えてしまった……
	IF TFLAG:91 >= 1
		PRINTFORML %CALLNAME:TARGET%は四肢を伸ばしたままぐったりとしている
		PRINT その唇の端とアヌスからは先程吐き出された
		SIF TFLAG:91 == 2
			PRINT 大量の
		PRINTL 精液が零れ出ている……
	ELSE
		PRINTFORML %CALLNAME:TARGET%は四肢を伸ばしたままぐったりとしている……
	ENDIF
ENDIF

;-------------------------------------------------
;このコマンドのデフォルト射精箇所タイプ等の処理
;-------------------------------------------------
@COM_SHOOT_652
;デフォルト射精箇所タイプを設定
TFLAG:703 = 15
;射精箇所選択の可・不可
TFLAG:704 = 0
