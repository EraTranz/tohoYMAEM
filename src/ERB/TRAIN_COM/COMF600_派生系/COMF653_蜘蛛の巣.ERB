;=============================================================================
;蜘蛛の巣
;=============================================================================
;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM653
PRINTL 蜘蛛の巣

;戦闘判定
CALL COM_TRY_BATTLE, 653
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 蜘蛛の巣
SELECTCOM = 653
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	DOWNBASE:0 += 120
	DOWNBASE:1 += 150
ELSEIF EXP:緊縛経験 < EXPLV:4
	DOWNBASE:0 += 100
	DOWNBASE:1 += 120
ELSE
	DOWNBASE:0 += 80
	DOWNBASE:1 += 90
ENDIF

LOCAL:1 = 400
LOCAL:2 = 1200
SOURCE:露出 = 900
SOURCE:屈従 = 500
SOURCE:逸脱 = 500

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL:1 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL:1 , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL:1 , 1.10
ELSE
	TIMES LOCAL:1 , 1.20
ENDIF

;ABL:従順をみる
IF ABL:従順 == 0
	TIMES LOCAL:1 , 0.40
ELSEIF ABL:従順 == 1
	TIMES LOCAL:1 , 0.60
ELSEIF ABL:従順 == 2
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:従順 == 3
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:従順 == 4
	TIMES LOCAL:1 , 1.10
ELSE
	TIMES LOCAL:1 , 1.20
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 2.00
ELSE
	TIMES LOCAL:1 , 3.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES SOURCE:逸脱 , 2.00
;倒錯的
SIF TALENT:倒錯的
	TIMES LOCAL:1 , 2.00
;絶壁・貧乳だと羞恥心が刺激される
SIF TALENT:絶壁 || TALENT:貧乳
	TIMES SOURCE:露出 , 3.50

;調教対象がリグルなら恐怖上昇
SIF NO:TARGET == 1024
	TIMES LOCAL:2 , 3.00

SOURCE:痛み += LOCAL:1
SOURCE:液体追加 += LOCAL:1
SOURCE:露出 += LOCAL:1
SOURCE:屈従 += LOCAL:1
SOURCE:逸脱 += LOCAL:1
SOURCE:恭順追加 += LOCAL:1
SOURCE:支配 += LOCAL:1+LOCAL:2

;縄の着脱
TEQUIP:緊縛関連 = 7
TEQUIP:目装着器具 = 2
TFLAG:142 += 200
TCVAR:緊縛経験 += 10

RETURN 1

;-------------------------------------------------
;蜘蛛の巣で拘束中
;-------------------------------------------------
@EQUIP_COM653
PRINTL ＜蜘蛛の巣で拘束中＞

;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	DOWNBASE:0 += 50
	DOWNBASE:1 += 80
ELSEIF EXP:緊縛経験 < EXPLV:4
	DOWNBASE:0 += 40
	DOWNBASE:1 += 60
ELSE
	DOWNBASE:0 += 30
	DOWNBASE:1 += 50
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	LOCAL = 50
ELSEIF ABL:マゾっ気 == 1
	LOCAL = 150
ELSEIF ABL:マゾっ気 == 2
	LOCAL = 300
ELSEIF ABL:マゾっ気 == 3
	LOCAL = 500
ELSEIF ABL:マゾっ気 == 4
	LOCAL = 750
ELSE
	LOCAL = 1000
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES LOCAL , 2.00

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL , 1.10
ELSE
	TIMES LOCAL , 1.20
ENDIF

SOURCE:痛み += LOCAL
SOURCE:液体追加 += LOCAL
SOURCE:露出 += LOCAL
SOURCE:屈従 += LOCAL
SOURCE:逸脱 += LOCAL
SOURCE:恭順追加 += LOCAL
SOURCE:支配 += LOCAL
SOURCE:トラウマ += SOURCE:逸脱

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 1
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 1
ENDIF
;主人経験フラグ
SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 2
	TFLAG:50 += 1

LOCAL:1 = 1 + RAND:20
TFLAG:142 -= LOCAL:1

SIF TFLAG:142 <= 0
	CALL SPIDER_SILK_TEAR

TCVAR:緊縛経験 += 5
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_653
PRINTFORML %CALLNAME:PLAYER%は糸疣から出した糸で蜘蛛の巣を張ると、
PRINTFORML 身動きができない%CALLNAME:TARGET%を吊し上げた
IF EQUIP:睡眠薬 == 0 && TFLAG:899 == 0
	;リグルきゅん
	IF NO:TARGET == 1024
		PRINTFORML %CALLNAME:TARGET%は粘りつく糸に絡み取られ、身動きがとれなくなった
		PRINTFORML そして、%CALLNAME:PLAYER%の姿に強い恐怖を感じて顔色を失っているようだ…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は粘りつく糸に絡み取られ、身動きがとれないようだ…
	ENDIF
ENDIF
