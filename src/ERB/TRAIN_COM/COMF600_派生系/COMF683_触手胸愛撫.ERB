;=============================================================================
;触手胸愛撫
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@EXCOM_ABLE683
#FUNCTION
;個別設定コマンドフィルタ
SIF DITEMTYPE:999:683 > 0
	RETURNF 0
;触手調教中でないとダメ
SIF TEQUIP:触手調教 == 0
	RETURNF 0
;距離のチェック
SIF EQUIP:戦闘中 && COM_ABLE_BATTLE(TARGET, PLAYER, 683) == 0
	RETURNF 0
RETURNF 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM683
PRINTL 触手胸愛撫

;戦闘判定
CALL COM_TRY_BATTLE, 683
;実行できない
SIF RESULT == 1
	RETURN 1

TSTR:0 = 触手胸愛撫
SELECTCOM = 683
CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

DOWNBASE:0 += 5
DOWNBASE:1 += 50

SOURCE:露出 = 100
SOURCE:性行動 = 60
SOURCE:不潔 = 20

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 500
	SOURCE:情愛 = 160
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1200
	SOURCE:情愛 = 200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2000
	SOURCE:情愛 = 230
ELSE
	SOURCE:快Ｂ = 2800
	SOURCE:情愛 = 250
ENDIF

;淫乳
IF TALENT:淫乳
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:不潔 , 0.80
ENDIF

;調教者が［幼児］または［幼稚］
IF TALENT:PLAYER:幼児 || TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF
;調教者が［幼児退行］
SIF TALENT:PLAYER:幼児退行
	TIMES SOURCE:情愛 , 1.20

;奴隷のＢに触手の汚れがつく
STAIN:胸 |= 2
STAIN:胸 |= 4

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	TCVAR:レズ経験 += 5
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	TCVAR:ＢＬ経験 += 5
ENDIF
RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_683
PRINTFORM %CALLNAME:TARGET%の
IF TALENT:絶壁
	PRINT ほぼ無に等しい
ELSEIF TALENT:小柄体型 && TALENT:貧乳
	PRINT 僅かに膨らみが感じられる程度の
ELSEIF TALENT:貧乳
	PRINT 標準よりやや小さめの
ELSEIF TALENT:巨乳
	PRINT 手から溢れそうなほどの
ELSEIF TALENT:爆乳
	PRINT 手に余るほど大きな
ENDIF
PRINTL 胸を絞り上げるように、幾本もの触手が絡みついた…
