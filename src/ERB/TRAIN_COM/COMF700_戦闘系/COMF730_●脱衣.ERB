;=============================================================================
;●脱衣
;=============================================================================
;--------------------------------------------------
;実行判定
;--------------------------------------------------
@COM_ABLE730
;個別設定コマンドフィルタ
;SIF DITEMTYPE:999:730 > 0
;	RETURN 0
;脱衣中はダメ
SIF EQUIP:脱衣
	RETURN 0
;戦闘中じゃないとダメ
SIF !EQUIP:戦闘中
	RETURN 0
;距離のチェック
SIF COM_ABLE_BATTLE(TARGET, PLAYER, 730) == 0
	RETURN 0
RETURN 1

;--------------------------------------------------
;コマンド本体
;--------------------------------------------------
@COM730
PRINTL 脱衣
TSTR:0 = 脱衣

;戦闘判定
CALL COM_TRY_BATTLE, 730
;実行できない
SIF RESULT == 1
	RETURN 1

CALL DISPLAY_KOJO_TRAIN_MESSAGE_F_COM

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
DOWNBASE:0 += 50
DOWNBASE:1 += 150

LOCAL:1 = 400
LOCAL:2 = 400

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES LOCAL:1 , 1.10
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES LOCAL:1 , 1.30
ELSE
	TIMES LOCAL:1 , 1.40
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:中毒充足 += 0
	SOURCE:液体追加 += 0
	SOURCE:屈従 += 600
	SOURCE:逸脱 += 700
	SOURCE:反感追加 += 700
	TIMES LOCAL:1 , 0.60
ELSEIF ABL:露出癖 == 1
	SOURCE:中毒充足 += 100
	SOURCE:液体追加 += 100
	SOURCE:屈従 += 500
	SOURCE:逸脱 += 400
	SOURCE:反感追加 += 400
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:露出癖 == 2
	SOURCE:中毒充足 += 200
	SOURCE:液体追加 += 200
	SOURCE:屈従 += 400
	SOURCE:逸脱 += 100
	SOURCE:反感追加 += 100
	TIMES LOCAL:1 , 1.60
ELSEIF ABL:露出癖 == 3
	SOURCE:中毒充足 += 400
	SOURCE:液体追加 += 400
	SOURCE:屈従 += 300
	TIMES LOCAL:1 , 2.00
ELSEIF ABL:露出癖 == 4
	SOURCE:中毒充足 += 600
	SOURCE:液体追加 += 600
	SOURCE:屈従 += 200
	TIMES LOCAL:1 , 2.60
ELSE
	SOURCE:中毒充足 += 800
	SOURCE:液体追加 += 800
	SOURCE:屈従 += 100
	TIMES LOCAL:1 , 3.80
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:1 , 1.20
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:1 , 1.40
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:1 , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:1 , 1.80
ELSE
	TIMES LOCAL:1 , 2.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES LOCAL:2 , 1.50
;無関心
SIF TALENT:無関心
	TIMES LOCAL:1 , 0.80
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:1 , 0.80
;好奇心
SIF TALENT:好奇心
	TIMES LOCAL:1 , 2.50
;解放
SIF TALENT:解放
	TIMES LOCAL:1 , 1.50
;恥じらい
SIF TALENT:恥じらい
	TIMES LOCAL:2 , 3.00
;倒錯的
IF TALENT:倒錯的
	TIMES LOCAL:1 , 1.50
	TIMES LOCAL:2 , 1.20
ENDIF

SOURCE:露出 += LOCAL:1
SOURCE:恭順追加 += LOCAL:1 /2
SOURCE:トラウマ += LOCAL:2 /2
SOURCE:羞恥扇情 += LOCAL:1 /2
SOURCE:逸脱 += LOCAL:2

;脱衣成功
EQUIP:脱衣 = 1

RETURN 1

;-------------------------------------------------
;脱衣中
;-------------------------------------------------
@EQUIP_COM730
;PRINTL ＜脱衣中＞

;睡眠中か気絶中ならば体力気力消費も減少
IF EQUIP:睡眠薬 || TFLAG:899 > 0
	DOWNBASE:0 += 5
	DOWNBASE:1 += 10
ELSE
	DOWNBASE:0 += 10
	DOWNBASE:1 += 20
ENDIF

RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
@SYSTXT_TRAIN_MESSAGE_F_COM_730
IF EQUIP:睡眠薬 || TEQUIP:時間止め
	PRINTFORML %CALLNAME:TARGET%に服を脱がせて、全裸にした。
ELSEIF TFLAG:899 >= 1 || MARK:屈服刻印 == 3
	PRINTFORML 虚ろな目をした%CALLNAME:TARGET%に服を脱がせて、全裸にした。
ELSEIF TEQUIP:緊縛関連 || TEQUIP:三角木馬乗馬中 || MARK:屈服刻印 >= 1
	PRINTFORML いやいやと首を振る%CALLNAME:TARGET%に、無理矢理服を脱がせて、全裸にした。
ELSE
	PRINTFORML ひどく抵抗する%CALLNAME:TARGET%を抑えつけて服や下着を無理やりはぎ取った。
ENDIF