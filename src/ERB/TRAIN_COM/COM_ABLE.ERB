;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;コマンド実行判定における汎用処理
;
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;=============================================================================
;性別、体格、体質に起因する実行判定
;=============================================================================
;--------------------------------------------------
;対象の性別が♂なのか判定
;--------------------------------------------------
@COM_ABLE_CHECK_MALE(ARG)
#FUNCTION

RETURNF (TALENT:ARG:オトコ) ? 1 # 0

;--------------------------------------------------
;対象の性別が♀なのか判定
;--------------------------------------------------
@COM_ABLE_CHECK_FEMALE(ARG)
#FUNCTION

RETURNF (TALENT:ARG:オトコ) ? 0 # 1

;-------------------------------------------------
;対象の[処女]の判定
;-------------------------------------------------
;ARG＝調教対象、ARG:1＝調教者
@COM_ABLE_VIRGIN(ARG, ARG:1)
#FUNCTION

;調教者が《サドっ気》がLV5以上または[狂気]だとＯＫ
SIF ABL:(ARG:1):20 >= 5 || TALENT:(ARG:1):158
	RETURNF 1
;調教者が主人以外で《従順》がLV5以上か[両刀]だとＯＫ
SIF ARG:1 != MASTER && (ABL:(ARG:1):10 > 4 || TALENT:(ARG:1):44)
	RETURNF 1
;調教者が主人で対象が[恋慕][淫乱][崩壊]のいずれかだとＯＫ
SIF ARG:1 == MASTER && (TALENT:ARG:6 || TALENT:ARG:7 || TALENT:ARG:8)
	RETURNF 1
;調教者が主人かつ調教対象の《潤滑》が十分であるか《従順》がLV3以上だとＯＫ
SIF ARG:1 == MASTER && (PALAM:ARG:潤滑 > PALAMLV:2 || ABL:ARG:従順 >= 3)
	RETURNF 1
;調教者が主人で対象の《従順》がLV3未満のとき、主人の《サドっ気》がLV3以上で対象を緊縛しているとＯＫ
SIF ARG:1 == MASTER && ABL:ARG:従順 < 3 && ABL:(ARG:1):20 >= 3 && TEQUIP:ARG:緊縛関連
	RETURNF 1

RETURNF 0

;=============================================================================
;その他の現象に起因する実行判定
;=============================================================================
;-------------------------------------------------
;潤滑不足のチェック
;-------------------------------------------------
;ARG＝調教対象、ARG:1＝調教者
@COM_ABLE_SLICK(ARG, ARG:1)
#FUNCTION

;《潤滑》が十分である
SIF PALAM:ARG:潤滑 > PALAMLV:2
	RETURNF 1
;調教者が主人だとＯＫ
SIF ARG:1 == MASTER
	RETURNF 1
;調教者の《従順》がLV3以上か《サドっ気》がLV3以上または[サド]または[狂気]だとＯＫ
SIF ABL:(ARG:1):10 > 2 || ABL:(ARG:1):20 > 2 || TALENT:(ARG:1):81 || TALENT:(ARG:1):158
	RETURNF 1
;調教者と対象が同性で調教者の《レズっ気》がLV3以上か《ＢＬっ気》がLV3以上だとＯＫ
SIF !(TALENT:ARG:オトコ ^^ TALENT:(ARG:1):120) && (ABL:(ARG:1):22 > 2 || ABL:(ARG:1):23 > 2)
	RETURNF 1

RETURNF 0

;--------------------------------------------------
;特定部位の汚れのチェック
;--------------------------------------------------
;ARG＝対象、ARG:1＝対象の部位、ARG:2＝判定から弾く汚れの種類(ビット演算)
@COM_ABLE_STAIN(ARG, ARG:1, ARG:2)
#FUNCTION

;愛液
SIF STAIN:ARG:(ARG:1) & 1 && (ARG:2 & 1) == 0
	RETURNF 0
;ペニス
SIF STAIN:ARG:(ARG:1) & 2 && (ARG:2 & 2) == 0
	RETURNF 0
;精液
SIF STAIN:ARG:(ARG:1) & 4 && (ARG:2 & 4) == 0
	RETURNF 0
;アナル
SIF STAIN:ARG:(ARG:1) & 8 && (ARG:2 & 8) == 0
	RETURNF 0
;母乳
SIF STAIN:ARG:(ARG:1) & 16 && (ARG:2 & 16) == 0
	RETURNF 0
;破瓜の血
SIF STAIN:ARG:(ARG:1) & 32 && (ARG:2 & 32) == 0
	RETURNF 0
;粘液
SIF STAIN:ARG:(ARG:1) & 64 && (ARG:2 & 64) == 0
	RETURNF 0
;血
SIF STAIN:ARG:(ARG:1) & 128 && (ARG:2 & 128) == 0
	RETURNF 0
	
RETURNF 1

;-------------------------------------------------
;ハードなコマンド使用のための判定
;-------------------------------------------------
;ARG＝調教者、ARG:1＝コマンドのハードさの度合い
@COM_ABLE_HARDTRAINING(ARG, ARG:1)
#FUNCTION

IF ARG:1 == 2
	;助手調教で[服従][サド][ドＳ][狂気]全て未取得の時、《従順》5未満かつ《サドっ気》5未満だと実行できない
	SIF ASSIPLAY && ABL:ARG:従順 < 5 && ABL:ARG:サドっ気 < 5 && TALENT:ARG:サド == 0 && TALENT:ARG:狂気 == 0 && TALENT:ARG:服従 == 0 && TALENT:ARG:ドＳ == 0
		RETURNF 0
ELSEIF ARG:1 == 1
	;助手調教で《潤滑》不足の時、[妄信][服従][サド][ドＳ][狂気]全て未取得かつ《従順》4未満かつ《サドっ気》4未満だと実行できない
	SIF ASSIPLAY && PALAM:潤滑 < PALAMLV:2 && ABL:ARG:従順 < 4 && ABL:ARG:サドっ気 < 4 && TALENT:ARG:サド == 0 && TALENT:ARG:妄信 == 0 && TALENT:ARG:狂気 == 0 && TALENT:ARG:服従 == 0 && TALENT:ARG:ドＳ == 0
		RETURNF 0
ELSE
	;助手調教で調教者と対象が同性で調教者の《レズっ気》(《ＢＬっ気》)がLV4未満かつ[妄信][服従]共に未取得の時、《従順》3未満かつ《サドっ気》3未満だと実行できない
	SIF ASSIPLAY && !(TALENT:ARG:オトコ ^^ TALENT:オトコ) && ABL:ARG:レズっ気 < 4 && ABL:ARG:ＢＬっ気 < 4 && ABL:ARG:従順 < 4 && ABL:ARG:サドっ気 < 4 && TALENT:ARG:妄信 == 0 && TALENT:ARG:服従 == 0
		RETURNF 0
ENDIF

RETURNF 1

;-------------------------------------------------
;ハードなコマンド使用されるのための判定
;-------------------------------------------------
;ARG＝調教者、ARG:1＝コマンドのハードさの度合い
@COM_ABLE_GAIN_HARDTRAINING(ARG, ARG:1)
#FUNCTION

IF ARG:1 == 2
	;助手調教で[服従][マゾ][ドＭ][狂気]全て未取得の時、《従順》5未満かつ《マゾっ気》5未満だと実行できない
	SIF ASSIPLAY && ABL:ARG:従順 < 5 && ABL:ARG:マゾっ気 < 5 && TALENT:ARG:マゾ == 0 && TALENT:ARG:狂気 == 0 && TALENT:ARG:服従 == 0 && TALENT:ARG:ドＭ == 0
		RETURNF 0
ELSEIF ARG:1 == 1
	;助手調教で[妄信][服従][マゾ][ドＭ][狂気]全て未取得の時、《従順》4未満かつ《マゾっ気》4未満だと実行できない
	SIF ASSIPLAY && ABL:ARG:従順 < 4 && ABL:ARG:マゾっ気 < 4 && TALENT:ARG:マゾ == 0 && TALENT:ARG:妄信 == 0 && TALENT:ARG:狂気 == 0 && TALENT:ARG:服従 == 0 && TALENT:ARG:ドＭ == 0
		RETURNF 0
ELSE
	;助手調教で[妄信][服従][マゾ][ドＭ][狂気]全て未取得の時、《従順》3未満かつ《マゾっ気》3未満だと実行できない
	SIF ASSIPLAY && ABL:ARG:従順 < 3 && ABL:ARG:マゾっ気 < 3 && TALENT:ARG:マゾ == 0 && TALENT:ARG:妄信 == 0 && TALENT:ARG:狂気 == 0 && TALENT:ARG:服従 == 0 && TALENT:ARG:ドＭ == 0
		RETURNF 0
ENDIF

RETURNF 1
